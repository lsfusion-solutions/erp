MODULE CashierTime;

REQUIRE System, Integration, Employee, CashRegister;

CLASS CashierTime 'Время работы кассиров';
TABLE cashierTime (CashierTime);

@defineExternalizable(cashierTime, STRING[100]);
employee = DATA Employee (CashierTime);
shortNameContact 'Кассир' (CashierTime cashierTime) = shortName(employee(cashierTime));

cashRegister = DATA CashRegister (CashierTime);
numberCashRegister 'Касса' (CashierTime cashierTime) = npp(cashRegister(cashierTime));

departmentStore (CashierTime cashierTime)  = departmentStore(cashRegister(cashierTime));
nameDeartmentStore 'Отдел магазина' (CashierTime cashierTime) = name(departmentStore(cashierTime));

store (CashierTime cashierTime)  = store(departmentStore(cashRegister(cashierTime)));
nameStore 'Магазин' (CashierTime cashierTime) = name(store(cashierTime));

logOnDateTime 'Вход в систему' = DATA DATETIME(CashierTime);
logOnDate 'Вход в систему' = toDate(logOnDateTime(CashierTime CashierTime));
logOnTime 'Вход в систему' = toTime(logOnDateTime(CashierTime CashierTime));
logOffDateTime 'Выход из системы' = DATA DATETIME(CashierTime);
logOffDate 'Выход из системы' = toDate(logOffDateTime(CashierTime CashierTime));
logOffTime 'Выход из системы' = toTime(logOffDateTime(CashierTime CashierTime));
isZReport 'Время смены' = DATA BOOLEAN(CashierTime);

FORM cashierTime 'Время работы кассиров'
    OBJECTS c = CashierTime PANEL 
    PROPERTIES(c) id, nameStore, isZReport, shortNameContact, numberCashRegister, logOnDateTime, logOffDateTime
    EDIT CashierTime OBJECT c
;

FORM dialogCashierTimes 'Время работы кассиров'
    OBJECTS c = CashierTime LAST
    PROPERTIES(c) READONLY id, nameStore, isZReport, shortNameContact, numberCashRegister, logOnDateTime, logOffDateTime
    LIST CashierTime OBJECT c
;

FORM cashierTimes 'Время работы кассиров'
    OBJECTS c = CashierTime LAST
    PROPERTIES(c) READONLY id, nameStore, isZReport, shortNameContact, numberCashRegister, logOnDateTime, logOffDateTime
    PROPERTIES(c) NEWSESSION NEW, EDIT, DELETE 
;

NAVIGATOR {
    machineryMasterData {
        NEW cashierTimes;
    }
}
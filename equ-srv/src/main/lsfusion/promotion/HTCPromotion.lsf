MODULE HTCPromotion;

REQUIRE System, Item, Machinery, MachineryPriceTransaction;

EXTEND CLASS RequestExchangeType  {
    promotion 'Загрузить акции'
}


CLASS HTCPromotionTime 'Временные скидки';
TABLE HTCPromotionTime (HTCPromotionTime);

@defineExternalizable(HTCPromotionTime, STRING[100]);
isStop 'Выключена' = DATA BOOLEAN (HTCPromotionTime);
day 'День недели' = DATA DOW(HTCPromotionTime);
captionDay 'День недели' (HTCPromotionTime promotion) = staticCaption(day(promotion));
numberDay 'День недели' (HTCPromotionTime promotion) = number(day(promotion));
beginTime 'Время начала' = DATA TIME(HTCPromotionTime);
endTime 'Время окончания' = DATA TIME(HTCPromotionTime);
percent 'Процент скидки' = DATA NUMERIC[6,2](HTCPromotionTime);


CLASS HTCPromotionQuantity 'Количественные скидки';
TABLE HTCPromotionQuantity (HTCPromotionQuantity);

@defineExternalizable(HTCPromotionQuantity, STRING[100]);
isStop 'Выключена' = DATA BOOLEAN(HTCPromotionQuantity);
item 'Товар' = DATA Item(HTCPromotionQuantity);
captionItem 'Товар' (HTCPromotionQuantity HTCPromotionQuantity) = caption(item(HTCPromotionQuantity));
barcodeItem 'Штрих-код' (HTCPromotionQuantity HTCPromotionQuantity) = idBarcode(item(HTCPromotionQuantity));
idItem 'Штрих-код' (HTCPromotionQuantity HTCPromotionQuantity) = id(item(HTCPromotionQuantity));
quantity 'Мин. кол-во' = DATA NUMERIC[12,3](HTCPromotionQuantity);
percent 'Процент скидки' = DATA NUMERIC[6,2](HTCPromotionQuantity);


CLASS HTCPromotionSum 'Суммовые скидки';
TABLE HTCPromotionSum (HTCPromotionSum);

@defineExternalizable(HTCPromotionSum, STRING[100]);
isStop 'Выключена' = DATA BOOLEAN(HTCPromotionSum);
sum 'Мин. сумма' = DATA NUMERIC[12,0](HTCPromotionSum);
percent 'Процент скидки' = DATA NUMERIC[6,2](HTCPromotionSum);


FORM HTCPromotions 'Скидки HTC'
    OBJECTS pt = HTCPromotionTime
    PROPERTIES (pt) isStop, captionDay, beginTime, endTime, percent, NEW, DELETE GRID
    LIST HTCPromotionTime OBJECT pt

    OBJECTS pq = HTCPromotionQuantity
    PROPERTIES (pq) isStop, captionItem, quantity, percent, NEW, DELETE GRID
    LIST HTCPromotionQuantity OBJECT pq

    OBJECTS ps = HTCPromotionSum
    PROPERTIES (ps) isStop, sum, percent, NEW, DELETE GRID
    LIST HTCPromotionSum OBJECT ps
;

NAVIGATOR {
    machineryMasterData {
        NEW HTCPromotions;
    }
}


MODULE MachineryPriceTransactionBalance;

REQUIRE MachineryPriceTransaction;

NAMESPACE Machinery;

in 'Вкл' (MachineryPriceTransaction t, Sku s) = GROUP MAX in(t, Barcode b) BY sku(b);

balance 'Баланс' (Barcode b, Stock st) = currentBalance(sku(b), st);

balance 'Остаток' = DATA NUMERIC[16,5] (MachineryPriceTransaction, Barcode);
balance(MachineryPriceTransaction t, Barcode barcode) => in(t, barcode) RESOLVE RIGHT;
@defineMachineryPriceTransactionAttributeBase(balance, 'Остаток', );

balanceToDate 'Загружать в оборудование остатки на дату/время транзакции' = DATA BOOLEAN ();
EXTEND FORM options
    PROPERTIES() balanceToDate
;
DESIGN options {
    machinery {
        MOVE PROPERTY(balanceToDate());
    }
}

extraLoad = ABSTRACT BOOLEAN (Stock);

createAttribute(MachineryPriceTransaction t, Stock stock) + {  
    IF NOT balanceToDate() THEN {
        balance(t, Barcode barcode) <- OVERRIDE balance(barcode, stock), 0.0 WHERE in(t, barcode) AND NOT extraLoad(sku(barcode)) AND NOT extraLoad(stock); 
    } ELSE {
        balance(t, Barcode barcode) <- OVERRIDE prevBalanceB(sku(barcode), stock, dateTime(t)), 0.0 WHERE in(t, barcode) AND NOT extraLoad(sku(barcode)) AND NOT extraLoad(stock);  
    }
    
}

balanceDate 'Дата остатка' = DATA DATETIME (MachineryPriceTransaction, Barcode);
balanceDate(MachineryPriceTransaction t, Barcode barcode) => in(t, barcode) RESOLVE RIGHT;
@defineMachineryPriceTransactionAttributeBase(balanceDate, 'Дата остатка', );

balanceDate'Дата остатка' = ABSTRACT DATETIME (Stock);

createAttribute(MachineryPriceTransaction t, Stock s) + {  
    balanceDate(t, Barcode b) <- OVERRIDE balanceDate(s), currentDateTime() WHERE in(t, b) AND NOT extraLoad(sku(b));
}
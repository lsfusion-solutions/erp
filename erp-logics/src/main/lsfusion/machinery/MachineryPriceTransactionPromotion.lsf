MODULE MachineryPriceTransactionPromotion;

REQUIRE MachineryPriceTransaction, Store;

NAMESPACE Machinery;


TABLE promotionSkuDepartmentStore(Sku, DepartmentStore);

isForbidPromotion 'Запрет' = DATA BOOLEAN (SkuGroup);
isForbidPromotion 'Запрет (склад)' = DATA BOOLEAN (SkuGroup, DepartmentStore);
overIsForbidPromotion 'Запрет (перегруженный)' = OVERRIDE isForbidPromotion(SkuGroup group, DepartmentStore stock), isForbidPromotion(group) IF stock IS DepartmentStore;

isForbidPromotion 'Запрет' = DATA BOOLEAN (Sku);
isForbidPromotion 'Запрет(склад)' = DATA BOOLEAN (Sku, DepartmentStore) TABLE promotionSkuDepartmentStore;
overIsForbidPromotion 'Запрет (перегруженный)' = OVERRIDE isForbidPromotion(Sku sku, DepartmentStore stock), isForbidPromotion(sku) IF stock IS DepartmentStore;

levelParent(SkuGroup group) = GROUP
    MIN level(group, SkuGroup parent) IF isForbidPromotion(parent);
nearestParent(SkuGroup group) = group(group, levelParent(group));
nearestForbidPromotion(SkuGroup group) = isForbidPromotion(nearestParent(group));
nearestForbidPromotion(Sku sku) = OVERRIDE nearestForbidPromotion(skuGroup(sku)), isForbidPromotion(sku);

levelParent (SkuGroup group, DepartmentStore stock) = GROUP
    MIN level(group, SkuGroup parent) IF isForbidPromotion(parent, stock);
nearestParent (SkuGroup group, DepartmentStore stock) = group(group, levelParent(group, stock));
nearestForbidPromotion (SkuGroup group, DepartmentStore stock) = isForbidPromotion(nearestParent (group, stock), stock);

levelParentOver (SkuGroup group, DepartmentStore stock) = GROUP
    MIN level(group, SkuGroup parent) IF overIsForbidPromotion(parent, stock);
nearestParentOver (SkuGroup group, DepartmentStore stock) = group(group, levelParentOver(group, stock));
nearestOverForbidPromotion (SkuGroup group, DepartmentStore stock) = overIsForbidPromotion(nearestParentOver (group, stock), stock);

resultForbidPromotion 'Запрет (перегруженный)' ( SkuGroup group, DepartmentStore stock) = OVERRIDE
    overIsForbidPromotion( group, stock),
    nearestOverForbidPromotion(group, stock) MATERIALIZED;

resultForbidPromotion 'Запрет (перегруженный)' = OVERRIDE 
    overIsForbidPromotion(Sku sku, DepartmentStore stock),
    resultForbidPromotion(skuGroup(sku), stock) MATERIALIZED TABLE promotionSkuDepartmentStore;

     
quantityChildWithForbidPromotion (SkuGroup group) = GROUP SUM 1 IF isForbidPromotion(SkuGroup childGroup) AND isParent(childGroup, group);
quantityParentWithForbidPromotion (SkuGroup group) = GROUP SUM 1 IF isForbidPromotion(SkuGroup parentGroup) AND isParent(group, parentGroup);          
backgroundIsForbidPromotion 'Цвет' (SkuGroup group) = CASE
    WHEN isForbidPromotion(group) THEN RGB(0,0,0)
    WHEN nearestForbidPromotion(group) THEN RGB(230,248,250) 
    WHEN quantityChildWithForbidPromotion(group) != descendantNumber(group) AND NOT quantityParentWithForbidPromotion(group) THEN RGB(203,203,203);           
   
quantityChildWithForbidPromotion (SkuGroup group, DepartmentStore stock) = GROUP SUM 1 IF isForbidPromotion(SkuGroup childGroup, stock) AND isParent(childGroup, group);
quantityParentWithForbidPromotion (SkuGroup group, DepartmentStore stock) = GROUP SUM 1 IF isForbidPromotion(SkuGroup parentGroup, stock) AND isParent(group, parentGroup);          
backgroundIsForbidPromotion 'Цвет' (SkuGroup group, DepartmentStore stock) = CASE
    WHEN isForbidPromotion(group, stock) THEN RGB(0,0,0)
    WHEN nearestForbidPromotion(group, stock) THEN RGB(230,248,250) 
    WHEN quantityChildWithForbidPromotion(group, stock) != descendantNumber(group) AND NOT quantityParentWithForbidPromotion(group, stock) THEN RGB(203,203,203);           

quantityChildWithOverForbidPromotion (SkuGroup group, DepartmentStore stock) = GROUP SUM 1 IF overIsForbidPromotion(SkuGroup childGroup, stock) AND isParent(childGroup, group);
quantityParentWithOverForbidPromotion (SkuGroup group, DepartmentStore stock) = GROUP SUM 1 IF overIsForbidPromotion(SkuGroup parentGroup, stock) AND isParent(group, parentGroup);          
backgroundIsOverForbidPromotion 'Цвет' (SkuGroup group, DepartmentStore stock) = CASE
    WHEN overIsForbidPromotion(group, stock) THEN RGB(0,0,0)
    WHEN nearestOverForbidPromotion(group, stock) THEN RGB(230,248,250) 
    WHEN quantityChildWithOverForbidPromotion(group, stock) != descendantNumber(group) AND NOT quantityParentWithOverForbidPromotion(group, stock) THEN RGB(203,203,203);           

FORM forbidPromotion 'Запрет на применение скидок'
    OBJECTS st = DepartmentStore PANEL 
    PROPERTIES name(st) SELECTOR
    FILTERS isCompany(st)
    
    TREE treeGroup g=SkuGroup PARENT parent(g)
    PROPERTIES READONLY order(g), name(g)
    ORDERS order(g), name(g)
    FILTERGROUP inactive FILTER 'Активные' active(g) 'F5' DEFAULT
    PROPERTIES isForbidPromotion( g) BACKGROUND backgroundIsForbidPromotion(g), isForbidPromotion (g, st) BACKGROUND backgroundIsForbidPromotion(g,st)
    PROPERTIES READONLY resultForbidPromotion(g, st) BACKGROUND backgroundIsOverForbidPromotion(g,st)

    OBJECTS s = Sku
    PROPERTIES(s) READONLY name, idBarcode
    FILTERS isParent(g, s) OR s IS Sku AND NOT g
    PROPERTIES isForbidPromotion(s), isForbidPromotion(s, st) 
    PROPERTIES READONLY resultForbidPromotion(s,st)
    
    FILTERGROUP forbid
        FILTER  'Отм.'  overIsForbidPromotion(s,st) 'F10'
        FILTER  'Отм. (перегруженный)'  resultForbidPromotion(s,st) 'F11'
;

DESIGN forbidPromotion {
    OBJECTS {
        NEW treeContainer{
            fill = 1;
            type = SPLITH;
            MOVE BOX(TREE treeGroup) { caption = 'Группы товаров';}
            MOVE BOX(s) ;
        }
    }
}

NAVIGATOR {
    machineryExport {
        NEW forbidPromotion;
    }
}
@extendFormFilterStockAccess(st, forbidPromotion);

overForbidPromotion 'Запрет продаж' = ABSTRACT BOOLEAN (Sku, Stock);

extraFlags(b, s) = ABSTRACT  INTEGER (Barcode, Stock);

dataFlags(Barcode b, Stock s) =
    (IF (resultForbidPromotion(sku(b), s) OR overForbidPromotion(sku(b),s)) THEN 0 ELSE 16) + (OVERRIDE extraFlags(b, s), 0);
                                     
@defineMachineryPriceTransactionAttributeStock(dataFlags, 'Флаги', INTEGER);
flags (MachineryPriceTransaction t, Barcode b) += dataFlags(t, b);

restrictionToDateTime = ABSTRACT DATETIME (Barcode, Stock);
@defineMachineryPriceTransactionAttributeStock(restrictionToDateTime, 'Дата окончания ограничения', DATETIME );
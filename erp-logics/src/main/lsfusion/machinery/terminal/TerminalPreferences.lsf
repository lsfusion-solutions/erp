MODULE TerminalPreferences;

REQUIRE System, Terminal;

CLASS Preference 'Настройки' {
// nameGroup|order|jsonName|name|type
    GENERAL_OPTIONS                     'GENERAL|000|Options|Опции приложения|3',
    GENERAL_FONT_SCALE                  'GENERAL|001|FontScale|Размер шрифта|2|1',
    GENERAL_AUTODOCNUM                  'GENERAL|002|AutoDocumentNumbering|Автоматическая нумерация документа|1',
    GENERAL_CONFIRM_OVERQUANTITY        'GENERAL|003|ConfirmOverQuantity|Подтверждать превышение количества при вводе документа|1',
    GENERAL_CONFIRM_LOAD_DB             'GENERAL|004|ConfirmLoadDB|Подтверждать загрузку БД при наличии невыгруженных документов|1',    
    GENERAL_DEFDOCVOP                   'GENERAL|005|DefaultDocumentVop|Код операции для документа по умолчанию',
    GENERAL_POST_VAN                    'GENERAL|006|PostVan|Вид аналитики поставщика|0|ПС',
    GENERAL_STOCK_VAN                   'GENERAL|007|StockVan|Вид аналитики склада|0|СК',
    GENERAL_VOICE                       'GENERAL|008|Voice|Голосовое сопровождение|1',
    GENERAL_VOICE_PROMO_PRICECHECKER    'GENERAL|009|VoicePromoPriceChecker|Озвучивать акционный товар при проверке цен|1',
    GENERAL_MAIN_MENU                   'GENERAL|010|MainMenu|Отображать меню на главном экране|1',
    GENERAL_SOFT_KEYBOARD               'GENERAL|011|ShowSoftKeyboard|Отображать виртуальную клавиатуру|1',
    GENERAL_ONLINE_CENNIK               'GENERAL|012|OnlineCennik|On-line проверка цен|1',
    GENERAL_ALIAS_DATE1                 'GENERAL|100|AliasDate1|Наименование поля ввода "Дата1"',
    GENERAL_ALIAS_DATE2                 'GENERAL|101|AliasDate2|Наименование поля ввода "Дата2"',
    GENERAL_ALIAS_FIELD1                'GENERAL|102|AliasField1|Наименование поля ввода "Поле1"',
    GENERAL_ALIAS_FIELD2                'GENERAL|103|AliasField2|Наименование поля ввода "Поле2"',
    GENERAL_ALIAS_FIELD3                'GENERAL|104|AliasField3|Наименование поля ввода "Поле3"',
    GENERAL_ALIAS_EXTRAQUANT            'GENERAL|105|AliasExtraQuant|Наименование поля ввода "Доп. кол-во"',
    GENERAL_VESTEMPLATE                 'GENERAL|106|VesTemplate|Шаблоны весовых штрихкодов|3',
    GENERAL_SHOW_MENU_IK                'GENERAL|107|ShowMenuIk|Пункт меню "Проверка цен"|1',
    GENERAL_SHOW_MENU_CHECKORDERPICKING 'GENERAL|108|ShowMenuCheckOrderPicking|Пункт меню "Проверка комплектации заказа"|1',
    GENERAL_ALLOW_RESTORE_DOCUMENTS     'GENERAL|109|AllowRestoreDocuments|Разрешить восстановление документов из архива|1',

    ONLINE_IMAGE_SERVER                 'ONLINE|200|OnlineImageServer|Сервер изображений',
    ONLINE_ENABLE                       'ONLINE|201|OnlineEnable|Включить On-line режим|1',
    ONLINE_SERVER                       'ONLINE|202|OnlineServer|Сервер',
    ONLINE_PORT	                        'ONLINE|203|OnlinePort|Порт|2|2004',
    ONLINE_SEND_ORDER_STATUS            'ONLINE|204|OnlineSendOrderStatus|Обмен статусами заказов|1',
    ONLINE_REQ_STOCK                    'ONLINE|205|OnlineReqStock|Запрашивать склад при авторизации|1',
    ONLINE_TEST_CONNECT                 'ONLINE|206|OnlineTestConnect|Мониторинг подключения к серверу|1',
    ONLINE_SAVE_LAST_PASS               'ONLINE|207|OnlineSaveLastPass|Сохранять последний пароль|1',
    ONLINE_LOAD_BATCHES                 'ONLINE|208|OnlineLoadBatches|Принимать партии при загрузке данных|1',
    ONLINE_LOGIN_ON_STARTUP             'ONLINE|209|OnlineLoginOnStartup|Запрос авторизации при старте приложения|1',
    
    OBMEN_TYPE                          'OBMEN|301|ObmenType|Формат файлового обмена|0|XML',
    OBMEN_DIR                           'OBMEN|302|ObmenDir|Каталог обмена|0|smb://192.168.1.1/obmen',
    OBMEN_USER                          'OBMEN|303|ObmenUserName|Пользователь',
    OBMEN_PASS                          'OBMEN|304|ObmenPass|Пароль',
    OBMEN_DOMAIN                        'OBMEN|305|ObmenDomain|Домен',
    OBMEN_FTP                           'OBMEN|306|ObmenFtp|Включить FTP-обмен|1',
    OBMEN_FTP_SERVER                    'OBMEN|307|ObmenFtpServer|FTP-сервер',
    OBMEN_FTP_USER                      'OBMEN|308|ObmenFtpUserName|FTP-логин',
    OBMEN_FTP_PASS                      'OBMEN|309|ObmenFtpPass|FTP-пароль',
    OBMEN_FTP_DIR                       'OBMEN|310|ObmenFtpDir|FTP-каталог обмена',
    
    UPDATEAPP_STARTUP                   'UPD|401|UpdateAppBackground|Фоновая проверка обновления|1',
    UPDATEAPP_SUGGEST_INSTALL           'UPD|402|UpdateAppSuggestInstall|Предлагать установку обновления|1',
    UPDATEAPP_SERVER                    'UPD|403|UpdateAppServer|FTP-сервер|0|office.luxsoft.by',
    UPDATEAPP_USER                      'UPD|404|UpdateAppUser|Логин|0|tsd',
    UPDATEAPP_PASS                      'UPD|405|UpdateAppPassword|Пароль|0|tsd',
    UPDATEAPP_DIR                       'UPD|406|UpdateAppDir|Каталог',
    UPDATEAPP_APK_FILE                  'UPD|407|UpdateAppApkFile|APK-файл',

    TICKET_PRINT_ENABLE                 'TICKET|500|MobCennikEnable|Включить мобильную печать|1',
    TICKET_VOP                          'TICKET|501|MobCennikVop|Код операции для ценника',
    TICKET_BOUQUET_ANA                  'TICKET|502|MobCennikBouquetAna|Код аналитики для ценника букета',
    TICKET_QUANTITY_REQUEST             'TICKET|503|MobCennikQuantityRequest|Запрос количества ценников для печати|1',
    TICKET_DIRECT_PRINT                 'TICKET|504|MobCennikDirectPrint|Прямая печать на мобильный принтер|1',
    TICKET_TCP_ADDRESS                  'TICKET|505|MobCennikTCPPrinterAddress|IP адрес принтера',
    TICKET_USE_BLUETOOTH_PRINTER        'TICKET|506|MobCennikBluetoothPrinterEnable|Печать на Bluetooth принтер|1',

    MARKING_A1_ENABLE                 'MARKING|610|MarkingOnlineEnable|A1. Онлайн проверка марок|1',
    MARKING_A1_URL                    'MARKING|611|MarkingOnlineURL|A1. URL',
    MARKING_A1_LOGIN                  'MARKING|612|MarkingOnlineLogin|A1. Имя пользователя',
    MARKING_A1_PASSWORD               'MARKING|613|MarkingOnlinePassword|A1. Пароль',
    MARKING_A1_UID                    'MARKING|614|MarkingOnlineUID|A1. ID устройства',

    MARKING_EZ_ENABLE                 'MARKING|620|MarkingEzEnable|ЭЗ. Онлайн проверка марок|1',
    MARKING_EZ_URL                    'MARKING|621|MarkingEzURL|ЭЗ. URL',
    MARKING_EZ_LOGIN                  'MARKING|622|MarkingEzLogin|ЭЗ. Имя пользователя',
    MARKING_EZ_PASSWORD               'MARKING|623|MarkingEzPassword|ЭЗ. Пароль'
}

CLASS Obmen 'Формат обмена' {
    Obmen_SQLite '0|SQLite',
    Obmen_CSV '1|CSV',
    Obmen_XML '2|XML'
}

value (Obmen o) = STRING(getWord(staticCaption(o), '|',1));
name 'Формат' (Obmen o) = STRING(getWord(staticCaption(o), '|',2));
obmen (STRING s) = CASE 
    WHEN s == 'SQLite' THEN Obmen.Obmen_SQLite
    WHEN s == 'CSV' THEN Obmen.Obmen_CSV
    WHEN s == 'XML' THEN Obmen.Obmen_XML
;
        
FORM dialogObmen 'Формат обмена'
    OBJECTS o = Obmen
    PROPERTIES(o) READONLY name
    LIST Obmen OBJECT o
    ORDERS name(o);

CLASS PreferenceType {
    TYPE_STRING,
    TYPE_INTEGER,
    TYPE_YESNO,
    TYPE_TEXT
}

CLASS YesNo {
    true 'true|Yes',
    false 'false|No'
}
value (YesNo y) = STRING(getWord(staticCaption(y), '|',1));
name (YesNo y) = STRING(getWord(staticCaption(y), '|',2));

nameYesNo(STRING s) = CASE 
    WHEN s = 'true' THEN name(YesNo.true)
    WHEN s = 'false' THEN name(YesNo.false) MATERIALIZED;

stringToYesNo(STRING s) = CASE 
    WHEN s = 'Yes' THEN YesNo.true
    WHEN s = 'No' THEN YesNo.false MATERIALIZED;

FORM dialogYesNo 'Значение'
    OBJECTS y = YesNo
    PROPERTIES name(y) READONLY
;

getType(STRING s) = CASE 
    WHEN s = '0' THEN PreferenceType.TYPE_STRING
    WHEN s = '1' THEN PreferenceType.TYPE_YESNO
    WHEN s = '2' THEN PreferenceType.TYPE_INTEGER
    WHEN s = '3' THEN PreferenceType.TYPE_TEXT
    ELSE PreferenceType.TYPE_STRING;

enable 'Вкл' (Preference p) = DATA BOOLEAN;
nameGroup (Preference p) = STRING(getWord(staticCaption(p),'|', 1));
order (Preference p) = STRING(getWord(staticCaption(p),'|', 2));
namePreference (Preference p) = STRING(getWord(staticCaption(p),'|', 3));
name 'Параметр' (Preference p) = STRING(getWord(staticCaption(p),'|', 4));
defaultValue (Preference p) = STRING(getWord(staticCaption(p),'|', 6));
value = DATA STRING (Preference);
type (Preference p) = getType(getWord(staticCaption(p),'|', 5));
overValue 'Значение' (Preference p) =
        OVERRIDE 'No' IF type(p) = PreferenceType.TYPE_YESNO AND NOT value(p),
        OVERRIDE defaultValue(p) IF NOT value(p), value(p);
foregroundLine (Preference p) = IF NOT enable(p) THEN RGB(169,169,169);

overNameGroup 'Группа' (Preference p) = CASE
    WHEN nameGroup(p) = 'GENERAL' THEN 'Основные настройки'
    WHEN nameGroup(p) = 'ONLINE' THEN 'On-line настройки'
    WHEN nameGroup(p) = 'OBMEN' THEN 'Обмен данными'
    WHEN nameGroup(p) = 'UPD' THEN 'Обновление'
    WHEN nameGroup(p) = 'TICKET' THEN 'Печать ценников'
    WHEN nameGroup(p) = 'MARKING' THEN 'Маркировка товара'
    ELSE nameGroup(p);  

TABLE preferences (Preference);

FORM dialogText 'Текст'
    OBJECTS s = TEXT PANEL
    PROPERTIES(s) objValue = VALUE
;

DESIGN dialogText {
    BOX {
        size = (300, 150); 
        MOVE PROPERTY(objValue) { fill = 1; caption = ''; focusable = FALSE; font = 'bold 16'; }
    }
    MOVE TOOLBARBOX; 
}

changePreferenceValue(Preference p)  {
    CASE
        WHEN p == Preference.OBMEN_TYPE THEN {
            DIALOG dialogObmen OBJECTS o = obmen(value(p)) INPUT DO {
                value(p) <- name(o);
            }
        }
        WHEN type(p) == PreferenceType.TYPE_TEXT THEN {
            DIALOG dialogText OBJECTS s = value(p) INPUT DO {
                value(p) <- s;
            }
        }
        WHEN type(p) == PreferenceType.TYPE_STRING THEN {
            INPUT s = STRING DO {
                value(p) <- s;
            }
        }
        WHEN type(p) == PreferenceType.TYPE_INTEGER THEN {
            INPUT s = INTEGER DO {
                value(p) <- trim(TEXT(s));
                IF p == Preference.GENERAL_FONT_SCALE AND value(p) != '1' AND value(p) != '2' AND value(p) != '3' AND value(p) != '4' AND value(p) != '5' THEN {
                    value(p) <- '1';
                }
            }
        }
        WHEN type(p) == PreferenceType.TYPE_YESNO THEN {           
            DIALOG dialogYesNo OBJECTS y = stringToYesNo(overValue(p)) INPUT DO {
                value(p) <- name(y);
            }
        }
}

FORM preferences 'Конфигурация ТСД'
    OBJECTS p = Preference    
    PROPERTIES(p) FOREGROUND foregroundLine(p) enable, overNameGroup, name, overValue ON CHANGE changePreferenceValue(p), order SHOWIF NULL
    ORDERS order(p)
;

jsonValue(Preference p) = CASE
    WHEN p = Preference.OBMEN_TYPE THEN value(obmen(overValue(p))) 
    WHEN type(p) = PreferenceType.TYPE_YESNO THEN value(stringToYesNo(overValue(p)))
    WHEN type(p) = PreferenceType.TYPE_INTEGER THEN overValue(p)
    ELSE '"' + overValue(p) + '"'
;

terminalPreferencesJSON = DATA LOCAL STRING();

getTerminalPreferences(terminalID) {

    LOCAL json = STRING();
    json() <- '';
    
    FOR Preference p IS Preference AND enable(p) DO {
        IF length(json()) > 0 THEN json() <- json() + ','; 
        json() <- json() + '"' + namePreference(p) + '":' + (OVERRIDE jsonValue(p), 'null');
    }
    IF length(json()) > 0 THEN json() <- '[\{' + json() + '\}]'; ELSE json() <- NULL;
    
    terminalPreferencesJSON() <- json();
}

NAVIGATOR {
    terminalNavigator {
        NEW preferences;
    }
}

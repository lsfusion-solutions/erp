MODULE TerminalLot;

REQUIRE Terminal, LotAggregate;

NAMESPACE Terminal;

CLASS TerminalLotDetail 'Марки документа ТСД';

terminalDocumentDetail = DATA TerminalDocumentDetail (TerminalLotDetail) AUTOSET NONULL DELETE INDEXED;
terminalDocument (TerminalLotDetail d) = terminalDocument(terminalDocumentDetail(d));

id 'Код' = DATA STRING[200] (TerminalLotDetail);
tail 'Хвост' = DATA STRING[200] (TerminalLotDetail);
quantity 'Кол-во' = DATA NUMERIC[14,3] (TerminalLotDetail);
idParent 'Родитель' = DATA STRING[200] (TerminalLotDetail);
expiryDate 'Родитель' = DATA DATE (TerminalLotDetail);

quantityLot 'Марок' (TerminalDocumentDetail td) = GROUP SUM quantity(TerminalLotDetail ld) IF terminalDocumentDetail(ld) = td; 

backgroundQuantityLot (TerminalDocumentDetail d) = (IF quantityLot(d) = quantity(d) THEN RGB(224,255,224) ELSE RGB(255,224,224)) 
                                                       IF lotType(sku(d));

EXTEND FORM terminalDocument
    OBJECTS tld = TerminalLotDetail
    PROPERTIES(tld) id, quantity, expiryDate, NEW, DELETE
    FILTERS terminalDocumentDetail(tld) = tdd
;

DESIGN terminalDocument {
    OBJECTS {
        NEW details {
            fill = 1;
            horizontal = TRUE;
            MOVE BOX(tdd);
            MOVE BOX(tld) {
                fill = 0.3;
            }
        }
    }
}

META defineAddDetailDialogTerminalLot(object)
    overAddDetailDialogTerminal###object##Detail(###object##Detail d, TerminalDocumentDetail td) + {
        FOR id(TerminalLotDetail tl) AND terminalDocumentDetail(tl) = td AND NOT lot(id(tl)) DO NEW l = Lot {
            id(l) <- id(tl);
            tail(l) <- tail(tl);
            sku(l) <- sku(td);
            dataCount(l) <- quantity(tl) IF quantity(tl) != 1;
            expiryDate(l) <- expiryDate(tl);
        }
        FOR [GROUP SUM 1 IF terminalDocumentDetail(TerminalLotDetail tl) = td BY idParent(tl)](STRING parrent) DO {
            aggregated(lot(parrent)) <- TRUE;
        }
        FOR id(TerminalLotDetail tl)==id(Lot l) AND terminalDocumentDetail(tl) = td AND idParent(tl) DO {
            parent(l) <- lot(idParent(tl));
        }
        FOR id(TerminalLotDetail tl)==id(Lot l) AND terminalDocumentDetail(tl) = td AND tail(tl) AND (tail(l) == '' OR NOT tail(l)) DO {
            tail(l) <- tail(tl);
        }
        quantity(d, Lot l) <- GROUP SUM 1 IF l = lot(id(TerminalLotDetail tld)) AND td = terminalDocumentDetail(tld) AND NOT aggregated(l); 
    }
END

quantity = ABSTRACT NUMERIC[16,5](Lot, Stock, Employee);
number = ABSTRACT STRING(Lot, Stock, Employee);
filter = ABSTRACT VALUE BOOLEAN(Lot, Stock, Employee);
MODULE Machinery;

REQUIRE System, SkuLedger, Barcode, PriceListLedger, DefaultData;

//---------------------------- группы оборудования ----------------------------------------//

CLASS ABSTRACT GroupMachinery 'Группы оборудования';
TABLE groupMachinery (GroupMachinery);

npp 'Порядковый номер' = DATA INTEGER (GroupMachinery) IN id NONULL INDEXED; 
name 'Наименование' = DATA ISTRING[200] (GroupMachinery) IN id CHARWIDTH 10;
directory = ABSTRACT STRING[200] (GroupMachinery);

inactive 'Неактивный' = ABSTRACT BOOLEAN (GroupMachinery);
active 'Активный' (GroupMachinery group) = group IS GroupMachinery AND NOT inactive(group);

priority 'Приоритет' = ABSTRACT INTEGER (GroupMachinery);

groupMachineryNpp = GROUP AGGR GroupMachinery groupMachinery BY npp(groupMachinery);

// -------------------------------- Склады --------------------------------------------- //

stock = ABSTRACT Stock (GroupMachinery) MATERIALIZED;
idStock 'Склад' (GroupMachinery groupMachinery) = id(stock(groupMachinery));
nameStock 'Склад' (GroupMachinery groupMachinery) = name(stock(groupMachinery));
section 'Секция' (GroupMachinery groupMachinery) = DATA STRING[100] (GroupMachinery);

// фильтрация по группам товаров

filterSku 'Фильтровать по классификатору' = DATA BOOLEAN (GroupMachinery);

TABLE groupMachinerySkuGroup (GroupMachinery, SkuGroup);
in 'Вкл' = DATA BOOLEAN (GroupMachinery, SkuGroup);

FORM filterSkuGroupMachinery 'Фильтрация по классификатору'
    OBJECTS gm = GroupMachinery PANEL
    PROPERTIES(gm) READONLY npp, nameStock, name

    TREE treeGroups g=SkuGroup PARENT parent(g)
    PROPERTIES READONLY order(g), id(g) SHOWIF showIDs(), name(g)
    ORDER order(g), name(g)
    FILTERGROUP inactive FILTER 'Активные' active(g) 'F5' DEFAULT

    OBJECTS cg=SkuGroup
    PROPERTIES(cg) READONLY canonicalName
    PROPERTIES(gm, cg)      in
    ORDER canonicalName(cg)
    FILTERS isParentLeaf(cg, g)
    FILTERGROUP inactive1 FILTER 'Активные' active(cg) 'F5' DEFAULT

    FILTERGROUP filters
        FILTER 'Только выбранные группы' in(gm, cg) 'F10'
;

DESIGN filterSkuGroupMachinery {
    BOX {
        MOVE BOX(gm);
        NEW row {
            fill = 1;
            type = SPLITH;

            MOVE BOX(TREE treeGroups);
            MOVE BOX(cg) {fill = 2;}
        }
        MOVE TOOLBARBOX;
    }

}

showFilterSku 'Выбрать группы' (GroupMachinery groupMachinery)  { SHOW filterSkuGroupMachinery OBJECTS gm = groupMachinery ; }

// --------------------------------- Вид цены ----------------------------- //

priceListType 'Вид цен' = DATA LedgerPriceListType (GroupMachinery) NONULL;
namePriceListType 'Вид цен' (GroupMachinery groupMachinery) = name(priceListType(groupMachinery));
idPriceListType 'Вид цен' (GroupMachinery groupMachinery) = id(priceListType(groupMachinery));

// ---------------------------- Модели оборудования ----------------------------------------//
CLASS ABSTRACT Model 'Модель';
TABLE model (Model);

name 'Наименование' = ABSTRACT ISTRING[110](Model);

sid 'Код' = DATA STRING[100] (Model) IN base;
note 'Примечание' = DATA STRING[200] (Model) IN base;
handler 'Обработчик' = DATA STRING[200] (Model) IN base;
maxProduct 'MAX допустимое колич. товаров' = DATA INTEGER (Model) IN base;

//---------------------------- типы оборудования  ----------------------------------------//
CLASS ABSTRACT Machinery 'Оборудование';
TABLE machinery(Machinery);

groupMachinery = ABSTRACT GroupMachinery (Machinery) MATERIALIZED;
dataDirectory = ABSTRACT STRING[200] (Machinery);
overDirectory (Machinery machinery) = OVERRIDE dataDirectory(machinery), directory(groupMachinery(machinery));
nppGroupMachinery 'Номер группы' (Machinery machinery) = npp(groupMachinery(machinery));
nameGroupMachinery 'Группа' (Machinery machinery) = name(groupMachinery(machinery));
stock (Machinery machinery) = stock(groupMachinery(machinery));
idStock (Machinery machinery) = id(stock(machinery));

model = ABSTRACT Model(GroupMachinery) MATERIALIZED;
sidModel 'Код' (GroupMachinery groupMachinery) = sid(model(groupMachinery)) IN base;
nameModel 'Модель' (GroupMachinery groupMachinery) = name(model(groupMachinery)) IN base;
handlerModel 'Обработчик' (GroupMachinery machinery) = handler(model(machinery)) IN base;
handlerModel (Machinery machinery) = handlerModel(groupMachinery(machinery));

npp 'Порядковый номер' = DATA INTEGER (Machinery) IN base NONULL;
shortDescription 'Краткое описание' = DATA STRING[10] (Machinery) IN base;
overNpp 'Номер' (Machinery machinery) = OVERRIDE shortDescription(machinery), STRING[10](npp(machinery));
description 'Описание' = DATA STRING[200] (Machinery) IN base;

port 'Адрес/порт' = DATA STRING[100] (Machinery) IN base CHARWIDTH 20;
ports 'Адрес/порт' = GROUP CONCAT port(Machinery m), ',' BY groupMachinery(m) CHARWIDTH 20;

overInactive = ABSTRACT BOOLEAN (Machinery);
inactive 'Неактивный' (Machinery m)= OVERRIDE overInactive(m), inactive(groupMachinery(m));

skipMachineryPriceTransaction = ABSTRACT BOOLEAN (GroupMachinery);

// ------------------------------------------------ Стандартные значения ------------------------------------ //

EXTEND FORM defaultData
    OBJECTS         pt=PriceListType PANEL
    PROPERTIES(pt)  SELECTOR name

    OBJECTS         s=Stock PANEL
    PROPERTIES(s)   SELECTOR name
;

DESIGN defaultData {
    pane {
        NEW machinery {
            fill = 1;
            caption = 'Оборудование';

            MOVE PROPERTY(name(pt)) {
                caption = 'Вид цен';
            }
            MOVE PROPERTY(name(s)) {
                caption = 'Склад';
            }
        }
    }
}

NAVIGATOR {
    NEW FOLDER machineryNavigator 'Оборудование' BEFORE administration WINDOW toolbar IMAGE 'machinery.png' {
       NEW FOLDER machineryExport 'Экспорт';
       NEW FOLDER machineryMasterData 'Справочники';
    }
}

DESIGN options {
    pane {
        NEW machinery {
            caption = 'Оборудование';
        }
    }
}
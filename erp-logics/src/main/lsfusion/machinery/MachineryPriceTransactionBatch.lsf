MODULE MachineryPriceTransactionBatch;

REQUIRE MachineryPriceTransaction, Item;

NAMESPACE Machinery;

TABLE machineryPriceTransactionBarcodeBatch (MachineryPriceTransaction, Barcode, Batch);

in 'Вкл' = DATA BOOLEAN (MachineryPriceTransaction, Barcode, Batch) INDEXED;

balance 'Баланс' (Batch bt, Barcode b, Stock st) = currentBalance(bt, st) IF barcode(sku(bt))==b;
price 'Цена' (Batch bt, Barcode b, GroupMachinery g) = priceA(priceListType(g),bt,stock(g),currentDateTime()) IF barcode(sku(bt))==b;

countDaysSendBatches 'Кол-во дней движения по партии для загрузки в оборудование' = DATA INTEGER ();

EXTEND FORM options
    PROPERTIES countDaysSendBatches()
;

DESIGN options {
    machinery {
        MOVE PROPERTY(countDaysSendBatches());
    }
}

createAttribute(MachineryPriceTransaction t) + {
    in(t, Barcode barcode, Batch batch) <- TRUE 
        WHERE in(t, barcode) AND price(batch, barcode, groupMachinery(t)) AND 
              (balance(batch, barcode, stock(t)) OR countLedger(batch, stock(t), subtract(currentDate(),(OVERRIDE countDaysSendBatches(),7)), currentDate()));            
}

name 'Наименование' = DATA STRING[255] (MachineryPriceTransaction, Barcode, Batch);
name(MachineryPriceTransaction t, Barcode barcode, Batch batch)  => in(t, barcode, batch) RESOLVE RIGHT;

price 'Цена' = DATA NUMERIC[16,4] (MachineryPriceTransaction, Barcode, Batch);
price(MachineryPriceTransaction t, Barcode barcode, Batch batch)  => in(t, barcode, batch) RESOLVE RIGHT;

// INDEX groupInMachineryPriceTransactionBarcode + barcode + Batch
groupIn = groupMachinery(MachineryPriceTransaction transaction) IF in(transaction, Barcode barcode, Batch bt) 
    AND NOT skipLastTransaction(groupMachinery(transaction)) MATERIALIZED;

dateTimeIn = dateTime(MachineryPriceTransaction transaction) IF in(transaction, Barcode barcode, Batch bt) 
    AND NOT skipLastTransaction(groupMachinery(transaction)) MATERIALIZED;
    
INDEX groupIn(MachineryPriceTransaction t, Barcode b, Batch bt), b, dateTimeIn(t,b,bt), t, bt;
    
lastTransaction (Batch bt, Barcode barcode, groupMachinery) = 
    GROUP LAST MachineryPriceTransaction transaction
          ORDER dateTimeIn(transaction, barcode, bt), transaction
          WHERE NOT canceled(transaction) 
          BY groupIn(transaction, barcode, bt) MATERIALIZED;
prevLastTransaction (Batch bt, Barcode b, GroupMachinery g) = lastTransaction(bt, b, g);

//lastTransaction (sku, stock) =
//    GROUP LAST lastTransaction(Barcode b, GroupMachinery g)
//          ORDER dateTimeIn(lastTransaction(b, g), b), lastTransaction(b, g)
//          WHERE lastTransaction(b, g) 
//          BY sku(b), stock(g);
          
lastFullTransaction (Batch bt, Barcode barcode, groupMachinery) = 
    GROUP LAST MachineryPriceTransaction transaction
          ORDER dateTime(transaction), transaction
          WHERE in(transaction, barcode, bt) AND NOT canceled(transaction) 
          BY groupMachinery(transaction);
          
prevLastFullTransaction (Batch bt, Barcode barcode, GroupMachinery groupMachinery) = PREV(lastFullTransaction (bt, barcode, groupMachinery));

//Остаток

balance 'Остаток' = DATA NUMERIC[16,5] (MachineryPriceTransaction, Barcode, Batch);
balance(MachineryPriceTransaction t, Barcode barcode, Batch bt) => in(t, barcode, bt) RESOLVE RIGHT;
prevTransactionBalance 'Остаток (тек.)' (Batch bt, Barcode barcode, GroupMachinery groupMachinery)  =
    balance(prevLastFullTransaction(bt, barcode, groupMachinery), barcode, bt);

snapshotAttribute(MachineryPriceTransaction t, GroupMachinery groupMachinery) +
    {  balance(t, Barcode barcode, Batch bt) <- prevTransactionBalance(bt, barcode, groupMachinery) WHERE in(t, barcode, bt); }
    
balanceBlister 'Остаток(блистеры)' = ABSTRACT NUMERIC[16,5] (MachineryPriceTransaction, Barcode, Batch);    

EXTEND FORM machineryPriceTransaction
    OBJECTS batch=Batch
    PROPERTIES(batch) READONLY id, idBarcodeSku, nameSku, name
    PROPERTIES(t, b, batch) in, name, price
    FILTERS in(t, b, batch)
;

DESIGN machineryPriceTransaction{
    barcodes{
        type=SPLITV;    
        MOVE BOX(batch);
    }
}

EXTEND FORM machineryPriceTransactions
    OBJECTS batch=Batch
    PROPERTIES(batch) READONLY id, idBarcodeSku, nameSku, name
    PROPERTIES(t, b, batch) READONLY in, name, price
    FILTERS in(t, b, batch)
    
    OBJECTS bbatch=Batch
    PROPERTIES(bbatch) READONLY id, idBarcodeSku, nameSku, name
    PROPERTIES(mt, bb, bbatch) READONLY in, name, price
    FILTERS in(mt, bb, bbatch)    
;

DESIGN machineryPriceTransactions{
    barcodes{
        type=SPLITV;
        MOVE BOX(batch);
    }
    barcodeContainer{
        MOVE BOX(bbatch) BEFORE BOX(bb);
    }
}

EXTEND FORM machineryPriceTransaction PROPERTIES(t, b, batch) balance, balanceBlister;
EXTEND FORM machineryPriceTransactions PROPERTIES(t, b, batch) READONLY balance, balanceBlister;
EXTEND FORM machineryPriceTransactions PROPERTIES(mt, bb, bbatch) READONLY balance, balanceBlister;

// -------------------- Создание новых атрибутов ----------------- //

META defineMachineryPriceTransactionAttributeBaseBatch(object, caption, nameProp)
    prevTransaction###object caption###' (тек.)' (Batch batch, Barcode barcode, GroupMachinery groupMachinery)  =
        object(prevLastFullTransaction(batch, barcode, groupMachinery), barcode, batch);

    snapshotAttribute(MachineryPriceTransaction t, GroupMachinery groupMachinery) +
        {  object(t, Barcode barcode, Batch batch) <- prevTransaction###object(batch, barcode, groupMachinery) WHERE in(t, barcode, batch); }

    EXTEND FORM machineryPriceTransaction PROPERTIES(t, b, batch) nameProp###object;
    EXTEND FORM machineryPriceTransactions PROPERTIES(t, b, batch) READONLY nameProp###object;
    EXTEND FORM machineryPriceTransactions PROPERTIES(mt, bb, bbatch) READONLY  nameProp###object;
END

META defineMachineryPriceTransactionAttributeActionBatch(object, caption, type, nameProp)
    @defineMachineryPriceTransactionAttributeBaseBatch(object, caption, nameProp);
    over###object = ABSTRACT type (MachineryPriceTransaction, Barcode, Batch);
    createAttribute(MachineryPriceTransaction t) + {  object(t, Barcode barcode, Batch batch) <- OVERRIDE over###object(t, barcode, batch), object(batch)
                                                          WHERE in(t, barcode, batch); }
END

META defineMachineryPriceTransactionAttributeBatch(object, caption, type)
    object caption = DATA type (MachineryPriceTransaction, Barcode, Batch);
    object(MachineryPriceTransaction t, Barcode barcode, Batch batch) => in(t, barcode, batch) RESOLVE RIGHT;
    @defineMachineryPriceTransactionAttributeActionBatch(object, caption, type, );
END

META defineMachineryPriceTransactionAttributeBatch(object, caption, type, nameProp)
    object = DATA type (MachineryPriceTransaction, Barcode, Batch);
    object(MachineryPriceTransaction t, Barcode barcode, Batch batch) => in(t, barcode, batch) RESOLVE RIGHT;
    nameProp##type caption (MachineryPriceTransaction transaction, Barcode barcode, Batch batch) =
        nameProp(object(transaction, barcode, batch));
    @defineMachineryPriceTransactionAttributeActionBatch(object, caption, type, nameProp);
END

META defineMachineryPriceTransactionAttributeStockActionBatch(type, object, caption, nameProp)
    @defineMachineryPriceTransactionAttributeBaseBatch(object, caption, nameProp);
    over###object = ABSTRACT type (MachineryPriceTransaction, Barcode, Batch);
    createAttribute(MachineryPriceTransaction t, Stock stock) + {  object(t, Barcode barcode, Batch batch) <- OVERRIDE over###object(t, barcode, batch), object(batch, stock)
                                                                      WHERE in(t, barcode, batch); }
END
META defineMachineryPriceTransactionAttributeStockBatch(object, caption, type)
    object caption = DATA type (MachineryPriceTransaction, Barcode, Batch);
    object(MachineryPriceTransaction t, Barcode barcode, Batch batch) => in(t, barcode, batch) RESOLVE RIGHT;
    @defineMachineryPriceTransactionAttributeStockActionBatch(type, object, caption, );
END

createAttribute(MachineryPriceTransaction t) + {
    price(t, Barcode barcode, Batch bt) <- price(bt, barcode, groupMachinery(t)) WHERE in(t, barcode, bt);              
}

balanceToDate 'Загружать в оборудование остатки на дату/время транзакции' = DATA BOOLEAN ();
EXTEND FORM options
    PROPERTIES() balanceToDate
;
DESIGN options {
    machinery {
        MOVE PROPERTY(balanceToDate());
    }
}

extraLoad = ABSTRACT BOOLEAN (Stock);

createAttribute(MachineryPriceTransaction t, Stock stock) + {  
    IF NOT balanceToDate() THEN {
        balance(t, Barcode barcode, Batch bt) <- OVERRIDE balance(bt, barcode, stock), 0.0 WHERE in(t, barcode, bt) AND NOT extraLoad(sku(barcode)) AND NOT extraLoad(stock); 
    } ELSE {
        balance(t, Barcode barcode, Batch bt) <- OVERRIDE prevBalanceB(bt, stock, dateTime(t)), 0.0 WHERE in(t, barcode, bt) AND NOT extraLoad(sku(barcode)) AND NOT extraLoad(stock);  
    }
    
}

balanceDate 'Дата остатка' = DATA DATETIME (MachineryPriceTransaction, Barcode, Batch);
balanceDate(MachineryPriceTransaction t, Barcode barcode, Batch bt) => in(t, barcode, bt) RESOLVE RIGHT;
@defineMachineryPriceTransactionAttributeBaseBatch(balanceDate, 'Дата остатка', );

balanceDate 'Дата остатка' = ABSTRACT DATETIME (Stock);

createAttribute(MachineryPriceTransaction t, Stock s) + {  
    balanceDate(t, Barcode b, Batch bt) <- OVERRIDE balanceDate(s), currentDateTime() WHERE in(t, b, bt) AND NOT extraLoad(sku(b));
}

@defineMachineryPriceTransactionAttributeBatch(idSku, 'Код товара', STRING[100]);

@defineMachineryPriceTransactionAttributeBatch(id, 'Код партии', STRING[100]);

@defineMachineryPriceTransactionAttributeBatch(idBarcodeSku, 'Штрихкод', STRING[15]);

@defineMachineryPriceTransactionAttributeBatch(expiryDate, 'Срок годности (дата)', DATE);

@defineMachineryPriceTransactionAttributeBatch(manufactureDate, 'Дата изготовления', DATE);

@defineMachineryPriceTransactionAttributeBatch(nameManufacturer, 'Производитель', STRING[200]);

@defineMachineryPriceTransactionAttributeBatch(nameCountry, 'Страна', STRING[250]);

@defineMachineryPriceTransactionAttributeBatch(sidCountry, 'Код страны', STRING[250]);

retailPrice 'Цена до' = ABSTRACT NUMERIC[16,4] (Batch, Stock);
retailPrice 'Цена до' = DATA NUMERIC[16,4] (MachineryPriceTransaction, Barcode, Batch);
retailPrice(MachineryPriceTransaction t, Barcode barcode, Batch batch) => in(t, barcode, batch) RESOLVE RIGHT;
@defineMachineryPriceTransactionAttributeStockActionBatch(NUMERIC[16,4], retailPrice, 'Цена до', );

//Фарм

//Серия лекарственного средства
seriesPharmacy 'Серия лекарственного средства' = DATA STRING[20] (MachineryPriceTransaction, Barcode, Batch);
seriesPharmacy(MachineryPriceTransaction t, Barcode barcode, Batch batch) => in(t, barcode, batch) RESOLVE RIGHT;
prevTransactionSeriesPharmacy 'Серия лекарственного средства (тек.)' (Batch batch, Barcode barcode, GroupMachinery groupMachinery)  =
    seriesPharmacy(prevLastFullTransaction(batch, barcode, groupMachinery), barcode, batch);

snapshotAttribute(MachineryPriceTransaction t, GroupMachinery groupMachinery) +
    {  seriesPharmacy(t, Barcode barcode, Batch batch) <- prevTransactionSeriesPharmacy(batch, barcode, groupMachinery) WHERE in(t, barcode, batch); }

EXTEND FORM machineryPriceTransaction PROPERTIES(t, b, batch) seriesPharmacy;
EXTEND FORM machineryPriceTransactions PROPERTIES(t, b, batch) READONLY seriesPharmacy;
EXTEND FORM machineryPriceTransactions PROPERTIES(mt, bb, bbatch) READONLY  seriesPharmacy;
overSeriesPharmacy = ABSTRACT STRING[20] (MachineryPriceTransaction, Barcode, Batch);
createAttribute(MachineryPriceTransaction t) + {seriesPharmacy(t, Barcode barcode, Batch batch) <- overSeriesPharmacy(t, barcode, batch) WHERE in(t, barcode, batch);}

//МНН
nameATCGroup 'МНН' = DATA ISTRING[250] (MachineryPriceTransaction, Barcode, Batch);
nameATCGroup(MachineryPriceTransaction t, Barcode barcode, Batch batch) => in(t, barcode, batch) RESOLVE RIGHT;
prevTransactionNameATCGroup 'МНН (тек.)' (Batch batch, Barcode barcode, GroupMachinery groupMachinery)  =
    nameATCGroup(prevLastFullTransaction(batch, barcode, groupMachinery), barcode, batch);

snapshotAttribute(MachineryPriceTransaction t, GroupMachinery groupMachinery) +
    {  nameATCGroup(t, Barcode barcode, Batch batch) <- prevTransactionNameATCGroup(batch, barcode, groupMachinery) WHERE in(t, barcode, batch); }

EXTEND FORM machineryPriceTransaction PROPERTIES(t, b, batch) nameATCGroup;
EXTEND FORM machineryPriceTransactions PROPERTIES(t, b, batch) READONLY nameATCGroup;
EXTEND FORM machineryPriceTransactions PROPERTIES(mt, bb, bbatch) READONLY  nameATCGroup;
overNameATCGroup = ABSTRACT ISTRING[250] (MachineryPriceTransaction, Barcode, Batch);
createAttribute(MachineryPriceTransaction t) + {nameATCGroup(t, Barcode barcode, Batch batch) <- overNameATCGroup(t, barcode, batch) WHERE in(t, barcode, batch);}

//Блистеры
blisterAmount 'Кол-во блистеров (в ед.)' = DATA INTEGER (MachineryPriceTransaction, Barcode, Batch);
blisterAmount(MachineryPriceTransaction t, Barcode barcode, Batch batch) => in(t, barcode, batch) RESOLVE RIGHT;
prevBlisterAmount 'Кол-во блистеров (в ед.) (тек.)' (Batch batch, Barcode barcode, GroupMachinery groupMachinery)  =
    blisterAmount(prevLastFullTransaction(batch, barcode, groupMachinery), barcode, batch);

snapshotAttribute(MachineryPriceTransaction t, GroupMachinery groupMachinery) +
    {  blisterAmount(t, Barcode barcode, Batch batch) <- prevBlisterAmount(batch, barcode, groupMachinery) WHERE in(t, barcode, batch); }

EXTEND FORM machineryPriceTransaction PROPERTIES(t, b, batch) blisterAmount;
EXTEND FORM machineryPriceTransactions PROPERTIES(t, b, batch) READONLY blisterAmount;
EXTEND FORM machineryPriceTransactions PROPERTIES(mt, bb, bbatch) READONLY  blisterAmount;
overBlisterAmount = ABSTRACT INTEGER (MachineryPriceTransaction, Barcode, Batch);
createAttribute(MachineryPriceTransaction t) + {blisterAmount(t, Barcode barcode, Batch batch) <- overBlisterAmount(t, barcode, batch) WHERE in(t, barcode, batch);}

//Флаги
flag 'Флаги' = DATA INTEGER (MachineryPriceTransaction, Barcode, Batch);
flag(MachineryPriceTransaction t, Barcode barcode, Batch batch) => in(t, barcode, batch) RESOLVE RIGHT;
prevTransactionFlag 'Флаги (тек.)' (Batch batch, Barcode barcode, GroupMachinery groupMachinery)  =
    flag(prevLastFullTransaction(batch, barcode, groupMachinery), barcode, batch);

snapshotAttribute(MachineryPriceTransaction t, GroupMachinery groupMachinery) +
    {  flag(t, Barcode barcode, Batch batch) <- prevTransactionFlag(batch, barcode, groupMachinery) WHERE in(t, barcode, batch); }

EXTEND FORM machineryPriceTransaction PROPERTIES(t, b, batch) flag;
EXTEND FORM machineryPriceTransactions PROPERTIES(t, b, batch) READONLY flag;
EXTEND FORM machineryPriceTransactions PROPERTIES(mt, bb, bbatch) READONLY  flag;
overFlag = ABSTRACT INTEGER (MachineryPriceTransaction, Barcode, Batch);
createAttribute(MachineryPriceTransaction t) + {flag(t, Barcode barcode, Batch batch) <- overFlag(t, barcode, batch) WHERE in(t, barcode, batch);}
MODULE LabelBarcodePrint;

REQUIRE Label, PurchaseShipmentLabel;

NAMESPACE Label;

FORM labelBarcodePrint 'Печать ценников'
    OBJECTS dt = LabelTransactionDetail LAST 
;

consumedChangeBarcodePrint = DATA LOCAL NESTED BOOLEAN();
overChangeBarcodePrint  ABSTRACT LIST ( STRING[15], DepartmentStore);

changeBarcodePrint(DepartmentStore d)  { 
    INPUT sb = STRING[15] DO {
        LOCAL dialogBarcodeSku = Sku();
        dialogBarcodeSku() <- skuBarcode(sb, currentDate());
        
        consumedChangeBarcodePrint() <- NULL;
        overChangeBarcodePrint(sb, d);
        IF NOT consumedChangeBarcodePrint() THEN {
            IF dialogBarcodeSku() IS Sku THEN {
                IF active(dialogBarcodeSku()) THEN {
                    select(Sku s) <- s == dialogBarcodeSku();
                    overQuantityPrint(Sku s) <- 1 IF s == dialogBarcodeSku();
                    labelPrintAuto() <- TRUE;
                    printSelectedSkuLabelTransaction(d);
                } ELSE {
                    MESSAGE CONCAT ' ', name(dialogBarcodeSku()), '(' + sb + ')', ' запрещен к продаже';
                }
                
            } ELSE
                MESSAGE CONCAT ' ', 'Не найден штрихкод', sb;
        }
        
        SEEK LAST labelBarcodePrint.dt;
    }
}

EXTEND FORM labelBarcodePrint 
    OBJECTS d = DepartmentStore BEFORE dt PANEL
    PROPERTIES name(d) SELECTOR
    
    OBJECTS b = STRING[15] PANEL
    PROPERTIES(b) barcode = VALUE ON CHANGE changeBarcodePrint(d) EVENTID 'SCANNER'
    
    PROPERTIES(dt) READONLY nameLabelType, date, time, idBarcode, nameSku, nameBatch, quantity, 
                   name, nameCurrency, price, retailPrice, discountSum, nameSkuGroupSku, shortNameUOMSku, nameCountrySku, residentCountrySku, nameSupplierSku, 
                   fullNameSupplierSku, shortNameOwnershipSupplierSku, nameAddressPhoneLegalEntity
    FILTERS departmentStore(dt) == d
;
@extendFormFilterStockAccess(d, labelBarcodePrint);

select 'Отм.' = DATA LOCAL NESTED BOOLEAN (Purchase.InvoiceDetail);

createInvoiceLabelTransaction 'Распечатать ценники'(Purchase.Invoice invoice)  { 
	NEWSESSION {        
	    
	    ASK 'Данный документ не проведен или дата документа не задана или больше текущей. Цены из него не будут учтены. Продолжить?' IF isDraft(invoice) OR NOT (dateTime(invoice) <= currentDateTime()) DO { 
	        LOCAL in = BOOLEAN (Sku);
	        IF [GROUP SUM 1 IF select(Purchase.InvoiceDetail detail) BY invoice(detail)](invoice) THEN {
	            FOR select(Purchase.InvoiceDetail detail) AND invoice(detail) DO {
	                in(Sku sku) <- TRUE IF select(detail) AND sku(detail) == sku;        
	            }
	        } ELSE  {
	            in(Sku sku) <- TRUE IF countInvoiceDetail(sku, invoice);        
	        } 
	    
	        // бежим по всем типам ценников из управленческих параметров
	        // для каждого создаем по операции печати ценников
	        FOR ([ GROUP SUM 1 IF in(Sku sku) AND inPrint(LabelType labelType, sku, DepartmentStore departmentStore) BY labelType, departmentStore](LabelType labelType, customerStock(invoice)))
	            NEW t = LabelTransaction DO {
	                departmentStore(t) <- customerStock(invoice);
	                priceTransactionDocument(t) <- invoice;
	                labelType(t) <- labelType;
	                
	                FOR invoice(Purchase.InvoiceDetail dd) == invoice AND 
	                    in(Sku sku) AND sku == sku(dd) AND 
	                    inPrint(labelType, sku, customerStock(invoice)) AND 
	                    PriceListType pt == overPriceListType(t) NOINLINE (pt)
	                    NEW d = LabelTransactionDetail DO {
	                        labelTransaction(d) <- t;
	                        sku(d) <- sku;
	                        batch(d) <- overBatch(dd) WHERE explicitBatchLedger(departmentStore(t));
	    
	                        name(d) <- IF batch(d) THEN documentNameSku(batch(d)) ELSE name(sku(d));
	                        createAttribute(d);
	    
	                        quantity(d) <- IF include(labelType, sku(d))
	                                                                THEN INTEGER(shipmentQuantity(dd))
	                                                                ELSE count(labelType, sku(d));
	    
	                        price(d) <- calcPrice(pt, d);
	                        retailPrice(d) <- overRetailPrice(d);
	                        createAttribute(dd, d);
	                }
	                DELETE LabelTransactionDetail d WHERE labelTransaction(d) == t AND NOT price(d) AND NOT printNullPrice(d);
	                
	                IF notCheckEqualRetailPrice() THEN
	                    DELETE LabelTransactionDetail d WHERE labelTransaction(d) == t AND NOT retailPrice(d) AND notPrintRetailPrice(d) AND NOT skipDelete(d);
	                ELSE
	                    DELETE LabelTransactionDetail d WHERE labelTransaction(d) == t AND NOT discountPercent(d) > 0 AND notPrintRetailPrice(d) AND NOT skipDelete(d);
	                
	                DELETE LabelTransactionDetail d WHERE labelTransaction(d) == t AND passScales(sku(d)) AND notPrintNetItem(d);
	                    
	                print(t);
	        }
	        printed(invoice) <- TRUE WHERE isPosted(invoice);
	        apply();
	    }
	}
} TOOLBAR CONFIRM;

createSaleLabelTransactionMothercare 'Распечатать ценники' (Purchase.Invoice invoice)  { 
    createInvoiceLabelTransaction(invoice);
    select(Purchase.InvoiceDetail detail) <- NULL WHERE invoice(detail)==invoice;
} TOOLBAR;

EXTEND FORM labelBarcodePrint

    OBJECTS date = DATE PANEL
    PROPERTIES(date) date = VALUE

    OBJECTS i = Purchase.Invoice
    PROPERTIES (i) READONLY isPosted, statusLabel  BACKGROUND backgroundPrintLabelTransaction(i), number, 
                   series, date, nameSupplier, nameSupplierStock, nameCustomer,
                   nameCustomerStock, nameCurrency, countInvoiceDetail, quantityInvoiceDetail, 
                   sumInvoiceDetail, VATSumInvoiceDetail, invoiceSumInvoiceDetail, note  

    PROPERTIES(i)  createInvoiceLabelTransaction SHOWIF showToPrintLabelTransaction(i),
                   createSnapshotLabelTransaction                                                       

    FILTERS isPosted(i),
            isCompany(customerStock(i)),
            isCompany(supplierStock(i)),
            isCompany(customer(i)),
            isCompany(supplier(i))

    FILTERGROUP filters0
        FILTER 'Для печати' showToPrintLabelTransaction(i) 'F11' DEFAULT            

    OBJECTS id = Purchase.InvoiceDetail
    PROPERTIES (id) select
    PROPERTIES (id) READONLY index, idBarcodeSku, nameSku, shortNameUOMSku
    PROPERTIES (id) READONLY quantity, price, sum,
                   valueVAT, VATSum, invoiceSum        
    FILTERS invoice(id)==i
;    

DESIGN labelBarcodePrint {
    OBJECTS {
        NEW headTab {
            fill = 1;
            type = TABBED; 
            NEW printBarcode {
                fill = 1;
                caption = 'Печать по штрих-коду';
                type = CONTAINERV;
                NEW headBarcode {
                    type = CONTAINERH;
                    MOVE PROPERTY(barcode) { caption = 'Ввести штрихкод'; panelCaptionVertical = TRUE; focusable = FALSE;  font = 'bold 22'; changeKey = 'F4'; } 
                    MOVE BOX(d);   
                }
                NEW detailBarcode {
                    fill = 1;
                    MOVE BOX(dt);
                }
            }
            NEW printInvoice {
                fill = 1;
                type = SPLITV;
                caption = 'Печать по накладной'; 
                MOVE BOX(i);
                MOVE BOX(id);   
            }
        }
    }
}

NAVIGATOR {
    retailDashboardNavigator{
        NEW labelBarcodePrint;
    }
}

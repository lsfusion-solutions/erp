MODULE PriceListLabel;

REQUIRE PriceList, Label, PriceListBefore;

NAMESPACE PriceList;

@Label.implementPriceTransactionDocument(PriceList);

printLabel 'Использовать для печати ценников' = DATA BOOLEAN (DataPriceListType, Operation);

EXTEND FORM operation
    PROPERTIES(pt,o) printLabel DRAW pt AFTER changeBefore(pt,o)
;

printLabelPriceListType (Operation o) = GROUP MIN DataPriceListType t IF printLabel(t,o);


FORM selectDepartmentStore 'Выбор отделов магазина для печати'
    OBJECTS p = PriceList PANEL 
    
    OBJECTS d = DepartmentStore 
    PROPERTIES READONLY id[Stock](d) SHOWIF showIDs(), name(d) 
    FILTERS in(p,d)
    FILTERGROUP inactiveStock 
        FILTER 'Активный' active(d) 'F10' DEFAULT    
;

createPriceListLabel 'Распечатать ценники'(PriceTransactionDocument priceList)  { 
	NEWSESSION {
	    DIALOG selectDepartmentStore OBJECTS p = priceList, d INPUT DO {
	        select(Sku sku, LabelType labelType, d) <- countPriceListDetail(sku,priceList) AND inPrint(labelType, sku, d);   
	        localPriceListType() <- printLabelPriceListType(operation[PriceList](priceList));
	        createLabelTransaction(d);  
	        priceTransactionDocument(LabelTransaction t) <- priceList WHERE t == addedTransaction();
	        
	        apply();
	    }
	}
} TOOLBAR;

select 'Вкл.' = DATA LOCAL BOOLEAN (LabelType, DepartmentStore);
FORM selectDepartmentStoreLabelType 'Выбор магазина и ценника для печати'
    OBJECTS p = PriceList PANEL 
    
    OBJECTS d = DepartmentStore 
    PROPERTIES READONLY id[Stock](d) SHOWIF showIDs(), name(d) 
    FILTERS in(p,d)
    PROPERTIES change(d)
    FILTERGROUP inactiveStock 
        FILTER 'Активный' active(d) 'F10' DEFAULT    
        
    
    OBJECTS l = LabelType GRID
    PROPERTIES(l,d) select
    PROPERTIES(l) READONLY id, name, width, height, nameOrientation, fileTemplate,
                  namePriceListType, nameGroupType, printNullPrice, notPrintRetailPrice, notPrintNetItem
    FILTERS in(l,d) 
    FILTERGROUP inactive FILTER 'Активные' active(l) 'F10' DEFAULT
;

createSelectedSkuStockSnapshotLabel 'Перепечатать ценники'(PriceTransactionDocument priceList)  {    
    DIALOG selectDepartmentStoreLabelType OBJECTS p = priceList, d INPUT, l INPUT DO {
        select(Sku sku) <- TRUE IF countPriceListDetail(sku,priceList);  
        IF NOT (GROUP SUM 1 IF select(LabelType type, d)) THEN {
            select(l, d) <- TRUE;
        }         
        FOR select(LabelType type, d) DO {
            localPriceListType() <- printLabelPriceListType(operation[PriceList](priceList));
            createSnapshotLabelTransactionDepartmentStore(d, type);   
            priceTransactionDocument(LabelTransaction t) <- priceList WHERE t == addedTransaction();
        } 
        apply();
    }
} TOOLBAR;


priceDoc = DATA LOCAL UserPriceList ();

createSelectedSkuStockSnapshotPriceList 'Перепечатать ценники (цены из прайса)'(PriceTransactionDocument priceList)  {    
    DIALOG selectDepartmentStoreLabelType OBJECTS p = priceList, d INPUT, l INPUT DO { 
        select(Sku sku) <- TRUE IF countPriceListDetail(sku,priceList);
        priceDoc() <- priceList; 
        IF NOT (GROUP SUM 1 IF select(LabelType type, d)) THEN {
            select(l, d) <- TRUE;
        }  
        FOR select(LabelType type, d) DO {
            localPriceListType() <- printLabelPriceListType(operation[PriceList](priceList));
            createSnapshotLabelTransactionDepartmentStore(d, type);   
            priceTransactionDocument(LabelTransaction t) <- priceList WHERE t == addedTransaction();
        } 
        apply();
    }
} TOOLBAR;

defaultDataPriceListType (UserPriceList p) = GROUP MAX DataPriceListType type IF in( p, type);
overCreate(LabelTransaction t) + { 
    IF  priceDoc() THEN {        
        price(LabelTransactionDetail d) <- price(userPriceListDetail(priceDoc(),sku(d)), overPriceListType(t) AS DataPriceListType) WHERE labelTransaction(d) == t;
        retailPrice(LabelTransactionDetail d) <- priceBefore(userPriceListDetail(priceDoc(),sku(d))) WHERE labelTransaction(d) == t;                
    }   
}

EXTEND FORM priceLists
    PROPERTIES(p)   createPriceListLabel, createSelectedSkuStockSnapshotLabel, createSelectedSkuStockSnapshotPriceList 
;
DESIGN priceLists {
    actionContainer {
        NEW priceContainer {
            caption = 'Ценники';                     
            type = CONTAINERV;
            MOVE PROPERTY (createPriceListLabel(p));
            MOVE PROPERTY (createSelectedSkuStockSnapshotLabel(p));
            MOVE PROPERTY (createSelectedSkuStockSnapshotPriceList(p));
        }
    }
}

overFillDocument  ABSTRACT ( LabelTransactionDetail, DocumentDetail); 
fillDocument 'Подбор документа'(LabelTransaction labelTransaction)  { 
    DIALOG documents DO {
        FOR inSession(document(DocumentDetail documentDetail)) NEW i = LabelTransactionDetail DO {
            labelTransaction(i) <- labelTransaction;
            sku(i) <- sku(documentDetail);
            name(i) <- name(sku(i));
            createAttribute(i);        
            overFillDocument(i, documentDetail);
        }
    }
} TOOLBAR;

overFillDocument (LabelTransactionDetail i, DocumentDetail d) + { 
    batch(i) <- batch(d);
}

EXTEND FORM customLabelTransaction
    PROPERTIES (l) DRAW d fillDocument  
;


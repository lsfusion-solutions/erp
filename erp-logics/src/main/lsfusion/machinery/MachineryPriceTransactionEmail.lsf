MODULE MachineryPriceTransactionEmail;

REQUIRE MachineryPriceTransaction;

NAMESPACE Machinery;


inDow 'Рабочий день' = DATA BOOLEAN (Employee, DOW);

EXTEND FORM employee
    OBJECTS dow = DOW
    PROPERTIES READONLY staticCaption(dow), number(dow), inDow(e,dow) CHANGEABLE 
    ORDERS number(dow)
;
DESIGN employee {
    MOVE BOX(dow) BEFORE TOOLBARBOX;
}


inDow (Employee employee, DATETIME dateTime)=inDow(employee, extractDOW(toDate(dateTime)));


countMinutesMachineryPriceTransaction 'Количество минут для проверки незагруженных в оборудование документов' = DATA LONG ();
EXTEND FORM options
    PROPERTIES countMinutesMachineryPriceTransaction() 
;

DESIGN options {
    machinery {
        MOVE PROPERTY(countMinutesMachineryPriceTransaction());                
    }
}

subtractMinutes(DATETIME dateTime, LONG mins) = [ FORMULA DATETIME PG '(($1)-($2)*CAST(\'1 minutes\' AS INTERVAL))', MS 'DATEADD(mm, $2, $1)'](dateTime AS DATETIME, mins AS LONG);

includeEmail (MachineryPriceTransaction tt, DATETIME dateTime)  = subtractMinutes(dateTime, countMinutesMachineryPriceTransaction())<= dateTime(tt) 
    IF dateTime >= dateTime(tt)
    AND  (NOT succeeded(tt) OR (countSucceededMachineries(tt) != countMachineries(groupMachinery(tt))));

    
countIncludeEmailMachineryPriceTransactions (DATETIME dateTime) = GROUP SUM 1 IF includeEmail (MachineryPriceTransaction tt, dateTime) AND inDow(Employee user, dateTime);

FORM emailMachineryPriceTransaction 'Загрузки в оборудование'
    
    OBJECTS dt = DATETIME PANEL 

    OBJECTS tt = MachineryPriceTransaction
    PROPERTIES(tt) READONLY  snapshot, nameGroupMachinery, date, 
                            time, description, succeeded, 
                            dateTimeSucceeded, canceled, 
                            quantityMachineryPriceTransactionError BACKGROUND quantityMachineryPriceTransactionError(tt),
                            npps PANEL SHOWIF snapshot(tt)
    PROPERTIES(tt) READONLY  createdNameUser, createdHostnameComputer

    FILTERS includeEmail(tt,dt) 
;

emailNotSucceededMachineryPriceTransaction 'Выслать уведомление о незагруженных документах в оборудование' (DATETIME dateTime, Contact user)  {
    LOCAL bodyFile = FILE (); 
    PRINT emailMachineryPriceTransaction OBJECTS dt = dateTime AS DATETIME HTML TO bodyFile;
    EMAIL
    SUBJECT 'Уведомление о незагруженных документах в оборудование'
    TO email(user)
    BODY bodyFile()
; }

emailNotSucceededMachineryPriceTransaction() { 
    IF countIncludeEmailMachineryPriceTransactions(currentDateTime()) THEN
        FOR inDow(Employee user,currentDateTime()) DO {
            emailNotSucceededMachineryPriceTransaction(currentDateTime(), user); 
        }    
}
MODULE Ukm5PickList;

REQUIRE Ukm5PriceTransaction, PickList, ExportCSO;

NAMESPACE Ukm5;

pickListGroupType() = DATA CustomGroupType ();
namePickListGroupType 'Классификатор пик-листа' = name(pickListGroupType());

pickListGroup(Group g) = IF groupType(g) = pickListGroupType() THEN TRUE;

idExt (Item i) = '"' + idBarcode(i) +'"';
productsByGroup = ABSTRACT STRING (Group, DepartmentStore);

idPickList = ABSTRACT INTEGER (CustomGroup, DepartmentStore);
idPickList = ABSTRACT INTEGER (CustomGroup);
changedImage = DATA BOOLEAN (CustomGroup, DepartmentStore);

WHEN SETCHANGED(changedImage(CustomGroup g)) AND g IS CustomGroup DO {
    changedImage(g, DepartmentStore s) <- TRUE;
}

GROUP stores;

inStore (Group g, DepartmentStore d) = GROUP SUM 1 IF
    customGroup[Stock.CustomGroupType,Item.Item](pickListGroupType(), Item i) = g AND
        currentBalance(i, d) > 0 AND (passScales(i) OR split(i))
;

FORM pickList FORMEXTID 'null'
    OBJECTS element = (elements = CustomGroup, stores = DepartmentStore) EXTID 'elements'

    PROPERTIES idPickList(elements, stores) EXTID 'id', = regexpReplace(name(elements), '^\\s*\\d+\\s*', '', '') EXTID 'name', = TFALSE IF elements IS CustomGroup EXTID 'isGlobal',
        = TFALSE IF elements IS CustomGroup EXTID 'delete', productsByGroup(elements, stores) EXTID 'items'
    FILTERS pickListGroup(elements) AND level(elements) > 1
    
    OBJECTS s = DepartmentStore EXTID 'stores'
    PROPERTIES = idStore(s) IF elements IS CustomGroup AND s IS DepartmentStore EXTID 'id'
    FILTERS inStore(elements, stores)
    FILTERS s = stores
    FILTERS hasUkm5GroupMachinery(stores)
;

exportPickList 'Экспорт пик-листа УКМ5'(DepartmentStore d){
    NEWSESSION {
        LOCAL answer = FILE ();
        Stock.in(d) <- TRUE;
        EXPORT pickList OBJECTS stores = d JSON;
        LOCAL group = GroupMachinery();
        fileToString(exportFile());
        logToFile('ukm5', resultString());
        group() <- GROUP MAX GroupCashRegister g IF stock(g) = d AND isUkm5(g);
        TRY{
            EXTERNAL HTTP POST urlUkm5ServerImport() + '/api/v1/import/picklists' PARAMS exportFile() TO answer;
            fileToString(answer());
            IF statusHttp() = 200 THEN MESSAGE 'Загрузка пик-листа успешно завершена\nСклад: ' + name(d);
        }
        CATCH MESSAGE messageCaughtException();
    }
}

EXTEND FORM integrationData
     PROPERTIES exportPickList(cs);
     
DESIGN integrationData{
    kso{
        MOVE PROPERTY (exportPickList(cs));
    }
}

//выгрузка изображений
GROUP images;

ftpConnectionStringUkm 'Строка подключения (ftp://username:password@host:port/path/)' = DATA STRING[250]();

sendImageItemToFtp(Item i){
    IF extension(FILE(dataImage(i))) != 'png' THEN {
        convertImage(dataImage(i), 'png');
        WRITE convertedImage() TO CONCAT '', ftpConnectionStringUkm(), 'items/', idBarcode(i);
    } ELSE {
        WRITE dataImage(i) TO CONCAT '', ftpConnectionStringUkm(), 'items/', idBarcode(i);
    }
}

sendImageGroupToFtp(CustomGroup c, DepartmentStore d){
    IF extension(FILE(image(c))) != 'png' THEN {
        convertImage(image(c), 'png');
        WRITE convertedImage() TO CONCAT '', ftpConnectionStringUkm(), 'groups/', idPickList(c, d);
    } ELSE {
        WRITE image(c) TO CONCAT '', ftpConnectionStringUkm(), 'groups/', idPickList(c, d);
    }
}

//задание для планировщика
sendUkmImageToFTP 'Выгрузка изображений на FTP'(){
    LOCAL errorsImagesItems = BOOLEAN (Item);
    LOCAL errorsImagesGroups = BOOLEAN (CustomGroup);
    FOR customGroup[Stock.CustomGroupType,Item.Item](pickListGroupType(), Item i) 
        AND dataImage(i) AND active(i) AND (changedImage(i) OR NOT loadOnlyChangedImages())  DO {
        TRY {
            sendImageItemToFtp(i);
            changedImage(i) <- NULL;
        }
        CATCH {
            errorsImagesItems(i) <- TRUE;
        }
    }
    FOR idPickList(CustomGroup c, DepartmentStore d) AND hasImage(c) IF pickListGroup(c) AND active(c) AND hasUkm5GroupMachinery(d) AND (changedImage(c, d) OR NOT loadOnlyChangedImages()) DO
        TRY {
            sendImageGroupToFtp(c, d);
            changedImage(c, d) <- NULL;
        }
            CATCH {
            errorsImagesGroups(c) <- TRUE;
        }
    IF (GROUP SUM 1 IF errorsImagesItems(Item i)) THEN {
        MESSAGE 'Изображения не выгружены для ТОВАРОВ:\n' +
            (GROUP CONCAT name(Item i) IF errorsImagesItems(i), '\n' ORDER i) NOWAIT;
    }

    IF (GROUP SUM 1 IF errorsImagesGroups(CustomGroup c)) THEN {
        MESSAGE 'Изображения не выгружены для ТОВАРНЫХ ГРУПП:\n' +
            (GROUP CONCAT name(CustomGroup c) IF errorsImagesGroups(c), '\n' ORDER c) NOWAIT;
    }
    APPLY;
}

EXTEND FORM integrationData
    PROPERTIES ftpConnectionStringUkm(), sendUkmImageToFTP(), namePickListGroupType();

DESIGN integrationData{
    ukm5{
        NEW pickListUkm5{
            caption = 'Пиклист';
            MOVE PROPERTY (namePickListGroupType());
            MOVE PROPERTY (ftpConnectionStringUkm());
            MOVE PROPERTY (sendUkmImageToFTP());
        }
    }
}
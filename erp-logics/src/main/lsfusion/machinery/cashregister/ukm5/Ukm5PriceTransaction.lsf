MODULE Ukm5PriceTransaction;

REQUIRE Item, CashRegisterPriceTransaction, NativeMachinery, LogTools, MachineryPriceTransactionLotBy, StopList, ItemSSC;

NAMESPACE Ukm5;

//кассы

isUkm5 (GroupCashRegister g) = TRUE IF handlerModel(g) = 'lsf.ukm5';
idStore 'ID магазина UKM5' = ABSTRACT STRING (DepartmentStore);
idStore 'ID магазина UKM5' (GroupCashRegister g) = idStore(stock(g));
urlServerUkm5 'URL сервера УКМ5' = DATA STRING[300] (GroupCashRegister);
directoryExchange 'Директория обмена УКМ5' = DATA STRING[300] (GroupCashRegister);
accepted(STRING result) = TRUE IF result = 'Accepted to processing\r\n';
hasUkm5GroupMachinery (Stock s) = GROUP SUM 1 IF stock(GroupMachinery g) = s AND handlerModel(g) = 'lsf.ukm5';
urlUkm5ServerImport 'Url сервера(импорт)' = DATA STRING ();
urlUkm5ServerExport 'Url сервера(экспорт)' = DATA STRING ();

uploadedUkm5 'Загружен в УКМ5' = DATA BOOLEAN (Sku);
uploadedUkm5 'Загружен в УКМ5' = DATA BOOLEAN (Group);
uploadedUkm5Properties 'Загружен в УКМ5' = DATA BOOLEAN (Sku);
uploadedUkm5Scale 'Загружен в УКМ5' = DATA BOOLEAN (Sku);

EXTEND FORM groupCashRegister
    PROPERTIES (grc) SHOWIF isUkm5(grc) idStore, urlServerUkm5, directoryExchange;

DESIGN groupCashRegister{
    topContainer{
        MOVE PROPERTY (idStore(grc));
        MOVE PROPERTY (urlServerUkm5(grc));
        MOVE PROPERTY (directoryExchange(grc));
    }
}

//атрибуты
@defineStaticObject(Attribut, );

EXTEND CLASS Attribut{
    alcohol 'alcohol', 
    tobacco 'tobacco', 
    egais 'egais', 
    markedTobacco 'markedTobacco', 
    markedGoods 'markedGoods', 
    advance 'advance', 
    noSellerRequired 'noSellerRequired', 
    service 'service'
}

skuTransaction(Sku s, MachineryPriceTransaction t) = GROUP LAST Barcode b ORDER b
    WHERE (in(t,b) AND sku(b) == s);

attributes = ABSTRACT STRING (Sku);

filterAttribut = ABSTRACT CASE BOOLEAN (Item, Attribut);
isCatchWeight (Sku i) = IF (id(UOM(i)) = 'кг.') THEN TRUE;

FORM importItems 'Импорт товаров'
    OBJECTS t = MachineryPriceTransaction
    
    OBJECTS elements = Item
    PROPERTIES = IF elements IS Item THEN 1 EXTID 'taxgroupId', = id(itemGroup(elements)) EXTID 'itemGroup',
         = OVERRIDE shortNameUOM(elements), '' EXTID 'measure', = OVERRIDE name(elements), '' EXTID 'name',
         = OVERRIDE idBarcode(elements), '' EXTID 'id', = IF active(elements) THEN TFALSE ELSE TTRUE EXTID 'delete', 
         = IF split(elements) OR isCatchWeight(elements) THEN 3 ELSE 0 EXTID 'measprec', 
         = IF elements IS Item THEN ' ' EXTID 'descr'
    
    FILTERS skuTransaction(elements, t) AND NOT uploadedUkm5(elements)
    
    OBJECTS barcodes = Barcode
    PROPERTIES id(barcodes) EXTID 'id', = IF isCatchWeight(sku(barcodes)) THEN 0.000 ELSE amount(barcodes) EXTID 'quantity'
    FILTERS in(t,barcodes) AND sku(barcodes) = elements
    
    OBJECTS a = Attribut EXTID 'attributes'
    PROPERTIES = name(a) EXTID 'id'
    FILTERS filterAttribut(elements, a);
;
            
owner = DATA LOCAL STRING(INTEGER);

attributes (ItemGroup g) = IF g IS ItemGroup THEN '[]';

skuGroupTransaction(SkuGroup sc, MachineryPriceTransaction t) = GROUP LAST Barcode b ORDER b
    WHERE (in(t,b) AND skuGroup(t,b) == sc) OR (level(skuGroup(t,b), sc));

FORM groupItems
    OBJECTS t = MachineryPriceTransaction EXTID ''
    
    OBJECTS elements = ItemGroup
    PROPERTIES = OVERRIDE id(parent(elements)), '0' EXTID 'owner', = OVERRIDE name(elements) EXTID 'name',
        = OVERRIDE id(elements) EXTID 'id', = IF NOT active(elements) THEN TTRUE ELSE TFALSE EXTID 'delete', 
        = attributes(elements) 
    
    FILTERS skuGroupTransaction(elements, t) AND NOT uploadedUkm5(elements)
;

FORM barcodePrices 'Импорт цен, назначенных на штрихкоды товара'
    OBJECTS t = MachineryPriceTransaction
    
    OBJECTS elements = Barcode
    PROPERTIES dataMinPrice(t, elements) EXTID 'minprice', price(t, elements), 
        = IF t IS CashRegisterPriceTransaction AND elements IS Barcode THEN TFALSE EXTID 'isPromoPrice', 
        = IF NOT active(elements) THEN TTRUE ELSE TFALSE EXTID 'delete', 
        = IF elements IS Barcode THEN toChar(DATETIME('2020-01-01'), 'yyyy-MM-ddThh24:mi:ss') EXTID 'dateFrom',
        = IF elements IS Barcode THEN toChar(DATETIME('2099-01-01'), 'yyyy-MM-ddThh24:mi:ss') EXTID 'dateTo',
        = IF t IS CashRegisterPriceTransaction AND elements IS Barcode THEN idStore(groupMachinery(t)) EXTID 'storeId',
        = OVERRIDE id(elements), '' EXTID 'barcodeId'
        
    FILTERS in(t,elements) AND skuTransaction(sku(elements), t)
;

isPromo = ABSTRACT TBOOLEAN (MachineryPriceTransaction, Item);
minPrice = ABSTRACT NUMERIC[18,4] (MachineryPriceTransaction, Item);

FORM itemPrices 'Импорт цен на товары'
    OBJECTS t = MachineryPriceTransaction
    
    OBJECTS elements = Item
    PROPERTIES = OVERRIDE idBarcode(elements), '' EXTID 'itemId',
        = OVERRIDE isPromo(t, elements), TFALSE IF elements IS Item EXTID 'isPromoPrice',
        minPrice(t, elements) EXTID 'minprice', = price(t, barcode(elements)) EXTID 'price',
        = IF NOT active(elements) THEN TTRUE ELSE TFALSE EXTID 'delete', 
        = IF elements IS Item THEN toChar(DATETIME('2020-01-01'), 'yyyy-MM-ddThh24:mi:ss') EXTID 'dateFrom',
        = IF elements IS Item THEN toChar(DATETIME('2099-01-01'), 'yyyy-MM-ddThh24:mi:ss') EXTID 'dateTo'
    
    FILTERS skuTransaction(elements, t)
;

skipUkz = ABSTRACT BOOLEAN (Item);
valueProp = ABSTRACT CASE STRING (STRING, Item);
nameProp = ABSTRACT CASE STRING (STRING, Item);
dropProp = ABSTRACT CASE STRING (STRING, Item);

GTINUKM5(Item i) = OVERRIDE dataGTIN(barcode(i)), GTIN(barcode(i));
RBMark(Item i) =  UKZ(i) == TTRUE AND NOT skipUkz(i);

valueProp (STRING s, Item i) += WHEN s = 'GTIN' AND i IS Item THEN GTINUKM5(i);
valueProp (STRING s, Item i) += WHEN s = 'RB_MARK' AND i IS Item AND RBMark(i) THEN 'RB_MARK';
valueProp (STRING s, Item i) += WHEN s = 'quantity_by_hand' AND i IS Item AND requireQuantityManual(i) THEN 'quantity_by_hand';

nameProp (STRING s, Item i) += WHEN s = 'GTIN' AND i IS Item THEN '';
nameProp (STRING s, Item i) += WHEN s = 'RB_MARK' AND i IS Item AND UKZ(i) == TTRUE AND NOT skipUkz(i) THEN '';
nameProp (STRING s, Item i) += WHEN s = 'quantity_by_hand' AND i IS Item AND requireQuantityManual(i) THEN '';

droppedLoadedGTIN = DATA TBOOLEAN (Item);
droppedLoadedRBMark= DATA TBOOLEAN (Item);
droppedLoadedQuantityByHand = DATA TBOOLEAN (Item);

WHEN DROPPED(GTINUKM5(Item i)) AND droppedLoadedGTIN(i) == TFALSE DO {
    droppedLoadedGTIN(i) <- TTRUE;
}
WHEN DROPPED(RBMark(Item i)) AND droppedLoadedRBMark(i) == TFALSE DO {
    droppedLoadedRBMark(i) <- TTRUE;
}
WHEN DROPPED(requireQuantityManual(Item i)) AND droppedLoadedQuantityByHand(i) == TFALSE DO {
    droppedLoadedQuantityByHand(i) <- TTRUE;
}

needDropPropertiest(Item i) = droppedLoadedGTIN(i) == TTRUE OR droppedLoadedRBMark(i) == TTRUE OR droppedLoadedQuantityByHand(i) == TTRUE;

dropProp (STRING s, Item i) += WHEN s = 'GTIN' AND i IS Item AND droppedLoadedGTIN(i) == TTRUE THEN 'GTIN';
dropProp (STRING s, Item i) += WHEN s = 'RB_MARK' AND i IS Item AND droppedLoadedRBMark(i) == TTRUE  THEN 'RB_MARK';
dropProp (STRING s, Item i) += WHEN s = 'quantity_by_hand' AND i IS Item AND droppedLoadedQuantityByHand(i) == TTRUE  THEN 'quantity_by_hand';

FORM itemProperties 'Дополнительные свойства товаров'
    OBJECTS t = MachineryPriceTransaction

    OBJECTS elements = Item
    PROPERTIES = OVERRIDE idBarcode(elements), '' EXTID 'itemId',
               = IF active(elements) THEN TFALSE EXTID 'deleted'
    FILTERS skuTransaction(elements, t) AND NOT uploadedUkm5Properties(elements)

    OBJECTS s = STRING EXTID 'properties'
    PROPERTIES nameProp(s, elements) EXTID 'name',
        = s EXTID 'id',
        = TFALSE IF valueProp(s, elements) EXTID 'showToCashier',
        = TFALSE IF valueProp(s, elements) EXTID 'printOnReceipt'
    FILTERS valueProp(s, elements)

    OBJECTS v = STRING EXTID 'values'
    PROPERTIES = nameProp(v, elements) EXTID 'name'
    PROPERTIES = valueProp(v, elements) EXTID 'id'
    FILTERS v = s
;

FORM itemPropertiesDrop 'Дополнительные свойства товаров(удаление)'
    OBJECTS t = MachineryPriceTransaction

    OBJECTS elements = Item
    PROPERTIES = OVERRIDE idBarcode(elements), '' EXTID 'itemId',
               = IF active(elements) THEN TTRUE EXTID 'deleted'
    FILTERS skuTransaction(elements, t) AND needDropPropertiest(elements)

    OBJECTS s = STRING EXTID 'properties'
    PROPERTIES dropProp(s, elements) EXTID 'name',
        = s EXTID 'id',
        = TFALSE IF dropProp(s, elements) EXTID 'showToCashier',
        = TFALSE IF dropProp(s, elements) EXTID 'printOnReceipt'
    FILTERS dropProp(s, elements)

    OBJECTS v = STRING EXTID 'values'
    PROPERTIES = v EXTID 'name'
    PROPERTIES = dropProp(v, elements) EXTID 'id'
    FILTERS v = s
;

GROUP attributes;
GROUP weight;

referenceWeight = DATA NUMERIC[12,3] ();
overReferenceWeight(Sku s) = referenceWeight();

minDelta 'Дельта отклонения от эталонного веса в меньшую сторону' = DATA NUMERIC[12,3] ();
overMinDelta(Sku s) = IF split(s) OR isCatchWeight(s) THEN OVERRIDE minDelta(), 0.01;

maxDelta 'Дельта отклонения от эталонного веса в большую сторону' = DATA NUMERIC[12,3] ();
overmaxDelta(Sku s) = IF split(s) OR isCatchWeight(s) THEN OVERRIDE maxDelta(), 0.01;

weightSize (Sku s) = CASE 
    WHEN lowWeight(s) THEN 2
    WHEN weightControlBypass(s) THEN 3
    ELSE 0
;

FORM importSecurityScale 'Импорт эталонных весов КВП и работа с типами товаров'
    OBJECTS t = MachineryPriceTransaction

    OBJECTS elements = Item
    PROPERTIES = OVERRIDE idBarcode(elements), '' EXTID 'item_id',
        = OVERRIDE idBarcode(elements), '' EXTID 'barcode_id',
        = IF active(elements) THEN TFALSE ELSE TTRUE EXTID 'delete',
        = IF split(elements) OR isCatchWeight(elements) THEN 3 ELSE 0 EXTID 'measprec'
    
    PROPERTIES(elements) IN attributes weightSize EXTID 'weight_size_item_type'
    
    PROPERTIES(elements) IN weight
        overMinDelta EXTID 'min_delta' EXTNULL,
        overReferenceWeight EXTID 'reference_weight' EXTNULL, 
        overmaxDelta EXTID 'max_delta' EXTNULL

    FILTERS skuTransaction(elements, t) AND NOT uploadedUkm5Scale(elements)    
;

sendPriceTransactionAPI(CashRegisterPriceTransaction t){
    LOCAL answer = FILE ();
    IF (GROUP SUM 1 IF NOT uploadedUkm5(Group g) AND skuGroupTransaction(g, t)) THEN {
        EXPORT groupItems OBJECTS t = t JSON ;
        EXTERNAL HTTP POST urlUkm5ServerImport() + '/api/v1/import/groupItems' PARAMS exportFile() TO answer;
        fileToString(answer());
        IF statusHttp() = 200 THEN uploadedUkm5(Group g) <- TRUE IF skuGroupTransaction(g, t) AND NOT uploadedUkm5(g);
        ELSE {
            addMachineryError(t, (CONCAT ' : ', nameGroupMachinery(t),
                'Ошибка при импорте групп товаров', resultString()));
            logToFile('ukm5PriceTransaction', CONCAT ': ', 'Ошибка при импорте групп товаров', resultString());
            APPLY;
            RETURN;
        }
    }
    IF (GROUP SUM 1 IF NOT uploadedUkm5(Item i) AND skuTransaction(i, t)) THEN {
        EXPORT importItems OBJECTS t = t JSON;
        EXTERNAL HTTP POST urlUkm5ServerImport() + '/api/v1/import/items' PARAMS exportFile() TO answer;
        fileToString(answer());
        IF statusHttp() = 200 THEN uploadedUkm5(Item i) <- TRUE IF skuTransaction(i, t) AND NOT uploadedUkm5(i);
        ELSE {
            addMachineryError(t, (CONCAT ' : ' , nameGroupMachinery(t), 
                'Ошибка при импорте товаров', resultString()));
            logToFile('ukm5PriceTransaction', CONCAT ': ', 'Ошибка при импорте товаров', resultString());
            APPLY;
            RETURN;
        }
    }
    IF (GROUP SUM 1 IF NOT uploadedUkm5Properties(Item i) AND skuTransaction(i, t)) THEN {
        EXPORT itemProperties OBJECTS t = t JSON;
        EXTERNAL HTTP POST urlUkm5ServerImport() + '/api/v1/import/itemProperties' PARAMS exportFile() TO answer;
        fileToString(answer());
        IF statusHttp() = 200 THEN {
            droppedLoadedGTIN(Item i) <- TFALSE IF GTINUKM5(i) AND skuTransaction(i, t);
            droppedLoadedRBMark(Item i) <- TFALSE IF RBMark(i) AND skuTransaction(i, t);
            droppedLoadedQuantityByHand(Item i) <- TFALSE IF requireQuantityManual(i) AND skuTransaction(i, t);
            uploadedUkm5Properties(Item i) <- TRUE IF skuTransaction(i, t) AND NOT uploadedUkm5Properties(i);
        } ELSE {
            addMachineryError(t, (CONCAT ' : ' , nameGroupMachinery(t),
                'Ошибка при импорте свойств товаров', resultString()));
            logToFile('ukm5PriceTransaction', CONCAT ': ', 'Ошибка при импорте свойств товаров', resultString());
            APPLY;
            RETURN;
        }
    }
    IF (GROUP SUM 1 IF needDropPropertiest(Item i) AND skuTransaction(i, t)) THEN {
        EXPORT itemPropertiesDrop OBJECTS t = t JSON;
        EXTERNAL HTTP POST urlUkm5ServerImport() + '/api/v1/import/itemProperties' PARAMS exportFile() TO answer;
        fileToString(answer());
        IF statusHttp() = 200 THEN {
            droppedLoadedGTIN(Item i) <- NULL IF skuTransaction(i, t) AND droppedLoadedGTIN(i) == TTRUE;
            droppedLoadedRBMark(Item i) <- NULL IF skuTransaction(i, t) AND droppedLoadedGTIN(i) == TTRUE;
            droppedLoadedQuantityByHand(Item i) <- NULL IF skuTransaction(i, t) AND droppedLoadedGTIN(i) == TTRUE;
        } ELSE {
            addMachineryError(t, (CONCAT ' : ' , nameGroupMachinery(t),
                'Ошибка при импорте удалённых свойств товаров', resultString()));
            logToFile('ukm5PriceTransaction', CONCAT ': ', 'Ошибка при импорте удалённых свойств товаров', resultString());
            APPLY;
            RETURN;
        }
    }
    IF (GROUP SUM 1 IF NOT uploadedUkm5Scale(Item i) AND skuTransaction(i, t)) THEN {
        EXPORT importSecurityScale OBJECTS t = t JSON;
        EXTERNAL HTTP POST CONCAT '', urlUkm5ServerImport() + '/api/v1/import/store/',
            idStore(groupCashRegister(t)), '/securityScale' PARAMS exportFile() TO answer;
        fileToString(answer());
        IF statusHttp() = 200 THEN uploadedUkm5Scale(Item i) <- TRUE IF skuTransaction(i, t) AND NOT uploadedUkm5Scale(i);
        ELSE {
            addMachineryError(t, (CONCAT ' : ', nameGroupMachinery(t),
                'Ошибка при импорте эталонных весов КВП', resultString()));
            logToFile('ukm5PriceTransaction', CONCAT ': ', 'Ошибка при импорте эталонных весов КВП', resultString());
            APPLY ;
            RETURN;
        }
    }
    EXPORT itemPrices OBJECTS t = t JSON;
    EXTERNAL HTTP POST CONCAT '', urlUkm5ServerImport() + '/api/v1/import/store/',
        idStore(groupCashRegister(t)), '/itemPrices' PARAMS exportFile() TO answer;
    fileToString(answer());
    IF statusHttp() = 200 THEN {
        succeeded(t) <- TRUE;
        dateTimeSucceeded(t) <- currentDateTime();
        APPLY;
    } ELSE {
        addMachineryError(t, (CONCAT ' : ', nameGroupMachinery(t),
            'Ошибка при импорте прайса', resultString()));
        logToFile('ukm5PriceTransaction', CONCAT ': ', 'Ошибка при импорте прайса', resultString());
        APPLY ;
        RETURN;
    }
}

isPromo = ABSTRACT TBOOLEAN (Stock, Item);
minTransactionPrice 'Текущая минимальня цена в оборудовании' (Barcode barcode, GroupMachinery groupMachinery) =
    minPrice(lastTransaction(barcode, groupMachinery), barcode) MATERIALIZED;

minTransactionPrice 'Текущая минимальня цена в оборудовании' (Barcode barcode, stock) =
    GROUP LAST minTransactionPrice(barcode, GroupMachinery g)
    ORDER dateTimeIn(lastTransaction(barcode, g), barcode), lastTransaction(barcode, g)
    WHERE lastTransaction(barcode, g)
    BY stock(g)
;

succeededNative 'Загружен в оборудование (Native)' = DATA BOOLEAN (Stock, StopList);
skuTransaction(Sku s, Stock st) = GROUP SUM 1 IF sku(st, StopListDetail d) = s AND NOT succeededNative(st, stopList(d))
    AND stock(GroupMachinery g) = st AND handlerModel(g) = 'lsf.ukm5' AND active(g) AND date(stopList(d)) > startDate(g);
include(Sku s, Stock st) = GROUP SUM 1 IF include(st, StopListDetail d) AND sku(st, d) = s AND NOT succeededNative(st, stopList(d));

EXTEND FORM stopList
    PROPERTIES(ts, sl) succeededNative LAST
;

FORM stopListImport 'Импорт стоп-листа'
    OBJECTS s = Stock

    OBJECTS elements = Item
    PROPERTIES = OVERRIDE idBarcode(elements), '' EXTID 'itemId',
        = OVERRIDE isPromo(s, elements), TFALSE IF elements IS Item EXTID 'isPromoPrice',
        = IF NOT active(elements) OR include(elements, s) THEN 0 ELSE minTransactionPrice(Barcode.barcode(elements), s) EXTID 'minprice',
        = IF NOT active(elements) OR include(elements, s) THEN 0 ELSE transactionPrice(Barcode.barcode(elements), s) EXTID 'price',
        = IF NOT active(elements) OR include(elements, s) THEN TTRUE ELSE TFALSE EXTID 'delete',
        = IF elements IS Item THEN toChar(DATETIME('2020-01-01'), 'yyyy-MM-ddThh24:mi:ss') EXTID 'dateFrom',
        = IF elements IS Item THEN toChar(DATETIME('2099-01-01'), 'yyyy-MM-ddThh24:mi:ss') EXTID 'dateTo'

    FILTERS skuTransaction(elements, s)
;

stopListImport 'Импорт стоп-листа (склад)' (Stock s){
    LOCAL answer = FILE ();
    EXPORT stopListImport OBJECTS s = s JSON;
    EXTERNAL HTTP POST CONCAT '', urlUkm5ServerImport() + '/api/v1/import/store/',
        idStore(groupCashRegister(s)), '/itemPrices' PARAMS exportFile() TO answer;
    fileToString(answer());
    IF statusHttp() = 200 THEN {
        succeededNative(s, StopList sl) <- TRUE;
        APPLY;
    } ELSE {
        logToFile('ukm5PriceTransaction', CONCAT ': ', 'Ошибка при импорте стоп-листа', resultString());
        APPLY;
        RETURN;
    }
}

stopListImport 'Импорт стоп-листа' () {
    FOR hasUkm5GroupMachinery(Stock st) AND skuTransaction(Sku s, st) DO {
        stopListImport(st);
    }
}

//WHEN ([GROUP SUM 1 IF CHANGED(name(Sku s)) OR CHANGED(itemGroup(s)) OR CHANGED(shortNameUOM(s)) OR CHANGED(idBarcode(s))
//    OR CHANGED(active(s)) OR CHANGED(filterAttribut(s, Attribut a)) BY s](Sku sku)) AND uploadedUkm5(sku) DO {
//    uploadedUkm5(sku) <- NULL;
//}
//
//WHEN ([GROUP SUM 1 IF CHANGED(id(Barcode b) OR CHANGED(amount(b))) BY b](Barcode barcode)) AND uploadedUkm5(sku(barcode)) DO {
//    uploadedUkm5(sku(barcode)) <- NULL;
//}
//
//WHEN ([GROUP SUM 1 IF CHANGED(name(ItemGroup g)) OR CHANGED(parent(g)) OR CHANGED(id(g)) OR CHANGED(active(g)) BY g](ItemGroup group))  AND uploadedUkm5(group) DO {
//    uploadedUkm5(group) <- NULL;
//}
//
//WHEN ([GROUP SUM 1 IF CHANGED(UKZ(Sku s))  OR CHANGED(dataGTIN(barcode(s))) OR CHANGED(GTIN(barcode(s))) OR CHANGED(skipUkz(s))
//    OR CHANGED(requireQuantityManual(s)) BY s](Sku sku)) AND uploadedUkm5Properties(sku) DO {
//    uploadedUkm5Properties(sku) <- NULL;
//}
//
//WHEN ([GROUP SUM 1 IF CHANGED(idBarcode(Sku s)) OR CHANGED(active(s)) OR CHANGED(split(s)) OR CHANGED(isCatchWeight(s))
//    OR CHANGED(weightSize(s)) OR CHANGED(overmaxDelta(s))
//    OR CHANGED(overMinDelta(s)) BY s](Sku sku)) AND uploadedUkm5Scale(sku) DO {
//    uploadedUkm5Scale(sku) <- NULL;
//}

EXTEND FORM integrationData
    PROPERTIES urlUkm5ServerImport(), urlUkm5ServerExport(), minDelta(), maxDelta();

DESIGN integrationData{
    pane{
        NEW ukm5{
            caption = 'УКМ5';
            MOVE PROPERTY (urlUkm5ServerImport());
            MOVE PROPERTY (urlUkm5ServerExport());
            NEW scale {
                caption = 'Импорт эталонных весов КВП';
                MOVE PROPERTY (minDelta());
                MOVE PROPERTY (maxDelta());
            }
        }
    }
}

sendMachineryPriceTransaction(MachineryPriceTransaction t) + {
    IF handlerModel(groupMachinery(t)) = 'lsf.ukm5' AND t IS CashRegisterPriceTransaction AND notSucceeded(t)
    THEN NEWSESSION {
        NEWSESSION { dateTimeProcessing(t) <- currentDateTime(); APPLY; }
        TRY sendPriceTransactionAPI(t);
            CATCH addMachineryError(t, (CONCAT ' : ', nameGroupMachinery(t), 'Ошибка при отправке прайса',
            messageCaughtException()), messageCaughtException());
    }
}
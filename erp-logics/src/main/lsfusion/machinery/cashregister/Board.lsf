MODULE Board;

REQUIRE System, POS, ScannerDaemon;
        
// Параметры табло
comPortBoard 'Порт табло' = DATA INTEGER (CashRegister) IN base;
comPortBoardCurrentCashRegister 'Порт табло' = comPortBoard(currentCashRegister());

ignoreDisplayText(CashRegister c) += comPortBoard(c) AND c IS CashRegister;

baudRateBoard 'Скорость табло' = DATA INTEGER (CashRegister) IN base;
baudRateBoardCurrentCashRegister 'Скорость табло' = baudRateBoard(currentCashRegister());

uppercaseBoard 'Верхний регистр табло' = DATA BOOLEAN (CashRegister) IN base;
uppercaseBoardCurrentCashRegister 'Верхний регистр' = uppercaseBoard(currentCashRegister());

useJsscOld 'Использовать библиотеку jssc вместо jSerialComm (backward compatibility)' = DATA BOOLEAN (CashRegister) IN base;
migaratedUseJsscOld = DATA BOOLEAN ();
onStarted() + {
    IF NOT migaratedUseJsscOld() THEN {
        migaratedUseJsscOld() <- TRUE;
        FOR Computer computer==computer(CashRegister cashRegister) AND useJsscOld(cashRegister) AND NOT useJssc(computer) DO {
            useJssc(computer) <- TRUE;
        }        
    }    
}

useJssc = DATA BOOLEAN ();

onStarted() + {
    IF NOT useJssc() AND NOT (GROUP SUM 1 IF useJssc(Computer c)) AND NOT (GROUP SUM 1 IF useJsscOld(CashRegister cr)) THEN {
        useJssc() <- TRUE;
        useJssc(Computer c) <- TRUE WHERE c IS Computer AND cashRegister(c);
    }
}

migratedUseJssc = DATA BOOLEAN ();
onStarted() + {
    IF NOT migratedUseJssc() THEN {
        FOR hostname(Computer c) DO {
            comLibrary(c) <- IF useJssc(c) THEN ComLibrary.jssc ELSE ComLibrary.jSerialComm;
        }
        migratedUseJssc() <- TRUE;
    }
}

comLibrary 'Библиотека Com' (CashRegister cashRegister) = comLibrary(computer(cashRegister)) IN base;
captionComLibrary 'Библиотека Com' (CashRegister cashRegister) = staticCaption(comLibrary(cashRegister)) IN base;
comLibraryCurrentCashRegister 'Библиотека Com' = staticName(comLibrary(currentCashRegister()));

defaultTextBoard 'Текст по умолчанию для табло' = DATA STRING[100] ();

EXTEND FORM options
    PROPERTIES() defaultTextBoard
;
DESIGN options {
    machinery {
        MOVE PROPERTY(defaultTextBoard());
    }
}

EXTEND FORM groupCashRegister
    PROPERTIES(c) comPortBoard, baudRateBoard, uppercaseBoard, captionComLibrary
;

// Использование в POS
fiscalBoardDisplayText 'Вывести текст на дисплей'  INTERNAL  'lsfusion.erp.region.by.machinery.board.fiscalboard.FiscalBoardDisplayTextAction' (ReceiptDetail);
fiscalBoardPostPrint 'Вывести итоги на дисплей'  INTERNAL  'lsfusion.erp.region.by.machinery.board.fiscalboard.FiscalBoardPostPrintAction' (Receipt, INTEGER);
fiscalBoardResetText 'Сбросить текст на дисплее'  INTERNAL  'lsfusion.erp.region.by.machinery.board.fiscalboard.FiscalBoardResetTextAction' (INTEGER);

fiscalDisplayText (ReceiptDetail receiptDetail) + {  
    IF comPortBoard(cashRegister(receipt((receiptDetail)))) AND NOT fiscalSkip(receipt(receiptDetail)) THEN
        fiscalBoardDisplayText(receiptDetail);
}
postPrint (Receipt receipt) + {  
    IF comPortBoard(cashRegister(receipt)) AND NOT fiscalSkip(receipt) THEN { 
        fiscalBoardPostPrint(receipt, 0);
        fiscalBoardResetText(5000);
    }
}
resetBoard (Receipt receipt) + {  
    IF comPortBoard(cashRegister(receipt)) AND NOT fiscalSkip(receipt) THEN 
        fiscalBoardResetText(0);
}
MODULE FitRequestExchange;

REQUIRE MachineryPriceTransaction, FitCashRegister;

NAMESPACE Fit;

@addRoleApplicationSetting(isCashierSenior, 'Cтарший кассир', BOOLEAN);

succeeded 'Выгружен' = DATA BOOLEAN (Machinery, RequestExchange);
notSucceeded 'Не выгружен' (Machinery machinery, RequestExchange requestExchange) = requestExchange IS RequestExchange AND machinery IS Machinery AND NOT succeededNative(requestExchange);
dateTimeSucceeded 'Время выгрузки' = DATA DATETIME (Machinery, RequestExchange);

countMachinery (RequestExchange re) = GROUP SUM 1 IF Machinery m IS Machinery AND in(m, re) AND active(m) AND isFit(m);
countSucceded (RequestExchange re) = GROUP SUM 1 IF succeeded(Machinery m, re) AND in(m, re) AND active(m) AND isFit(m);   

WHEN SET(countMachinery(RequestExchange re) = countSucceded(re) AND requestExchangeType(re) = RequestExchangeType.cashierInfo) DO succeededNative(re) <- TRUE;

EXTEND FORM requestExchange
    PROPERTIES (m,r) SHOWIF isFit(m) AND ignoreCashRegisterServerCashRegisterModel(groupMachinery(m)) succeeded, dateTimeSucceeded 
;

sendToFit (CashRegister cr, RequestExchange re) {
    IF GROUP SUM 1 IF in(stock(re), Employee e) THEN {
        NEWSESSION {
            exportFile() <- NULL;
            EXPORT CSV '' NOHEADER NOESCAPE CHARSET 'CP866' FROM 
                number = numeric(INTEGER(right(id(Employee e), 4)), 4), // 1. Код кассира
                placeholder1 = alpha('', 4),                            // 2. Заполнитель
                FIO = charSpaceR((OVERRIDE shortName(e), 'Кассир'), 20), // 3. ФИО
                placeholder2 = alpha('', 20),                           // 4. Заполнитель
                passwordCashier = char(right(id(e), 4), 4),             // 5. Личный пароль кассира 
                blocking = CASE WHEN isLocked(e) OR inactive(e) THEN  '1'
                                WHEN isCashierSenior(mainRole(e)) THEN 'S'
                                ELSE '0', // 6. Флаг блокировки
                placeholder3 = alpha('', 45),                           // 7. Заполнитель 
                append0D = '\r' IF append0D()
            WHERE in(stock(re), e) AND numeric(INTEGER(right(id(e), 4)), 4) ORDER active(e), e;
                
            put(cr, 'cassir', exportFile());
            IF returnExchange(cr) THEN {
                addMachineryError(cr);
            } ELSE {
                succeeded(cr, re) <- TRUE;
                dateTimeSucceeded(cr, re) <- currentDateTime();
            }
            APPLY;
        }
    }
}

sendRequestExchange (CashRegister cr, RequestExchange re) + WHEN requestExchangeType(re) = RequestExchangeType.cashierInfo AND cr IS CashRegister THEN {
    IF isFit(cr) AND active(cr) AND NOT succeeded(cr, re) THEN {
        sendToFit(cr, re);        
    }
}

EXTEND FORM requestExchange
    PROPERTIES sendRequestExchange(m, r)
;
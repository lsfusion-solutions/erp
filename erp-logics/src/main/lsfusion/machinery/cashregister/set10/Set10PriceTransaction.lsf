MODULE Set10PriceTransaction;

REQUIRE MachineryPriceTransactionStockTax, NativeMachinery, Item, CashRegisterPriceTransaction, LogTools, Set10, ItemSSC;

NAMESPACE Set10;

isSet10 (GroupCashRegister g) = TRUE IF handlerModel(g) = 'lsf.set10' OR handlerModel(g) = 'equ.clt.handler.kristal10.Kristal10Handler';
idStoreSet 'Код магазина Set10' = DATA STRING[20] (GroupCashRegister);
idStoreSet (CashRegister c) = idStoreSet(groupCashRegister(c));
idCashRegisterSet 'Номер кассы' = DATA STRING[20] (CashRegister);

CONSTRAINT SETCHANGED(handlerModel(GroupCashRegister g)) AND isSet10(g) 
    AND handlerModel(g) = 'lsf.set10' AND NOT idStoreSet(g) MESSAGE 'Код магазина Set10';

EXTEND FORM groupCashRegister
    PROPERTIES (grc) SHOWIF isSet10(grc) idStoreSet
    PROPERTIES (c) idCashRegisterSet AFTER npp(c)
;

DESIGN groupCashRegister{
    topContainer{
        MOVE PROPERTY (idStoreSet(grc));
    }
}

EXTEND FORM groupsCashRegister
    PROPERTIES (grc) READONLY idStoreSet
    PROPERTIES (c) READONLY idCashRegisterSet AFTER npp(c)
;

EXTEND FORM integrationData
    PROPERTIES useOutSet10Log();

DESIGN integrationData {
    pane {
        set10 {
            MOVE PROPERTY (useOutSet10Log());
        }
    }
}

//выгрузка товаров
GROUP bar_code EXTID 'bar-code';
GROUP good;
GROUP group;

GROUP parent_group EXTID 'parent-group' : group;
GROUP parent_group1 EXTID 'parent-group' : parent_group;
GROUP measure_type EXTID 'measure-type';
GROUP country;

productType = ABSTRACT CASE STRING (Sku);
productType (Sku s) += WHEN passScales(s) AND split(s) THEN 'ProductWeightEntity';
productType (Sku s) += WHEN passScales(s) AND NOT split(s) THEN 'ProductPieceWeightEntity';

value = ABSTRACT CASE STRING (STRING , Barcode, MachineryPriceTransaction);
value (STRING s, Barcode b, MachineryPriceTransaction m) += WHEN s = 'precision' AND in(m, b) AND (passScales(m, b) OR split(m, b)) THEN '0.001';
value (STRING s, Barcode b, MachineryPriceTransaction m) += WHEN s = 'precision' AND in(m, b) AND NOT (passScales(m, b) OR split(m, b)) THEN '1.0';
value (STRING s, Barcode b, MachineryPriceTransaction m) += WHEN s = 'composition' AND in(m, b) AND expiryDate(m, b) THEN STRING('Годен до ' + toChar(expiryDate(m, b), 'dd.MM.yyyyy'));
value (STRING s, Barcode b, MachineryPriceTransaction m) += WHEN s = 'composition' AND in(m, b) AND expiryDays(m, b) THEN STRING('Срок годности ' + expiryDays(m, b) + ' дней');

FORM goodsCatalog FORMEXTID 'goods-catalog'
    OBJECTS m = MachineryPriceTransaction PANEL
    
    OBJECTS good = Barcode.Barcode
    PROPERTIES name(m, good), = OVERRIDE VAT(m, good), 0 EXTID 'vat', 
        = idSku(good) EXTID 'marking-of-the-good' ATTR,=  IF in(m, good) THEN 'false' EXTID 'delete-from-cash',
        = OVERRIDE productType(sku(good)), 'ProductPieceEntity' EXTID 'product-type', = age(sku(good)) EXTID 'age-limit',
        = IF good IS Barcode THEN idStoreSet(groupMachinery(m)) EXTID 'shop-indices'
    PROPERTIES IN measure_type = OVERRIDE shortNameUOM(m, good) EXTID 'id' ATTR, shortNameUOM(m, good) EXTID 'name'
    
    PROPERTIES IN group = idItemGroup(sku(good)) EXTID 'id' ATTR, = canonicalNameItemGroup(sku(good)) EXTID 'name'
    
    OBJECTS plugin_property = STRING EXTID 'plugin-property'
    PROPERTIES value(plugin_property, good, m) ATTR EXTID 'value', VALUE(plugin_property) EXTID 'key' ATTR
    FILTERS value(plugin_property, good, m)
    
    FILTERS in(m, good)
;

FORM barcodesCatalog FORMEXTID 'goods-catalog'
    OBJECTS m = MachineryPriceTransaction PANEL 
    
    OBJECTS good = Barcode.Barcode EXTID 'bar-code'
    PROPERTIES = idSku(good) EXTID 'marking-of-the-good' ATTR, id(good) EXTID 'code' ATTR , 
        = IF primary(good) THEN 'true' ELSE 'false' EXTID 'default-code'
    FILTERS in(m, good)
    
    OBJECTS barcode = Barcode.Barcode EXTID 'bar-code'
    PROPERTIES = idSku(barcode) EXTID 'marking-of-the-good' ATTR, = weightCode(groupMachinery(m)) + id(barcode) EXTID 'code' ATTR ,
        = IF barcode IS Barcode.Barcode THEN  'false' EXTID 'default-code'
    FILTERS in(m, barcode) AND weightCode(groupMachinery(m)) AND passScales(sku(barcode)) AND split(sku(barcode)) AND length(id(barcode)) < 13
;

GROUP department;
   
FORM priceEntry FORMEXTID 'goods-catalog'
    OBJECTS m = MachineryPriceTransaction PANEL 
    
    OBJECTS good = Barcode.Barcode EXTID 'price-entry'
    PROPERTIES = idSku(good) EXTID 'marking-of-the-good' ATTR, = NUMERIC[16,2](round2(price(m, good))) EXTID 'price' ATTR,
        = IF in(m, good) THEN 'false' EXTID 'deleted' ATTR, = IF in(m, good) THEN '1' EXTID 'number',
        = IF in(m, good) THEN idStoreSet(groupMachinery(m)) EXTID 'shop-indices'
    PROPERTIES IN department = IF in(m, good) THEN '1' EXTID 'number'
    FILTERS in(m, good)
;

statusTransaction = DATA STRING[100] (MachineryPriceTransaction, STRING);

sendRequsetGoods(FILE f, STRING type, MachineryPriceTransaction t){
    fileToString(f);
    encodeBase64(resultString());
    goodsCatalogXML()<- encodedBase64();
    LOCAL headersSet = STRING (STRING);
    headersSet('Content-Type') <- 'application/xml';
    generateUUID();
    TI() <- generatedUUID();
    LOCAL answer = FILE();
    LOCAL req = FILE();
    EXPORT requestItem XML TO req;
    fileToString(req());
    IF useOutSet10Log() THEN logToFile('set10', (CONCAT ' ', 'Send Request Goods', 'Type:' + type, 'TI:' + TI(), 'Request:' + resultString()));
    TRY{
        timeoutHttp() <- timeoutSet10();
        EXTERNAL HTTP POST urlServerSet10() + 'SET-ERPIntegration/SET/WSGoodsCatalogImport' HEADERS headersSet PARAMS req() TO answer;
        IMPORT answer XML FROM answer();
        IF return() = 'true' THEN statusTransaction(t, type) <- TI();
        fileToString(answer());
        IF useOutSet10Log() THEN logToFile('set10', (CONCAT ' ', 'Send Request Goods', 'Type:' + type, 'TI:' + TI(), 'Response:' + resultString()));
    }
    CATCH {
        fileToString(answer());
        addMachineryError(t, 'Ошибка загрузки ' + type, (CONCAT '\n', messageCaughtException(), resultString()));
        APPLY;
    }
    APPLY;
}

isCorrect (MachineryPriceTransaction t) = IF (GROUP SUM 1 IF statusTransaction(t, STRING s) = 'correct')
    = (GROUP SUM 1 IF statusTransaction(t, STRING s)) THEN TRUE;

getFeedback(MachineryPriceTransaction t){
    LOCAL message = STRING ();
    FOR statusTransaction(t, STRING s) != 'correct' DO{
        LOCAL answer = FILE ();
        TRY{
            ti() <- statusTransaction(t, s);
            EXPORT getFeedbackRequest XML;
            LOCAL headersSet = STRING (STRING);
            headersSet('Content-Type') <- 'application/xml';
            EXTERNAL HTTP POST urlServerSet10() + 'SET-ERPIntegration/SET/FeedbackWS' HEADERS headersSet 
                PARAMS exportFile() TO answer;
            fileToString(answer());
            IF useOutSet10Log() THEN logToFile('set10', (CONCAT ' ', 'Get Feedback', s, resultString()));
            IMPORT getFeedbackAnswer XML FROM answer();
            CASE
                WHEN status() = '2' THEN message() <- CONCAT '\n', message(), s, status_message();
                WHEN status() = '3' THEN statusTransaction(t, s) <- status_message();
                ELSE {
                    addMachineryError(t, (CONCAT ' : ', s, status_message()), resultString());
                    statusTransaction(t, s) <- 'fault';
                } 
        }
        CATCH{
            addMachineryError(t, (CONCAT ' : ', s, status_message()), resultString()); 
        }
    }
    IF isCorrect(t) THEN {
        succeeded(t) <-TRUE;
        dateTimeSucceeded(t) <- currentDateTime();
    }
    IF (GROUP SUM 1 IF statusTransaction(t, STRING s) = 'fault') THEN statusTransaction(t, STRING s) <- NULL;
    APPLY;
}
customSend = ABSTRACT CASE BOOLEAN (MachineryPriceTransaction);
customSend ABSTRACT LIST (MachineryPriceTransaction);

afterExportTransaction ABSTRACT LIST (MachineryPriceTransaction);

sendSet10Transaction(MachineryPriceTransaction t){
    EXPORT goodsCatalog OBJECTS m = t XML;
    sendRequsetGoods(exportFile(), 'item', t);

    EXPORT barcodesCatalog OBJECTS m = t XML;
    sendRequsetGoods(exportFile(), 'barcode', t);

    EXPORT priceEntry OBJECTS m = t XML;
    sendRequsetGoods(exportFile(), 'price', t);
    afterExportTransaction(t);
}

sendPriceTransaction (MachineryPriceTransaction t){
    TRY{
        IF (GROUP SUM 1 IF statusTransaction(t, STRING s)) THEN getFeedback(t);
        ELSE {
            NEWSESSION { dateTimeProcessing(t) <- currentDateTime(); APPLY; }
            IF customSend(t) THEN customSend(t);
            ELSE sendSet10Transaction(t);
        }
    }
    CATCH {
        addMachineryError(t, (CONCAT ' : ' , nameGroupMachinery(t), 'Ошибка при выполнении операции ', 
            messageCaughtException()), messageCaughtException());
    }
}

sendMachineryPriceTransaction(MachineryPriceTransaction t) + {
    IF handlerModel(groupMachinery(t)) = 'lsf.set10' AND countBarcodes(t) AND t IS CashRegisterPriceTransaction
        AND notSucceeded(t) THEN NEWSESSION {
        sendPriceTransaction(t);
    }
}

setNULL(MachineryPriceTransaction t){
    statusTransaction(t, STRING s) <- NULL;
    APPLY;
}
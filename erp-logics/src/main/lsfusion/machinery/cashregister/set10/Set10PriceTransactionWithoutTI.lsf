MODULE Set10PriceTransactionWithoutTI;

REQUIRE Set10PriceTransaction;

NAMESPACE Set10;

urlServerESB 'Адрес:порт для ESB шины' = DATA STRING ();
useOutSetESBLog 'Логировать исх. запросы ESB шины' = DATA BOOLEAN ();
dataTimeoutSetESB 'Таймаут ESB шины, в миллисекундах' = DATA INTEGER ();
timeoutSetESB 'Таймаут ESB шины, в миллисекундах' () = OVERRIDE dataTimeoutSetESB(), 120000;


EXTEND FORM integrationData
    PROPERTIES urlServerESB(), timeoutSetESB(), useOutSetESBLog();

DESIGN integrationData {
    pane {
        set10 {
            NEW esb {
                caption = 'ESB';
                alignment = STRETCH;
                MOVE PROPERTY (urlServerESB());
                MOVE PROPERTY (timeoutSetESB());
                MOVE PROPERTY (useOutSetESBLog());
            }
        }
    }
}

useESB 'Использовать ESB шину' = DATA BOOLEAN ();

EXTEND FORM integrationData
    PROPERTIES() useESB
;
DESIGN integrationData{
    set10{
        esb {
            MOVE PROPERTY (useESB());
        }
    }
}

GROUP bodyRequestWithoutTI EXTID 'soapenv:Body';
GROUP getGoodsCatalog EXTID 'plug:getGoodsCatalog' : bodyRequest;

headerWithoutTI = DATA LOCAL STRING();
goodsCatalogXMLWithoutTI = DATA LOCAL STRING();

FORM requestItemWithoutTI FORMEXTID 'soapenv=http://schemas.xmlsoap.org/soap/envelope/:Envelope'
    PROPERTIES ATTR ='http://plugins.products.ERPIntegration.crystals.ru/' EXTID 'xmlns:plug'
    PROPERTIES() header EXTID 'soapenv:Header'
    PROPERTIES() IN getGoodsCatalog goodsCatalogXMLWithoutTI EXTID 'goodsCatalogXML'
;

//обработка ответа по товарам
GROUP getGoodsCatalogWithoutTiResponse
EXTID 'ns2=http://plugins.products.ERPIntegration.crystals.ru/:getGoodsCatalogResponse' : bodyAnswer;

returnWithoutTi = DATA LOCAL STRING();

FORM answerWithoutTi FORMEXTID 'soap=http://schemas.xmlsoap.org/soap/envelope/:Envelope'
    PROPERTIES() IN getGoodsCatalogWithoutTiResponse returnWithoutTi EXTID 'return';
;

sendRequestGoodsWithoutTI(FILE f, STRING type, MachineryPriceTransaction t){
    fileToString(f);
    encodeBase64(resultString());
    goodsCatalogXMLWithoutTI()<- encodedBase64();
    LOCAL headersSet = STRING (STRING);
    headersSet('Content-Type') <- 'application/xml';
    LOCAL answer = FILE();
    LOCAL req = FILE();
    EXPORT requestItemWithoutTI XML TO req;
    fileToString(req());
    IF useOutSetESBLog() THEN logToFile('setESB', (CONCAT ' ', 'Send Request Goods Without TI', 'Type:' + type, 'Request:' + resultString()));
    TRY {
        timeoutHttp() <- timeoutSetESB();
        EXTERNAL HTTP POST urlServerESB() + 'SET-ERPIntegration/SET/WSGoodsCatalogImport' HEADERS headersSet PARAMS req() TO answer;
        IMPORT answerWithoutTi XML FROM answer();
        fileToString(answer());
        IF useOutSetESBLog() THEN logToFile('setESB', (CONCAT ' ', 'Send Request Goods Without TI', 'Response', resultString()));
        IF returnWithoutTi() = 'true' THEN succeeded(t) <- TRUE;
    } CATCH {
        fileToString(answer());
        addMachineryError(t, 'Ошибка загрузки ' + type, (CONCAT '\n', messageCaughtException(), resultString()));
        APPLY;
    }
    APPLY;
}

sendESBTransactionWithoutTI(MachineryPriceTransaction t){
    EXPORT goodsCatalog OBJECTS m = t XML;
    sendRequestGoodsWithoutTI(exportFile(), 'item', t);

    EXPORT barcodesCatalog OBJECTS m = t XML;
    sendRequestGoodsWithoutTI(exportFile(), 'barcode', t);

    EXPORT priceEntry OBJECTS m = t XML;
    sendRequestGoodsWithoutTI(exportFile(), 'price', t);
    afterExportTransaction(t);
}


customSend(MachineryPriceTransaction t) += WHEN t IS CashRegisterPriceTransaction AND useESB() THEN TRUE;

customSend(MachineryPriceTransaction t) + {
    IF useESB() THEN {
        sendESBTransactionWithoutTI(t);
    }
}
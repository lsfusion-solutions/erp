MODULE Set10DiscountCard;

REQUIRE ZReportDiscountCard, Set10WebServer;

NAMESPACE Set10;

createDiscountCardSet10 'Создавать дисконтные карты' = DATA BOOLEAN ();
EXTEND FORM integrationData
    PROPERTIES createDiscountCardSet10()
;
DESIGN integrationData{
    set10{
        MOVE PROPERTY (createDiscountCardSet10());
    }
}

purchaseCard = DATA LOCAL INTEGER(INTEGER);
numberD = DATA LOCAL STRING (INTEGER);
typeD = DATA LOCAL STRING (INTEGER);


EXTEND FORM purchasesImport
    OBJECTS card = INTEGER
    FILTERS purchaseCard(card) == purchase
    PROPERTIES(card) typeD EXTID 'type' ATTR, numberD EXTID 'number' ATTR
    FILTERS imported(card)
;
discountCardNumber = DATA LOCAL STRING (INTEGER);

beforeCreateReceipts() +{
    FOR purchaseCard(INTEGER card) AND numberD(card) AND typeD(card) != 'COUPON_CARD' AND NOT discountNumber(numberD(card)) AND createDiscountCardSet10() NEW d = DiscountCard DO{
        number(d) <- STRING[48](numberD(card));
    }
    discountCardNumber(INTEGER i) <- GROUP MAX numberD(INTEGER j) IF typeD(j) != 'COUPON_CARD' AND purchaseCard(j) = i;
}

overCreateReceipt(Receipt r, INTEGER i)+{
    IF discountCardNumber(i) THEN discountCard(r) <- discountNumber(discountCardNumber(i));
}
MODULE Set10SaleDeniedRestrictionStopList;

REQUIRE Set10SaleDeniedRestriction, StopList;

NAMESPACE Set10;

stopListDetail = DATA StopListDetail(SaleDeniedRestriction);
seriesNumberStopList 'Стоп-лист' (SaleDeniedRestriction d) = seriesNumber(stopListDetail(d));
excludeStopList 'Исключить из запрета (стоп-лист)' (SaleDeniedRestriction d) = exclude(stopListDetail(d));
EXTEND FORM saleDeniedRestrictions
    PROPERTIES (r) READONLY seriesNumberStopList, excludeStopList
;

needGreate (StopListDetail d, Stock st) = in(st, stopList(d)) AND isPosted(d) AND sku(d);
prevNeedGreate(StopListDetail d, Stock st)= PREV(needGreate(d,st));

saleDeniedRestriction = GROUP MAX SaleDeniedRestriction d BY stopListDetail(d),sku(d),stock(d);
saleDeniedRestriction(StopListDetail d, Stock st) = saleDeniedRestriction(d,sku(d),st);
saleDeniedRestriction = GROUP MAX SaleDeniedRestriction d BY stopListDetail(d);
prevSaleDeniedRestriction(StopListDetail d) = PREV(saleDeniedRestriction(d));

skipGreateStopList = DATA BOOLEAN ();

WHEN SET(needGreate(StopListDetail d, Stock st)) AND NOT exclude(d) AND NOT saleDeniedRestriction(d, st) AND NOT skipGreateStopList() DO {
//    LOCAL restriction = SaleDeniedRestriction (Sku);
//    restriction(Sku s) <- GROUP MAX SaleDeniedRestriction r IF createdTime(r) AND NOT deleted(r) AND sku(r) = s AND stock(r) = stock;
    NEW detail = SaleDeniedRestriction{
        stopListDetail(detail) <- d;
        sku(detail) <- sku(d);
        stock(detail) <- st;
        balance(detail) <- OVERRIDE currentBalanceRestriction(sku(d), st), 0;
    }
}

skipCheckSku = DATA BOOLEAN () PREREAD;

skipDelete(StopListDetail d) += TRUE IF saleDeniedRestriction(d);

isGreate (StopListDetail d, Stock st) = in(st, stopList(d)) AND isPosted(d) AND sku(d) AND saleDeniedRestriction(d,st);
prevIsGreate(StopListDetail d, Stock st)= PREV(isGreate(d,st));

CONSTRAINT DROPPED(isGreate(StopListDetail d AS StopListDetail, Stock st)) AND st IS Stock AND prevIsGreate(d,st) AND NOT skipCheckSku()
    MESSAGE 'Запрещено изменять/удалять строки или склады в документе "Запрет продаж по кассе", так как он выгружен в СЕТ.';

//CONSTRAINT DROPPED(needGreate(StopListDetail d AS StopListDetail, Stock st)) AND st IS Stock AND prevNeedGreate(d,st) AND NOT skipCheckSku()
//    MESSAGE 'Запрещено изменять/удалять строки или склады в документе "Запрет продаж по кассе", так как он выгружен в СЕТ.';

skipUseSaleDeniedRestrictionsMigrate() {
    skipUseSaleDeniedRestrictions() <- TRUE;
}
@defOnStartMigrate(skipUseSaleDeniedRestrictionsMigrate);

inactiveItem 'Товар неактивный' = DATA BOOLEAN (SaleDeniedRestriction);

EXTEND FORM saleDeniedRestrictions
    PROPERTIES (r) READONLY inactiveItem
;

greateInactiveItem 'Надо создать "Запрет продажи Set10" для неактивного товара' = DATA BOOLEAN (Item);
WHEN SET(inactive(Item s)) AND NOT greateInactiveItem(s) DO {
    greateInactiveItem(s) <- TRUE;
}

greateInactiveItem 'Создать "Запрет продажи Set10" для неактивного товара' (){
    NEWSESSION {
        LOCAL  to = INTEGER (DepartmentStore);
        to(DepartmentStore ds) <- countCashRegister(ds);

        LOCAL  to = BOOLEAN (Item);
        to(Item s) <- greateInactiveItem(s) WHERE greateInactiveItem(s);

        LOCAL restriction = SaleDeniedRestriction (DepartmentStore, Item);
        restriction(DepartmentStore ds, Item s) <- (GROUP MAX SaleDeniedRestriction r IF createdTime(r) AND NOT deleted(r) AND sku(r) = s AND stock(r) = ds AND to(ds) AND to(s));
                    
        FOR to(DepartmentStore ds) AND to(Item s) AND NOT restriction(ds, s) ORDER s DO {                
            NEW detail = SaleDeniedRestriction{
                inactiveItem(detail) <- TRUE;
                sku(detail) <- s;
                stock(detail) <- ds;
                balance(detail) <- OVERRIDE currentBalanceRestriction(s, ds), 0;
            }
        }
        greateInactiveItem(Item s) <- NULL WHERE to(s);

        APPLY;
    }
}

afterExportTransaction(MachineryPriceTransaction t) +{
    IF skipUseSaleDeniedRestrictions() THEN {
        IF useSaleDeniedRestrictions() THEN{
            LOCAL NESTED  in = BOOLEAN (Sku);
            in(Sku s)  <- in(t,barcode(s)) AND active(s);
            
            LOCAL stop = BOOLEAN (Sku);
            stop(Sku s) <- TRUE WHERE (GROUP  SUM 1 IF active(SaleDeniedRestriction r) AND stock(t) = stock(r) AND in(sku(r)) AND sku(r)==s AND stopListDetail(r) AND NOT excludeStopList(r));

            LOCAL exclude = BOOLEAN (Sku);
            exclude(Sku s) <- TRUE WHERE (GROUP  SUM 1 IF active(SaleDeniedRestriction r) AND stock(t) = stock(r) AND in(sku(r)) AND sku(r)==s AND excludeStopList(r));

            LOCAL NESTED  to = BOOLEAN (Sku);
            to(Sku s) <- TRUE WHERE (GROUP  SUM 1 IF active(SaleDeniedRestriction r) AND stock(t) = stock(r) AND in(sku(r)) AND sku(r)==s)
                                    AND currentBalanceRestriction(s, stock(t)) > 0 AND NOT stop(s);
            to(Sku s) <- TRUE WHERE exclude(s) AND NOT stop(s);            
            
            FOR in(t, SaleDeniedRestriction r) AND to(sku(r)) DO{
                deleteSaleRestriction(r);
            }
        }
    }
}
MODULE ArtixSettings;

REQUIRE CashRegister;

NAMESPACE Artix;

directoryArtix 'Путь к каталогу All' = DATA STRING[250](CashRegisterModel);
globalDirectoryArtix = directoryArtix(cashRegisterSID('Artix'));

EXTEND FORM cashRegisterModel 
    PROPERTIES (c) SHOWIF sid(c) == 'Artix' directoryArtix;
    
DESIGN cashRegisterModel{
    BOX (c){
        NEW artix  {
            MOVE PROPERTY (directoryArtix(c));
        } 
    }
}

artixError = DATA LOCAL NESTED BOOLEAN ();

createFilesArtix(STRING data, STRING directory, BOOLEAN message) {

    IF NOT directory THEN {
        IF message THEN {
            MESSAGE 'Каталог не задан.';
        }
        RETURN;
    }

    LOCAL aif = FILE ();
    stringToFile(data, 'UTF-8', 'aif');
    aif() <- resultFile();

    LOCAL flz = FILE ();
    stringToFile('', 'UTF-8', 'flz');
    flz() <- resultFile();

    LOCAL name = STRING[50]();
    name() <- 'pos' + toChar(currentDateTimeMillis(), 'YYYYMMDDHH24MISSMS');

    artixError() <- NULL;
    TRY {
        WRITE aif() TO directory + '/' + name();
        WRITE flz() TO directory + '/' + name();
    } CATCH {
        IF message THEN {
            MESSAGE 'Ошибка при записи файла в каталог ' + directory NOWAIT;
        }
        artixError() <- TRUE;
    }
}
createFilesArtix(STRING data, STRING directory){
    createFilesArtix(data,directory,TRUE);
}


directoryPickList 'Директория для пиклиста' = DATA STRING[250](GroupCashRegister);

EXTEND FORM groupCashRegister
    PROPERTIES (grc) SHOWIF sidModel(grc) == 'Artix' directoryPickList;

DESIGN groupCashRegister{
    topContainer{
        MOVE PROPERTY (directoryPickList(grc));
    }
}
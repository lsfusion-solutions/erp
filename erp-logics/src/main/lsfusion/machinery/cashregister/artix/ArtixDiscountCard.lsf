MODULE ArtixDiscountCard;

REQUIRE DiscountCard, ArtixSettings;

NAMESPACE Artix;


stepArtix 'Шаг выгрузки дисконтных карт' = DATA INTEGER (CashRegisterModel);
EXTEND FORM cashRegisterModel 
    PROPERTIES (c) SHOWIF sid(c) == 'Artix' stepArtix;

DESIGN cashRegisterModel {
    artix{
        NEW artixDiscountCard{
            MOVE PROPERTY (stepArtix(c));
        }
    }
}    

CLASS CardKind 'Тип карты';
TABLE cardKind (CardKind);

id 'Код' = DATA INTEGER (CardKind) CHARWIDTH 15;
name 'Наименование' = DATA ISTRING[100] (CardKind) CHARWIDTH 15;

FORM cardKind 'Тип карты'
    OBJECTS o = CardKind PANEL
    PROPERTIES(o) id, name
    
    EDIT CardKind OBJECT o
;

FORM cardKinds 'Типы карты'
    OBJECTS o = CardKind
    PROPERTIES(o) READONLY id, name
    PROPERTIES(o) NEWSESSION NEW, EDIT, DELETE
    LIST CardKind OBJECT o
;

cardKind = DATA CardKind(DiscountCardType);
idCardKind (DiscountCardType dt) = id(cardKind(dt));
nameCardKind 'Типы карты Artix' (DiscountCardType dt) = name(cardKind(dt));

dataId = DATA STRING[100](DiscountCardType);
idArtix 'Код для Artix'(DiscountCardType d) = OVERRIDE dataId(d), id(d);

EXTEND FORM discountCardType
    PROPERTIES nameCardKind(t), idArtix(t);


inExportStep = DATA LOCAL BOOLEAN (DiscountCard);

overExportName = ABSTRACT STRING (DiscountCard);
exportName 'Имя для выгрузки' (DiscountCard d) = OVERRIDE overExportName(d),
    (CONCAT ' ', lastNameContact(d), firstNameContact(d), middleNameHttpServerContact(d));

overExportNumber = ABSTRACT STRING (DiscountCard);
exportNumber 'Номер для выгрузки' (DiscountCard d) = OVERRIDE overExportNumber(d), seriesNumber(d);

overExportAddClient = ABSTRACT TEXT (DiscountCard);
afterExportDiscountCardsStep ABSTRACT LIST ();

exportDiscountCardsStep 'Выгрузить дисконтные карты' () {
    
    IF NOT (GROUP SUM 1 IF inExportStep(DiscountCard s)) THEN {
        MESSAGE 'Нет карт к выгрузке' NOWAIT;
        RETURN;
    }
    LOCAL rezult = TEXT ();
    
    rezult() <- CONCAT '', rezult(),
                    (GROUP CONCAT ('\{"command":"addCardGroup","cardGroup":\{' + 
                        TEXT (CONCAT ',', 
                            '"idcardgroup":"' + idArtix(DiscountCardType t) + '"',
                            '"name":"' + escapeJSONValue(name(t)) + '"',
                            '"text":"' + id(t) + '"',
                            '"cardmode":' + idCardKind(t),
                            '"notaddemptycard":true',
                            '"pattern":"[0-9]"',
                            '"inputmask":7'
                        )
                    + '\}\}') IF (GROUP SUM 1 IF discountCardType(DiscountCard dc) == t AND inExportStep(dc)), '\n---\n' ORDER t)
                +'\n---\n';
                
    rezult() <- CONCAT '', rezult(),
                    (GROUP CONCAT ('\{"command":"addClient","client":\{' + 
                        TEXT (CONCAT ',', 
                            '"idclient":"' + exportNumber(DiscountCard d) + '"',
                            '"name":"' + trim(escapeJSONValue(exportName())) + '"',
                            '"sex":' + numberSexHttpServerContact(d),
                            '"birthday":"' + (birthdayContact(d) IF birthdayContact(d) > 1900_01_01) + '"',
                            '"email":"' + escapeJSONValue(emailLegalEntity(d)) + '"',
                            overExportAddClient(d),
                            '"extendedoptions":' + jsonObject(jsonObject(extInfo(d), 'artix'), 'extendedoptions'),
                            '"options":\{"sendchecktoemail":'+jsonObject(jsonObject(extInfo(d), 'artix'), 'sendchecktoemail')+',"emailconfirmed":'+jsonObject(jsonObject(extInfo(d), 'artix'), 'sendchecktoemail')+'\}'
                        )
                    + '\}\}') IF inExportStep(d), '\n---\n' ORDER d)
                +'\n---\n';

    rezult() <- CONCAT '', rezult(),
                    (GROUP CONCAT ('\{"command":"addCard","card":\{' + 
                        TEXT (CONCAT ',', 
                            '"idcard":"' + exportNumber(DiscountCard d) + '"',
                            '"idcardgroup":"' + idArtix(discountCardType(d)) + '"',
                            '"idclient":"' + exportNumber(d) + '"',
                            '"number":"' + exportNumber(d) + '"',
                            '"validitydatebeg":"' + date(d) + '"',
                            '"validitydateend":"' + dateTo(d) + '"',
                            '"cardSum":' + initialSum(d),
                            '"blocked":' + (IF dateTo(d) < currentDate() THEN 'true' ELSE 'false'),
                            '"discountpercent":' + percent(d)
                        )
                    + '\}\}') IF inExportStep(d), '\n---\n' ORDER d)
                +'\n---\n';

    rezult() <- CONCAT '', rezult(),
                    (GROUP CONCAT ('\{"command":"addChangeCardAccount","ChangeCardAccount":\{' + 
                        TEXT (CONCAT ',', 
                            '"cardNumber":"' + exportNumber(DiscountCard d) + '"',
                            '"accountnumber":"' + exportNumber(d) + '"',
                            '"cardstatus":' + (OVERRIDE jsonObject(jsonObject(extInfo(d), 'artix'), 'cardstatus'), '"EARN_ONLY"')
                        )
                    + '\}\}') IF inExportStep(d) AND STRING(jsonObject(jsonObject(extInfo(d), 'artix'), 'ChangeCardAccount')) == 'true', '\n---\n' ORDER d)
                +'\n---\n';
    
    createFilesArtix(rezult(), globalDirectoryArtix());
    
    afterExportDiscountCardsStep();
    inExportStep(DiscountCard d) <- NULL;
}

inExport = DATA LOCAL BOOLEAN (DiscountCard);

exportDiscountCards 'Выгрузить дисконтные карты в Artix' () {
    inExport(DiscountCard d) <- NULL WHERE inExport(d) AND (skipLoad(d) OR NOT exportNumber(d));
    
    WHILE GROUP SUM 1 IF inExport(DiscountCard d) DO {
        IF NOT stepArtix(cashRegisterSID('Artix')) THEN {
            inExportStep(DiscountCard d) <- inExport(d);
        } ELSE {
            inExportStep(DiscountCard d) <- (PARTITION SUM 1 IF inExport(d) ORDER d) <= stepArtix(cashRegisterSID('Artix'));
        }
        
        inExport(DiscountCard d) <- NULL WHERE inExportStep(d);
        exportDiscountCardsStep();
    }
}
MODULE ArtixSSC;

REQUIRE CashRegisterPriceTransaction, ItemSSC, ArtixSettings;

NAMESPACE Artix;


useNewTaraMode 'Тарирование на кассе' = DATA BOOLEAN (CashRegisterModel);
idCatalog 'ID Каталога для тары' = DATA INTEGER (CashRegisterModel);
idCatalogTara = idCatalog(cashRegisterSID('Artix')) IF useNewTaraMode(cashRegisterSID('Artix'));

EXTEND FORM cashRegisterModel 
    PROPERTIES (c) useNewTaraMode SHOWIF sid(c) == 'Artix', idCatalog SHOWIF useNewTaraMode(c);
    
DESIGN cashRegisterModel{
    artix {
        MOVE PROPERTY (useNewTaraMode(c));
        MOVE PROPERTY (idCatalog(c));
    } 
}

useNewTaraMode 'Тарирование на кассе' = DATA BOOLEAN (GroupCashRegister);
EXTEND FORM groupCashRegister
    PROPERTIES (grc) useNewTaraMode SHOWIF useNewTaraMode(cashRegisterModel(grc));
DESIGN groupCashRegister {
    topContainer {
        MOVE PROPERTY(useNewTaraMode(grc));
    }
}

isUseNewTaraMode 'Тарирование на кассе' (GroupCashRegister cr) = useNewTaraMode(cashRegisterModel(cr)) AND useNewTaraMode(cr);
@difineSSCAttribute(BOOLEAN, taraMode, 'Тарирование на кассе');
@difineSSCAttribute(BOOLEAN, isTara, 'Упаковка для тарирования на кассе');

overArtixInfo = ABSTRACT TEXT (MachineryPriceTransaction, Barcode);

skipLoadSSC = ABSTRACT BOOLEAN (GroupMachinery);
skipLoadTaraCapacity = ABSTRACT BOOLEAN (GroupMachinery);
//стараемся выполнять последним
createAttribute(MachineryPriceTransaction mpt, GroupMachinery gm) + {
    IF sidModel(gm) == 'Artix' THEN {
        
        split(mpt, Barcode b) <- NULL WHERE in(mpt, b) AND split(mpt, b) AND NOT split(UOM(sku(b)));
      
        info(mpt, Barcode b) <- ('\{"artix":\{'+
                (CONCAT ',',
                    '"age":' + age(sku(b)),
                    '"ageverify":1' IF ageVerify(sku(b)),
                    '"visualverify":1' IF visualVerify(sku(b)) AND NOT skipLoadSSC(gm),
                    '"lowweight":1' IF lowWeight(sku(b)) AND NOT skipLoadSSC(gm),
                    '"weightcontrolbypass":1' IF weightControlBypass(sku(b)) AND NOT skipLoadSSC(gm),
                    '"requirequantitymanual":1' IF requireQuantityManual(sku(b)) AND NOT skipLoadSSC(gm),
                    '"requirequantityscales":1' IF requireQuantityScales(sku(b)) AND NOT skipLoadSSC(gm),
                    ('"taracapacity":' + taraCapacity(sku(b))) IF NOT skipLoadTaraCapacity(gm) AND NOT isUseNewTaraMode(gm),
                    '"taramode":7' IF taraMode(sku(b)) AND isUseNewTaraMode(groupMachinery(mpt)),
                    ('"cquant":' + taraCapacity(sku(b))) IF isTara(sku(b)) AND isUseNewTaraMode(groupMachinery(mpt)),
                    overArtixInfo(mpt,b)
                )
                +'\}\}') WHERE in(mpt, b);
    }
}

//товар
imageArtix = DATA IMAGEFILE (Item);

WHEN CHANGED (image(Item i)) DO { 
    imageArtix(i) <- NULL;
}

overName = ABSTRACT ISTRING[255](Item);

orderArtix 'Порядок отображения для Artix' = DATA LOCAL INTEGER (Item);
idParent = DATA LOCAL STRING (Item);

//группа
imageArtix 'Изображение для Artix' = DATA IMAGEFILE (CustomGroup);
orderArtix 'Порядок отображения для Artix' = DATA INTEGER (CustomGroup);
export = DATA LOCAL BOOLEAN (CustomGroup);

EXTEND FORM customGroups
    PROPERTIES (g) READONLY imageArtix,orderArtix;
    
EXTEND FORM customGroup
    PROPERTIES (g) imageArtix, orderArtix;

//группа
dataCustomGroupTypeArtix = DATA CustomGroupType(GroupCashRegister);
nameCustomGroupTypeArtix 'Тип классификатора КСО'(GroupCashRegister gs) = name(dataCustomGroupTypeArtix(gs));
directoryPickList 'Директория для пиклиста' = DATA STRING[250](GroupCashRegister);

skipFilterBalance = ABSTRACT BOOLEAN (GroupCashRegister, Sku);
beforeExport ABSTRACT LIST (GroupCashRegister);

//выгрузка
skipFilterBalance 'Не фильтровать по остаткам' = DATA LOCAL NESTED BOOLEAN ();
doClear 'С очисткой' = DATA LOCAL NESTED BOOLEAN ();

encodeBase64 = FORMULA STRING 'regexp_replace(encode(($1),\'base64\'), E\'[\\n\\r]+\', \' \', \'g\' )';

convertImageArtix(Sku s, IMAGEFILE f){
    TRY {
        convertedImage() <- NULL;
        convertImage(f, 'jpg');
        imageArtix(s) <- convertedImage();
        
        getFileSize(FILE(imageArtix(s)));
        LOCAL count = INTEGER ();
        count() <- 0;
        
        WHILE fileSize() > 45000 AND count() < 5 DO {
            resizeImage(imageArtix(s), sqrt(DOUBLE(fileSize())/40000));
            imageArtix(s) <- resizedImage();
            
            getFileSize(FILE(imageArtix(s)));
            count() <- count() + 1;
        }
    }
}

extraTextPickList = DATA LOCAL TEXT();
createFilePickList (GroupCashRegister gm) {
    LOCAL rezult = TEXT ();
    
    IF doClear() THEN {
        rezult() <-'\{"command": "clearPicklist"\}\n---\n';
    }
    
    rezult() <- CONCAT '', rezult(),
                    (GROUP CONCAT ('\{"command": "addPicklist","picklist": \{' + 
                        (CONCAT ',', 
                            '"code": "' + id(CustomGroup cg) + '"',
                            '"name": "' + escapeJSONValue(name(cg)) + '"',
                            '"image": "' + encodeBase64(imageArtix(cg)) + '"',
                            '"parent": ' + (OVERRIDE '"'+id(parent(cg))+'"', 'null'),
                            '"itemorder": ' + orderArtix(cg)
                        ) 
                    + '\}\}') IF export(cg), '\n---\n' ORDER level(cg), cg)
                +'\n---\n';
    rezult() <- CONCAT '', rezult(),
                    (GROUP CONCAT ('\{"command": "addPicklist","picklist": \{' + 
                        (CONCAT ',', 
                            '"code": "' + id(Sku s) + '"',
                            '"name": "' + escapeJSONValue(OVERRIDE overName(s), name(s)) + '"',
                            '"image": "' + encodeBase64(imageArtix(s)) + '"',
                            '"parent": "' + idParent(s) + '"',
                            '"tmccode": "' + idBarcode(s) + '"',
                            '"itemorder": ' + orderArtix(s)
                        ) 
                    + '\}\}') IF idParent(s), '\n---\n' ORDER s)
                +'\n---\n';
                
    IF isUseNewTaraMode(gm) AND (GROUP SUM 1 IF idParent(Sku s) AND isTara(s)) THEN {
        rezult() <- CONCAT '', rezult(),
                        ('\{"command": "addCatalog","catalog": \{' + 
                            (CONCAT ',', 
                                '"catalogid": "' + idCatalogTara() + '"',
                                '"catalogname": "Упаковка"',
                                '"type": 1'
                            )
                        + '\}\}') 
                    +'\n---\n';
        rezult() <- CONCAT '', rezult(),
                        (GROUP CONCAT ('\{"command": "addInventCatalog","inventcatalog": \{' + 
                            (CONCAT ',', 
                                '"tmccode": "' + id(Sku s) + '"',
                                '"catalogId": ' + idCatalogTara() + '',
                                '"tmcprice": ' + (GROUP MAX price(Barcode b) IF s == sku(b)) + '',
                                '"image": "' + encodeBase64(imageArtix(s)) + '"'
                            ) 
                        + '\}\}') IF idParent(s) AND isTara(s), '\n---\n' ORDER s)
                    +'\n---\n';
    }
                
    rezult() <- CONCAT '', rezult(), extraTextPickList();
    
    createFilesArtix(rezult(), directoryPickList(gm));

}

exportPickList 'Выгрузить' (GroupCashRegister gm) {       
    IF NOT directoryPickList(gm) THEN {
        MESSAGE name(gm) +': Не задан путь выгрузки пиклиста' NOWAIT;
        RETURN;
    }
    
    idParent(Sku s) <- id(customGroup(dataCustomGroupTypeArtix(gm), s)) IF (currentBalance(s, stock(gm)) OR skipFilterBalance(gm, s) OR skipFilterBalance()) 
                    AND customGroup(dataCustomGroupTypeArtix(gm), s) AND active(s); 
    
    export(CustomGroup cg) <- customGroupType(cg) == dataCustomGroupTypeArtix(gm) AND (GROUP SUM 1 IF isParent(cg, Sku s) AND idParent(s)); 
    
    beforeExport(gm);
    
    IF NOT (GROUP SUM 1 IF idParent(Sku s)) THEN {
        MESSAGE name(gm) +': Нет товаров к выгрузке' NOWAIT;
        RETURN;
    }
    
    NEWSESSION NESTED LOCAL {
        FOR idParent(Sku s) AND hasImage(s) AND NOT imageArtix(s) DO {
            convertImageArtix(s, (OVERRIDE smallImage(s), image(s)));
        }
        APPLY NESTED LOCAL;
    }
    
    createFilePickList(gm);
    
    MESSAGE name(gm) +': Пиклист выгружен' NOWAIT;
}

exportPickListAndClose 'Выгрузить' (GroupMachinery gm) {
    exportPickList(gm);
    formClose();
}

FORM exportParams 'Пиклист'
    OBJECTS gm = GroupCashRegister PANEL
    PROPERTIES () skipFilterBalance, doClear
    PROPERTIES (gm) exportPickListAndClose
;
DESIGN exportParams {
    NEW a{
        MOVE PROPERTY (skipFilterBalance());
        MOVE PROPERTY (doClear());
        NEW b{
            type = CONTAINERH;
            MOVE PROPERTY (exportPickListAndClose(gm));
            MOVE PROPERTY (formClose());
        }
    }
    REMOVE TOOLBARBOX ;
}

exportPickListForm 'Выгрузить Пиклист' (GroupCashRegister gm) {
    SHOW exportParams OBJECTS gm = gm;
}

EXTEND FORM groupCashRegister 
    PROPERTIES (grc) SHOWIF sidModel(grc) == 'Artix' nameCustomGroupTypeArtix, directoryPickList, exportPickListForm;
    
DESIGN groupCashRegister{
    topContainer{
        MOVE PROPERTY (nameCustomGroupTypeArtix(grc));
        MOVE PROPERTY (directoryPickList(grc));
        MOVE PROPERTY (exportPickListForm(grc));
    }
}

createAttribute(MachineryPriceTransaction mpt, GroupMachinery gm) + {
    IF sidModel(gm) == 'Artix' AND dataCustomGroupTypeArtix(gm) THEN {
        
        //выгрузка в пиклист
        idParent(Sku s) <- id(customGroup(dataCustomGroupTypeArtix(gm), s)) IF (currentBalance(s, stock(gm)) OR skipFilterBalance(gm, s) OR skipFilterBalance()) 
                        AND customGroup(dataCustomGroupTypeArtix(gm), s) AND active(s) AND (GROUP SUM 1 IF in(mpt, Barcode b) AND s == sku(b)); 
        
        beforeExport(gm);
         
        IF (GROUP SUM 1 IF idParent(Sku s)) THEN {
            createFilePickList(gm);
        }
    }
}
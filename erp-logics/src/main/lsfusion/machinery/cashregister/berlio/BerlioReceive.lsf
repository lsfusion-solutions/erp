MODULE BerlioReceive;

REQUIRE NativeMachinery, ItemBerlio, ZReportSection, GiftCard, ZReportFuelLtr, CashOperationCashOrder;

NAMESPACE Berlio;

CLASS KindOperation 'Вид операции в Берлио';
TABLE kindOperation(KindOperation);

id 'Код' = DATA INTEGER (KindOperation);
kindOperation = GROUP AGGR KindOperation k BY id(k);
name 'Наименование' = DATA STRING[40](KindOperation);

paymentType = DATA PaymentType(KindOperation);
namePaymentType 'Тип платежа' (KindOperation k) = name(paymentType(k));

FORM kindOperations 'Виды операций Берлио'
    OBJECTS k = KindOperation
    PROPERTIES (k) name, id, namePaymentType, NEW, DELETE
    LIST KindOperation OBJECT k 
;

NAVIGATOR {
    retailMasterData {
        NEW kindOperations;
    }
}

kindOperation = DATA KindOperation (Payment);
idKindOperation 'Вид операции' (Payment p) = id(kindOperation(p));

CLASS BerlioCard 'Карта Berlio';
TABLE berlioCard (BerlioCard);

number 'Номер' = DATA STRING[48] (BerlioCard) IN numbered INDEXED CHARWIDTH 13;
contract 'Договор' = DATA STRING[20] (BerlioCard);
berlioNumber = GROUP MAX BerlioCard dberlioCard BY number(dberlioCard);

FORM berlioCards 'Карты Берлио'
    OBJECTS k = BerlioCard
    PROPERTIES (k) number, NEW, DELETE
    LIST BerlioCard OBJECT k
;

NAVIGATOR {
    retailMasterData {
        NEW berlioCards;
    }
}

berlioCard = DATA BerlioCard (Receipt);
numberBerlioCard 'Номер карты Berlio' (Receipt receipt) = number(berlioCard(receipt));

berlioFile 'Имя файла Берлио' = DATA STRING[15](ZReport) INDEXED IN documentHeader;
dateTimeClose 'Время закрытия смены' = DATA DATETIME (ZReport) IN documentHeader;

EXTEND FORM zReports
    PROPERTIES READONLY berlioFile(z)
    PROPERTIES READONLY numberBerlioCard(b) BEFORE sumReceiptDetail(b)
    PROPERTIES READONLY idKindOperation(p)
;
EXTEND FORM zReport
    PROPERTIES READONLY berlioFile(z)
    PROPERTIES numberBerlioCard(b) BEFORE sumReceiptDetail(b)
    PROPERTIES idKindOperation(p)
;

dateStartlistFilesberlio 'Дата начиная с которой читать файлы в каталоге (YYMM)' = DATA BPISTRING[4]();
timeShiftOpenBerlio 'Время после которого переносить смену на следующий день' = DATA TIME ();
EXTEND FORM cashRegisterModel 
    PROPERTIES () SHOWIF sid(c) == 'berlio' dateStartlistFilesberlio, timeShiftOpenBerlio;

overImportPayment ABSTRACT LIST (Receipt, NUMERIC, NUMERIC, NUMERIC);
overImport ABSTRACT LIST (ZReport);

isGiftCard = ABSTRACT BOOLEAN (STRING);

receiveFromBerlio (GroupCashRegister gcr, STRING fn) {
    NEWSESSION {
        READ directory(gcr) +'/S/'+ fn TO file;

        LOCAL shiftDate, date = DATE (INTEGER);
        LOCAL time = TIME (INTEGER);
        LOCAL shiftIime = TIME ();
        LOCAL closeIime = DATETIME ();
        LOCAL vid, kind, receipt, npp, dep, skno, index = INTEGER (INTEGER);
        LOCAL shift, lDate, code, cardNum, item = STRING (INTEGER);
        LOCAL idZReport, idReceipt, idReceiptDetail, user = STRING (INTEGER);
        LOCAL qty, density, weight, price, sum, discountSum, discount, mixCash, mixBank, mixOther = NUMERIC(INTEGER);

        IMPORT DBF CHARSET 'cp866' FROM file() TO shift = SHIFTNO, lDate = DT, time = TM, code = CODE, vid = VID, kind = KIND, qty = REL,
                density = SOL, weight = RELKG, price = PRICEW, sum = 'SUM', discountSum = DISCSUM, discount = DISCOUNT,
                cardNum = CARDNUM, receipt = CHECKNUM, npp = KKM, dep = DEPNO, user = username, skno = SKNO, item = NOMNUM, mixCash = MIXCASH,
                mixBank = MIXBANK, mixOther = MIXOTHER;

        FOR [GROUP SUM 1 IF npp(INTEGER rr) > 0 BY npp(rr)](INTEGER npp) AND NOT cashRegisterNppGroupCashRegister(npp(gcr), npp) ORDER npp DO NEW c = CashRegister {
            groupCashRegister(c) <- gcr;
            npp(c) <- npp;
        }

        date(INTEGER r) <- toDateFormat(lDate(r), 'DD.MM.YYYY');
        index(INTEGER r) <- (PARTITION SUM 1 ORDER r BY npp(r), receipt(r)) WHERE imported(r) AND skno(r) > 0;
        FOR INTEGER open == (GROUP MIN INTEGER r IF code(r) == 'OPEN') DO {
            shiftDate(INTEGER r) <- IF time(open) >= timeShiftOpenBerlio() THEN sum(date(open), 1) ELSE date(open) WHERE imported(r);
            shiftIime() <- IF time(open) >= timeShiftOpenBerlio() THEN 00:00 ELSE time(open);
        }
        FOR INTEGER close == (GROUP MIN INTEGER r IF code(r) == 'CLOSE') DO {
            closeIime() <- dateTimeToDateTime(date(close), time(close));
        }

        mixCash(INTEGER r) <- -mixCash(r) WHERE vid(r) == 2;
        mixBank(INTEGER r) <- -mixBank(r) WHERE vid(r) == 2;
        mixOther(INTEGER r) <- -mixOther(r) WHERE vid(r) == 2;

        FOR [GROUP SUM 1 IF npp(INTEGER rr) == npp(CashRegister crr) BY crr](CashRegister cr) DO {

            idZReport(INTEGER r) <- (CONCAT '_', nppGroupMachinery(cr), npp(cr), shift(r), shiftDate(r)) IF npp(cr) == npp(r);
            idReceipt(INTEGER r) <- (CONCAT '_', idZReport(r), receipt(r)) IF index(r) AND npp(cr) == npp(r);
            idReceiptDetail(INTEGER r) <- (CONCAT '_', idReceipt(r), index(r)) IF index(r) AND npp(cr) == npp(r);

            FOR INTEGER rz == [GROUP MIN INTEGER rr BY idZReport(rr)](STRING idZreport) AND NOT zReport(idZreport) NEW z = ZReport DO {
                id(z) <- idZreport;
                isPosted(z) <- TRUE;
                date(z) <- shiftDate(rz);
                time(z) <- shiftIime();
                cashRegister(z) <- cr;
                number(z) <- shift(rz);
                berlioFile(z) <- fn;
                dateTimeClose(z) <- closeIime();

                FOR [GROUP SUM 1 IF kind(INTEGER r) == 2 BY cardNum(r)](STRING berlio) AND NOT berlioNumber(getWord(berlio, '/', 1)) NEW g = BerlioCard DO {
                    number(g) <- getWord(berlio, '/', 1);
                }
                FOR [GROUP SUM 1 IF kind(INTEGER r) == 2 BY cardNum(r)](STRING berlio) AND BerlioCard g == berlioNumber(getWord(berlio, '/', 1)) DO {
                    contract(g) <- getWord(berlio, '/', 2);
                }
                
                FOR [GROUP SUM 1 IF idReceipt(INTEGER rr) BY user(rr)](STRING user) AND NOT (GROUP SUM 1 IF user = shortName(Employee e)) NEW e = Employee DO {
                    lastName(e) <- getWord(user, ' ', 1);
                    initialsEmployee(e) <- right(user, -length(getWord(user, ' ', 1)) - 1) ;
                    position(e) <- position('berlio');
                    inactive(e) <- TRUE;
                }  

                FOR INTEGER r == [GROUP MIN INTEGER rr BY idReceipt(rr)](STRING idReceipt) AND NOT receipt(idReceipt) ORDER receipt(r) NEW rc = Receipt DO {
                    id(rc) <- idReceipt;
                    zReport(rc) <- z;
                    berlioCard(rc) <- berlioNumber(getWord(cardNum(r), '/', 1));
                    employee(rc) <- (GROUP MIN Employee e IF user(r) = shortName(e));
                    number(rc) <- receipt(r);
                    date(rc) <- date(r);
                    time(rc) <- time(r);
                }

                FOR INTEGER r == [GROUP MIN INTEGER rr BY idReceiptDetail(rr)](STRING idReceiptDetail) AND (vid(r) == 1 OR vid(r) == 2) AND NOT receiptGiftCardSaleDetail(idReceiptDetail)
                    AND isGiftCard(code(r))  ORDER index(r) NEW d = ReceiptGiftCardSaleDetail DO {
                    id(d) <- idReceiptDetail;
                    receipt(d) <- receipt(idReceipt(r));
                    isReturn(d) <- vid(r) == 2;
                    idBarcode(d) <- code(r);
                    price(d) <- price(r);
                    sum(d) <- IF vid(r) == 2 THEN -sum(r) ELSE sum(r);
                    section(d) <- section(STRING[100](dep(r)));
                }

                FOR INTEGER r == [GROUP MIN INTEGER rr BY idReceiptDetail(rr)](STRING idReceiptDetail) AND NOT receiptDetail(idReceiptDetail)
                    AND vid(r) == 1 AND NOT isGiftCard(code(r)) ORDER index(r) NEW d = ReceiptSaleDetail DO {
                    id(d) <- idReceiptDetail;
                    receipt(d) <- receipt(idReceipt(r));
                }

                FOR INTEGER r == [GROUP MIN INTEGER rr BY idReceiptDetail(rr)](STRING idReceiptDetail) AND NOT receiptDetail(idReceiptDetail)
                    AND vid(r) == 2 AND NOT isGiftCard(code(r)) ORDER index(r) NEW d = ReceiptReturnDetail DO {
                    id(d) <- idReceiptDetail;
                    receipt(d) <- receipt(idReceipt(r));
                }

                FOR INTEGER r == [GROUP MIN INTEGER rr BY idReceiptDetail(rr)](STRING idReceiptDetail) AND ReceiptDetail d == receiptDetail(idReceiptDetail) DO {
                    idBarcode(d) <- code(r);
                    sku(d) <- OVERRIDE itemIdBerlio(INTEGER(item(r))), itemIdBarcodeBerlio(INTEGER(code(r))), skuBarcode(code(r)) IF length(code(r)) > 3;
                    quantity(d) <- IF vid(r) == 2 THEN -qty(r) ELSE qty(r);
                    weight(d) <- weight(r) IF weight(r) != 0.0;
                    density(d) <- density(r) IF density(r) != 0.0;
                    price(d) <- price(r);
                    sum(d) <- sum(r);
                    discountPercent(d) <- -discount(r) IF discount(r) != 0.0;
                    discountSum(d) <- -discountSum(r) IF discountSum(r) != 0.0;
                    section(d) <- section(STRING[100](dep(r)));
                }

                //сертификат
                FOR [GROUP SUM 1 IF paymentType(kindOperation(kind(INTEGER r))) == minGiftCardPaymentType() OR (kind(r) == 88 AND mixOther(r) != 0) BY cardNum(r)](STRING giftCard) AND NOT giftCard(giftCard) NEW g = GiftCard DO {
                    id(g) <- giftCard;
                    number(g) <- giftCard;
                }

                FOR INTEGER r == [GROUP MIN INTEGER rr BY idReceipt(rr)](STRING idReceipt) AND paymentType(kindOperation(kind(r))) == minGiftCardPaymentType() AND NOT payment(idReceipt + '_' + kind(r)) NEW p = PaymentGiftCard DO {
                    id(p) <- idReceipt + '_' + kind(r);
                    receipt(p) <- receipt(idReceipt);
                    kindOperation(p) <- kindOperation(kind(r));
                    paymentType(p) <- minGiftCardPaymentType();
                    ZReport.sum(p) <- sumReceiptDetail(receipt(idReceipt));
                    giftCard(p) <- giftCard(cardNum(r));
                }

                //смешаная оплата
                FOR INTEGER r == [GROUP MIN INTEGER rr BY idReceipt(rr)](STRING idReceipt) AND kind(r) == 88 DO {
                    IF NOT payment(idReceipt + '_' + kind(r) + '_' + 1) AND mixCash(r) != 0 THEN NEW p = Payment {
                        id(p) <- idReceipt + '_' + kind(r) + '_' + 1;
                        receipt(p) <- receipt(idReceipt);
                        kindOperation(p) <- kindOperation(kind(r));
                        paymentType(p) <- minCashPaymentType();
                        sum(p) <- mixCash(r);
                    }
                    IF NOT payment(idReceipt + '_' + kind(r) + '_' + 2) AND mixBank(r) != 0 THEN NEW p = Payment {
                        id(p) <- idReceipt + '_' + kind(r) + '_' + 2;
                        receipt(p) <- receipt(idReceipt);
                        kindOperation(p) <- kindOperation(kind(r));
                        paymentType(p) <- minCardPaymentType();
                        sum(p) <- mixBank(r);
                    }
                    IF NOT payment(idReceipt + '_' + kind(r) + '_' + 3) AND mixOther(r) != 0 THEN NEW p = PaymentGiftCard {
                        id(p) <- idReceipt + '_' + kind(r) + '_' + 3;
                        receipt(p) <- receipt(idReceipt);
                        kindOperation(p) <- kindOperation(kind(r));
                        paymentType(p) <- minGiftCardPaymentType();
                        ZReport.sum(p) <- mixOther(r);
                        giftCard(p) <- giftCard(cardNum(r));
                    }
                    overImportPayment(receipt(idReceipt), mixCash(r), mixBank(r), mixOther(r));
                }

                //other
                FOR INTEGER r == [GROUP MIN INTEGER rr BY idReceipt(rr)](STRING idReceipt) AND (kind(r) != 88 AND NOT paymentType(kindOperation(kind(r))) == minGiftCardPaymentType()) AND NOT payment(idReceipt + '_' + kind(r)) NEW p = Payment DO {
                    id(p) <- idReceipt + '_' + kind(r);
                    receipt(p) <- receipt(idReceipt);
                    kindOperation(p) <- kindOperation(kind(r));
                    paymentType(p) <- paymentType(kindOperation(kind(r)));
                    sum(p) <- sumReceiptDetail(receipt(idReceipt));
                }

                //кассовые операции
                FOR vid(INTEGER r) == INTEGER(id(CashOperation.Operation o)) AND discountSum(r) != 0 AND idZReport(r) AND NOT cashDocument(idZReport(r) + '_' + vid(r) + '_' + r) ORDER date(r), time(r) DO {
                    IF allowIncomeCashOperation(o) THEN NEW ic = IncomeCashOperation {
                        id(ic) <- idZReport(r) + '_' + vid(r) + '_' + r;
                        date(ic) <- date(r);
                        time(ic) <- time(r);
                        cashRegister(ic) <- cr;
                        zReport(ic) <- z;
                        operation(ic) <- CashOperation.operation(STRING(vid(r)));
                        sumCash(ic) <- -discountSum(r);
                    } ELSE NEW oc = OutcomeCashOperation {
                        id(oc) <- idZReport(r) + '_' + vid(r) + '_' + r;
                        date(oc) <- date(r);
                        time(oc) <- time(r);
                        cashRegister(oc) <- cr;
                        zReport(oc) <- z;
                        operation(oc) <- CashOperation.operation(STRING(vid(r)));
                        sumCash(oc) <- discountSum(r);
                    }
                }

                overImport(z);
                
                IF NOT countReceipt(z) THEN DELETE z;
            }
        }

        APPLY NESTED LOCAL;
        IF NOT canceled() THEN MESSAGE fn +' обработан' NOWAIT;
    }
}

receiveFromBerlio (GroupCashRegister gcr) {
    listFiles(directory(gcr)+'/S');
    FOR ISTRING fn = fileName(INTEGER i) AND NOT fileIsDirectory(i) 
        AND right(fileName(i), 3) == 'dbf' AND INTEGER(substr(fileName(i), 2, 2)) == npp(gcr) 
        AND substr(fileName(i), 4, 4) >= (OVERRIDE dateStartlistFilesberlio(), '2508')
        AND NOT (GROUP SUM 1 IF fn == berlioFile(ZReport z)) ORDER fn DO {
            receiveFromBerlio (gcr, fn);
    }
}

receiveGroupMachinery(GroupMachinery g) + {
    IF isBerlio(g) AND directory(g) THEN {
        receiveFromBerlio(g);
    }
}
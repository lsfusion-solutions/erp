MODULE AstronSSC;

REQUIRE AstronCashRegisterPriceTransaction, ItemSSC, StopList;

NAMESPACE Astron;


//товар
imageAstron= DATA IMAGEFILE (Barcode);

WHEN CHANGED (image(Item i)) OR CHANGED(allBarcodes(i)) DO { 
    imageAstron(Barcode b) <- NULL WHERE sku(b) == i;
}
//группа
imageAstron 'Изображение для PowerPOS' = DATA IMAGEFILE (CustomGroup);

//группа касс
dataCustomGroupTypeAstron = DATA CustomGroupType(GroupCashRegister) INDEXED;
nameCustomGroupTypeAstron 'Тип дополнительного классификатора'(GroupCashRegister gs) = name(dataCustomGroupTypeAstron(gs));

testModeAstron 'Выгружать дополнительную информацию' = DATA BOOLEAN (GroupCashRegister);

TABLE customGroupGroupCashRegister(CustomGroup, GroupCashRegister);
id 'Код в PowerPOS' = DATA INTEGER (CustomGroup, GroupCashRegister);

TABLE maxId() NODEFAULT;
maxGroupId = (GROUP MAX id(CustomGroup g, GroupCashRegister gc)) MATERIALIZED TABLE maxId;

WHEN SET(customGroupType(CustomGroup g) == dataCustomGroupTypeAstron(GroupCashRegister gc)) AND NOT id(g, gc) DO {
    id(g, gc) <- maxGroupId() (+) 1 ;
}

del = DATA BOOLEAN (INTEGER, Sku, GroupCashRegister);

prevCustomGroup(CustomGroupType t, Sku s) = PREV(customGroup(t, s));

WHEN CHANGED(customGroup(CustomGroupType t, Sku s)) AND t == dataCustomGroupTypeAstron(GroupCashRegister g) DO {
    del(INTEGER r, s, g) <- TRUE WHERE r == id(prevCustomGroup(t, s), g);
    del(INTEGER r, s, g) <- NULL WHERE r == id(customGroup(t, s), g);
}

CONSTRAINT [GROUP SUM 1 IF INTEGER r == id(CustomGroup g, GroupCashRegister gc) BY r](INTEGER r) > 1 
    MESSAGE 'Данный код группы в PowerPOS уже используется';


EXTEND FORM customGroups
    OBJECTS gc= GroupCashRegister PANEL BEFORE g
    PROPERTIES name(gc) SELECTOR SHOWIF (GROUP SUM 1 IF dataCustomGroupTypeAstron(GroupCashRegister gcr) == t)
    FILTERS dataCustomGroupTypeAstron(gc) == t
    PROPERTIES READONLY id(g, gc), imageAstron(g)
;

DESIGN customGroups {
    OBJECTS {
        NEW filters{
            horizontal = TRUE;
            MOVE BOX (t);
            MOVE BOX (gc);
        }
        MOVE BOX (TREE treeGroups);
    }
}

EXTEND FORM customGroup
    PROPERTIES (g) imageAstron;

//выгрузка
encodeBase64 = FORMULA STRING 'regexp_replace(encode(($1),\'base64\'), E\'[\\n\\r]+\', \' \', \'g\' )';

updateImage = DATA LOCAL NESTED BOOLEAN (Barcode);
updateAllImagesAstron = DATA BOOLEAN ();

overAstronInfo1(MachineryPriceTransaction mpt, Barcode b) += 
    (CONCAT ', ',
        //свойства
        '"propertyGrpId":'+value(b) + ',' +
        '"propertyGrp":[\{"propertyGrpId":'+value(b)+',"propertyGrpName":"Свойства для '+value(b)+'"\}],' +
        (CONCAT ', ',
        '"numProperty":[' + (CONCAT ', ',
                '\{"numPropertyKey":13,"propertyGrpId":'+value(b)+',"numberId":101\}' IF antiTheftDevice(sku(b)),
                '\{"numPropertyKey":14,"propertyGrpId":'+value(b)+',"numberId":'+(100 + age(sku(b)))+'\}' IF ageVerify(sku(b)),
                '\{"numPropertyKey":22,"propertyGrpId":'+value(b)+',"numberId":101\}' IF requireQuantityManual(sku(b)),
                '\{"numPropertyKey":24,"propertyGrpId":'+value(b)+',"numberId":101\}' IF lowWeight(sku(b)),
                '\{"numPropertyKey":25,"propertyGrpId":'+value(b)+',"numberId":101\}' IF weightControlBypass(sku(b)),
                '\{"numPropertyKey":26,"propertyGrpId":'+value(b)+',"numberId":101\}' IF inaccurateReferenceWeight(sku(b)),
                '\{"numPropertyKey":27,"propertyGrpId":'+value(b)+',"numberId":101\}' IF hasPromotionPrice(sku(b), stock(mpt)),
                '\{"numPropertyKey":28,"propertyGrpId":'+value(b)+',"numberId":101\}' IF requireProductSelection(sku(b))
            ) +']',
        '"numbers":[' + (CONCAT ', ',
                '\{"numberId":101,"numberValue":1,"numberName":"Да"\}' IF requireQuantityManual(sku(b)) OR lowWeight(sku(b)) OR weightControlBypass(sku(b)) OR hasPromotionPrice(sku(b), stock(mpt)),
                '\{"numberId":'+(100 + age(sku(b)))+',"numberValue":'+age(sku(b))+',"numberName":"'+age(sku(b))+' лет"\}' IF ageVerify(sku(b))
            ) +']',
        '"binProperty":[' + (CONCAT ', ',
                '\{"binPropertyKey":1,"propertyGrpId":'+value(b)+',"binaryDataId":'+value(b)+'\}' IF imageAstron(b)
            ) +']',
        '"binaryData":[' + (CONCAT ', ',
                '\{"binaryDataId":'+value(b)+',"binaryDataValue":"'+encodeBase64(imageAstron(b))+'","binaryDataName":"'+value(b)+'"\}' IF updateImage(b) OR updateAllImagesAstron()
            ) +']'
        ),
        //группы
        ('"extGrp":['+(GROUP CONCAT ('\{'+ (CONCAT ',','"extGrpId":'+id(CustomGroup g, groupMachinery(mpt)),
                '"sareaId":'+nppGroupMachinery(mpt),
                '"parentExtGrpId":'+id(parent(g), groupMachinery(mpt)),
                '"extGrpName":"'+escapeJSONValue(name(g))+'"',
                '"extGrpPicture":"'+(encodeBase64(imageAstron(g)))+'"') + '\}') 
                    IF (isParent(g, sku(b)) OR del(id(g, groupMachinery(mpt)), sku(b), groupMachinery(mpt))) AND customGroupType(g) == dataCustomGroupTypeAstron(groupMachinery(mpt)), ', ' ORDER g) + '],'+
        '"artExtGrp":['+(CONCAT ',', ('\{"extGrpId":'+id(customGroup(dataCustomGroupTypeAstron(groupMachinery(mpt)),sku(b)), groupMachinery(mpt)) + '\}'), 
                                     (GROUP CONCAT ('\{"extGrpId":'+ INTEGER  r+ ',"delFlag":1\}') IF del(r, sku(b), groupMachinery(mpt)), ',' ORDER r) 
                        )+']'
        ) IF customGroup(dataCustomGroupTypeAstron(groupMachinery(mpt)),sku(b))
    )
;

skipLoadImages 'Не выыгружать изображдения' = DATA BOOLEAN (CashRegisterModel);
EXTEND FORM cashRegisterModel
    PROPERTIES (c) SHOWIF sid(c) LIKE 'Astron%' skipLoadImages;

beforeCreateAstronPriceTransaction(MachineryPriceTransaction mpt) + {
    IF dataCustomGroupTypeAstron(groupMachinery(mpt)) AND NOT skipLoadImages(model(groupMachinery(mpt)))THEN {

        LOCAL needResize = BOOLEAN(Barcode);
        needResize(Barcode b) <- TRUE IF GROUP SUM 1 IF in(mpt, b) AND customGroup(dataCustomGroupTypeAstron(groupMachinery(mpt)), sku(b))
            AND hasImage(sku(b)) AND NOT imageAstron(b);

        IF (GROUP SUM 1 IF needResize(Barcode b)) THEN NEWSESSION NESTED LOCAL {
            FOR needResize(Barcode b) DO {
                TRY {
                    resizedImage() <- NULL;
                    resizeImage(image(sku(b)), 128, 128);
                    convertedImage() <- NULL;
                    convertImage(resizedImage(), 'png');
                    imageAstron(b) <- convertedImage();
                }
                updateImage(b) <- TRUE IF imageAstron(b);
            }
            APPLY NESTED LOCAL;
        }
    }
}

loadImages 'Загрузить классификатор с картинками в кассы' (GroupCashRegister gtc) {
    NEWSESSION {
        createMachineryPriceTransactionSnapshot() <- TRUE;
        createMachineryPriceTransactionComment() <- 'Загрузка изображений';

        createMachineryPriceTransaction(Sku sku) <- TRUE IF customGroup(dataCustomGroupTypeAstron(gtc), sku) AND hasImage(sku);
        exclude(GroupMachinery group) <- stock(group) == stock(gtc) AND group != gtc;
        createMachineryPriceTransaction(stock(gtc));

        APPLY NESTED LOCAL;
        IF canceled() THEN {
            cancel();
        }
    }
}

clearBalance 'Удалить товары без остатка из пик-листа' (GroupCashRegister gcr) {

    EXTERNAL SQL directory(gcr) EXEC 'SELECT artid, extgrpid FROM artextgrp WHERE delflag = 0' TO file;

    LOCAL artid, extgrpid = INTEGER (INTEGER);
    LOCAL item = Item (INTEGER);
    LOCAL delete = BOOLEAN (INTEGER);

    IMPORT TABLE FROM file() TO artid, extgrpid;

    extgrpid(INTEGER r) <- extgrpid(r) IF GROUP SUM 1 IF id(CustomGroup g, gcr) == extgrpid(r);

    FOR INTEGER(id(Item i)) == artid(INTEGER r) DO {
        item(r) <- i;
    }

    FOR Item i == item(INTEGER r) AND id(customGroup(dataCustomGroupTypeAstron(gcr), i), gcr) == extgrpid(r) DO {
        delete(r) <- TRUE WHERE inStopList(i, stock(gcr), currentDateTime()) OR NOT currentBalance(i, stock(gcr)) > 0;
    }

    LOCAL recordnum = INTEGER ();

    EXTERNAL SQL directory(gcr) EXEC 'SELECT recordnum FROM datapump WHERE lower(dirname) = \'artextgrp\'' TO file;
    IMPORT TABLE FROM file() TO () recordnum;

    recordnum() <- recordnum() (+) 1;
 
    LOCAL query = TEXT();
    query() <- 'BEGIN;';
    query() <- CONCAT'\n', query(), GROUP CONCAT ('UPDATE artextgrp SET delflag = 1, updatenum = ' + recordnum() + ' WHERE artid =' + artid(INTEGER r) + ' AND extgrpid = ' + extgrpid(r) + ';') IF delete(r), '\n' ORDER r;
    query() <- CONCAT'\n', query(), 'UPDATE datapump SET recordnum = ' + recordnum() + ' WHERE lower(dirname) = \'artextgrp\';';
    query() <- CONCAT'\n', query(), 'COMMIT;';

    EXTERNAL SQL directory(gcr)
        EXEC query();

}

EXTEND FORM groupCashRegister
    PROPERTIES (grc) testModeAstron SHOWIF sidModel(grc) LIKE 'Astron%'
    PROPERTIES (grc) SHOWIF (sidModel(grc) LIKE 'Astron%' AND testModeAstron(grc)) nameCustomGroupTypeAstron, loadImages, clearBalance;

DESIGN groupCashRegister{
    topContainer{
        MOVE PROPERTY (testModeAstron(grc));
        MOVE PROPERTY (nameCustomGroupTypeAstron(grc));
        MOVE PROPERTY (loadImages(grc));
        MOVE PROPERTY (clearBalance(grc));
    }
}

MODULE AstronCashRegisterPriceTransaction;

REQUIRE CashRegisterPriceTransaction, LotTypeBy, EDIGTIN;

NAMESPACE Astron;


sendGTIN 'Выгружать GTIN' = DATA BOOLEAN (GroupCashRegister);
sendLotType 'Выгружать признак маркировки' = DATA BOOLEAN (GroupCashRegister);

EXTEND FORM groupCashRegister
    PROPERTIES (grc) SHOWIF sidModel(grc) == 'Astron' sendGTIN, sendLotType;

DESIGN groupCashRegister {
    topContainer {
        MOVE PROPERTY (sendGTIN(grc));
        MOVE PROPERTY (sendLotType(grc));
    }
}


overAstronInfo1 = ABSTRACT TEXT (MachineryPriceTransaction, Barcode);
overAstronInfo2 = ABSTRACT TEXT (MachineryPriceTransaction, Barcode);
localAstronInfo = DATA LOCAL TEXT (MachineryPriceTransaction, Barcode);

beforeCreateAstronPriceTransaction ABSTRACT LIST (MachineryPriceTransaction);

astronInfo2(MachineryPriceTransaction mpt, Barcode b) = (JSON FROM
    'astron2' = JSON FROM
        'numbers' = JSON FROM
            'numberId' = INTEGER numberId,
            'numberValue' = CASE 
                    WHEN numberId == 103 THEN 1 
                    WHEN numberId == 104 THEN 2 
                    WHEN numberId == 105 THEN 3 
                    ELSE 0,
            'numberName' = CASE 
                    WHEN numberId == 103 
                    THEN 'подлежит маркировке СИ' 
                    WHEN numberId == 104 THEN 'подлежит УКЗ' 
                    WHEN numberId == 105 THEN 'подлежит маркировке и СИ и УКЗ'     
                ELSE 'не подлежит маркировке'
            WHERE iterate (numberId, 102, 105),
        'numProperty' = JSON FROM
            'numPropertyKey' = INTEGER numPropertyKey,
            'propertyGrpId' = value(b),
            'numberId' = CASE
                    WHEN idLotType(sku(b)) != 'ukz' AND UKZ(sku(b)) THEN 105
                    WHEN idLotType(sku(b)) == 'ukz' OR UKZ(sku(b)) THEN 104
                    WHEN idLotType(sku(b)) THEN 103
                    ELSE 102
            WHERE numPropertyKey == 29,
        'propertyGrp' = JSON FROM
            'propertyGrpId' = INTEGER propertyGrpId,
            'propertyGrpName' = 'Свойства для '+value(b)
            WHERE propertyGrpId == value(b),
        'propertyGrpId' = value(b)
    ) IF in(mpt, b) AND sendLotType(groupMachinery(mpt));

gtinAstron(Sku s) = IF lotType(s) OR UKZ(s) THEN lpad(GTIN(s), 14, '0') ELSE GTIN(s);
createAttribute(MachineryPriceTransaction mpt) + {
    IF sidModel(groupMachinery(mpt)) == 'Astron' THEN {
        localAstronInfo(mpt, Barcode b) <- NULL; //очищаем в первом действии, следить чтобы дальше добавлялись позже

        localAstronInfo(mpt, Barcode b) <- (CONCAT ',', localAstronInfo(mpt, b), '"gtin":"' + gtinAstron(sku(b)) + '"')
            WHERE in(mpt, b) AND sendGTIN(groupMachinery(mpt));
    }
}

//стараемся выполнять последним
createAttribute(MachineryPriceTransaction mpt, GroupMachinery gm) + {
    IF sidModel(gm) == 'Astron' THEN {
        beforeCreateAstronPriceTransaction(mpt);
        info(mpt, Barcode b) <- TEXT(merge((OVERRIDE JSON('\{"astron":\{'+
                (CONCAT ',',
                    overAstronInfo1(mpt, b),
                    overAstronInfo2(mpt, b),
                    localAstronInfo(mpt, b)
                )
                +'\}\}'), JSON('\{\}')),
            (OVERRIDE astronInfo2(mpt, b), JSON('\{\}'))
        )) WHERE in(mpt, b);
    }
} 
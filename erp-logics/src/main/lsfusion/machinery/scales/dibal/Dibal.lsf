MODULE Dibal;

REQUIRE ScalesPriceTransaction, Image, Item;

dataCustomGroupType = DATA CustomGroupType(GroupScales);
customGroupType = ABSTRACT CustomGroupType(GroupScales);
customGroupTypeDibal(GroupScales gs) = OVERRIDE dataCustomGroupType(gs), customGroupType(gs);
nameCustomGroupTypeDibal 'Тип классификатора'(GroupScales gs) = name(customGroupTypeDibal(gs));
EXTEND FORM groupScales 
    PROPERTIES (grs) nameCustomGroupTypeDibal SHOWIF sidModel(grs) == 'Dibal';

overId = ABSTRACT STRING[100] (CustomGroup);
idDibal(CustomGroup cg) = OVERRIDE overId(cg), id(cg);

createAttribute(MachineryPriceTransaction mpt) + {
    IF sidModel(groupScales(mpt)) == 'Dibal' THEN {
        LOCAL custGroup = CustomGroup (Barcode);
        custGroup(Barcode b) <- customGroup(customGroupTypeDibal(groupMachinery(mpt)), sku(b)) WHERE in(mpt, b);
        
        info(mpt, Barcode b) <- ('\{"Dibal":\{'+
                (CONCAT ',',
                    (' "numberGroup": "' + (OVERRIDE idDibal(custGroup(b)), '1') +
                    '", nameGroup: "' + (OVERRIDE name(custGroup(b)),'Все') + '"')
                    )
                +'\}\}') WHERE in(mpt, b);
    }
}


//ip, indexImage, imagePath
sendImageToScale 'Send image to single scale'  INTERNAL  'lsfusion.erp.machinery.scales.dibal.DibalSendImageToScaleAction' (STRING, INTEGER, STRING);

//indexImage: 1-99 = группы, 100 и выше = товары (plu + 100)
sendGroupImageToScale(STRING ip, INTEGER indexImage, STRING imagePath) { sendImageToScale(ip, indexImage, imagePath); }
sendItemImageToScale(STRING ip, INTEGER indexImage, STRING imagePath) { sendImageToScale(ip, indexImage + 100, imagePath); }
MODULE Digi;

REQUIRE ScalesPriceTransaction, Image, Item;

writeFileToSocketResult = DATA LOCAL BOOLEAN ();
//Для Digi необходимо прогрузить 2 файла: 0x34 = 52, 0x38 = 56
writeFileToSocket (file, type, ip)  INTERNAL  'lsfusion.erp.machinery.scales.WriteFileToSocketAction' (FILE, INTEGER, STRING[30]);


//DigiSM5300
overImage = ABSTRACT IMAGEFILE (Barcode);
image = OVERRIDE overImage(Barcode b), image(sku(b));

overPluNumber = ABSTRACT INTEGER (Barcode, Stock);
pluNumber = OVERRIDE overPluNumber(Barcode b, Stock s), plu(b, s);

loadImages 'Загрузить картинки в весы' () {
    NEWSESSION NESTED (select[Sku,Stock]) {
        IF (GROUP SUM 1 IF select(Sku sku, Stock stock)) THEN {
            FOR [ GROUP SUM 1 IF select(sku(Barcode barcode), Stock stock) AND length(id(barcode)) < 6 AND hasImage(sku(barcode)) BY stock](Stock stock) DO {
                FOR stock(Scales scales) == stock AND active(groupScales(scales)) AND active(scales) AND sidModel(groupScales(scales)) == 'DigiSM5300' DO {
                    TRY {
                        mkdir('ftp://root:teraoka@' + port(scales) + '/../opt/pcscale/files/img/plu/');
                    }
                    FOR select(sku(Barcode barcode), stock) AND pluNumber(barcode, stock) AND length(id(barcode)) < 6 AND hasImage(sku(barcode)) DO {
                        TRY {
                            convertImage(image(barcode), 'bmp');
                            WRITE convertedImage() TO 'ftp://root:teraoka@' + port(scales) + '/../opt/pcscale/files/img/plu/plu' + pluNumber(barcode, stock) + '?binarytransfermode=false';
                        } CATCH {
                            //logToFile('scales','ERROR WRITE TO ' + 'ftp://root:teraoka@' + port(scales) + '/../opt/pcscale/files/img/plu/plu' + (OVERRIDE STRING (pluNumber(barcode, stock)),'NULL'));
                            MESSAGE 'Ошибка загрузки изображений в весы' NOWAIT;
                            BREAK;
                        }
                    }
                }
            }
            IF NOT notResetMachinerySelectSku() THEN {
                select(Sku sku, Stock stock) <- NULL;
            }
        }
    }
} TOOLBAR;

deleteImages 'Удалить картинки из весов' () {
    NEWSESSION NESTED (select[Sku,Stock]) {
        IF (GROUP SUM 1 IF select(Sku sku, Stock stock)) THEN {
            FOR [ GROUP SUM 1 IF select(sku(Barcode barcode), Stock stock) AND length(id(barcode)) < 6 AND hasImage(sku(barcode)) BY stock](Stock stock) DO {
                FOR stock(Scales scales) == stock AND active(groupScales(scales)) AND active(scales) AND sidModel(groupScales(scales)) == 'DigiSM5300' DO {
                    FOR select(sku(Barcode barcode), stock) AND pluNumber(barcode, stock) AND length(id(barcode)) < 6 AND hasImage(sku(barcode)) DO {
                        TRY {
                            delete('ftp://root:teraoka@' + port(scales) + '//opt/pcscale/files/img/plu/Plu' + pluNumber(barcode, stock));
                        }
                    }
                }
            }
            IF NOT notResetMachinerySelectSku() THEN {
                select(Sku sku, Stock stock) <- NULL;
            }
        }
    }
} TOOLBAR;

EXTEND  FORM currentBalanceSkuStock
    PROPERTIES() SHOWIF (GROUP SUM 1 IF sidModel(GroupScales grsk) == 'DigiSM5300' AND stock(grsk) == ss) loadImages DRAW sts, deleteImages DRAW sts
;
DESIGN currentBalanceSkuStock {
    machineryContainer {
        MOVE PROPERTY (loadImages());
        MOVE PROPERTY (deleteImages());
    }
}

customGroupType = ABSTRACT CustomGroupType(GroupScales);
tareWeight = ABSTRACT NUMERIC[12,3] (Barcode);

overId = ABSTRACT STRING[100] (CustomGroup);
idDigi(CustomGroup cg) = OVERRIDE overId(cg), id(cg);

createAttribute(MachineryPriceTransaction mpt) + {
    IF sidModel(groupScales(mpt)) == 'DigiSM5300' THEN {
        LOCAL custGroup = CustomGroup (Barcode);
        custGroup(Barcode b) <- customGroup(customGroupType(groupMachinery(mpt)), sku(b)) WHERE in(mpt, b);
        LOCAL newPluNumber = INTEGER (Barcode);
        newPluNumber(Barcode b) <- PARTITION SUM 1 IF id(b) AND active(b) ORDER b BY custGroup(b) WHERE in(mpt, b);
        
        info(mpt, Barcode b) <- ('\{"digism5300":\{'+
                (CONCAT ',',
                    (' "numberGroup": "' + (OVERRIDE idDigi(custGroup(b)), '') +
                    '", nameGroup: "' + (OVERRIDE name(custGroup(b)),'')+ 
                    '", overPluNumber: "' + newPluNumber(b) + '"'),
                    '"tareWeight":'+tareWeight(b))
                +'\}\}') WHERE in(mpt, b);
    }
}

imageDigi 'Изображение для Digi' = DATA IMAGEFILE (CustomGroup);
loadImagesGroup 'Загрузить картинки в весы' (GroupScales gm) {
    NEWSESSION {
        FOR gm == groupScales(Scales scales) AND active(scales) DO {
            TRY {
                mkdir('ftp://root:teraoka@' + port(scales) + '/../opt/pcscale/files/img/common/');
            }
            FOR idDigi(CustomGroup cg) AND customGroupType(cg) == customGroupType(gm) DO {
                TRY {
                    convertImage(imageDigi(cg), 'bmp');
                    WRITE convertedImage() TO 'ftp://root:teraoka@' + port(scales) + '/../opt/pcscale/files/img/common/' + idDigi(cg) + '?binarytransfermode=false';
                } CATCH {
                    //logToFile('scales','ERROR WRITE TO ' + 'ftp://root:teraoka@' + port(scales) + '/../opt/pcscale/files/img/plu/plu' + (OVERRIDE STRING (pluNumber(barcode, stock)),'NULL'));
                    MESSAGE 'Ошибка загрузки изображений в весы' NOWAIT;
                    BREAK;
                }
            }
        }
    }
} TOOLBAR;

EXTEND FORM customGroups
    PROPERTIES imageDigi(g) READONLY;
    
EXTEND FORM customGroup
    PROPERTIES imageDigi(g) ;
    
EXTEND FORM groupScales 
    PROPERTIES loadImagesGroup(grs) SHOWIF sidModel(grs) == 'DigiSM5300';
    
MODULE CL7000;

REQUIRE ScalesSelfService, Image;

//CL7000

imageCAS = DATA IMAGEFILE (Item) TABLE itemImage;
WHEN CHANGED (image(Item i)) DO { 
    imageCAS(i) <- NULL;
}

migrateToFont = DATA BOOLEAN ();
onStarted() +{
    IF NOT migrateToFont() THEN {
        NEWSESSION {
            migrateToFont() <- TRUE;
            imageCAS(Item i) <- NULL;
            APPLY;
        }
    }
}

toLoadImages = DATA LOCAL BOOLEAN ();
createAttribute(MachineryPriceTransaction mpt, GroupMachinery gm) + {
    IF sidModel(gm) == 'CasCL7000' AND toLoadImages() THEN {
        LOCAL needResize = BOOLEAN(Item);
        needResize(Item i) <- TRUE IF (GROUP SUM 1 IF in(mpt, Barcode b) AND i == sku(b)) AND hasImage(i) AND NOT imageCAS(i);
    
        IF (GROUP SUM 1 IF needResize(Item i)) THEN NEWSESSION NESTED LOCAL {
            resizeImage(IMAGEFILE (decode('Qk06AAAAAAAAADYAAAAoAAAAAQAAAAEAAAABABgAAAAAAAQAAAAAAAAAAAAAAAAAAAAAAAAA////AA==','base64')), 140, 132);
            LOCAL background = IMAGEFILE ();
            background() <- resizedImage();
            FOR needResize(Item s) DO {
                TRY {
                    resizeImageMaxSize(image(s),132);
                    mergeImages(background(),resizedImage());
                    imageCAS(s) <- mergedImage();
                }
            }
            APPLY NESTED LOCAL;
        }
        itemImage (mpt, Barcode b) <- imageCAS(sku(b)) WHERE in(mpt, b);
    }
}

loadImages() + {
    NEWSESSION NESTED (select[Sku,Stock]) {
        IF (GROUP SUM 1 IF select(Sku sku, Stock stock)) THEN {
            FOR [GROUP SUM 1 IF select(Sku sku, Stock stock) BY stock](Stock stock) AND 
                (GROUP SUM 1 IF sidModel(GroupScales grsk) == 'CasCL7000' AND stock(grsk) == stock AND active(grsk)) DO {
                          
                createMachineryPriceTransactionSnapshot() <- TRUE;
                createMachineryPriceTransactionComment() <- 'Загрузка изображений';
                toLoadImages() <- TRUE;
                
                createMachineryPriceTransaction(Sku sku) <- TRUE IF select(sku, stock);
                exclude(GroupMachinery group) <- stock(group) == stock AND NOT sidModel(group) == 'CasCL7000';
                createMachineryPriceTransaction(stock);
            }
            APPLY NESTED LOCAL;
            IF canceled() THEN {
                cancel();
            }
        }
    }
}
showLoadImage(Stock ss) += (GROUP SUM 1 IF sidModel(GroupScales grsk) == 'CasCL7000' AND stock(grsk) == ss AND active(grsk)) > 0;

isSelfService(GroupScales s) += sidModel(s) == 'CasCL7000';

createAttribute(MachineryPriceTransaction mpt) + {
    IF sidModel(groupScales(mpt)) == 'CasCL7000' THEN {
        
        info(mpt, Barcode b) <- ('\{"CL7000":\{'+
                (CONCAT ',',
                    (' "numberGroup":' + idPart(groupMachinery(mpt), customGroup(customGroupType(groupMachinery(mpt)), sku(b))) +
                    ', nameGroup: "' + name(customGroup(customGroupType(groupMachinery(mpt)), sku(b))) + '"'
                    )
                )
                +'\}\}') WHERE in(mpt, b);
    }
}

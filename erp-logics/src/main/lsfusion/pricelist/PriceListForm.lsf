MODULE PriceListForm;

REQUIRE PriceListLedger, StockMovement;

NAMESPACE PriceList;
// -------------------------- Текущие цены -------------------- //

FORM currentPrices 'Текущие цены'
    
    OBJECTS pt = PriceListType
    PROPERTIES(pt) READONLY name
     
    OBJECTS s = Stock PANEL
    PROPERTIES(s) name SELECTOR
    FILTERS isCompany(s)
        
    OBJECTS dt = DATETIME PANEL
    PROPERTIES(dt) VALUE

    TREE skuTree sg = SkuGroup PARENT parent(sg)
    PROPERTIES READONLY order(sg), skuTreeName = name(sg)
    ORDER order(sg), skuTreeName
    FILTERGROUP inactive FILTER 'Активные' active(sg) 'F5' DEFAULT
     
    OBJECTS sk = Sku 
    PROPERTIES(sk) READONLY name ON CONTEXTMENU showMovementSku(sk, s) ON CONTEXTMENU showMovementPriceSku(sk, s), idBarcode, id SHOWIF showIDs()
    PROPERTIES(sk, s, dt) READONLY balanceA
    PROPERTIES(pt, sk, s, dt) READONLY priceA, nameCompanyA , descriptionA 
    ORDER name(sk)
    FILTERS isParent(sg, sk),
            priceA(pt, sk, s, dt)
           
    OBJECTS pp = PriceListType
    PROPERTIES(pp) READONLY name, nameCurrency 
    PROPERTIES(pp, sk, s, dt) READONLY priceA
    FILTERS  priceA(pp, sk, s, dt)
    
    TREE skuTree2 sg2 = SkuGroup PARENT parent(sg2)
    PROPERTIES READONLY order(sg2), skuTreeName2 = name(sg2)
    ORDER order(sg2), skuTreeName2
    FILTERGROUP inactive1 FILTER 'Активные' active(sg2) 'F5' DEFAULT

    OBJECTS b = Batch
    PROPERTIES(b) READONLY isPosted, date, time, id SHOWIF showIDs(), nameSku, nameStock, name, cost
    PROPERTIES(b, s, dt) READONLY balanceA
    ORDER date(b), time(b)
    PROPERTIES(pt, b, s, dt) READONLY priceA, nameCompanyA , descriptionA 
    FILTERGROUP filters
    FILTER 'Проведенные партии' isPosted(b) 'F10' DEFAULT
    FILTERS isParent(sg2, b),
            priceA(pt, b, s, dt)
    
    OBJECTS pb = PriceListType
    PROPERTIES(pb) READONLY name, nameCurrency
    PROPERTIES(pb, b, s, dt) READONLY priceA
    FILTERS  priceA(pb, b, s, dt)
;
@extendFormFilterStockAccess(s, currentPrices);

DESIGN currentPrices {
     NEW pane {
         fill = 1;
         type = CONTAINERV;
             NEW firstContainer{
                type = CONTAINERV;
                NEW topContainer{
                    type = CONTAINERH;
                    MOVE BOX(s);
                    MOVE BOX(dt){
                        caption = 'Дата/время';
                    }
                }
             }
             NEW rightContainer {
                 fill = 1;
                 type = CONTAINERV;
                 NEW bottomConteiner {
                     fill = 1;
                     type = SPLITH;
                     NEW leftPriceType {
                         type = CONTAINERH;
                         fill = 0.2;
                         MOVE BOX(pt) {
                            caption = 'Виды цен';
                         }
                     }
                     NEW priceContainer{
                         fill = 1;
                         caption = 'Текущие цены';
                         NEW currentPriceContainer {
                             fill = 1;
                             type = TABBED;
                             NEW skuContainer {
                                 fill = 1;
                                 caption = 'Товары';
                                 type = SPLITH;
                                 MOVE BOX(TREE skuTree);
                                 MOVE BOX(sk) {
                                     fill = 3;
                                     MOVE BOX(pp);
                                 }
                             }
                             NEW batchContainer {
                                fill = 1;
                                caption = 'Партии';
                                type = SPLITH;
                                MOVE BOX(TREE skuTree2);
                                MOVE BOX(b) {
                                   fill = 3;
                                   MOVE BOX(pb);
                                }
                             }
                         }
                     }
                 }   
             }
             
         
     }
     MOVE TOOLBARBOX;
}

NAVIGATOR {
    priceListDocuments {
        NEW currentPrices;
    }
}
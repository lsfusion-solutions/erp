MODULE PriceListItem;

REQUIRE PriceList, Item, PriceListAccount;

NAMESPACE PriceList;

//Производитель
filterManufacturer = DATA LOCAL Manufacturer (UserPriceList);
nameFilterManufacturer 'Производитель' (UserPriceList userPriceList) = name(filterManufacturer(userPriceList)); 
   
filterManufacturer (UserPriceList userPriceList, Item sku) =
    filterManufacturer(userPriceList) == manufacturer(sku) OR sku IS Item AND NOT filterManufacturer(userPriceList);    
   
filterManufacturer (UserPriceList userPriceList, Batch batch) =
    filterManufacturer(userPriceList) == manufacturer(batch) OR batch IS Batch AND NOT filterManufacturer(userPriceList);      

EXTEND FORM userPriceList
    PROPERTIES(p)  nameFilterManufacturer
    FILTERS filterManufacturer(p, s2)
    FILTERS filterManufacturer(p, b)
;

DESIGN userPriceList {
    filtersContainer {
        MOVE PROPERTY(nameFilterManufacturer(p));
    }
}
                 
currentPrice 'Текущая цена' (LedgerPriceListType type, Sku sku, Stock stock) = prevActivePriceA(type, sku, stock, currentDateTime());
currentActiveLedgerPriceA (LedgerPriceListType type, Sku sku, Stock stock) = activePriceListLedgerA(type, sku, stock, currentDateTime());     

changePrice(LedgerPriceListType type, Sku sku, Stock stock)  {   //-- todo: что делать если нет детейла?
    INPUT n = NUMERIC[16,4] DO
        IF n THEN {
            IF currentActiveLedgerPriceA(type, sku, stock) THEN {
                price(PriceListLedger ledger, type) <- n WHERE ledger == currentActiveLedgerPriceA(type, sku, stock);
            }    
        } ELSE {
            DELETE PriceListLedger ledger WHERE ledger == currentActiveLedgerPriceA(type, sku, stock);
        }
}                
notePriceLIst 'Примечание' = note(priceList(PriceListDetail d)) CHARWIDTH 30;

EXTEND FORM item
    OBJECTS tc = DataPriceListType GRID
        FILTERGROUP filters
            FILTER 'С текущей ценой' currentPrice  (tc, i, st2) 'F10'
            
    PROPERTIES  READONLY name(tc), includeVAT[PriceListType](tc), nameCurrency(tc), nameRoundCondition(tc) 
    PROPERTIES(tc, i, st2)  currentPrice ON CHANGE  changePrice(tc, i, st2) 
                              
    OBJECTS pd = PriceListDetail
    PROPERTIES(pd) READONLY 
           isPosted , seriesNumber, dateTime, 
           fromDate, fromTime, toDate, toTime, 
           nameCompany, nameCompanyStock, priceListTypes, stocks, notePriceLIst
    ORDERS dateTime(pd)
           
    FILTERS sku(pd)==i
    FILTERGROUP inactivePriceList
        FILTER 'Текущие' isActive(pd) 'F11'            
        FILTER 'Активные' isActive(pd) OR (fromDateTime(pd) > currentDateTime()) 'F10' DEFAULT                      
;
DESIGN item {
    calcPriceListTypePrm {
        MOVE BOX(tc);        
    }
    itemDetail {
        MOVE BOX(pd) {caption = 'Прайс';}
    }
}

showInfo 'Показывать в информации по товару' = DATA BOOLEAN (LedgerPriceListType);

EXTEND FORM options
    OBJECTS plt = LedgerPriceListType
    PROPERTIES(plt) READONLY id, name, objectClassName
    PROPERTIES(plt) showInfo
;

DESIGN options {
    items {
        MOVE BOX(plt);
    }
}

currentPrice 'Текущая цена' (CalcPriceListType type, Sku sku, Stock stock) = priceA(type, sku, stock, currentDateTime());

EXTEND FORM itemInfo
    OBJECTS t = LedgerPriceListType
    
    PROPERTIES READONLY DRAW st currentPrice(t,s,st) COLUMNS (t) HEADER name(t)
    FILTERS showInfo(t)
    
    PROPERTIES accountPriceA(b,st1)
    
    OBJECTS t1 = LedgerPriceListType
    PROPERTIES READONLY name(t1), objectClassName(t1), currentPrice(t1,s,st1)
    FILTERS currentPrice(t1,s,st1)
    
    OBJECTS pp = (t2 = LedgerPriceListType, pl = PriceListLedger, st2 = Stock)
    PROPERTIES READONLY  name(t2), description(pl),  fromDateTime(pl, t2, st2), toDateTime(pl, t2, st2), price(pl, t2, st2)
    PROPERTIES(pl) EDIT
    ORDERS name(t2), fromDateTime(pl, t2, st2) DESC
    FILTERS sku(pl, t2, st2) == s,
            st1 == st2,
            t2 == t1
            
    OBJECTS t3 = CalcPriceListType
    PROPERTIES READONLY name(t3), nameCurrency(t3), markup(t3,s), currentPrice(t3,s,st)
    FILTERS currentPrice(t3,s,st)
;

DESIGN itemInfo {
    bottomContainer {
        NEW priceLists {
            caption = 'По видам цен';
            type = TABBED;
            NEW ledger {
                caption = 'Регистровый';
                MOVE BOX(t1);
                MOVE BOX(pp) {caption = 'Изменение цены';}
            }
            NEW calc {
                caption = 'Расчетный';
                MOVE BOX(t3);
            }
        }
    }
}
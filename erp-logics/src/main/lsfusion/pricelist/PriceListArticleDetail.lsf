MODULE PriceListArticleDetail;

REQUIRE PriceListArticleDetailMain;

NAMESPACE PriceList;

// Цены
TABLE userAPriceListDetailDataPriceListType(UserAPriceListDetail, DataPriceListType);
TABLE sessionUserAPriceListDetailDataPriceListType(Session, UserAPriceListDetail, DataPriceListType);
price 'Цена (новая)' = DATA NUMERIC[16,4] (UserAPriceListDetail, DataPriceListType); //annotation не нужен, делим скриптом с учетом валюты вида цен

in (UserAPriceListDetail detail, DataPriceListType type) = in(userPriceList(detail), type);

price(UserAPriceListDetail detail, DataPriceListType type) => in (detail, type) RESOLVE RIGHT;

CLASS APriceListDetail 'Строка прайса по товару на основе артикула' : PriceListDetail;
in (UserAPriceListDetail d, Item i) = article(d) == article(i);
aPriceListDetail = AGGR APriceListDetail WHERE in(UserAPriceListDetail userAPriceListDetail, Item item) MATERIALIZED INDEXED;
priceList (APriceListDetail d) += userPriceList(userAPriceListDetail(d));
sku (APriceListDetail d) += item(d);
price(APriceListDetail d, DataPriceListType t) += price(userAPriceListDetail(d), t);

fromDataDate(APriceListDetail d) += fromDataDate(userAPriceListDetail(d));
fromDataTime(APriceListDetail d) += fromDataTime(userAPriceListDetail(d));
toDataDate(APriceListDetail d) += toDataDate(userAPriceListDetail(d));
toDataTime(APriceListDetail d) += toDataTime(userAPriceListDetail(d));

countPriceListDetails = GROUP SUM 1 BY article(UserAPriceListDetail detail), userPriceList(detail);

// Действующие цены

priceB 'Цена' (DataPriceListType type, Article article, Stock stock, DATETIME dateTime) =
    GROUP LAST price(UserAPriceListDetail detail, type)
    ORDER fromDateTime(detail), detail
    WHERE isPosted(detail) AND in(userPriceList(detail), type) AND in(userPriceList(detail), stock) AND
        fromDateTime(detail) < dateTime AND NOT toDateTime(detail) < dateTime
    BY article(detail) COMPLEX;

prevPriceB 'Цена' (DataPriceListType type, Article article, Stock stock, DATETIME dateTime) =
    PREV(priceB(type, article,stock, dateTime));

priceB 'Цена' (DataPriceListType type, Article article, DATETIME dateTime) =
    GROUP LAST price(UserAPriceListDetail detail, type)
          ORDER fromDateTime(detail), detail
          WHERE isPosted(detail) AND in(userPriceList(detail), type) AND
                fromDateTime(detail) < dateTime AND NOT toDateTime(detail) < dateTime
          BY article(detail) COMPLEX;

prevPriceB 'Цена' (DataPriceListType type, Article article, DATETIME dateTime) = PREV(priceB(type, article, dateTime));

priceB 'Цена до' (UserAPriceListDetail d, DataPriceListType t) = priceB(t, article(d), fromDateTime(d));
prevPriceB 'Цена до' (UserAPriceListDetail d, DataPriceListType t) = PREV(priceB(d, t));


EXTEND FORM userPriceList
    PROPERTIES(ad, tc) AFTER toTime(ad) price COLUMNS (tc) HEADER headerNameData(tc) BACKGROUND background(tc)
    PROPERTIES(ad, to) AFTER price(ad, tc) READONLY prevPriceB COLUMNS 'type' (to) HEADER headerNameCurrentData(to) BACKGROUND backgroundCurrent(to) DRAW ad 
;
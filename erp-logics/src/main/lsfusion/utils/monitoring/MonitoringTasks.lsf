MODULE MonitoringTasks;

REQUIRE Scheduler, Time, Messenger;

NAMESPACE Monitoring;

active 'Вкл' = DATA BOOLEAN (ScheduledTask);
timeout 'Таймаут' = DATA INTEGER (ScheduledTask);
account = DATA Messenger.Account (ScheduledTask);
nameAccount 'Аккаунт' (ScheduledTask t) = name(account(t));

in 'Вкл' = DATA BOOLEAN (ScheduledTask, Chat);
sendNotification ABSTRACT LIST (ScheduledTask, STRING);

monitoringTasks 'Мониторинг заданий'(){
    FOR active(ScheduledTask t) AND date(last(t)) < sumMinutes(currentDateTime(), -timeout(t)) DO {
        LOCAL message = STRING();
        message() <- STRING[2500](CONCAT '\n', 'Таймаут выполнения задания', name(t), 'Последнее выполнение ' + date(last(t)));
        sendNotification(t, message());
    }
}

FORM monitoringTasks 'Мониторинг заданий'
    OBJECTS t = ScheduledTask
    PROPERTIES (t) active, name READONLY, period READONLY, timeout, nameAccount
    PROPERTIES monitoringTasks() TOOLBAR 
    
    OBJECTS c = Chat
    PROPERTIES (c) READONLY name, captionChatType
    PROPERTIES in(t, c) GRID DRAW c FIRST 
    FILTERS account(c) = account(t)
;
customCaption = ABSTRACT STRING (ScheduledTaskLog);

WHEN SET (exceptionOccurred(ScheduledTaskLog l)) AND active(scheduledTask(l)) AND account(scheduledTask(l)) DO {
    LOCAL message = STRING();
    message() <- STRING[2500](CONCAT '\n', 'Ошибка при выполнении задания', name(scheduledTask(l)), customCaption(l), (result(l)));
    sendNotification(scheduledTask(l), message());
} 

NAVIGATOR {
    scheduler{
        NEW monitoringTasks;
    }
}


MODULE ComWeight;

REQUIRE WeightDaemon, SerialPort;

NAMESPACE WeightDaemon;

scalesBaudRate 'Baud rate весов' = DATA INTEGER (Computer);

vidScalesComPort 'VID весов' = DATA STRING[4] (Computer) CHARWIDTH 8;
pidScalesComPort 'PID весов' = DATA STRING[4] (Computer) CHARWIDTH 8;

EXTEND FORM computer 
    PROPERTIES(c) scalesBaudRate, vidScalesComPort, pidScalesComPort
    
    PROPERTIES requestScalesPort = JSON(FROM caption = 'Запросить') CUSTOM 'serialPortRequestPort' ON CHANGE {
        INPUT s = TEXT DO {
            stringToFile(s);
            
            LOCAL vid = STRING[4]();
            LOCAL pid = STRING[4]();
            IMPORT JSON FROM resultFile() TO() vid, pid;
            
            vidScalesComPort(c) <- vid();
            pidScalesComPort(c) <- pid();
            
            scalesComPort(c) <- 1 IF NOT vid();
        }
    }
;

DESIGN computer {
    weight {
        MOVE PROPERTY(scalesBaudRate(c)); 
        MOVE PROPERTY(vidScalesComPort(c)); 
        MOVE PROPERTY(pidScalesComPort(c));
        MOVE PROPERTY(requestScalesPort); 
    }
} 

// не запустится на версии ниже 5й
jsonScalesComPort (Computer c) = JSON(FROM number = scalesComPort(c),
                                           baudRate = (OVERRIDE scalesBaudRate(c), 9600), 
                                           vid = vidScalesComPort(c), 
                                           pid = pidScalesComPort(c)); 

openScalesPortReader (Computer c) { openPortReader(jsonScalesComPort(c), 100); }
openScalesPortReader () { openScalesPortReader(currentComputer()); } 
MODULE CrossImport;

REQUIRE Utils;

imported = DATA LOCAL STRING (INTEGER, INTEGER);

var (INTEGER count) = 'LOCAL ' + (GROUP CONCAT 'value' + STRING(INTEGER i),',' ORDER i IF iterate(i, 0, count - 1)) + '=STRING(INTEGER);';
import (INTEGER count, STRING sheet) = 'IMPORT XLS SHEET ' + sheet + ' FROM excelFile TO (INTEGER) ' + GROUP CONCAT 'value' + STRING (INTEGER i), ',' ORDER i IF iterate(i, 0, count - 1);
let (INTEGER count) = GROUP CONCAT 'CrossImport.imported(' + STRING(INTEGER i) + ',i)<-value' + STRING(i) + '(i) WHERE System.imported(i)', ';' ORDER i IF iterate(i, 0, count - 1);

eval (INTEGER count, STRING sheet) = CONCAT ';', var(count), import(count, sheet), let(count);

crossImport(EXCELFILE file, STRING sheet) {
    columnsCount(file);
    imported(INTEGER i, INTEGER j) <- NULL WHERE imported(i, j);
    EVAL 'run(EXCELFILE excelFile) \{' + eval(columnsCount(), sheet) + ';\}' PARAMS file;
}

crossImport(EXCELFILE file, INTEGER sheet) {
    crossImport(file, STRING(sheet));
}

crossImport(EXCELFILE file) {
    crossImport(file, 'ALL');
}

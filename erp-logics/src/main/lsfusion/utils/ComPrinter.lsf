MODULE ComPrinter;

REQUIRE Authentication, SerialPort;

NAMESPACE Utils;

vidPrinterComPort 'VID принтера' = DATA STRING[4] (Computer);
pidPrinterComPort 'PID принтера' = DATA STRING[4] (Computer);

codePageComPort 'Кодировка' = DATA STRING[10] (Computer);

EXTEND FORM computer PROPERTIES(c) vidPrinterComPort, pidPrinterComPort, codePageComPort;

DESIGN computer {
    OBJECTS {
        NEW comPrinter {
            caption = 'Печать';
            NEW comPrinterRow1 {
                type = CONTAINERH;
                alignment = STRETCH;
                MOVE PROPERTY(vidPrinterComPort(c));
                MOVE PROPERTY(pidPrinterComPort(c));
                MOVE PROPERTY(codePageComPort(c));
            }
        }
    }
} 

jsonPrinterComPort (Computer c) = JSON(FROM vid = vidPrinterComPort(c), pid = pidPrinterComPort(c)); 

localFileName 'Печатать в файл' = DATA STRING (Computer) CHARWIDTH 20;
EXTEND FORM computer PROPERTIES(c) localFileName; 

DESIGN computer {
    comPrinter {
        NEW comPrinterRow2 {
            type = CONTAINERH;
            alignment = STRETCH;
            MOVE PROPERTY(localFileName(c));
        }
    }
} 

printComPort (TEXT t) {
    IF localFileName(currentComputer()) THEN {
        stringToFile(t, (OVERRIDE codePageComPort(currentComputer()), 'UTF-8'), 'prl');
        open(resultFile(), localFileName(currentComputer()));
    } ELSE
        serialPortSend(jsonPrinterComPort(currentComputer()), t);
}

// для печати на принтер через файл нужно создать такой bat-файл (или подключить отдельный exe-файл ниже, который будет печатать файл)
//copy %1 \\localhost\%~n1
//del %1
// файл (exe) можно скчаать кнопкой, так как лежит в ресурсах. Exe собирается из vbs-файла из ресурсов при помощи утилиты vbs to exe
// затем в Chrome ассоциировать расширение prl с этим файлом, и включить автоматический запуск этого скачанного файла  
downloadPrintFile 'Скачать приложение печати этикеток на локальный принтер' () {
    readResource('/lib/win32/printfile');
    open(setExtension(resourceFile(), 'exe'), 'printfile');
}

EXTEND FORM computer
    PROPERTIES downloadPrintFile()
;

DESIGN computer {
    comPrinterRow2 {
        MOVE PROPERTY(downloadPrintFile());
    }
}
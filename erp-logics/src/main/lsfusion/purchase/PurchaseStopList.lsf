MODULE  PurchaseStopList;

REQUIRE StopList,
        PurchaseLedger
        ;

NAMESPACE StopList;

//-------------------- Создание стоп-листа для склада, если нет движения и остатков----------------------//
     
ff 'Дата с' = subtract(currentDate(), LONG int);
FORM stockDays 'Выбор склада и количества дней'

    OBJECTS s=Stock PANEL

    PROPERTIES(s) SELECTOR name
                   
    OBJECTS i=INTEGER PANEL
    PROPERTIES    val =VALUE(i)     
    PROPERTIES(i) ff
    FILTERS isCompany(s)                               

;
DESIGN stockDays {
    BOX {
        NEW top {
            type = CONTAINERH;
            
            MOVE BOX(s) {
                caption = 'Выбор склада';
            }
            MOVE BOX(i) {
                caption = 'Выбор количества дней';
                PROPERTY (val) {caption = 'Количество дней без поставок';}
            } 
        } 
        MOVE TOOLBARBOX;
    }     
}


quantityPurchase 'Закуплено за интервал (кол-во)' (Sku sku, Stock stock, DATE dateFrom) = GROUP 
        SUM
        PurchaseLedger.quantityPurchase(sku, stock, DATE date) IF date >= dateFrom;
        
notPurchase (Stock stock, DATE dateFrom, DATETIME dateTime)= GROUP 
        SUM 1 IF NOT (quantityPurchase(Sku sku, stock, dateFrom) OR currentBalance(sku, stock) OR lastStopListDetail(sku, stock, dateTime)) 
    AND sku IS Sku AND stock IS Stock AND dateFrom IS DATE AND dateTime IS DATETIME; 
     
createStopList 'Создать по товарам без прихода и остатка'()   { 
    DIALOG stockDays OBJECTS s INPUT, i INPUT DO {
        
        IF notPurchase(s, subtract(currentDate(), i), currentDateTime()) THEN {
            NEW sl = StopList {
            
                in(s, sl) <- TRUE;  
               
                
                FOR Sku sku IS Sku IF NOT (quantityPurchase(sku, s, subtract(currentDate(), i)) OR currentBalance(sku, s) OR lastStopListDetail(sku, s, currentDateTime()))   
                    NEW d = StopListDetail DO {
                    
                    stopList(d) <- sl;
                    sku(d) <- sku;
                }   
                SHOW stopList OBJECTS sl = sl DOCKED NOCANCEL;
            }
        } ELSE {
            MESSAGE 'Для данного склада не требуется создание "Запрета продаж по кассе".';
        }
         
    }
} TOOLBAR;

EXTEND FORM stopLists
    PROPERTIES () createStopList DRAW sl 
;

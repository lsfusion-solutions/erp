MODULE PurchaseInvoiceExtraPack;

REQUIRE PurchaseInvoice, ExtraPack;

NAMESPACE Purchase;

isExtraPack 'Поставка в других единицах' = ABSTRACT BOOLEAN (Purchase.InvoiceDetail);
isExtraPack 'Поставка в других единицах' = DATA BOOLEAN (Purchase.UserInvoiceDetail);
isExtraPack(Purchase.UserInvoiceDetail detail) += isExtraPack(detail);

skipExtraPack = ABSTRACT BOOLEAN (Purchase.UserInvoiceDetail);

WHEN LOCAL (CHANGED(sku(UserInvoiceDetail detail)) OR CHANGED(supplier(detail)) OR CHANGED(showPack(detail))) DO {
    isExtraPack(detail) <- TRUE IF extraPack(supplier(detail), sku(detail)) AND showPack(detail) AND NOT skipExtraPack(detail);
}
WHEN LOCAL (CHANGED(sku(UserInvoiceDetail detail)) OR CHANGED(supplier(detail)) OR SET(isExtraPack(detail))) AND isExtraPack(detail) DO {
    barcodePack(detail) <- extraPack(supplier(detail), sku(detail));
}
WHEN LOCAL (CHANGED(sku(UserInvoiceDetail detail)) OR CHANGED(supplier(detail)) OR SET(isExtraPack(detail))) AND isExtraPack(detail) DO {
    amountPack(detail) <- amount(extraPack(supplier(detail), sku(detail)));
}
WHEN LOCAL CHANGED(packQuantity(UserInvoiceDetail detail) OR SET(isExtraPack(detail))) AND isExtraPack(detail) DO {
    quantity(detail) <- NUMERIC[16,5](packQuantity(detail) * (OVERRIDE amountPack(detail), 1.0));
}

WHEN LOCAL CHANGED(quantity(UserInvoiceDetail detail) OR SET(isExtraPack(detail))) AND isExtraPack(detail) DO {
    packQuantity(detail) <- NUMERIC[16,5](quantity(detail)/amountPack(detail));
}

EXTEND FORM userInvoice
    PROPERTIES (d) READONLY isExtraPack BEFORE idBarcodePack(d) SHOWIF showPack(i)
;

calcSum(UserInvoiceDetail userInvoiceDetail) += NUMERIC[18,4](Utils.round(packQuantity(userInvoiceDetail) * price(userInvoiceDetail), priceRound(userInvoiceDetail))) IF isExtraPack(userInvoiceDetail);
MODULE PurchaseReturnInvoiceTracking;

REQUIRE PurchaseReturnInvoice, ItemTracking, BatchInventoryTracking, PurchaseInvoiceTracking;

NAMESPACE PurchaseReturn;

dateStartSPT 'Дата начала прослеживания' (UserInvoiceDetail d)=dateStartSPT(sku(d));
isTracking (Invoice i) = GROUP SUM 1 IF isTrackingOver(sku(UserInvoiceDetail d)) AND date(d) >= dateStartSPT(d) AND invoice(d)==i;
showSPT (Invoice i)= TRUE IF isTracking(i);
isTracking 'Подлежит прослеживанию' (UserInvoiceDetail d) = TRUE IF isTrackingOver(sku(d)) AND date(d) >= dateStartSPT(d);

trackingInvoiceDetail = DATA Purchase.InvoiceDetail (UserInvoiceDetail) INDEXED;
basisTracking 'Основание' (UserInvoiceDetail d) = description(trackingInvoiceDetail(d));

quantityDespatchedSPT 'Кол-во в ед.СПТ ' (UserInvoiceDetail d) =  NUMERIC[12,5] (CASE
    WHEN trackingUOMCustomsGroup(sku(d))=='112'  THEN (OVERRIDE quantity(d), 1.0)* volume(sku(d))
    WHEN trackingUOMCustomsGroup(sku(d))=='166'  THEN (OVERRIDE quantity(d), 1.0)* netWeight(sku(d))
    ELSE (OVERRIDE quantity(d), 1.0));
lineItemPriceSPT'Цена за единицу товара для прослеживаемости' (UserInvoiceDetail d) = NUMERIC[12,2] (CASE
    WHEN trackingUOMCustomsGroup(sku(d))=='112'  THEN (OVERRIDE price(d), 1.0)/ volume(sku(d))
    WHEN trackingUOMCustomsGroup(sku(d))=='166'  THEN (OVERRIDE price(d), 1.0)/ netWeight(sku(d))
    ELSE (OVERRIDE price(d), 1.0));

itemCustomCode 'Код ТНВЭД' (UserInvoiceDetail d) = OVERRIDE itemCustomCode(batch(d), supplierStock(d)), codeCustomsGroupDefaultCountry(sku(d));
itemCustomCodeOther 'Дополнительный таможенный код'(UserInvoiceDetail d) = OVERRIDE itemCustomCodeOther(batch(d), supplierStock(d)), idExtraCode(sku(d));
deliveryTypePrev 'Тип приходного документа'(UserInvoiceDetail d) = OVERRIDE deliveryTypePrev(batch(d), supplierStock(d)), deliveryTypePrev(trackingInvoiceDetail(d)), deliveryTypePrev(batch(d));
deliveryNotePrev 'Номер приходного документа' (UserInvoiceDetail d) = OVERRIDE deliveryNotePrev(batch(d), supplierStock(d)),deliveryNotePrev(trackingInvoiceDetail(d)), deliveryNotePrev(batch(d));
deliveryNoteDate 'Дата создания приходного документа' (UserInvoiceDetail d) = OVERRIDE deliveryNoteDate(batch(d), supplierStock(d)), deliveryNoteDate(trackingInvoiceDetail(d)), deliveryNoteDate(batch(d));
deliveryNotePrevLineID 'Номер строки в приходном документе' (UserInvoiceDetail d) = OVERRIDE deliveryNotePrevLineID(batch(d), supplierStock(d)),deliveryNotePrevLineID(trackingInvoiceDetail(d)), deliveryNotePrevLineID(batch(d));
lineItemQuantitySPT 'Ед. изм.СПТ' (UserInvoiceDetail d) = OVERRIDE lineItemQuantitySPT(batch(d), supplierStock(d)),  trackingUOMCustomsGroup(sku(d));

FORM basisInvoiceDetail 'Строка накладной'
    OBJECTS sk = Sku PANEL
    PROPERTIES(sk) READONLY name, idBarcode, shortNameUOM

    OBJECTS st = Stock PANEL
    PROPERTIES (st) READONLY name

    OBJECTS dt = DATETIME PANEL
    OBJECTS d = Purchase.InvoiceDetail LAST

    PROPERTIES(d) READONLY date, index, description, quantity, price, sum,  numberVAT, valueVAT, VATSum, invoiceSum,  nameCustomerStock, nameSupplierStock
    FILTERS sku(d) == sk

    FILTERGROUP filterStock
        FILTER  'По складу пок.' customerStock(d) == st DEFAULT
    
    FILTERGROUP filterDateStart
        FILTER  'По дате СПТ' date(d) >= dateStartSPT(sk) DEFAULT
;

DESIGN basisInvoiceDetail {
    BOX {
        MOVE BOX(sk) {
            horizontal = TRUE;
            PROPERTY(name(sk)) {focusable = FALSE;}
            PROPERTY(idBarcode(sk)) {focusable = FALSE;}
            PROPERTY(shortNameUOM(sk)) {focusable = FALSE;}
        }
        MOVE BOX(d);
        MOVE TOOLBARBOX;
    }
}
changeBasisInvoiceDetaill(InvoiceDetail detail)  {
    DIALOG basisInvoiceDetail OBJECTS sk = sku(detail), st = supplierStock(detail), dt = dateTime(detail),
        d = trackingInvoiceDetail[UserInvoiceDetail](detail) CHANGE ;
}

EXTEND FORM userInvoice
    OBJECTS d6 = UserInvoiceDetail GRID
    PROPERTIES(d6) SHOWIF showSPT(i) READONLY index, idBarcodeSku, nameSku, isTracking BACKGROUND backgroundEC(extraCode(sku(d6))), itemCustomCode, itemCustomCodeOther, dateStartSPT,
        deliveryTypePrev, deliveryNotePrev, deliveryNoteDate, deliveryNotePrevLineID, lineItemQuantitySPT, quantityDespatchedSPT, lineItemPriceSPT
    PROPERTIES (d6) SHOWIF showSPT(i) basisTracking ON CHANGE changeBasisInvoiceDetaill(d6)
    FILTERS userInvoice(d6) == i, isTracking(d6)

;

DESIGN userInvoice{
    specificationBox {
        NEW sptContainer {
            caption = 'Прослеживание';
            MOVE BOX(d6);
        }
    }
}



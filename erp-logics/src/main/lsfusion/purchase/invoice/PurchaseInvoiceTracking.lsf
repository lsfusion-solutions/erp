MODULE PurchaseInvoiceTracking;

REQUIRE PurchaseInvoice, ItemTracking, BatchTracking, PurchaseShipment;

NAMESPACE Purchase;

isTracking 'Подлежит прослеживанию' = ABSTRACT VALUE BOOLEAN (Purchase.InvoiceDetail);
isTracking (UserInvoiceDetail d) += isTrackingOver(sku(d));

isTracking(Invoice i) = GROUP SUM 1 IF isTrackingOver(sku(UserInvoiceDetail d)) AND invoice(d)==i;
showSPT (Invoice i)= TRUE IF isTracking(i);

itemCustomCode'Код ТНВЭД' = DATA STRING[10] (UserInvoiceDetail);
itemCustomCodeOther 'Дополнительный таможенный код' = DATA STRING[4] (UserInvoiceDetail);
deliveryTypePrev 'Тип приходного документа' = DATA INTEGER (UserInvoiceDetail);
deliveryNotePrev 'Номер приходного документа' = DATA STRING[70] (UserInvoiceDetail);
deliveryNoteDate 'Дата создания приходного документа' = DATA DATE (UserInvoiceDetail);
deliveryNotePrevLineID 'Номер строки в приходном документе' = DATA INTEGER (UserInvoiceDetail);
lineItemQuantitySPT 'Единица измерения прослеживаемости' = DATA STRING[3](UserInvoiceDetail);
quantityDespatchedSPT 'Количество в единицах прослеживаемости' = DATA NUMERIC[12,5] (UserInvoiceDetail);
lineItemPriceSPT 'Цена за единицу товара для прослеживаемости' = DATA NUMERIC[12,2] (UserInvoiceDetail);
idShippingDocument 'Код сопроводительного документа' = DATA STRING[20](UserInvoice);
nameShippingDocument 'Наименование сопроводительного документа' = DATA STRING[40](UserInvoice);
dateShippingDocument 'Дата сопроводительного документа' = DATA DATE (UserInvoice);
numberShippingDocument 'Регистрационный номер сопроводительного документа' = DATA STRING[40](UserInvoice);

@defineOperationProperty(importEAEU, 'Импорт из ЕАЭС', showContainer);

importEAEU 'Импорт из ЕАЭС' = DATA BOOLEAN (UserInvoice);
showImportEAEU (Invoice i) = TRUE  IF importEAEU(operation(i));
countrySupplier 'Страна грузоотправителя' (Invoice i) = name(country(supplier(i)));
idCountrySupplier 'Код страны' (Invoice i) = sidOrigin2(country(supplier(i)));

EXTEND FORM userInvoice
    OBJECTS d6 = UserInvoiceDetail GRID
    PROPERTIES (d6) SHOWIF showSPT(i) READONLY index, idBarcodeSku, nameSku, isTracking BACKGROUND backgroundEC(extraCode(sku(d6))), itemCustomCode, itemCustomCodeOther,
        deliveryTypePrev, deliveryNotePrev, deliveryNoteDate, deliveryNotePrevLineID, lineItemQuantitySPT, quantityDespatchedSPT, lineItemPriceSPT
    PROPERTIES (i) SHOWIF (showSPT(i) AND showImportEAEU(i)) idShippingDocument, nameShippingDocument, numberShippingDocument, dateShippingDocument
    PROPERTIES (i) SHOWIF (showSPT(i) AND showImportEAEU(i)) importEAEU, countrySupplier READONLY, idCountrySupplier READONLY
    FILTERS userInvoice(d6) == i, isTracking(d6)

;

DESIGN userInvoice{
    specificationBox {
        NEW sptContainer {
            caption = 'Прослеживание';
            NEW headerCertificateContainer {
                horizontal = TRUE;
                caption = 'Сопроводительный документ';
                MOVE PROPERTY(idShippingDocument(i)) { caption='Код';}
                MOVE PROPERTY(nameShippingDocument(i)) { caption='Наименование';}
                MOVE PROPERTY(numberShippingDocument(i)) { caption='Рег.№';}
                MOVE PROPERTY(dateShippingDocument(i)) { caption='Дата';}
                MOVE PROPERTY(importEAEU(i)) ;
                MOVE PROPERTY(countrySupplier(i)) ;
                MOVE PROPERTY(idCountrySupplier(i)) ;
            }
            MOVE BOX(d6);
        }
    }
}

deliveryTypePrev(ShipmentBatch batch) += deliveryTypePrev(invoiceDetail(shipmentDetail(batch)));
deliveryNotePrev(ShipmentBatch batch) += deliveryNotePrev(invoiceDetail(shipmentDetail(batch)));
deliveryNoteDate(ShipmentBatch batch) += deliveryNoteDate(invoiceDetail(shipmentDetail(batch)));
deliveryNotePrevLineID(ShipmentBatch batch) += deliveryNotePrevLineID(invoiceDetail(shipmentDetail(batch)));
lineItemQuantitySPT(ShipmentBatch batch) += lineItemQuantitySPT(invoiceDetail(shipmentDetail(batch)));
itemCustomCode(ShipmentBatch batch) += itemCustomCode(invoiceDetail(shipmentDetail(batch)));
itemCustomCodeOther(ShipmentBatch batch) += itemCustomCodeOther(invoiceDetail(shipmentDetail(batch)));

EXTEND FORM userInvoice
    PROPERTIES (d) isTracking
;
EXTEND FORM invoices
    PROPERTIES (d) READONLYIF  isReadonly() isTracking
;
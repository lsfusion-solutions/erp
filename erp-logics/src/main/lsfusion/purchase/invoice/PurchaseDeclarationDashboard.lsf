MODULE PurchaseDeclarationDashboard;

REQUIRE Dashboard, PurchaseDeclarationDetail;

NAMESPACE Purchase;

minSelectedUserInvoice = GROUP MIN UserInvoice i IF selected(i); 

countSelectedUserInvoice 'Кол-во отм.' = GROUP SUM 1 IF selected(UserInvoice invoice);

countSelectedCustomerUserInvoices 'Кол-во получателей' = GROUP SUM 1 IF [ GROUP SUM 1 IF selected(UserInvoice invoice) BY customer(invoice)](LegalEntity invoice);
countSelectedCurrencyUserInvoices 'Кол-во валют' = GROUP SUM 1 IF [ GROUP SUM 1 IF selected(UserInvoice invoice) BY currency(invoice)](Currency invoice);
createMultiDeclaration ABSTRACT LIST (UserInvoice,Declaration);

createMultiDeclarationCustom (UserInvoice invoice){
    NEW d = Declaration {
        legalEntity(d) <- customer(invoice);
        currency(d) <- currency(invoice);                
        declaration(invoice) <- d;  
        createMultiDeclaration(invoice,d);    
        SHOW declaration OBJECTS d = d DOCKED NOCANCEL;  
    } 
}

createMultiDeclaration 'Создать декларацию'(UserInvoice invoice)  { 
	NEWSESSION {
	    IF countSelectedUserInvoice() THEN {
	        IF countSelectedCustomerUserInvoices()!=1 OR countSelectedCurrencyUserInvoices() != 1 THEN {
	            MESSAGE 'Выбраны накладные с разными покупателями или валютами';               
	        } ELSE {
	            FOR UserInvoice i ==minSelectedUserInvoice() DO {
	                createMultiDeclarationCustom(i);
	            }        
	        }
	    } ELSE {
	        createMultiDeclarationCustom(invoice);          
	    }    
	}
} TOOLBAR;


FORM declarationDashboard 'Таможенное оформление'

    OBJECTS dt = DATE PANEL
    PROPERTIES(dt) date = VALUE

    OBJECTS i=UserInvoice GRID
    PROPERTIES (i) selected
    PROPERTIES (i) READONLY isPosted, number, series, date, time,                                 
                   nameSupplier, nameCustomer, 
                   nameCurrency, countUserInvoiceDetail, quantityUserInvoiceDetail, sumUserInvoiceDetail,
                   VATSumUserInvoiceDetail, invoiceSumUserInvoiceDetail

    PROPERTIES (i) createMultiDeclaration 
    FILTERS        showDeclaration(i), 
                   TRUE AND NOT  declaration(i)
    
    OBJECTS d = Declaration GRID
    PROPERTIES(d)  READONLY isPosted , number, series, date, time,
                   nameLegalEntity, nameHomeCurrency, nameCurrency, 
                   sumDeclarationDetail, deliverySum, chargeSum,
                   homeSumDeclarationDetail, dutySumDeclarationDetail, VATSumDeclarationDetail, 
                   registrationSum
    PROPERTIES(d)  NEWSESSION NEW, EDIT, deleted =DELETE                        
                       
    FILTERGROUP filters FILTER 'Накладные на дату' date(i)<=dt 'F5' DEFAULT
    FILTERGROUP filters1 FILTER 'Декларации на дату' date(d)==dt 'F5' DEFAULT
                               
;

NAVIGATOR {
    customsDashboardNavigator {
        NEW declarationDashboard;
    }
}
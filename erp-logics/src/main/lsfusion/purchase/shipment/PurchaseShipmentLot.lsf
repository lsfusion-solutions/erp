MODULE PurchaseShipmentLot;

REQUIRE PurchaseShipment, 
        Lot;

NAMESPACE Purchase;

@defineOperationProperty(useLot, 'Марки', showContainer);
useLot (Invoice i) = useLot(operation(i));

quantity = ABSTRACT NUMERIC[16,5] (InvoiceDetail, Lot);

quantityLot 'Кол-во марок' (InvoiceDetail d) = GROUP SUM quantity(d, Lot l) MATERIALIZED;
quantity (Invoice i, Lot l) = GROUP SUM quantity(InvoiceDetail d, l) IF invoice(d) = i;
quantityLot 'Кол-во марок' = GROUP SUM quantityLot(InvoiceDetail d) BY invoice(d) MATERIALIZED;

isPostedShipment (UserInvoiceDetail d) = isPosted(d) AND createShipment(d);
@defineDocumentLotIn(userInvoice, shipmentQuantity, i, invoices, isPostedShipment);

quantity(UserInvoiceDetail d, Lot l) += quantity(d, l);

@defineDocumentLotType(invoice);

// shipment detail
// пока упрощенная схема без поддержки UserShipmentDetail

useLot (Shipment i) = useLot(operation(i));

quantity = ABSTRACT NUMERIC[16,5] (ShipmentDetail, Lot);
quantity(InvoiceShipmentDetail d, Lot l) += quantity(invoiceDetail(d), l);

quantity (Shipment s, Lot l) = GROUP SUM quantity(ShipmentDetail d, l) IF shipment(d) = s;

quantityLot 'Кол-во марок' (ShipmentDetail d) = GROUP SUM quantity(d, Lot l) MATERIALIZED;
quantityLot 'Кол-во марок' = GROUP SUM quantityLot(ShipmentDetail d) BY shipment(d) MATERIALIZED;

backgroundQuantityLot (ShipmentDetail d) = (IF quantityLot(d) = quantity(d) THEN RGB(224,255,224) ELSE RGB(255,224,224)) 
                                                       IF lotType(sku(d));

EXTEND FORM shipments
    PROPERTIES(s) READONLY quantityLot

    PROPERTIES(d) READONLY quantityLot AFTER quantity(d) 
                  BACKGROUND backgroundQuantityLot(d) 
                  SHOWIF useLot(s)
                  READONLYIF NOT lotType(sku(d))
;
MODULE PurchaseShipmentLot;

REQUIRE PurchaseShipment, 
        Lot, SkuLedgerLot;

NAMESPACE Purchase;

@defineOperationProperty(useLot, 'Марки', showContainer);
useLot (Invoice i) = useLot(operation(i));

quantity = ABSTRACT NUMERIC[16,5] (InvoiceDetail, Lot);

quantityLot 'Кол-во марок' (InvoiceDetail d) = GROUP SUM quantity(d, Lot l) MATERIALIZED;
quantity (Invoice i, Lot l) = GROUP SUM quantity(InvoiceDetail d, l) IF invoice(d) = i;
quantityLot 'Кол-во марок' = GROUP SUM quantityLot(InvoiceDetail d) BY invoice(d) MATERIALIZED;

isPostedShipment (UserInvoiceDetail d) = isPosted(d) AND createShipment(d);
@defineDocumentLotIn(userInvoice, shipmentQuantity, i, invoices, isPostedShipment);

quantity(UserInvoiceDetail d, Lot l) += quantity(d, l);

@defineDocumentLotType(invoice);

// shipment detail
// пока упрощенная схема без поддержки UserShipmentDetail

quantity = ABSTRACT NUMERIC[16,5] (ShipmentDetail, Lot);
quantity(InvoiceShipmentDetail d, Lot l) += quantity(invoiceDetail(d), l);

quantity (Shipment s, Lot l) = GROUP SUM quantity(ShipmentDetail d, l) IF shipment(d) = s;

// ledger

quantity(ShipmentBatch b, Lot l) += quantity(shipmentDetail(b), l);
quantity(ShipmentSkuLedger sl, Lot l) += quantity(shipmentDetail(sl), l);
MODULE PurchaseShipmentMachineryLot;

REQUIRE PurchaseShipmentMachinery, PurchaseShipmentLot, CashRegisterPriceTransactionCustomLot;

NAMESPACE Purchase;


createCashRegisterPriceTransactionDocumentLot 'Загрузить в оборудование' (Shipment i){
    createMachineryPriceTransactionLot(Lot l) <- NULL;
    createMachineryPriceTransactionLot(Lot l) <- TRUE WHERE quantity(i, l);
    createMachineryPriceTransactionLotDocument() <- i;
    
    createMachineryPriceTransactionLot(customerStock(i));
    APPLY;
} CONFIRM;

createMachineryPriceTransactionCustom 'Загрузить в оборудование' (Shipment i) {
    IF useLot(i) THEN createCashRegisterPriceTransactionDocumentLot(i);
    ELSE createMachineryPriceTransaction(i);
}

createSnapshotMachineryPriceTransactionCustom 'Перегрузить все позиции' (Shipment i) {
    IF useLot(i) THEN createCashRegisterPriceTransactionDocumentLot(i);
    ELSE createSnapshotMachineryPriceTransaction(i);
}

EXTEND FORM shipments
    PROPERTIES (s) createMachineryPriceTransactionCustom SHOWIF showCreateMachineryPriceTransaction(s),
                   createSnapshotMachineryPriceTransactionCustom SHOWIF isPosted(s) 
;

DESIGN shipments{
    machinery{
        REMOVE PROPERTY (createMachineryPriceTransaction(s));
        MOVE PROPERTY (createMachineryPriceTransactionCustom(s));
        REMOVE PROPERTY (createSnapshotMachineryPriceTransaction(s));
        MOVE PROPERTY (createSnapshotMachineryPriceTransactionCustom(s));
    }
}
MODULE PurchaseReturnCreditNoteSkuLedger;

REQUIRE PurchaseReturnCreditNote, SkuLedger;

NAMESPACE PurchaseReturn;

skipChangedShipmentQuantity = ABSTRACT BOOLEAN (UserCreditNoteDetail);
shipmentQuantity(UserCreditNoteDetail detail) <- quantity(detail)
    WHEN CHANGED(quantity(detail)) AND NOT skipChangedShipmentQuantity(detail);

@defineDocumentInterfaceHeaderQuantityCustomPrefix (creditNote, creditNoteDetail, shipment, ' по факту');

@defineDocumentDetailPriceCustomPrefix(userCreditNoteDetail, shipment, ' (учетная)');

@deriveDocumentDetailPriceAccountBatchDisable (userCreditNote, shipment, sku, customerStock);

@deriveDocumentDetailSumCustomRoundPrefix(userCreditNoteDetail, shipment, currency, shipmentQuantity, homePriceRound);

backgroundShipped 'Цвет' (CreditNote creditNote) = RGB(255, 224, 255) IF creditNote IS CreditNote;
backgroundShipped (CreditNoteDetail detail) = backgroundShipped(creditNote(detail));

EXTEND FORM userCreditNote
    PROPERTIES(c) BACKGROUND backgroundShipped(c) READONLY shipmentQuantityUserCreditNoteDetail, shipmentSumUserCreditNoteDetail
    PROPERTIES(d) BACKGROUND backgroundShipped(d) shipmentQuantity, shipmentPrice, shipmentSum
;

EXTEND FORM creditNotes
    PROPERTIES(c) BACKGROUND backgroundShipped(c) READONLY shipmentQuantityCreditNoteDetail, shipmentSumCreditNoteDetail
    PROPERTIES(d) BACKGROUND backgroundShipped(d) READONLY shipmentQuantity, shipmentPrice, shipmentSum
;

@implementSkuLedgerInLIFO(UserCreditNoteDetail, sku, customerStock);

quantity[InLIFOSkuLedger] (UserCreditNoteDetail ledger) += quantity (ledger);
batch[SkuLedger](UserCreditNoteDetail ledger) += batch(ledger);
@implementSkuLedgerInLIFOBatchBalance(userCreditNoteDetail, customerStock);

overSum = ABSTRACT NUMERIC[18,4] (UserCreditNoteDetail);
sum[InSkuLedger](UserCreditNoteDetail ledger) += OVERRIDE overSum(ledger), sum(ledger);

show[SkuLedger](UserCreditNoteDetail d) + {  show(d); }

// ------------ Проведение по товарному отчету ----------------- //
@implementStockDocumentLedgerInc(UserCreditNote, customerStock);

stockDocumentLedger(UserCreditNoteDetail ledger) += userCreditNote(ledger);
type(UserCreditNote l) += STRING[50]('Акт расхождения (закупка-возврат)') IF l IS UserCreditNote;
sumItem (UserCreditNote ledger) += sumItemUserCreditNoteDetail(ledger);
sumContainer (UserCreditNote ledger) += sumContainerUserCreditNoteDetail(ledger);

legalEntity(UserCreditNote ledger) += supplier(ledger);
legalEntityStock(UserCreditNote ledger) += supplierStock(ledger);

operation[StockDocumentLedger](UserCreditNote ledger) += operation(ledger);
isReturn(UserCreditNote ledger) += ledger IS UserCreditNote;
close[StockDocumentLedger](UserCreditNote l) + {  close(l); }

MODULE PurchaseScheduleOrder;

REQUIRE PurchaseOrder,
        ScheduleOrder;

NAMESPACE Purchase;

@defineScheduleOrder(' (закупка)', supplier, company, customer);

@extendFormFilterStockAccess(ts, scheduleOrder);
@extendFormFilterStockGroupAccess(sg, scheduleOrder);

WHEN LOCAL FORMS scheduleOrder CHANGED(customer(ScheduleOrder o)) DO {
    IF countAccessCompanyStock (currentUser(), customer(o)) == 1 THEN         
        dataIn(o, Stock stock) <- TRUE WHERE defaultCompanyStock(currentUser(), customer(o))==stock;               
}
@defineDocumentLegalEntityAccess(ScheduleOrder, customer, company);

@defineDocumentOperationRoleOver(scheduleOrder, purchase);
    
countShipment 'Кол-во поставок' (DOW, Stock stock, DATE date)= GROUP SUM 1 IF 
    type(scheduleOrder(ScheduleOrderTime scheduleOrderTime))== GenType.genDayMonth AND 
    date >=  startDate(scheduleOrder(scheduleOrderTime)) AND NOT 
    (date >  endDate(scheduleOrder(scheduleOrderTime))) AND 
    in(scheduleOrder(scheduleOrderTime), stock)
        BY shipmentDOW(scheduleOrderTime);

countShipment 'Кол-во поставок' (DOW, Stock stock, ScheduleOrderTime scheduleOrderTime, DATE date)= GROUP SUM 1 IF 
    type(scheduleOrder(scheduleOrderTime))== GenType.genDayMonth AND 
    date >=  startDate(scheduleOrder(scheduleOrderTime)) AND NOT 
    (date >  endDate(scheduleOrder(scheduleOrderTime))) AND 
    in(scheduleOrder(scheduleOrderTime), stock)
        BY shipmentDOW(scheduleOrderTime);
 
nameSupplier 'Поставщик'  (ScheduleOrderTime o)= name(supplier(scheduleOrder(o))) IN id;
countShipmentDate 'Кол-во поставок' (DOW DOW, Stock stock, ScheduleOrder order) =  countShipment(DOW, stock, startDate(order));  
countShipmentDate 'Кол-во поставок' (DOW DOW, Stock stock, ScheduleOrderTime scheduleOrderTime, ScheduleOrder order) =  countShipment(DOW, stock, scheduleOrderTime, startDate(order));  
    
overPeriod 'Кол-во недель между поставками' (ScheduleOrderTime o)= overPeriod(scheduleOrder(o));        
        
EXTEND FORM scheduleOrder
    OBJECTS st=Stock PANEL  
    PROPERTIES (st) SHOWIF toShowGenDayMonth(s) SELECTOR name
    FILTERS isCompany(st)

    OBJECTS do=DOW GRID
    PROPERTIES(do) SHOWIF toShowGenDayMonth(s) staticCaption
    PROPERTIES READONLY SHOWIF toShowGenDayMonth(s) countShipmentDate(do,st,s)
    FILTERS countShipmentDate(do,st,s)
    
          
    OBJECTS l=ScheduleOrderTime GRID
    PROPERTIES(l) SHOWIF toShowGenDayMonth(s) READONLY nameSupplier, orderNameDOW, overPeriod 
//    PROPERTIES READONLY SHOWIF toShowGenDayMonth(s) countShipmentDOWStockScheduleOrderTimeDateScheduleOrder(do,st,l,s)     
    
    FILTERS shipmentDOW(l) == do,
            countShipmentDate(do,st,l,s) 
;
DESIGN scheduleOrder {

    days {
        fill = 1;
        tabbed = TRUE;
        NEW days1 {
            horizontal = TRUE;
            fill = 1;
            caption = 'Выбор';
            MOVE BOX(dow);
            MOVE BOX(d);
            MOVE BOX(m);
        }
        NEW days2 {
            fill = 1;
            caption = 'Статистика';
            MOVE BOX(st);
            NEW days21 {
                fill = 1;
                horizontal = TRUE;
                MOVE BOX(do) {caption = 'День поставки';}
                MOVE BOX(l) { caption = 'Поставщики';}
            }            
        }        
    }
}


WHEN LOCAL CHANGED(supplier(ScheduleOrder o)) AND NOT CHANGED(supplierStock(o)) DO {
    supplierStock(o) <- defaultStock(supplier(o)) IF countStocks(supplier(o)) == 1;
}

//WHEN LOCAL CHANGED(customerScheduleOrder(o)) DO {
//    customerStockScheduleOrder(o) <- defaultCustomerStockEmployeeLegalEntity(currentUser(), customerScheduleOrder(o))
//        IF countAccessCustomerStockEmployeeLegalEntity (currentUser(), customerScheduleOrder(o)) == 1;
//}

CONSTRAINT supplier(ScheduleOrder scheduleOrder) AND NOT isSupplier(supplier(scheduleOrder))
    CHECKED BY supplier[ScheduleOrder] MESSAGE 'Для заказа выбрано в качестве поставщика организация, не являющаяся поставщиком';
CONSTRAINT customer(ScheduleOrder scheduleOrder) AND NOT isCompany(customer(scheduleOrder))
    CHECKED BY customer[ScheduleOrder] MESSAGE 'Для заказа выбрано в качестве покупателя организация, не являющаяся компанией';

CONSTRAINT supplier() AND NOT isSupplier(supplier())
    CHECKED BY supplier[] MESSAGE 'Выбрана в качестве поставщика организация, не являющаяся поставщиком';
CONSTRAINT customer() AND NOT isCompany(customer())
    CHECKED BY customer[] MESSAGE 'Выбрана в качестве покупателя организация, не являющаяся компанией';

isCompanySupplier(LegalEntity l, LegalEntity s) = isCompany(l) AND isSupplier(s);

EXTEND FORM legalEntity
    PROPERTIES(s, l) addSaleSchedulerOrderPurchase = addScheduleOrderSupplierCustomer DRAW ss SHOWIF isCompanySupplier(l, s) //isCompanyLegalEntity(l) AND isSupplierLegalEntity(s)
    PROPERTIES(l, c) addPurchaseSchedulerOrderPurchase = addScheduleOrderSupplierCustomer DRAW sc SHOWIF isCompanySupplier(c, l) //isCompanyLegalEntity(c) AND isSupplierLegalEntity(l)
;

// Добавляем Dashboard для работы с графиками закупок
@defineScheduleOrderDashboard(' (закупка)', customer);

countStocks (ScheduleOrder order)= GROUP
    SUM 1 IF in(order,Stock stock);       
countAccessCompanyEmployeeStock (ScheduleOrder order)= GROUP
    SUM 1 IF accessCompany(currentUser(),Stock stock) AND in(order,stock);
EXTEND FORM scheduleOrderDashboard FILTERS countAccessCompanyEmployeeStock(s) OR NOT countStocks(s);
EXTEND FORM scheduleOrderDashboard FILTERS countAccessCompanyEmployeeStock(ss) OR NOT countStocks(ss);

autoCloseScheduleOrderInterval 'За сколько дней аннулировать точки заказов по графику' = DATA INTEGER ();
autoCloseScheduleOrderDetails 'Аннулировать точки заказов'()  { 
    NEWSESSION {
        isClosed(ScheduleOrderDetail o) <- TRUE WHERE dateOrder(o) <= subtract(currentDate(), autoCloseScheduleOrderInterval());    
        APPLY;
    }
}

autoCreatedDayMonth 'Создавать точки заказов в момент создания или изменения графика (Генерация по дням недели и месяца)' = DATA BOOLEAN ();
EXTEND FORM options
    PROPERTIES() autoCloseScheduleOrderInterval, autoCloseScheduleOrderDetails, autoCreatedDayMonth 
;
DESIGN options {
    orders {
        NEW schedule {
            caption = 'Графики заказов';
            MOVE PROPERTY(autoCloseScheduleOrderInterval());
            MOVE PROPERTY(autoCloseScheduleOrderDetails());
            MOVE PROPERTY(autoCreatedDayMonth());
        }
    }
}

NAVIGATOR {
    purchaseDashboardNavigator {
        NEW scheduleOrderDashboard;
        NEW scheduleOrderDetails;          
    }
}

CONSTRAINT customerStock() AND NOT isCompany(customerStock())
    CHECKED BY customerStock[] MESSAGE 'Склад покупателя должен быть складом компании';
    
EXTEND FORM scheduleOrderDetails
    FILTERGROUP myOrders
        FILTER 'Мои' createdUser(o) == currentUser() 'F7' DEFAULT
;

// ---------------------------- Циклический заказ, создаётся на основании последнего   ----------------------------

overCopy(ScheduleOrder scheduleOrder, ScheduleOrder o)+{
    isCycled(o) <- isCycled(scheduleOrder);
    dataIn(o,StockGroup st) <- dataIn(scheduleOrder,st);
    dataIn(o,Stock st) <- dataIn(scheduleOrder,st);
}

EXTEND FORM scheduleOrder 
    PROPERTIES (s) isCycled 
;

DESIGN scheduleOrder {
    dateContainer {
        MOVE PROPERTY(isCycled(s));  
    } 
}

WHEN (CHANGED(supplier(ScheduleOrder o)) OR CHANGED(supplierStock(o)) OR CHANGED(customer(o)) OR CHANGED(startDate(o)) OR CHANGED(endDate(o)) OR CHANGED(timeToOrder(o))
    OR (GROUP SUM 1 IF CHANGED (in(o,Stock st))) OR (GROUP SUM 1 IF (CHANGED(orderDOW(ScheduleOrderTime t)) OR CHANGED(shipmentDOW(t)) OR CHANGED(timeToOrder(t))) AND scheduleOrder(t)==o))
    AND type(o)==GenType.genDayMonth AND autoCreatedDayMonth() AND startDate(o) AND customer(o) AND supplier(o)  DO {
    reCreate(o);    
}

isEditableScheduleOrderDashboard '{system.is.editable}' = DATA LOCAL NESTED BOOLEAN();

WHEN LOCAL CHANGED (isEditableScheduleOrderDashboard()) DO {
    isEditable() <- isEditableScheduleOrderDashboard();
}

EXTEND FORM scheduleOrderDashboard
    PROPERTIES() isEditableScheduleOrderDashboard
;
DESIGN scheduleOrderDashboard {
    TOOLBARBOX {
        REMOVE PROPERTY (isEditable());
        MOVE PROPERTY(isEditableScheduleOrderDashboard()) BEFORE TOOLBARRIGHT;
    }
}

//----------------- Логирование времени поставок ------------------- //

CLASS ScheduleOrderTimeLog 'Запись лога времени поставок';
TABLE scheduleOrderTimeLog(ScheduleOrderTimeLog);

scheduleOrder = DATA ScheduleOrder (ScheduleOrderTimeLog) INDEXED;
shipmentNameDOW 'День недели поставки' = DATA BPISTRING[100] (ScheduleOrderTimeLog);

nameUser 'Имя пользователя' = DATA STRING[100] (ScheduleOrderTimeLog);
dateTime 'Время изменения' = DATA DATETIME (ScheduleOrderTimeLog);

WHEN [GROUP SUM 1 IF CHANGED(ScheduleOrderTime t IS ScheduleOrderTime) BY OVERRIDE scheduleOrder(t), PREV(scheduleOrder(t))](ScheduleOrder o) DO {
    NEW l = ScheduleOrderTimeLog {
        scheduleOrder(l) <- o;
        shipmentNameDOW(l) <- STRING[100](GROUP CONCAT shipmentNameDOW(ScheduleOrderTime t) IF scheduleOrder(t) == o, ',' ORDER numberM(shipmentDOW(t)));
        nameUser(l) <- ISTRING[30](name(currentUser()));
        dateTime(l) <- currentDateTime();
    }
}

EXTEND FORM scheduleOrder
    OBJECTS logT = ScheduleOrderTimeLog
    PROPERTIES(logT) READONLY SHOWIF toShowGenDayMonth(s) shipmentNameDOW, nameUser, dateTime
    FILTERS s == scheduleOrder(logT)
;

DESIGN scheduleOrder {
    days {
        MOVE BOX(logT) {
            caption = 'История';
        }
    }
}


countTreadsaddScheduleOrderDetailData = DATA INTEGER ();
countTreadsaddScheduleOrderDetail 'К-во потоков для действия "Сгенерировать заказы (закупка) по графикам без даты окончания (многопоточно)"' () = OVERRIDE countTreadsaddScheduleOrderDetailData(), 6;

addScheduleOrderDetailMultithread  'Сгенерировать заказы (закупка) по графикам без даты окончания (многопоточно)' ()  {    
    LOCAL to = BOOLEAN (ScheduleOrder);
    to(ScheduleOrder o) <- o IS ScheduleOrder AND ((NOT endDate(o)) OR (endDate(o) >= currentDate() AND NOT countDetailsCurrent(o)));

    NEWEXECUTOR {
        FOR to(ScheduleOrder o)  ORDER supplier(o), o DO  NEWTHREAD NEWSESSION NEWSQL {
            create(o);
            APPLY;
        }
    } THREADS countTreadsaddScheduleOrderDetail();
}

EXTEND FORM options 
    PROPERTIES() countTreadsaddScheduleOrderDetail, addScheduleOrderDetailMultithread
;
DESIGN options {
    orders {
        MOVE PROPERTY(countTreadsaddScheduleOrderDetail());
        MOVE PROPERTY(addScheduleOrderDetailMultithread());
    }
}
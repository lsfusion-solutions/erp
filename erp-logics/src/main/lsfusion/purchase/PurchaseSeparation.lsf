MODULE PurchaseSeparation;

REQUIRE LegalEntity, ItemManager;

NAMESPACE Purchase;

// 
CLASS Separation 'Признак разделения';
TABLE separation(Separation);

@defineExternalizable(separation, STRING[100]);

name 'Наименование' = DATA ISTRING[50](Separation) IN id;
legalEntity = DATA LegalEntity (Separation) AUTOSET;
nameLegalEntity 'Организация' (Separation s)= name(legalEntity(s)) CHARWIDTH 20;

FORM separation 'Признак разделения'
    OBJECTS t=Separation PANEL
    PROPERTIES(t) name, id SHOWIF showIDs(), nameLegalEntity
    EDIT Separation OBJECT t
;

FORM separations 'Признаки разделения'
    OBJECTS t=Separation
    PROPERTIES(t) READONLY name, id READONLY SHOWIF showIDs(), nameLegalEntity
    ORDERS name(t)
    LIST Separation OBJECT t
;
DESIGN separations { BOX { size = (600, 400); } }

TABLE brandLegalEntity(Brand, LegalEntity);

separation = DATA  Separation(Brand, LegalEntity);
nameSeparation 'Признак разделения' (Brand b, LegalEntity l) = name(separation(b, l));

EXTEND CLASS Separation {
    none 'Отсутствует'
}

//-- Выбор групп товара  
TABLE legalEntitySkuGroup(LegalEntity, SkuGroup);   
  
dataSeparation  = DATA Separation (LegalEntity, SkuGroup);
nameDataSeparation 'Признак разделения' (LegalEntity l, SkuGroup g) = name(dataSeparation(l,g));

levelParentSeparation (LegalEntity supplier, SkuGroup skuGroup) = GROUP
                                                              MIN level(skuGroup, SkuGroup parent) IF dataSeparation(supplier, parent) MATERIALIZED;    
nearestParentSeparation (LegalEntity supplier, SkuGroup skuGroup) = skuGroup(skuGroup, levelParentSeparation(supplier, skuGroup));
nearestSeparation (LegalEntity supplier, SkuGroup skuGroup) = dataSeparation(supplier, nearestParentSeparation (supplier, skuGroup));

separation  (LegalEntity supplier, SkuGroup skuGroup) =
    OVERRIDE dataSeparation(supplier, skuGroup), 
             nearestSeparation (supplier, skuGroup) MATERIALIZED;    
nameSeparation 'Признак разделения' (LegalEntity l, SkuGroup g) = name(separation(l,g));

TABLE separationLegalEntitySku (LegalEntity, Sku);
dataSeparation = DATA Separation (LegalEntity, Sku) TABLE separationLegalEntitySku;
nameDataSeparation 'Признак разделения' (LegalEntity l, Sku s) = name(dataSeparation(l, s));

nullSeparation (LegalEntity l, Sku s) = OVERRIDE dataSeparation(l, s), separation(brand(s), l), separation(l, skuGroup(s)) MATERIALIZED TABLE separationLegalEntitySku;
separation (LegalEntity l, Sku s) = OVERRIDE nullSeparation(l, s), Separation.none IF l IS LegalEntity AND s IS Sku;  
nameSeparation 'Признак разделения (перегруженный)' (LegalEntity l, Sku s) = name(separation(l,s));

countSeparationSkuGroup  (LegalEntity supplier) = GROUP SUM 1 IF separation(supplier, SkuGroup sku);                       
countSeparationSku (LegalEntity supplier) = GROUP SUM 1 IF separation(supplier, Sku sku);   


CONSTRAINT dataSeparation (LegalEntity l, SkuGroup g) AND NOT legalEntity(dataSeparation (l, g)) == l  
    CHECKED BY dataSeparation[LegalEntity,SkuGroup]
        MESSAGE 'Признак разделения, заданный для группы, должен действовать и для организации.';
CONSTRAINT separation(Brand b, LegalEntity l) AND NOT legalEntity(separation(b, l)) == l  
    CHECKED BY separation[Brand,LegalEntity]
        MESSAGE 'Признак разделения, заданный для бренда, должен действовать и для организации.';
CONSTRAINT dataSeparation(LegalEntity l, Sku s) AND NOT legalEntity(dataSeparation(l, s)) == l  
    CHECKED BY dataSeparation[LegalEntity,Sku]
        MESSAGE 'Признак разделения, заданный для SKU, должен действовать и для организации.';

EXTEND FORM legalEntity
    OBJECTS se = Separation GRID 
    PROPERTIES(se) name, id
    PROPERTIES(se) NEW, deletese=DELETE GRID 
    FILTERS legalEntity(se) == l        
    
    TREE skuTree1 sk1 = SkuGroup PARENT parent(sk1)
    PROPERTIES READONLY order(sk1), name(sk1)
    ORDERS order(sk1), name(sk1)
    FILTERGROUP inactive1 FILTER 'Активные' active(sk1) 'F6' DEFAULT    
    PROPERTIES nameSeparation(l, sk1)
    FILTERS childAccess(currentUser(), sk1) OR NOT sk1 IS SkuGroup
    
    OBJECTS bb = Brand
    PROPERTIES(bb) READONLY name
    PROPERTIES nameSeparation(bb, l)
    ORDERS name(bb)
    
    OBJECTS ks = Sku
    PROPERTIES (ks) READONLY idBarcode, name, id SHOWIF showIDs()
    PROPERTIES nameDataSeparation(l, ks), nameSeparation(l, ks) READONLY
    FILTERS isParent(sk1, ks)
;

DESIGN legalEntity {
    extendContainer {
        NEW separation {
            caption = 'Признаки разделения';
            horizontal = TRUE;
            MOVE BOX(se);
            NEW separationParams {
                fill = 4;
                horizontal = TRUE;
                MOVE BOX(TREE skuTree1);
                NEW sepTab {
                    fill = 1;
                    tabbed = TRUE;
                    MOVE BOX(bb);
                    MOVE BOX(ks);
                }
            }
        }
    }
}

// Метакоды по добавлению на форму

// ----------------------------------------- Метакод по добавлению соглашений в документы -------------------------------------- //

META defineDocumentAbstractHeaderSeparation (object)
    separation (object) = ABSTRACT Separation (###object) MATERIALIZED INDEXED;
    nameSeparation 'Признак разделения' (###object object)= name(separation(object)) IN documentPrm CHARWIDTH 15;
END
META defineDocumentHeaderSeparation (object)
    separation (object) = DATA Separation (###object);
    nameSeparation 'Признак разделения' (###object object)= name(separation(object)) IN documentPrm CHARWIDTH 15;
END
META defineDocumentInterfaceHeaderSeparation (object)
    @defineDocumentAbstractHeaderSeparation(object);
    @defineDocumentHeaderSeparation(user###object);
    separation(User###object object) += separation(object);

END

META deriveDocumentHeaderSeparation (object, supplierProp, caption)
    CONSTRAINT separation(###object o) AND NOT supplierProp(o) == legalEntity(separation(o)) AND 
               (CHANGED(separation(o)) OR CHANGED(supplierProp(o)))
               CHECKED BY separation[###object]
               MESSAGE 'Признак разделения в '##caption##' должен соответствовать организации';

END

META defineDocumentDetailSeparation (object, detail)
    separation (###detail detail) = separation(object(detail));
    nameSeparation 'Признак разделения' (###detail detail) = name(separation(detail));
END

META defineDocumentDetailSeparation (object)
    @defineDocumentDetailSeparation(object, object##Detail);
END

META defineDocumentInterfaceSeparation (object)
    @defineDocumentInterfaceHeaderSeparation(object);
    @defineDocumentDetailSeparation(object, object##Detail);
END
MODULE Calendar;

REQUIRE MasterData, Time;


CLASS PeriodType 'Тип периода';
TABLE periodType(PeriodType);

name 'Наименование'  = DATA STRING[50] (PeriodType) IN id;

CLASS Period 'Период';
TABLE period(Period);

number 'Номер' = DATA STRING[30] (Period) NONULL;
type 'Тип периода' = DATA PeriodType(Period) NONULL;
nameType 'Тип периода' (Period p)= name(type(p));
dateFrom 'Действует (с)' = DATA DATE (Period) NONULL;
dateTo 'Действует (по)' = DATA DATE (Period) NONULL;

WHEN LOCAL SET(Period p IS Period) DO {
    IF p IS Period AND NOT dateFrom(p) THEN dateFrom(p) <- currentDate();
    IF p IS Period AND NOT dateTo(p) THEN dateTo(p) <- currentDate();
};

countPeriodCross (PeriodType pt) = GROUP SUM 1 IF dateFrom(Period p2)>= dateFrom(Period p1) AND dateFrom(p2)<= dateTo(p1) 
                                                    AND type(p1) == type(p2) AND p1 != p2 BY type(p1);     
CONSTRAINT countPeriodCross(PeriodType pt) > 0 MESSAGE 'В указанных датах уже есть период такого типа';

period(PeriodType type, DATE date) = GROUP LAST Period period 
                                                ORDER dateFrom(period), period WHERE dateFrom(period)<=date AND dateTo(period)>=date BY type(period); 

FORM periodType 'Тип периода'
    OBJECTS pt = PeriodType PANEL
    PROPERTIES (pt) name

    EDIT PeriodType OBJECT pt
;

FORM period 'Период'
    OBJECTS p = Period PANEL
    PROPERTIES (p) number, nameType, dateFrom, dateTo

    EDIT Period OBJECT p
;

FORM periods 'Периоды'
    OBJECTS pt = PeriodType
    PROPERTIES(pt) READONLY name
    PROPERTIES(pt) NEWSESSION NEW, EDIT 
    ORDERS name(pt)
     
    OBJECTS p = Period
    PROPERTIES(p) READONLY number, nameType, dateFrom, dateTo
    PROPERTIES(p) NEWSESSION NEW, EDIT, DELETE 
    FILTERS type(p) == pt
    ORDERS number(p)
    
    LIST Period OBJECT p
;

NAVIGATOR {
    masterData {
        NEW periods;
    }
}

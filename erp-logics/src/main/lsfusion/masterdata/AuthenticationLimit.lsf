MODULE AuthenticationLimit;

REQUIRE Authentication, SystemEvents, Security;

NAMESPACE Authentication;

mxcn 'Максимально разрешённое кол-во подключений (без админов)' = DATA INTEGER ();
useCustomConnectionLimit = ABSTRACT BOOLEAN ();
 
CONSTRAINT (GROUP SUM 1 IF connectionStatus(Connection c) = ConnectionStatus.connectedConnection 
                        AND login(user(c)) != 'admin') > mxcn() AND NOT useCustomConnectionLimit()
    MESSAGE 'Превышено количество закупленных лицензий на программное обеспечение.';

showMxcn = DATA BOOLEAN();

EXTEND FORM connections
    PROPERTIES () READONLY mxcn SHOWIF showMxcn() DRAW c TOOLBAR
;
    
// эту опцию не предлагать. Только если клиент сам попросит и договорится с руководителем проекта
// срабатывает до отключения по опции для роли. Поэтому вместе их включать смысла нет
dblck = DATA BOOLEAN ();

CONSTRAINT [GROUP SUM 1 IF connectionStatus(Connection c) = ConnectionStatus.connectedConnection 
                        BY user(c)](User u) > 1 AND login(u) != 'admin' AND dblck()
    MESSAGE 'Пользователь с таким логином уже работает. Возможно было некорректное завершение работы программы.';

autoLogoutSameUser 'Отключать при повторном входе' = DATA BOOLEAN (UserRole);
EXTEND FORM securityPolicy
    PROPERTIES(ur) autoLogoutSameUser PANEL
;
DESIGN securityPolicy {
    roleApplicationSettings { MOVE PROPERTY(autoLogoutSameUser(ur)); }
}

onClientStarted() + {
    IF GROUP SUM 1 IF has(currentUser(), UserRole r) AND autoLogoutSameUser(r) THEN 
        FOR connectionStatus(Connection c) = ConnectionStatus.connectedConnection AND user(c) = currentUser() AND c != currentConnection() 
            DO shutdown(c);
}

MODULE LegalEntityManager;

REQUIRE LegalEntity, EmployeeManager, Stock;

NAMESPACE LegalEntity;

manager = DATA Employee (LegalEntity);
nameManager 'Менеджер' (LegalEntity l) = shortName(manager(l));
inManager 'Отм' (Employee e, LegalEntity l) = e == manager(l);
countInManager (Employee e) = GROUP SUM 1 IF inManager (e, LegalEntity l);

manager = DATA Employee (Stock);
nameManager 'Менеджер' (Stock s) = shortName(manager(s));
inManager 'Отм' (Employee e, Stock s) = e == manager(s);

overManager 'Менеджер' (Stock s) = OVERRIDE manager(s), manager(legalEntity(s));
nameOverManager 'Менеджер' (Stock s) = shortName(overManager(s));
overInManager 'Отм' (Employee e, Stock s) = OVERRIDE inManager(e, s), inManager(e, legalEntity(s));

EXTEND FORM legalEntity
    PROPERTIES (l) nameManager
    OBJECTS stlm=Stock
    PROPERTIES(stlm) READONLY name
    PROPERTIES nameOverManager(stlm)
    FILTERS legalEntity(stlm) == l
;

DESIGN legalEntity {
    extendContainer {
        NEW managerContainer {
            caption = 'Менеджмент';
            MOVE PROPERTY(nameManager(l));
            MOVE BOX(stlm);
        }
    } 
}

EXTEND FORM employee
    TREE legalEntityTreeM lgm = LegalEntityGroup PARENT parent(lgm)
    PROPERTIES READONLY  lgmTreeName = name(lgm)
    ORDER lgmTreeName

    OBJECTS lm = LegalEntity GRID
    PROPERTIES(lm) READONLY name, nameManager
    PROPERTIES (e, lm) inManager
    ORDER name(lm)
    FILTERS isParent(lgm, lm) OR (lm IS LegalEntity AND NOT lgm)
    
    OBJECTS leSt = Stock
    PROPERTIES(leSt) READONLY name, nameOverManager
    PROPERTIES overInManager(e, leSt)
    FILTERS legalEntity(leSt) == lm
;

DESIGN employee {
    managerTabContainer {
        NEW managerContainer {
            caption = 'По организации';
            fill = 1;
            type = SPLITH;
            MOVE BOX(TREE legalEntityTreeM) {fill = 1;};
            MOVE BOX(lm) {
                    fill = 3;
                    MOVE BOX(leSt);
                };
        }
    }
}
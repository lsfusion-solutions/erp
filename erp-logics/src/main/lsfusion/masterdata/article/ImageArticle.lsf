MODULE ImageArticle;

REQUIRE ItemArticle;

NAMESPACE Item;

thumbnailImage 'Изображение' = DATA LOCAL IMAGEFILE (INTEGER);
urlImage 'Ссылка' = DATA LOCAL TEXT (INTEGER);
sizeImage 'Размер' = DATA LOCAL STRING[10] (INTEGER);
startImage 'Страница поиска' = DATA LOCAL INTEGER ();
articleImage 'Артикул' = DATA LOCAL Article ();

searchImage 'Искать изображение в сети'  INTERNAL  'lsfusion.erp.integration.image.SearchImageArticleAction' (Article);  
searchFirstImage 'Мне повезёт'  INTERNAL  'lsfusion.erp.integration.image.SearchFirstImageArticleAction' (Article); 
searchMoreImageArticle 'Следующие'  INTERNAL  'lsfusion.erp.integration.image.SearchMoreImageArticleAction' (); 
loadImage 'Загрузить изображение в артикул'  INTERNAL  'lsfusion.erp.integration.image.LoadImageArticleAction' (Article, BPSTRING[1000]); 
uploadImageFromDirectory 'Загрузить изображение из папки'  INTERNAL  'lsfusion.erp.integration.image.UploadImageArticleFromDirectoryAction' (Article);
uploadImageFromURL 'Загрузить изображение по ссылке'  INTERNAL  'lsfusion.erp.integration.image.UploadImageArticleFromURLAction' (Article);

patternImage 'Шаблон поиска изображений' = DATA STRING[100] (Brand);
patternImage 'Шаблон поиска изображений' (Article article) = patternImage(brand(article));

urlImage 'Ссылка' = DATA STRING[1000] (Article) CHARWIDTH 20;
WHEN LOCAL CHANGED(urlImage(Article a)) DO uploadImageFromURL(a); 

idImage 'Идентификатор изображения' = DATA STRING[100] (Article);
overIdImage (Article article) = OVERRIDE idImage(article), id(article);
timeChangedImage 'Время последнего изменения' = DATA DATETIME (Article);
pathImageArticles 'Папка с изображениями' = DATA BPSTRING[100] ();
uploadAllImagesArticle 'Загрузить все изображения' ()  { 
    FOR (overIdImage(Article a)) DO {
        uploadImageFromDirectory(a);
        IF CHANGED(timeChangedImage(a)) THEN
            APPLY;
    }
};

EXTEND FORM article 
    PROPERTIES(a) idImage, searchImage, searchFirstImage, uploadImageFromDirectory, 
              urlImage, patternImage;

DESIGN article {
    imageBox {
        MOVE PROPERTY(idImage(a));
        NEW buttonsBox {
            horizontal = TRUE;
            MOVE PROPERTY(searchImage(a));
            MOVE PROPERTY(searchFirstImage(a));
            MOVE PROPERTY(uploadImageFromDirectory(a));
            MOVE PROPERTY(urlImage(a));
            MOVE PROPERTY(patternImage(a));
        }
    }
}

EXTEND FORM brand PROPERTIES(t) patternImage;
EXTEND FORM attributesItem PROPERTIES(brand) patternImage;

EXTEND FORM options PROPERTIES() pathImageArticles, uploadAllImagesArticle;
DESIGN options {
    items {
        NEW itemImages {
            caption = 'Изображения';
            MOVE PROPERTY(pathImageArticles());
            MOVE PROPERTY(uploadAllImagesArticle());
        }
    }
}

FORM chooseImage 'Выбор изображения' 
    OBJECTS i = INTEGER
    PROPERTIES(i) READONLY thumbnailImage PANEL, urlImage PANEL, sizeImage PANEL
    PROPERTIES() searchMoreImageArticle DRAW i
    FILTERS thumbnailImage(i)
; 

DESIGN chooseImage {
    NEW imageBox {
        caption = '';
        MOVE PROPERTY(thumbnailImage(i)) {
                valueHeight = 200;
                valueWidth = 300;
        }
        MOVE PROPERTY(urlImage(i)) {
                valueWidth = 400;
        };
        MOVE PROPERTY(sizeImage(i));
    }
    MOVE PROPERTY(searchMoreImageArticle());
    MOVE TOOLBARBOX;
}

chooseImageAction 'Выбор изображения' (Article article)  { 
    DIALOG chooseImage OBJECTS i INPUT DO {
        loadImage(article, urlImage(i));
        startImage() <- NULL;
        articleImage() <- NULL;
    }   
}
MODULE EmployeeLegalEntity;

REQUIRE Employee, LegalEntity;

TABLE legalEntityEmployee (LegalEntity, Employee);
TABLE legalEntityGroupEmployee(LegalEntityGroup, Employee);

//--  Компания
dataAccessCompany 'Доступ разрешен (компания)' = DATA BOOLEAN (LegalEntity, Employee);
dataAccessCompany 'Доступ разрешен (компания)' = DATA BOOLEAN (LegalEntityGroup, Employee);

levelParentCompany (Employee employee, LegalEntityGroup legalEntityGroup) = GROUP
                                                               MIN level(legalEntityGroup, LegalEntityGroup parent) IF dataAccessCompany(parent, employee) MATERIALIZED;
nearestParentCompany (Employee employee, LegalEntityGroup legalEntityGroup) = legalEntityGroup(legalEntityGroup, levelParentCompany(employee, legalEntityGroup));
nearestInCompany (Employee employee, LegalEntityGroup legalEntityGroup) =
    dataAccessCompany(nearestParentCompany (employee, legalEntityGroup), employee);

accessCompany 'Доступ разрешен (компания)' (Employee employee, LegalEntityGroup legalEntityGroup) = OVERRIDE
    dataAccessCompany(legalEntityGroup, employee),
    nearestInCompany(employee, legalEntityGroup),
    employee IS Employee AND legalEntityGroup IS LegalEntityGroup AND NOT limitAccess(employee); // MATERIALIZED;

accessCompany 'Доступ разрешен (компания)' (Employee employee, LegalEntity legalEntity) = OVERRIDE
    dataAccessCompany(legalEntity, employee),
    nearestInCompany(employee, legalEntityGroup(legalEntity)) AND isCompany(legalEntity),
    employee IS Employee AND isCompany(legalEntity) AND NOT limitAccess(employee) MATERIALIZED;

countAccessCompany 'Кол-во доступных подразделений (компания)' (Employee employee, LegalEntityGroup legalEntityGroup) =
    GROUP
          SUM 1 IF accessCompany(employee, LegalEntity legalEntity)
                AND isParent(legalEntityGroup(legalEntity), legalEntityGroup) MATERIALIZED;
          
legalEntity = DATA LegalEntity (Employee) COMPLEX;
nameLegalEntity 'Организация' (Employee e) = name(legalEntity(e));

overCopy(Employee e,Employee employee) + { 
    legalEntity(e) <- legalEntity(employee);    
}
EXTEND FORM employee
    PROPERTIES(e) nameLegalEntity
    TREE legalEntityTree lg = LegalEntityGroup PARENT parent(lg)
    PROPERTIES READONLY  SHOWIF limitAccess(e) lgTreeName = name(lg)
    ORDER lgTreeName
    PROPERTIES(e,lg) SHOWIF limitAccess(e) accessCompany

    OBJECTS l = LegalEntity GRID
    PROPERTIES(l) READONLY id SHOWIF showLimitAccess(e), legalEntityName = name SHOWIF limitAccess(e)
    ORDER legalEntityName
    FILTERS isParent(lg, l) OR (l IS LegalEntity AND NOT lg)
    FILTERGROUP inactiveLegalEntity FILTER 'Активная' active(l) 'shift F10' DEFAULT
    PROPERTIES(e,l) SHOWIF limitAccess(e) accessCompany
;
DESIGN employee {
    personal {
        MOVE PROPERTY(nameLegalEntity(e)) AFTER PROPERTY(firstName(e));
    }
    pane {
        NEW legalEntityContainer {
            caption = 'Организации';
            type = SPLITH;
            MOVE BOX(TREE legalEntityTree) {
                caption = 'Группы организаций';
                PROPERTY (accessCompany(e,lg)) {valueWidth = 100; }
            }
            MOVE BOX(l) {
                GRID(l) {
                    PROPERTY (accessCompany(e,l)) {valueWidth = 100; }
                }
            }
        }
    }
}

EXTEND FORM employees
    PROPERTIES(e) READONLYIF isReadonly() nameLegalEntity
;

EXTEND FORM  legalEntity
    OBJECTS e = Employee
    PROPERTIES(e) READONLY name[Contact], objectClassName
    PROPERTIES(e) NEWEDIT, EDIT, DELETE 
    
    FILTERS  legalEntity(e) == l
    FILTERGROUP active FILTER 'Активные' active(e) 'F5'  DEFAULT 
;
DESIGN  legalEntity {
    extendContainer {
        MOVE BOX(e);
    }
}
// ------------------------------------ Доступные организации для пользователей -------------------------- //

countAccessCompany (Employee employee) = GROUP
                                              SUM 1 IF isCompany(LegalEntity legalEntity) AND accessCompany(employee, legalEntity);

defaultCompany 'Компания по умолчанию' (Employee employee)= GROUP
                                                                 MIN LegalEntity legalEntity IF isCompany(legalEntity) AND accessCompany(employee, legalEntity);

// Для документа, у которого задана организация
META extendFormFilterAccessLegalEntity(object, concrete, form, organizationProp, filter)
    EXTEND FORM form FILTERS access###filter[Employee,LegalEntity](currentUser[](),organizationProp[###object](concrete)) OR NOT organizationProp[###object](concrete);
END
// Когда организация идет объектом
META extendFormFilterLegalEntityAccess(concrete, form, filter)
    EXTEND FORM form FILTERS access###filter(currentUser(), concrete);
END
META extendFormFilterLegalEntityAccess(concrete, form) // по умолчанию для компании
    @extendFormFilterLegalEntityAccess(concrete, form, company);
END
// Для группы организаций
META extendFormFilterLegalEntityGroupAccess(concrete, form, filter)
    EXTEND FORM form FILTERS countAccess###filter(currentUser(), concrete);
END
META extendFormFilterLegalEntityGroupAccess(concrete, form) // по умолчанию для компании
    @extendFormFilterLegalEntityGroupAccess(concrete, form, company);
END

// ----------------------------- Автоматическое проставление организаций и складов --------------------------------------- //

META defineDocumentLegalEntityAccess(objectClass, prefix, filter)
    prefix(objectClass o) <- default###filter(currentUser()) IF countAccess###filter (currentUser()) == 1
                                           WHEN SET(o IS objectClass);
END

// ----------------------------- Проставляем фильтры на формы --------------------------------------- //

@extendFormFilterLegalEntityGroupAccess(lg, companyLegalEntities, company);
@extendFormFilterLegalEntityAccess(l, companyLegalEntities, company);
MODULE Country;

REQUIRE System, MasterData, Utils, DefaultData, I18n, Currency;

CLASS Country 'Страна' : MultiLanguageNamed;

TABLE country(Country);
TABLE countryDate(Country, DATE);
TABLE countryCountry(Country, Country);

name 'Наименование' = DATA ISTRING[50](Country) CHARWIDTH 15;

nameOrigin 'Наименование (ориг.)' (country) = DATA ISTRING[50] (Country) CHARWIDTH 15 IN base; //logics.country.name.origin.country
sid 'Код страны' (country) = DATA BPSTRING[3] (Country) INDEXED IN base; // logics.country.key
//sid3Country 'Код 3 знака' (country) = DATA BPSTRING[3] (Country) IN base CHARWIDTH 15;
country 'Страна' (sID) =  GROUP AGGR Country country BY sid(country);  // logics.country

sidOrigin2 'Код 2 знака (ориг.)' (country) = DATA BPSTRING[2] (Country) IN base CHARWIDTH 15; // logics.country.sid.origin.2.country
sidOrigin3 'Код 3 знака (ориг.)' (country) = DATA BPSTRING[3] (Country) IN base CHARWIDTH 15; // logics.country.sid.origin.3.country

currency 'Валюта' (country) = DATA Currency (Country); // logics.country.currency
nameCurrency 'Валюта' (Country country) = name(currency(country)) IN base CHARWIDTH 10;

generateDates 'Генерировать выходные' (country) = DATA BOOLEAN (Country); // logics.day.generate.days.off

resident 'Отечественный производитель' (country) = DATA BOOLEAN (Country) IN base CHARWIDTH 15; // logics.country.resident.country
isEEU 'Входит в ЕАЭС' (country) = DATA BOOLEAN (Country) IN base CHARWIDTH 15; 

countryName(string) = GROUP NAGGR Country country WHERE country IS Country BY name(country);

countryOrigin(name) = GROUP NAGGR Country country WHERE country IS Country BY nameOrigin(country);

countrySIDOrigin2(string) = GROUP NAGGR Country country WHERE country IS Country BY sidOrigin2(country);

defaultCountry 'Страна по умолчанию' () = DATA Country (); // logics.country.default.country
nameDefaultCountry 'Страна по умолчанию' () = name(defaultCountry()) CHARWIDTH 30; // logics.country.default.country

jumpWorkdays = FORMULA NULL DATE PG 'jumpWorkdays($1, $2, $3)', MS 'dbo.jumpWorkdays($1, $2, $3)'; //1 - country, 2 - date, 3 - days to jump

ensureJumpWorkDays INTERNAL 'lsfusion.erp.utils.EnsureJumpWorkDaysAction' ();
onStarted() + {
    ensureJumpWorkDays();
}

EXTEND FORM options
    PROPERTIES() nameDefaultCountry
;

DESIGN options {
    commons {
        MOVE PROPERTY(nameDefaultCountry());
    }
}

language = DATA Language (Country);
nameLanguage 'Язык' (Country country) = name(language(country)) CHARWIDTH 5;

FORM country 'Страна'
    OBJECTS c = Country PANEL

    PROPERTIES(c) name, nameOrigin, sid, sidOrigin2, sidOrigin3,
                  resident, isEEU, nameCurrency, nameLanguage

    OBJECTS l = Language
    PROPERTIES name(l) READONLY, languageName(c, l)

    EDIT Country OBJECT c
;

FORM countries 'Страны'
    OBJECTS c = Country
    PROPERTIES(c) READONLY name, nameOrigin, sid, sidOrigin2, sidOrigin3,
                           resident, isEEU, nameCurrency, nameLanguage

    PROPERTIES(c) NEWSESSION NEW, EDIT, DELETE 
    ORDERS name(c)

    LIST Country OBJECT c
;

NAVIGATOR {
    regionalData {
        NEW countries;
    }
}

// ------------------------------------------------ Выходные дни --------------------------------------- //

isDayOff 'Выходной' (country, date) = DATA BOOLEAN (Country, DATE) IN base; // logics.day.off

addDayOff 'Добавить выходной день'(Country country)  { 
	NEWSESSION {
	    DIALOG dialogDate OBJECTS d INPUT DO {
	        isDayOff (country, d) <- TRUE;
	        apply();
	    }
	}
}

GROUP response;
GROUP date;

iso = DATA LOCAL DATE(INTEGER);
primary_type = DATA LOCAL STRING(INTEGER);
holidaysType = DATA LOCAL INTEGER(INTEGER);

FORM calendarific
    OBJECTS holidays = INTEGER IN response

    PROPERTIES(holidays) primary_type
    PROPERTIES(holidays) IN date iso
    FILTERS imported(holidays)
;

calendarificApiKey 'Calendarific Api Key' = DATA STRING() ECHO;
isWeekend(INTEGER dow) = dow == 6 OR dow == 0;
fillDaysOff 'Заполнить выходные дни' (Country country, YEAR year) {
    NEWSESSION {
        FOR iterate(DATE date, DATE(STRING(year) + '-01-01'), DATE(STRING(year) + '-12-31')) DO {
            isDayOff(country, date) <- isWeekend(extractDOWNumber(date));
        }
        IF calendarificApiKey() THEN {
            LOCAL response = FILE();
            EXTERNAL HTTP GET 'https://calendarific.com/api/v2/holidays?&api_key=' + calendarificApiKey() + '&country=' + sid(country) + '&year=' + STRING(year) TO response;
            IMPORT calendarific FROM response();

            FOR primary_type(INTEGER i) == 'National Holiday' DO {
                isDayOff(country, DATE(iso(i))) <- TRUE;
            }
            FOR primary_type(INTEGER i) == 'Working day on weekend' DO {
                isDayOff(country, DATE(iso(i))) <- NULL;
            }
        }
        apply();
    }
}

FORM daysOffForm 'Выходные дни' // logics.days.off
    OBJECTS country 'Страна' = Country, year 'Год' = YEAR PANEL, day 'День' = DATE
    PROPERTIES VALUE(year), fillDaysOff(country, year), addDayOff(country)
    PROPERTIES(country) READONLY nameOrigin, sid, sidOrigin2, sidOrigin3, nameCurrency, resident
    PROPERTIES VALUE(day), isDayOff(country, day)
    FILTERS isDayOff(country, day), extractYear(day) == year
    
    PROPERTIES calendarificApiKey() DRAW year
;

NAVIGATOR {
    regionalData {
        NEW daysOffForm;
    }
}
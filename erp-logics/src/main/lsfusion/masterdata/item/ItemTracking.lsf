MODULE ItemTracking;

REQUIRE Item, CustomsGroupItem;

NAMESPACE Item;

isTrackingData 'Подлежит прослеживанию' = DATA BOOLEAN (CustomsGroup) IN base;
isTracking 'Прослеживание'= DATA BOOLEAN (ExtraCode);
levelTrackingParent (CustomsGroup customsGroup) = GROUP MIN level(customsGroup, CustomsGroup parent) IF isTrackingData(parent) MATERIALIZED;
isTrackingParent (CustomsGroup customsGroup) = TRUE IF levelTrackingParent(customsGroup);
isTracking 'Подлежит прослеживанию' (CustomsGroup customsGroup) = OVERRIDE isTrackingData(customsGroup), isTrackingParent(customsGroup) MATERIALIZED;
//isTrackingCustomsGroup 'Подлежит прослеживанию' (Sku sku) = isTracking(customsGroup(defaultCountry(), sku)) MATERIALIZED;
dateStartSPT 'Дата начала прослеживания' = DATA DATE (CustomsGroup) IN base;

dataTrackingUOM 'Ед.изм по ОКРБ 008-95' = DATA STRING[3] (CustomsGroup);
levelParentTrackingUOM(CustomsGroup child)  = GROUP MIN level(child, CustomsGroup parent) IF dataTrackingUOM(parent) MATERIALIZED;
nearestParentTrackingUOM (CustomsGroup cg) = customsGroup(cg, levelParentTrackingUOM(cg));
nearestTrackingUOM (CustomsGroup cg) = dataTrackingUOM(nearestParentTrackingUOM (cg));
trackingUOM 'Ед.изм по ОКРБ 008-95' (CustomsGroup cg) = OVERRIDE dataTrackingUOM(cg), nearestTrackingUOM (cg) MATERIALIZED;
trackingUOMCustomsGroup 'Ед.изм по ОКРБ 008-95' (Sku sku) = trackingUOM(customsGroup(defaultCountry(), sku)) MATERIALIZED;
backgroundEC(ExtraCode e) = IF isTracking(e) THEN RGB(255, 204, 153);


countECTracking =  GROUP SUM 1 IF isTracking (ExtraCode ec) BY customsGroup(ec);

WHEN LOCAL (SET(isTracking(ExtraCode ec))) AND countECTracking(customsGroup(ec)) AND NOT isTracking(customsGroup(ec)) DO {
    isTrackingData(customsGroup(ec)) <- TRUE ;
}

WHEN LOCAL (DROPPED(isTracking(ExtraCode ec))) AND NOT countECTracking(customsGroup(ec)) AND isTracking(customsGroup(ec)) DO {
    isTrackingData(customsGroup(ec)) <- NULL ;
}

countDroppEC  = GROUP SUM 1 IF DROPPED(ExtraCode ec IS ExtraCode) BY PREV(customsGroup(ec));

WHEN countDroppEC(CustomsGroup g) AND NOT countECTracking(g) DO {
    isTrackingData(g) <- NULL ;
    dateStartSPT(g) <- NULL ;
    dataTrackingUOM(g) <- NULL ;
}

isTracking 'Подлежит прослеживанию' = DATA BOOLEAN (Item) IN itemBase;

dataExtraCode = DATA ExtraCode (SkuGroup);
nearestExtraCode (SkuGroup child) = GROUP LAST dataExtraCode(SkuGroup parent) ORDER DESC level(child, parent) WHERE dataExtraCode(parent);

extraCode (SkuGroup skuGroup) = OVERRIDE dataExtraCode(skuGroup), nearestExtraCode(skuGroup) MATERIALIZED;
idExtraCode '4-зн. код' (SkuGroup skuGroup) = id(extraCode(skuGroup)) IN paramsItemGroup;

extraCode = DATA ExtraCode (Brand);
idExtraCode '4-зн. код' (Brand b) = id(extraCode(b)) CHARWIDTH 4;

dataExtraCode = DATA ExtraCode (Sku);
defaultExtraCode (Sku s) = extraCode(customsGroup(defaultCountry(), s)) IF countExtraCodes(customsGroup(defaultCountry(),s))=1;
extraCode '4-зн. код' (Sku s) = OVERRIDE dataExtraCode(s), extraCode(brand(s)), extraCode(skuGroup(s)), defaultExtraCode(s) MATERIALIZED; 
idExtraCode'4-зн. код к ТН ВЭД' (Sku s)=id(extraCode(s)) CHARWIDTH 4;

isTrackingCustomsGroup 'Подлежит прослеживанию' (Sku sku) = TRUE  IF (isTracking(customsGroup(defaultCountry(), sku)) AND isTracking(extraCode(sku))) MATERIALIZED;
isTrackingOver 'Подлежит прослеживанию' (Sku s) =  OVERRIDE isTracking(s), isTrackingCustomsGroup(s) IN itemBase MATERIALIZED;
dateStartSPT 'Дата начала прослеживания' (Sku s)  = dateStartSPT(customsGroup(defaultCountry(), s)) MATERIALIZED;

needExtraCode(Sku s) = TRUE IF isTracking(customsGroup(defaultCountry(), s)) AND NOT extraCode(s);
needUOM(CustomsGroup cg) =  TRUE  IF isTracking(cg) AND NOT trackingUOM(cg);

background(Item i) += WHEN i IS Item AND needExtraCode(i) THEN RGB(250, 187, 168);
backgroundCustomsGroup(CustomsGroup cg)  += WHEN cg IS CustomsGroup AND needUOM(cg) THEN RGB(250, 187, 168);
backgroundECExtraCode(CustomsGroup cg) = IF countECTracking(cg) >= 1 THEN RGB(255, 204, 153);

EXTEND FORM customsGroup
    PROPERTIES (cg) isTracking BACKGROUND backgroundECExtraCode(cg) READONLY, trackingUOM, dateStartSPT
    PROPERTIES (ec) isTracking BACKGROUND backgroundEC(ec) BEFORE description(ec)
;

EXTEND FORM customsGroups
    PROPERTIES (gcg) READONLY isTrackingData, trackingUOM
    PROPERTIES (cg) BEFORE canonicalName(cg) READONLY isTracking BACKGROUND backgroundECExtraCode(cg), trackingUOM
    PROPERTIES (cg) READONLYIF isReadonly() dateStartSPT
;

DESIGN customsGroup {
    topContainer  {
        NEW trackingContainer{
            horizontal = TRUE;
            caption = 'Прослеживание';
            MOVE PROPERTY(isTracking(cg));
            MOVE PROPERTY(trackingUOM(cg));
            MOVE PROPERTY (dateStartSPT(cg));
        }
    }
}

EXTEND FORM extraCode
    PROPERTIES(ec) isTracking BACKGROUND backgroundEC(ec) BEFORE description(ec)
;

EXTEND FORM dialogExtraCodes
    PROPERTIES(ec) READONLY isTracking BACKGROUND backgroundEC(ec) AFTER id(ec)
    
    FILTERGROUP isTracking
        FILTER 'Только прослеживаемые' isTracking(ec) DEFAULT
;

changeExtraCode (Item i) {
    DIALOG dialogExtraCodes OBJECTS ec=extraCode(customsGroup(defaultCountry(), i)) INPUT NULL FILTERS customsGroup(ec)==customsGroup(defaultCountry(), i) DO {
        dataExtraCode(i) <- ec;
    }
}

changeExtraCode (ItemGroup g) {
    DIALOG dialogExtraCodes OBJECTS ec INPUT NULL DO{
        dataExtraCode(g) <- ec;
    }
}

EXTEND FORM itemGroup
    PROPERTIES(g) idExtraCode ON CHANGE changeExtraCode(g)
;

EXTEND FORM itemGroups
    PROPERTIES(g) READONLYIF isReadonly() idExtraCode ON CHANGE changeExtraCode(g) BACKGROUND RGB(190,190,190) IF dataExtraCode(g)
;

EXTEND FORM item
    PROPERTIES (i) BACKGROUND backgroundEC(extraCode(i)) isTrackingOver,
        idExtraCode ON CHANGE changeExtraCode(i)
;

EXTEND FORM items
    PROPERTIES (i) BACKGROUND backgroundEC(extraCode(i)) READONLYIF isReadonly() isTrackingOver,
        idExtraCode ON CHANGE changeExtraCode(i)
;

DESIGN item {
    regionPrm {
        MOVE PROPERTY (idExtraCode(i));
    }
}

DESIGN items {
    PROPERTY (idExtraCode(i)) {caption='4-зн. код';}
}

DESIGN itemGroup {
    PROPERTY (idExtraCode(g)) {caption='4-зн. код к ТН ВЭД';}
}

skipCheckCG 'Не проверять соотвествие 4-зн. кода к ТН ВЭД ' = ABSTRACT VALUE BOOLEAN (Sku);
skipCheckEC 'Не проверять соотвествие 4-зн. кода к ТН ВЭД ' = ABSTRACT VALUE BOOLEAN (Sku);

skipCheckCG 'Не проверять соотвествие 4-зн. кода к ТН ВЭД ' = DATA BOOLEAN () PREREAD;
skipCheckEC 'Не проверять соотвествие 4-зн. кода к ТН ВЭД ' = DATA BOOLEAN () PREREAD;

skipCheckCG(Sku sku) += skipCheckCG() IF sku IS Sku;
skipCheckEC(Sku sku) += skipCheckEC() IF sku IS Sku;

CONSTRAINT (CHANGED(extraCode(Sku s))) AND customsGroup(defaultCountry(),s) AND extraCode(s) AND NOT customsGroup(defaultCountry(),s)==customsGroup(extraCode(s)) AND NOT skipCheckCG(s)
    MESSAGE '4-зн. код должен соответствовать коду ТН ВЭД товара' ;

CONSTRAINT (CHANGED(customsGroup(defaultCountry(),Sku s))) AND extraCode(s) AND NOT customsGroup(defaultCountry(),s)==customsGroup(extraCode(s)) AND NOT skipCheckEC(s)
    MESSAGE '4-зн. код не соответствует коду ТН ВЭД товара' ;

changeExtraCode (Brand b) {
    DIALOG dialogExtraCodes OBJECTS ec INPUT DO {
        extraCode(b) <- ec;
    }
}

EXTEND FORM brand 
    PROPERTIES(t) idExtraCode ON CHANGE changeExtraCode(t)
;

EXTEND FORM attributesItem 
    PROPERTIES(brand) READONLY idExtraCode BEFORE site(brand) BACKGROUND backgroundEC(extraCode(brand))
;

WHEN LOCAL (CHANGED(extraCode(Sku sku))) AND extraCode(sku) AND NOT customsGroup(defaultCountry(),sku) DO {
    customsGroup(defaultCountry(),sku) <-  customsGroup(extraCode(sku));
}

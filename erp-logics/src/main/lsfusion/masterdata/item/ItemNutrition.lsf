MODULE ItemNutrition;

REQUIRE System, Item;

NAMESPACE Item;

GROUP itemNutrition 'Энергетическая ценность' : public;

energy 'Энергетическая ценность на 100г, ккал' = DATA NUMERIC[8,2] (Item) IN itemNutrition;
fats 'Жиры на 100г, г' = DATA NUMERIC[8,2] (Item) IN itemNutrition;
carbohydrates 'Углеводы на 100г, г' = DATA NUMERIC[8,2] (Item) IN itemNutrition;
proteins 'Белки на 100г, г' = DATA NUMERIC[8,2] (Item) IN itemNutrition;
containsGMO 'Содержит ГМО' = DATA BOOLEAN (Item) IN itemNutrition;
isSupplement 'Пищевая добавка' = DATA BOOLEAN (Item) IN itemNutrition;

//energyItem 'Энергетическая ценность на 100г, ккал' = DATA NUMERIC[8,2] (Item) IN itemNutrition;
//fatsItem 'Жиры на 100г, г' = DATA NUMERIC[8,2] (Item) IN itemNutrition;
//carbohydratesItem 'Углеводы на 100г, г' = DATA NUMERIC[8,2] (Item) IN itemNutrition;
//proteinsItem 'Белки на 100г, г' = DATA NUMERIC[8,2] (Item) IN itemNutrition;

@defineItemAttribute(energy, 'Энергетическая ценность', itemNutrition);

EXTEND FORM itemGroup PROPERTIES(g) userShowEnergy;
EXTEND FORM itemOptions PROPERTIES(g) READONLYIF  isReadonly() showEnergy BACKGROUND backgroundShowEnergy(g);
DESIGN itemGroup {
    itemGroupDetail1 {
        MOVE GROUP(itemNutrition,g);
    }
}

overCopy(Item s, Item d) + { 
    energy(d) <- energy(s);
    fats(d) <- fats(s);
    carbohydrates(d) <- carbohydrates(s);
    proteins(d) <- proteins(s);
    containsGMO(d) <- containsGMO(s);
    isSupplement(d) <- isSupplement(s);
}

EXTEND FORM item 
    PROPERTIES(i) SHOWIF showEnergy(i) energy, fats, carbohydrates, proteins, containsGMO, isSupplement
;
DESIGN item {
    itemPrimaryColumn2 {
        MOVE GROUP(itemNutrition,i) {
            columns = 2;
        }
    }
}
EXTEND FORM items 
    PROPERTIES(i) READONLYIF isReadonly() BEFORE deletei SHOWIF showEnergy(g)
                  energy, fats, carbohydrates, proteins, containsGMO, isSupplement
;

//----------------------- Переход на новый основной классификатор ---------------------------//
overReplaceToItemGroup(CustomGroup g, ItemGroup ng) + {
    userShowEnergy(ng) <- userShowEnergy(itemGroup(g));
}
MODULE ItemHumidity;

REQUIRE System, Item;

NAMESPACE Item;

GROUP itemHumidity 'Влажность' : public;

percentHumidity 'Влажность, %' = DATA NUMERIC[8,2] (Item) IN itemHumidity;
percentDryness 'Доля сухих веществ, %' (Item item) = 100 - (OVERRIDE percentHumidity(item), 0 IF item IS Item) IN itemHumidity; 

changePercentDryness(Item item)   { 
    INPUT n = NUMERIC[8,2] DO
        percentHumidity(item) <- 100.00-n;
}

@defineItemAttribute(humidity, 'Влажность', itemHumidity);

EXTEND FORM itemGroup PROPERTIES(g) userShowHumidity;
EXTEND FORM itemOptions PROPERTIES(g) READONLYIF  isReadonly() showHumidity BACKGROUND backgroundShowHumidity(g);
DESIGN itemGroup {
    itemGroupDetail1 {
        MOVE GROUP(itemHumidity,g) {
        }
    }
}

overCopy(Item s, Item d) + { 
    percentHumidity(d) <- percentHumidity(s);
}

EXTEND FORM item 
    PROPERTIES(i) SHOWIF showHumidity(i) percentHumidity, percentDryness ON CHANGE  changePercentDryness(i)
;
DESIGN item {
    itemPrimaryColumn2 {
        MOVE GROUP(itemHumidity,i) {
            alignment = STRETCH;
//            columns = 2;
        }
    }
}
EXTEND FORM items 
    PROPERTIES(i) READONLYIF isReadonly() BEFORE deletei SHOWIF showHumidity(g) percentHumidity
;

//----------------------- Переход на новый основной классификатор ---------------------------//
overReplaceToItemGroup(CustomGroup g, ItemGroup ng) + {
    userShowHumidity(ng) <- userShowHumidity(itemGroup(g));
}
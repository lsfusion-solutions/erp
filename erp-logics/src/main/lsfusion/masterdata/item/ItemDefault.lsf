MODULE ItemDefault;

REQUIRE Item, ScalesItem;

NAMESPACE Item;

// --------------------------- Нумераторы

loadDefaultItemNumerator 'Добавить нумератор для товара' (VARISTRING[100] name, BPSTRING[2] series, LONG minValue, LONG maxValue, INTEGER length)  { 
    NEW n = Numerator {
        name(n) <- name;
        series(n) <- series;
        minValue(n) <- minValue;
        maxValue(n) <- maxValue;
        stringLength(n) <- length;
    }
}

loadDefaultItemNumerators 'Загрузить стандартные нумераторы для товаров' ()  { 
    loadDefaultItemNumerator('Штрих-коды штучные', '', 990000000000L, 999999999999L, 12);    
    loadDefaultItemNumerator('Штрих-коды весовые', '', 1L, 99999L, 5);    
} IN loadDefault;

@implementLoadDefaultData(loadDefaultItemNumerators);

// --------------------------- Группы

loadDefaultItemGroup 'Добавить группы товаров' (VARISTRING[250] iname, VARSTRING[100] id)  { 
    NEW g = ItemGroup {
        name(g) <- iname;
        id(g) <- id;
        userShowSplit(g) <- TRUE;
        userShowCaption(g) <- TRUE;
        userShowUOM(g) <- TRUE;
        userShowBrand(g) <- TRUE;
        userShowManufacturer(g) <- TRUE;
        userShowCountry(g) <- TRUE;
    }
}

loadDefaultItemGroups 'Загрузить стандартные группы товаров' ()  { 
    loadDefaultItemGroup('Все','all');    

    pieceNumerator(ItemGroup g) <- [ GROUP MAX Numerator n BY name(n)]('Штрих-коды штучные');
    weightNumerator(ItemGroup g) <- [ GROUP MAX Numerator n BY name(n)]('Штрих-коды весовые');
} IN loadDefault;

@implementLoadDefaultData(loadDefaultItemGroups);
MODULE ScannerDaemon;

REQUIRE SystemEvents;

scannerComPort 'Com-port сканнера' = DATA INTEGER (Computer);
currentScannerComPort = scannerComPort(currentComputer());
scannerSingleRead 'Одно событие на весь штрихкод' = DATA BOOLEAN (Computer);
currentScannerSingleRead = scannerSingleRead(currentComputer());

CLASS ComLibrary 'Библиотека Com' {
    jSerialComm 'JSerialComm',
    jssc 'Jssc',
    pureJavaComm 'PureJavaComm'
}

comLibrary = DATA ComLibrary (Computer);
captionComLibrary 'Библиотека Com' (Computer c) = staticCaption(comLibrary(c));

EXTEND FORM computer PROPERTIES(c) scannerComPort, scannerSingleRead, captionComLibrary;
EXTEND FORM computers PROPERTIES(c) AFTER hostname(c) READONLY scannerComPort, scannerSingleRead, captionComLibrary;
EXTEND FORM computersDialog PROPERTIES(c) scannerComPort, scannerSingleRead, captionComLibrary;

scannerDaemonError = DATA LOCAL STRING();
scannerDaemon INTERNAL  'lsfusion.erp.daemon.ScannerDaemonAction' (INTEGER, BOOLEAN, STRING);
onDesktopClientStarted() + {
    IF currentScannerComPort() THEN
        scannerDaemon(currentScannerComPort(), currentScannerSingleRead(), staticName(comLibrary(currentComputer())));
        IF scannerDaemonError() THEN
            MESSAGE scannerDaemonError();
}
MODULE SkuSegment;

REQUIRE Barcode, PriceListPromotion, ItemNutrition;

NAMESPACE Stock;

CLASS SkuSegment 'Сегментирование по товарам';

number 'Номер' = DATA STRING[28] (SkuSegment) CHARWIDTH 8;
name 'Наименование' = DATA STRING[100](SkuSegment);

stock 'Склад'  = DATA Stock (SkuSegment);
nameStock 'Наименование склада' (SkuSegment s) = name(stock(s));

CLASS SkuSegmentLine 'Детализация';
skuSegment 'Документ' = DATA SkuSegment (SkuSegmentLine) INDEXED NONULL DELETE;

sku 'Товар' = DATA Sku (SkuSegmentLine);
nameItem 'Наименование' (SkuSegmentLine sl) = name(sku(sl));
idBarcode 'Штрихкод' (SkuSegmentLine sl) = barcode(sku(sl));

skuGroup 'Группа' = DATA SkuGroup (SkuSegmentLine);
nameItemGroup 'Группа' (SkuSegmentLine sl) = name(skuGroup(sl));
idItemGroup 'Код группы' (SkuSegmentLine sl) = id(skuGroup(sl));

uom 'Единица измерения' = DATA UOM (SkuSegmentLine);
shortNameUom 'Единица измерения' (SkuSegmentLine sl) = shortName(uom(sl));

brand 'Бренд' = DATA Brand (SkuSegmentLine);
nameBrand 'Бренд' (SkuSegmentLine sl) = name(brand(sl));

country 'Страна изготовитель' = DATA Country (SkuSegmentLine);
nameCountry 'Страна изготовитель' (SkuSegmentLine sl) = name(country(sl));

minNetWeight 'Мин. вес нетто, кг' = DATA NUMERIC[11,5] (SkuSegmentLine);
maxNetWeight 'Макс. вес нетто, кг' = DATA NUMERIC[11,5] (SkuSegmentLine);

//Для фильтра в доп. модулях
extraInNotMaterialized = ABSTRACT BOOLEAN (SkuSegmentLine, Sku);
extraInMaterialized = ABSTRACT BOOLEAN (SkuSegmentLine, Sku);

exclude 'Исключение' = DATA BOOLEAN (SkuSegmentLine);

CLASS SegmentBool 'Установленное значение'
{
    yes 'Да',
    no 'Нет'      
}

FORM segmentBool 'Установленное значение'
    OBJECTS s = SegmentBool
    PROPERTIES(s) READONLY staticCaption
    LIST SegmentBool OBJECT s
;

inPromotion 'Вхождение в акционные списки' =  DATA SegmentBool (SkuSegmentLine);
captionInPromotion 'Вхождение в акционные списки' (SkuSegmentLine s) = staticCaption(inPromotion(s)) CHARWIDTH 10;

//Для CONSTRAINT
extraIn = ABSTRACT BOOLEAN (SkuSegmentLine);

CONSTRAINT SkuSegmentLine sl IS SkuSegmentLine AND NOT (sku(sl) OR skuGroup(sl) OR uom(sl) OR brand(sl) OR country(sl) OR minNetWeight(sl) OR maxNetWeight(sl) OR extraIn(sl))
    MESSAGE 'Не должно быть строк с пустыми параметрами'
;

inD (SkuSegmentLine sl, Sku sk) = (sl IS SkuSegmentLine AND active(sk) 
                                                                AND (sk == sku(sl) OR NOT sku(sl)) 
                                                                AND (isParent(skuGroup(sk), skuGroup(sl)) OR NOT skuGroup(sl)) 
                                                                AND (UOM(sk) == uom (sl) OR NOT uom(sl)) 
                                                                AND (brand(sk) == brand(sl) OR NOT brand(sl)) 
                                                                AND (country(sk) == country(sl) OR NOT country(sl)) 
                                                                AND (netWeight(sk) >= minNetWeight(sl) OR NOT minNetWeight(sl))
                                                                AND (netWeight(sk) <= maxNetWeight(sl) OR NOT maxNetWeight(sl))
                                                                AND (extraInMaterialized(sl, sk))) MATERIALIZED;

in (SkuSegmentLine sl, Sku sk)  = inD(sl, sk) AND extraInNotMaterialized(sl, sk) AND (((inPromotion(sl) = SegmentBool.yes AND prevPromotionPriceListDetailA(sk, stock(skuSegment(sl))))
                                                              OR (inPromotion(sl) = SegmentBool.no AND NOT prevPromotionPriceListDetailA(sk, stock(skuSegment(sl))))) OR NOT inPromotion(sl));
                                                                
line (SkuSegment s, Sku sk) = (GROUP LAST SkuSegmentLine sl ORDER sl IF skuSegment(sl) = s AND in(sl, sk));
in (SkuSegment s, Sku sk) = line(s, sk) AND NOT exclude(line(s, sk));

dialogTreeItemGroups (SkuSegmentLine sl)  {   
    DIALOG dialogTreeItemGroups OBJECTS tg = skuGroup(sl) CHANGE;
}

FORM skuSegments 'Сегментирование по товарам'
    OBJECTS s = SkuSegment
    PROPERTIES(s) READONLY number, name
    PROPERTIES (s) NEWSESSION NEW, EDIT, DELETE
    LIST SkuSegment OBJECT s
;

FORM skuSegment 'Сегментирование по товарам'
    OBJECTS s = SkuSegment PANEL
    PROPERTIES(s) number, name, nameStock
    
    OBJECTS sl = SkuSegmentLine
    PROPERTIES(sl) exclude, idBarcode, nameItem, idItemGroup ON CHANGE dialogTreeItemGroups(sl), 
                   nameItemGroup ON CHANGE dialogTreeItemGroups(sl), shortNameUom, nameBrand, nameCountry, captionInPromotion, 
                   minNetWeight, maxNetWeight
    PROPERTIES(sl) NEW, DELETE
    FILTERS skuSegment(sl) = s
    EDIT SkuSegment OBJECT s
    
    OBJECTS ss 'Товары' = Sku
    PROPERTIES(ss) READONLY name, idBarcode, idSkuGroup, nameSkuGroup, shortNameUOM, nameBrand, nameCountry, netWeight
    FILTERS in(s, ss)
;

DESIGN skuSegment {
    OBJECTS {
        NEW headContainev {
            type = CONTAINERV;
            fill = 1;
            NEW nameContainer{
                type = CONTAINERH;
                MOVE BOX(s);
            }
            NEW tabContainer {
                fill = 1;
                type = TABBED ;                
                MOVE BOX (sl) ;
                MOVE BOX (ss);
            }                   
        }        
    }    
}

NAVIGATOR {
    skuNavigator {
        NEW Stock.skuSegments
        ;
    }
}
MODULE PriceListStore;

REQUIRE PriceListCalc, Store;

// ------------------------- Виды цен по умолчанию для формата -------------- //

retailPriceListType 'Розничная цена (управленческая)' = DATA PriceListType(StoreType);
nameRetailPriceListType 'Розничная цена (управленческая)' (StoreType t) = name(retailPriceListType(t)); 

dataRetailPriceListType 'Розничная цена (управленческая)' = DATA PriceListType(Store);
nameDataRetailPriceListType 'Розничная цена (управленческая)' (Store s) = name(dataRetailPriceListType(s));

retailPriceListType (Store s) = OVERRIDE dataRetailPriceListType(s), retailPriceListType(storeType(s)) MATERIALIZED;
nameRetailPriceListType 'Розничная цена (управленческая)' (Store s) = name(retailPriceListType(s));
retailPriceListType (DepartmentStore d) = retailPriceListType(store(d)) MATERIALIZED;

retailPriceA 'Розничная цена (управленческая)' (Sku s, DepartmentStore d, DATETIME t) =  
    priceA[LedgerPriceListType,Sku,Stock,DATETIME](ledgerPriceListType(retailPriceListType(d)), s, d, t);
retailPriceB 'Розничная цена (управленческая)' (Sku s, DepartmentStore d, DATETIME t) =
    priceB(retailPriceListType(d), s, d, t);

retailPriceA 'Розничная цена (управленческая)' (Sku s, DepartmentStore d) = retailPriceA(s, d, currentDateTime());
retailPriceB 'Розничная цена (управленческая)' (Sku s, DepartmentStore d) = retailPriceB(s, d, currentDateTime());

retailCalcPriceListType 'Розничная цена (расчетная)' = DATA CalcPriceListType(StoreType);
nameRetailCalcPriceListType 'Розничная цена (расчетная)' (StoreType t) = name(retailCalcPriceListType(t)); 

dataRetailCalcPriceListType 'Розничная цена (расчетная)' = DATA CalcPriceListType(Store);
nameDataRetailCalcPriceListType 'Розничная цена (расчетная)' (Store s) = name(dataRetailCalcPriceListType(s));

retailCalcPriceListType (Store s) = OVERRIDE dataRetailCalcPriceListType(s), retailCalcPriceListType(storeType(s)) MATERIALIZED;
nameRetailCalcPriceListType 'Розничная цена (расчетная)' (Store s) = name(retailCalcPriceListType(s));
retailCalcPriceListType (DepartmentStore d) = retailCalcPriceListType(store(d)) MATERIALIZED;

markup 'Надбавка, % (расчетная)' (Sku s, DepartmentStore st) =
    markup(retailCalcPriceListType(st), s);

markupPriceA 'Розничная цена (расчетная)' (Sku s, DepartmentStore d, DATETIME t) =  
    priceA(retailCalcPriceListType(d), s, d, t);

markupPriceA 'Розничная цена (расчетная) ' (BasePriceListType pt, Sku s, DepartmentStore d, DATETIME t) =
    priceA(pt, retailCalcPriceListType(d), s, d, t);

EXTEND FORM chainStores
    PROPERTIES(s) nameRetailPriceListType, nameRetailCalcPriceListType AFTER name(s) 
;

EXTEND FORM store
    PROPERTIES(s) nameRetailPriceListType, nameRetailCalcPriceListType
;

DESIGN store {
    headerPriceParams {
        MOVE PROPERTY(nameRetailPriceListType(s));
        MOVE PROPERTY(nameRetailCalcPriceListType(s));
    }
}
EXTEND FORM stores
    PROPERTIES(s) READONLYIF isReadonly() nameRetailPriceListType, nameRetailCalcPriceListType
;
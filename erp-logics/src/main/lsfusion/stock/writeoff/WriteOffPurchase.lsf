MODULE WriteOffPurchase;

REQUIRE WriteOff, PurchaseShipment, WriteOffShipment;

PRIORITY Purchase; 

NAMESPACE WriteOff;

//---------------------------------- Норма отходов ------------------------------------//
CLASS WriteOffRate 'Норма отходов';
TABLE writeOffRate(WriteOffRate);

@defineExternalizable(writeOffRate, STRING[100]);

name 'Наименование' = DATA ISTRING[100](WriteOffRate);

percent 'Норма,%' = DATA NUMERIC[10,3] (WriteOffRate);
country = DATA Country (WriteOffRate);
nameCountry 'Страна' (WriteOffRate writeOffRate) = name(country(writeOffRate)) IN base;

writeOffRateName = GROUP NAGGR WriteOffRate w WHERE w IS WriteOffRate BY name(w), percent(w);

FORM writeOffRate 'Норма отходов'
    OBJECTS r=WriteOffRate PANEL
    PROPERTIES(r) name, percent, nameCountry
    EDIT WriteOffRate OBJECT r
;

writeOffRate 'Норма отходов' = ABSTRACT WriteOffRate (Country, Sku);
nameWriteOffRate 'Норма отходов' = name(writeOffRate(Country country, Sku sku));
percentWriteOffRate 'Норма отходов,%' = percent(writeOffRate(Country country, Sku sku));

FORM writeOffRateDialog 'Нормы отходов'
    OBJECTS r=WriteOffRate
    PROPERTIES(r) READONLY name, percent, nameCountry
    PROPERTIES(r) NEWEDIT, EDIT, DELETE GRID
    LIST WriteOffRate OBJECT r
;
NAVIGATOR {
    writeOffNavigator  {
        NEW writeOffRateDialog;
    }
}

@defineShipmentWriteOffOperation(Purchase);

@defineShipmentWriteOff (customer, ledgerBatch, '(закупка)',);

// ------------------------------------------------ Стандартные значения --------------------------------------- //

loadDefaultWriteOffRate 'Добавить нормы отходов'(BPSTRING[3] idCountry, ISTRING[100] string, NUMERIC[10,3] numeric)  { 
    NEW g = WriteOffRate {
        name(g) <- string;
        percent(g) <-  numeric;
        country(g) <- country(idCountry);
    }
};

loadDefaultWriteOffRates 'Загрузить стандартные нормы отходов'  ABSTRACT LIST ( ) IN loadDefault;

@implementLoadDefaultData(loadDefaultWriteOffRates);

shipment 'Накладная' = ABSTRACT Shipment (WriteOff);
shipment 'Накладная' = DATA Shipment (UserWriteOff);
shipment(UserWriteOff i) += shipment(i);

descriptionShipment 'Накладная' (WriteOff c) = description(shipment(c)) IN documentPrm;

shipmentDetail = DATA ShipmentDetail (UserWriteOffDetail);
shipmentDetail(UserWriteOffDetail writeOffDetail) += shipmentDetail(writeOffDetail);



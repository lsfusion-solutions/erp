MODULE OrderLedgerArticle;

REQUIRE StockArticleDocument, OrderLedger;

NAMESPACE OrderLedger;

FORM orderArticleLedgerNotDate 'Резерв по артикулам'

    OBJECTS st = Stock PANEL
    PROPERTIES  SELECTOR name(st)

    OBJECTS ar = Article PANEL
    PROPERTIES SELECTOR id(ar)

    OBJECTS s = OrderLedger
    PROPERTIES(s) READONLY BACKGROUND backgroundReserve(s) statusReserve     
    PROPERTIES(s) READONLY  date, 
                  dateTime, nameContact, nameContactStock, description,
                  toShipQuantity, quantity
    ORDERS statusReserve(s), dateTime(s)
                  
    FILTERS stock(s)==st,
            article(sku(s))==ar

    FILTERS active(s)
    FILTERGROUP toShipQuantityFilters
        FILTER 'Не выполнено' toShipQuantity(s) > 0 'F7' DEFAULT   

    FILTERGROUP stockTimesFilters
        FILTER 'Резерв (закупки) всего' isPurchase(s) 'F11'
        FILTER 'Резерв (продажи) всего' TRUE AND NOT isPurchase(s) 'F10'
;

DESIGN orderArticleLedgerNotDate {
    BOX {
        type = CONTAINERV;
        NEW row{
            type = CONTAINERH;
            MOVE BOX(st);
            MOVE BOX(ar);
        }
        MOVE BOX(s);
    }
    MOVE TOOLBARBOX;
}

FORM orderArticleLedger 'Резерв по артикулам'

    OBJECTS t = DATETIME PANEL
    PROPERTIES  dVal=VALUE(t)

    OBJECTS st = Stock PANEL
    PROPERTIES  SELECTOR name(st)

    OBJECTS ar = Article PANEL
    PROPERTIES SELECTOR id(ar)

    OBJECTS s = OrderLedger
    PROPERTIES(s) READONLY BACKGROUND backgroundReserve(s) statusReserve     
    PROPERTIES(s) READONLY BACKGROUND background(s,t)  date, 
                  dateTime, nameContact, nameContactStock, description,
                  toShipQuantity, quantity
    ORDERS statusReserve(s)
                  
    FILTERS stock(s)==st,
            article(sku(s))==ar

    FILTERS active(s)
    FILTERGROUP toShipQuantityFilters
        FILTER 'Не выполнено' toShipQuantity(s) > 0 'F7' DEFAULT   

    FILTERGROUP stockTimesFilters
        FILTER 'Резерв (закупки) всего' isPurchase(s) 'F11'
        FILTER 'Резерв (продажи) всего' TRUE AND NOT isPurchase(s) 'F10'
        FILTER 'Резерв (закупки) на дату' dateTime(s) < t AND isPurchase(s) 'F9'
        FILTER 'Резерв (продажи) на дату' dateTime(s)< t AND NOT isPurchase(s) 'F8'
        FILTER 'На дату' dateTime(s) < t 'F7'


;

DESIGN orderArticleLedger {
    BOX {
        type = CONTAINERV;
        NEW row{
            type = CONTAINERH;
            MOVE BOX(t);
            MOVE BOX(st);
            MOVE BOX(ar);
        }
        MOVE BOX(s);
    }
    MOVE TOOLBARBOX;
}
//-- Для просмотра резерва несколько складов
META extendFormDocumentArticleStockOrderLedger(object)
    reviewReserve 'Резерв' (Article article, Stock stock, ###object object)  { 
        IF shipmentDateTime(object) THEN {
            SHOW orderArticleLedger OBJECTS t=shipmentDateTime(object), st=stock, ar = article ;
        } ELSE {
            SHOW orderArticleLedgerNotDate OBJECTS st=stock, ar = article ;    
        }
    }
    
END
//-- Для просмотра резерва один склад
META extendFormDocumentArticleOrderLedger(object, stockProp)
    reviewReserve 'Резерв' (Article article, ###object object)  { 
        IF dateTime(object) THEN {
            SHOW orderArticleLedger OBJECTS t=dateTime(object), st=stockProp(object), ar = article ;
        } ELSE {
            SHOW orderArticleLedgerNotDate OBJECTS st=stockProp(object), ar = article ;
        }
    }
    
END
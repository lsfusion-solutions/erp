MODULE DisparityLot;

REQUIRE Disparity, DisparityOperation, Lot;

NAMESPACE Disparity;

@defineOperationProperty(useLot, 'Марки', showContainer);

useLot (Disparity i) = useLot(operation(i));

@defineDocumentLotOut(userDisparity, disparity, stock, quantity, dis, isPosted);

quantity = ABSTRACT NUMERIC[16,5] (DisparityDetail, Lot);
quantity(UserDisparityDetail d, Lot l) += quantity(d, l);

returnConditionChangeSkuLot = ABSTRACT BOOLEAN (UserDisparity);
returnMessageChangeSkuLot = ABSTRACT STRING ();

changeSkuLot 'Перенести марки' (UserDisparity d) {
    IF returnConditionChangeSkuLot(d) THEN {
        MESSAGE returnMessageChangeSkuLot();
        RETURN;
    }

    APPLY;
    IF canceled() THEN RETURN;
    
    ASK 'Вы уверены, что хотите перенести марки на новый товар ? Отмена операции будет невозможна.' DO {
        NEWSESSION {
            FOR disparity(UserDisparityDetail dd) = d AND inputSku(dd) AND quantity(dd, Lot l) AND sku(dd) = sku(l) DO {
                note(l) <- 'Изменение сортности ' + number(dd) + '. Замена товара с ' + idSku(l) + ' на ' + id(inputSku(dd));
                sku(l) <- inputSku(dd);
            }
            setNoCancelInTransaction();
            APPLY;
        }
    }
}

countLot (UserDisparity d) = GROUP SUM 1 IF quantity(UserDisparityDetail dd, Lot l) AND sku(dd) = sku(l) AND disparity(dd) = d;
countInputLot (UserDisparity d) = GROUP SUM 1 IF quantity(UserDisparityDetail dd, Lot l) AND inputSku(dd) = sku(l) AND disparity(dd) = d;

statusLot 'Статус марок' (UserDisparity d) = 
                                       CASE WHEN countInputLot(d) AND NOT countLot(d) THEN 'Перенесены'  
                                             WHEN countLot(d) AND NOT countInputLot(d) THEN 'Не перенесены'
                                             WHEN countLot(d) AND countInputLot(d) THEN 'Частично перенесены'
                                           ELSE 'Не заданы';

EXTEND FORM disparity
    PROPERTIES(dis) DRAW d TOOLBAR changeSkuLot, statusLot READONLY 
;
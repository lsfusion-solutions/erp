MODULE Disparity;

REQUIRE System,
        Stock,
        StockSkuDocument,
        Currency,
        Numerator,
        Barcode,
        Document,
        Employee,
        StockDocumentSkuLedger,
        Utils,
        Item,
        OrderLedger;

NAMESPACE Disparity;

// ---- Добавляем отдельную вкладку на форму Настройки
DESIGN options {
    pane {
        NEW disparity {
            caption = 'Изменение сортности';
        }
    }
}

//----------------------------------- Пересорт товара -------------------------------------------------------

CLASS Disparity 'Изменение сортности';
CLASS DisparityDetail 'Строка изменения сортности';

@defineDocument(disparity);
@deriveDocumentHeaderTimePrefix(Disparity, );

@defineDocumentHeaderNumber(Disparity, BPSTRING[2]);
@defineNumeratedDefault(Disparity, 'Изменения сортности', 'ИС');

@defineDocumentDataStock (disparity, stock, 'Склад', );

@defineExternalizable(disparity, STRING[100]);

id = DATA STRING[100] (DisparityDetail);
disparityDetail (id, disparity) = GROUP AGGR DisparityDetail disparityDetail WHERE disparityDetail IS DisparityDetail BY id(disparityDetail), disparity(disparityDetail);

@defineDocumentDialogStocks(disparity, stock, company, , );
@defineDocumentStockAccess(Disparity, stock, company);

@defineDocumentPosted(disparity);
@defineDocumentClosed(disparity);
@defineDocumentClosedConstraint(Disparity);
@defineDocumentDescription (disparity, 'Изменение сортности');
@defineDocumentCurrency (disparity);
@deriveDocumentCurrency(disparity, stock);

@defineDocumentDetailSkuPrefix (disparity, sku, , ' (расход)');
@defineDocumentDetailSkuPrefix (disparity, sku, input, ' (приход)');

@showDocumentDetailBatch(disparity);
skipFillBatch = ABSTRACT CASE BOOLEAN (DisparityDetail);
@defineDocumentDetailBatchCustomPrefix(disparityDetail, batch, );
changeBatch(DisparityDetail d)  { 
    DIALOG dialogBatchStockOut OBJECTS st = stock(d),
                             t = dateTime(d),
                             sk = sku(d),
                             bt = batch(d) INPUT NULL NEWSESSION DO
        batch(d) <- bt;
}

idBatch 'Код партии' (DisparityDetail detail) = id(batch(detail));

costBatch 'Себестоимость партии' = DATA NUMERIC[16,4] (DisparityDetail);

WHEN LOCAL (CHANGED(batch(DisparityDetail detail)) OR CHANGED(sku(detail)) OR CHANGED(stock(detail))) DO
    costBatch(detail) <- prevCost(OVERRIDE batch(detail), prevLastOrderBatch(sku(detail), stock(detail)));

expiryDateBatch 'Годен до' = DATA DATE (DisparityDetail);
expiryDateBatch(DisparityDetail detail) <- prevExpiryDate(batch(detail)) WHEN CHANGED(batch(detail));

disallowLineDisparities 'Запретить строки без партий в изменениях сортности' = DATA BOOLEAN ();

EXTEND FORM options
    PROPERTIES() disallowLineDisparities
;

DESIGN options {
    pane {        
        disparity {
            MOVE PROPERTY(disallowLineDisparities());   
        }
    }
}

CONSTRAINT isPosted(DisparityDetail d) AND disallowLineDisparities() AND NOT batch(d) 
    MESSAGE 'Для строки изменения сортности должна быть задана партия';  

@defineDocumentDetailQuantityPrefix (disparity, , ' (расход)');
@defineDocumentDetailQuantityPrefix (disparity, input, ' (приход)');
inputQuantity(DisparityDetail detail) <- quantity(detail) WHEN CHANGED(quantity(detail));

@defineDocumentDetailPricePrefix (disparity, , ' учетная (расход)');


@deriveDocumentDetailPriceAccountBatch (disparity, sku, stock);
@defineDocumentDetailPricePrefix (disparity, input, ' учетная (приход)');
inputPrice(DisparityDetail detail) <- price(detail) WHEN CHANGED(price(detail));

@defineDocumentDetailDataSumCustomPrefix (disparityDetail, , ' учетная (расход)');
@deriveDocumentDetailSumPrefix(disparity, , currency, quantity);
@defineDocumentDetailDataSumCustomPrefix (disparityDetail, input, ' учетная (приход)');
@deriveDocumentDetailSumPrefix(disparity, input, currency, inputQuantity);

@defineDocumentHeaderSumPrefix (disparity, , ' учетная (расход)');
@defineDocumentHeaderSumPrefix (disparity, input, ' учетная (приход)');

@defineDocumentHeaderQuantityPrefix (disparity, , ' (расход)');
@defineDocumentHeaderQuantityPrefix (disparity, input, ' (приход)');

@defineAddDetailDialogSkuStock(disparity, sku, stock, dialogSku);
@defineAddDetailDialogBarcode(disparity, sku);

@defineDocumentDetailDiffSumCustomPrefix(disparityDetail, sum, , input, ' изменения учетной');
diffSumDisparityDetail 'Сумма' (disparity) =
    GROUP SUM diffSum(DisparityDetail idetail) 
        BY disparity(idetail) IN documentSum;
            
@defineDocumentHeaderItemSum (disparity, diff, diff);
// ------------ Проведение по товарному отчету ----------------- //
@implementStockDocumentLedgerOut(Disparity, stock);
type(Disparity l) += STRING[50]('Сортность') IF l IS Disparity;
sumItem (Disparity ledger) += diffSumItemDisparityDetail(ledger);
sumContainer (Disparity ledger) += diffSumContainerDisparityDetail(ledger);

// ------------ Проведение по регистру ----------------- //
@implementSkuLedgerOutFIFO(DisparityDetail, sku, stock);
quantity (DisparityDetail ledger) += quantity(ledger);
batch(DisparityDetail ledger) += batch(ledger);
sum (DisparityDetail ledger) += sum(ledger);

stockDocumentLedger(DisparityDetail ledger) += disparity(ledger); 

//----------------------------  Изменение остатка пересорт --------------------------------------------//

CLASS InputDisparityDetail 'Приход пересортицы';
TABLE inputDisparityDetail (InputDisparityDetail);

needToInput (DisparityDetail disparityDetail) = inputQuantity(disparityDetail) > 0
    AND isPosted(disparityDetail);

inputDisparityDetail = AGGR InputDisparityDetail WHERE needToInput(DisparityDetail disparityDetail) MATERIALIZED INDEXED;

@defineDocumentAggregationDetailTime(disparity, inputDisparity);
@defineDocumentAggregationDetailPosted(disparity, inputDisparity);
@defineDocumentAggregationDetailClosed(disparity, inputDisparity);
@defineDocumentAggregationDetailStock(disparity, inputDisparity, stock, 'Склад');
@defineDocumentAggregationDetailSkuPrefix(disparity, inputDisparity, sku, input);
quantity(InputDisparityDetail inputDisparityDetail) = inputQuantity(disparityDetail(inputDisparityDetail));
price(InputDisparityDetail inputDisparityDetail) = inputPrice(disparityDetail(inputDisparityDetail));
sum(InputDisparityDetail inputDisparityDetail) = inputSum(disparityDetail(inputDisparityDetail));
description(InputDisparityDetail inputDisparityDetail) = description(disparityDetail(inputDisparityDetail));
costBatch(InputDisparityDetail inputDisparityDetail) = costBatch(disparityDetail(inputDisparityDetail));
expiryDateBatch (InputDisparityDetail inputDisparityDetail) = expiryDateBatch(disparityDetail(inputDisparityDetail));


@implementBatch(InputDisparityDetail, sku, stock, costBatch);
quantity (InputDisparityDetail ledger) += quantity(ledger);
expiryDate (InputDisparityDetail ledger) += expiryDateBatch(ledger);
sum (InputDisparityDetail ledger) += sum(ledger);
series (InputDisparityDetail ledger) += series(disparity(disparityDetail(ledger)));
number (InputDisparityDetail ledger) += number(disparity(disparityDetail(ledger)));

supplier = DATA LegalEntity (DisparityDetail);
supplier(DisparityDetail detail) <- prevSupplier(batch(detail)) WHEN CHANGED(batch(detail));
nameSupplier = name(supplier(DisparityDetail detail) );

stockDocumentLedger(InputDisparityDetail ledger) += disparity(disparityDetail(ledger));

supplier = OVERRIDE supplier(disparityDetail(InputDisparityDetail detail)), legalEntityStock(disparityDetail(detail));
supplier(InputDisparityDetail batch) += supplier(batch);

supplierStock = DATA Stock (DisparityDetail);
supplierStock(DisparityDetail detail) <- prevSupplierStock(batch(detail)) WHEN CHANGED(batch(detail));
supplierStock = OVERRIDE supplierStock(disparityDetail(InputDisparityDetail detail)), stock(disparityDetail(detail));
supplierStock(InputDisparityDetail batch) += supplierStock(batch);

//CONSTRAINT supplier(DisparityDetail d) AND supplierStock(d) AND NOT in(supplier(d), supplierStock(d))
//    MESSAGE 'Поставщик и склад поставщика в строке изменения сортности не имеют связи';
@defineBarcodeSku(disparityDetail); //-- показываем по нажатию правой клавиши все штрихкоды для товара

//------------------------------------------- Пересорт товара----------------------------------------------------//

//quantityDisparityDetail 'Кол-во для закачки в оборудование (приход)' (sku, disparity) = GROUP SUM inputQuantity(DisparityDetail disparityDetail) BY inputSku(disparityDetail), disparity(disparityDetail);

FORM disparity 'Изменение сортности'

    OBJECTS dis=Disparity PANEL

    PROPERTIES(dis) isPosted, nameNumerator, number, series, nameStock ON CHANGE changeStockCompany(dis), 
                    date, time, quantityDisparityDetail, sumDisparityDetail,
                    inputQuantityDisparityDetail, inputSumDisparityDetail, note

    OBJECTS d = DisparityDetail
    PROPERTIES(d)   index,
                    idBarcodeSku, idSku SHOWIF showIDs(), nameSku, shortNameUOMSku, idBatch, nameBatch ON CHANGE changeBatch(d),
                    expiryDateBatch,
                    quantity, price, sum,
                    inputIdBarcodeSku, inputIdSku SHOWIF showIDs(), inputNameSku, inputShortNameUOMSku, costBatch,
                    inputQuantity, inputPrice, inputSum
    PROPERTIES(d)   NEW, deletedd=DELETE GRID

    PROPERTIES(dis) DRAW d addDetailDialogSkuStockDisparityDetail,
                             addDetailInputBarcodeDisparityDetail, deleteDisparityDetail

    FILTERS         disparity(d) == dis

    EVENTS
        ON OK prePost(dis)

    EDIT Disparity OBJECT dis
;

DESIGN disparity {
    BOX {
        size = (1024, 768);
        NEW mainTabbed {
            type = TABBED;
            fill = 1;
            NEW documentBox {
                fill = 1;
                type = CONTAINERV;
                caption = 'Изменение сортности';
                NEW headerBox {
                    type = CONTAINERH;
        
                    NEW headerRow1 {
                        fill = 1;
                        MOVE GROUP(documentHeader,dis) {
                            type = CONTAINERH;
                            MOVE PROPERTY(isPosted(dis)) { charWidth = 10; }
                            MOVE PROPERTY(nameStock(dis));
                            MOVE PROPERTY(nameNumerator(dis));
                            MOVE PROPERTY(number(dis));
                            MOVE PROPERTY(series(dis));
                            MOVE PROPERTY(date(dis));
                            MOVE PROPERTY(time(dis));
                        }
        
                        NEW headerTabbedPane {
                            alignment = STRETCH;
                            type = TABBED;
                            NEW headerMainParams {
                                caption = 'Основные параметры';
                                MOVE GROUP(documentPrm,dis) {
                                    type = COLUMNS;
                                    columns = 2;
                                }
                            }
                            NEW headerExtraParams {
                                caption = 'Дополнительные параметры';
                            }
                            NEW headerCreateDetail {
                                caption = 'Основание';
                            }
                            NEW headerCreateDocuments {
                                caption = 'Производные документы';
                            }
                        }
                    }
        
                    MOVE GROUP(documentSum,dis) {
                        columns = 1;
                    }
                }
                NEW specificationBox {
                    fill = 1;
                    type = TABBED;
                    MOVE BOX(d) {
                        caption = 'Спецификация';
                    }
                }
            }
        }
        MOVE TOOLBARBOX;
        PROPERTY(formOk()) {
            caption = 'Провести';
        }
        PROPERTY(sumDisparityDetail(dis)) { charWidth = 13;}
        PROPERTY(inputSumDisparityDetail(dis)) { charWidth = 13;}
    }
}

edit (DisparityDetail d) + {  DIALOG disparity OBJECTS dis = disparity(d), d = d DOCKED; }

show 'Просмотреть' (DisparityDetail d)  { 
	NEWSESSION {
	    SHOW disparity OBJECTS dis = disparity(d), d = d DOCKED READONLY;
	}
}
show[SkuLedger](InputDisparityDetail batch) + {  show(disparityDetail(batch)); } 
show(DisparityDetail d) + {  show(d); } 

@addEventGenerateNumberOnForm(disparity, dis, disparity);

//-- SKU
@defineDocumentSkuStockPriceListTypeSystemLedger(disparity, );
@extendFormDocumentSkuStock(disparity, disparity, dis, legalEntityStock, );

@defineDocumentBatchStockPriceListTypeSystemLedger(disparity, );
  
@defineDocumentHeaderSkuQuantity(disparity, inputSku);  

@extendFormDocumentBatchStock(disparity, disparity, dis, legalEntityStock, );

shipmentDateTime(Disparity disparity) = dateTime(disparity);

@extendFormDocumentSkuStockOrderLedgerOver(disparity, disparity, dis, stock);

@extendFormDocumentSkuStockOrderLedgerAll(disparity, disparity, dis);

@extendFormDocumentBatchStockOrderLedger(disparity, disparity, dis);

close(Disparity l) + {  isClosed(l) <- TRUE; }

EXTEND FORM disparity

    FILTERGROUP filter
        FILTER 'С остатком ' prevCurrentBalance(ks, st) 'F10' DEFAULT
        FILTER 'В документе ' quantity(ks, dis, st) 'F9'
;

EXTEND FORM disparity

    FILTERGROUP filter3
        FILTER 'С остатком ' prevCurrentBalance(b, sto) 'F10' DEFAULT
        FILTER 'В документе ' quantity(b, dis, sto) 'F9'
;

skipShowEdit = ABSTRACT BOOLEAN (Disparity);
skipShowDelete = ABSTRACT BOOLEAN (Disparity);
overShowEditDelete = ABSTRACT BOOLEAN (Disparity);

showEdit (Disparity d) = isOpened(d) AND overShowEditDelete(d) AND NOT skipShowEdit(d); 
showDelete(Disparity d) = isOpened(d) AND overShowEditDelete(d) AND NOT skipShowDelete(d);

FORM disparities 'Изменения сортности'

    OBJECTS dis=Disparity
    PROPERTIES (dis) READONLYIF isReadonly() isClosed, isPosted, number, series, date, time, nameStock
    PROPERTIES(dis)  READONLY countDisparityDetail, quantityDisparityDetail, sumDisparityDetail,
                              inputQuantityDisparityDetail, inputSumDisparityDetail
    PROPERTIES (dis) READONLYIF isReadonly() note

    PROPERTIES(dis)  READONLY PANEL createdNameUser, createdTime, createdHostnameComputer, 
                                          postedNameUser, postedTime, postedHostnameComputer

    PROPERTIES (dis) NEWSESSION NEW, EDIT SHOWIF showEdit(dis)
    PROPERTIES(dis)  close SHOWIF isOpened(dis), open SHOWIF isClosed(dis)     
    
    PROPERTIES (dis) NEWSESSION deleted=DELETE SHOWIF showDelete(dis)

    OBJECTS d=DisparityDetail
    PROPERTIES(d)   READONLY index,
                    idBarcodeSku, idSku SHOWIF showIDs(), nameSku, shortNameUOMSku, nameBatch,
                    expiryDateBatch, quantity, price, sum,
                    inputIdBarcodeSku, inputIdSku SHOWIF showIDs(), inputNameSku, inputShortNameUOMSku, costBatch,
                    inputQuantity, inputPrice, inputSum

    FILTERS         disparity(d) == dis
;
@extendFormFilterAccessStock(Disparity, dis, disparities, stock, company);

DESIGN disparities {
    BOX {
        size = (1024, 768);
        NEW documentContainer  {
            fill = 1;
            type = SPLITV;

            MOVE BOX(dis) {
                fill = 2;
            }
            NEW documentDetail {
                fill = 1;
                type = TABBED;

                MOVE BOX(d) {
                    caption = 'Спецификация';
                }
                NEW documentHistory {
                    caption = 'История';

                    MOVE GROUP(created,dis);
                    MOVE GROUP(posted,dis);
                }
                NEW printTab {
                    caption = 'Печатные формы';
                    NEW printContainer {
                        caption = 'Печать';
                    }
                }
                NEW actionContainer {
                    caption = 'Действия';
                    type = CONTAINERH;
                }
            }
        }
        MOVE TOOLBARBOX;
    }
}
@extendFormEditable(disparities);
@defineFilterIsOpened (disparity, disparities, dis);
NAVIGATOR {
    stockNavigator {
        NEW FOLDER disparityNavigator 'Изменение сортности' BEFORE balanceSku {
            NEW disparities;
        }
    }
}

//Родительский товар

disparityItem 'Уцененный товар' = DATA Item (Item) INDEXED;
nameSkuDisparityItem 'Уцененный товар' = name(disparityItem(Item item));

countItemDisparity (Item disparityItem) = GROUP SUM 1 IF disparityItem(Item item) == disparityItem; 
countDisparityItem (Item item) = GROUP SUM 1 IF disparityItem(item) == Item disparityItem; 

skipGenerateBarcode = DATA LOCAL BOOLEAN () ;

overCopyDisparityData  ABSTRACT LIST (Item, Item);

copyDisparityData (Item item, Item i)  { 
    disparityItem(i) <- item; 
    itemGroup(i) <- itemGroup(item);
    caption(i) <- caption(item) + ' (УЦЕН.)';
    originalCaption(i) <- originalCaption(item);
    UOM(i) <- UOM(item);
    brand(i) <- brand(item);
    manufacturer(i) <- manufacturer(item);
    composition(i) <- composition(item);
    originalComposition(i) <- originalComposition(item);
    netWeight(i) <- netWeight(item);
    grossWeight(i) <- grossWeight(item);
    country(i) <- country(item);
    dataImage(i) <- dataImage(item);
    skuType(i) <- skuType(item);
    split(i) <- split(item);
    passScales(i) <- passScales(item);
    inactive(i) <- inactive(item);
    
    IF NOT skipGenerateBarcode() THEN
        generateBarcode(i);       
    
    customGroup[CustomGroupType,Item](CustomGroupType type, i) <- customGroup(type, item);
    
    EXEC overCopy(item, i);
    overCopyDisparityData(item, i);    
}

copyDisparity 'Создать уцененный товар'(Item item)  { 
    IF (item IS Item) THEN {
        NEWSESSION NESTED (addedSku[]) {
            NEW i = Item {
                copyDisparityData(item, i);
                DIALOG item OBJECTS i = i DOCKED DO {
                    addedSku() <- i;
                }
            }
        }
    }
} CHANGEKEY 'F6' TOOLBAR;

EXTEND FORM item
    PROPERTIES(i) nameSkuDisparityItem 
    OBJECTS di = Item
    PROPERTIES(di) READONLY idBarcode, name
    FILTERS disparityItem(di)==i 
;

DESIGN item {
    itemDetail {
        NEW disparityItem AFTER calcPriceListTypePrm {
            type = CONTAINERV;
            caption = 'Уценки';
            MOVE PROPERTY(nameSkuDisparityItem(i));
            MOVE BOX(di);
        }
    }    
}

isNotDisparity = Item item IS Item AND NOT disparityItem(item);
hideCopyDisparity = ABSTRACT BOOLEAN (Item);

EXTEND FORM items
    PROPERTIES (i) copyDisparity SHOWIF NOT hideCopyDisparity(i)
    FILTERGROUP disparityFilter
        FILTER 'Без уцен.' isNotDisparity(i) 'F5' DEFAULT
        FILTER 'С уценкой' disparityItem(i) 'ctrl F5' 
;

DESIGN items {
    actionsRow2 {
        NEW disparity {
            caption = 'Уценка';
            type = CONTAINERH;
            MOVE FILTERGROUP(disparityFilter);
            MOVE PROPERTY(copyDisparity(i));
        }
    }
}

EXTEND FORM disparity
    PROPERTIES  copyDisparity(ks)
;

MODULE DisassemblyCost;

REQUIRE Disassembly, SkuLedgerCost;

NAMESPACE Disassembly;

fixedCost(DisassemblyInSkuLedger l) += l IS DisassemblyInSkuLedger;
fixedCostPrice(DisassemblyInSkuLedger l) += price(l);

recalculateCostCalculated(DATE dFrom, DATE dTo) +{
    logToFile('cost', 'Начат пересчет цен в разборках');
    APPLY NESTED (calculatedCost[Sku, Stock]) {
        price(Disassembly d) <- calculatedCost(sku(d), stock(d))
            WHERE date(d) >= dFrom AND date(d) <= dTo AND NOT date(d) < documentsClosedDate(stock(d)) AND
            calculatedCost(sku(d), stock(d)) AND NOT calculatedCost(sku(d), stock(d)) = price(d);
    }
    logToFile('cost', 'Закончен пересчет цен в разборках');
}

recalculateCostAccountBeforeFirst(DATE dFrom, DATE dTo) +{
    logToFile('cost', 'Начат пересчет цен в разборках');
    APPLY NESTED LOCAL {
        price(Disassembly d) <- prevAdjustedAccountPriceB(sku(d), stock(d), dateTime(d))
            WHERE date(d) >= dFrom AND date(d) <= dTo AND calcRecalculateCost(sku(d)) AND NOT date(d) < documentsClosedDate(stock(d));
    }
    
    // в Assembly это делают события, а здесь есть только пересчёт
    APPLY NESTED LOCAL {
        FOR date(Disassembly d) >= dFrom AND date(d) <= dTo AND calcRecalculateCost(sku(d)) AND NOT date(d) < documentsClosedDate(stock(d)) DO {
            sum(DisassemblyDetail dd) <- (PARTITION UNGROUP sum
                PROPORTION STRICT ROUND(2) percent(dd)
                ORDER dd
                BY disassembly(dd)) WHERE disassembly(dd) = d;
            price(DisassemblyDetail dd) <- NUMERIC[16,4](sum(dd) / quantity(dd)) WHERE disassembly(dd) = d;
        }
    }
    logToFile('cost', 'Закончен пересчет цен в разборках');
}
MODULE SkuLedgerCostStockDocument;

REQUIRE SkuLedgerCost, StockDocument;

NAMESPACE Stock;


FORM recalulateParams 'Пересчёт производится по всем местам хранения. Укажите параметры'
    OBJECTS df = DATE PANEL
    PROPERTIES dateF 'Дата с' = VALUE(df)

    OBJECTS dt = DATE PANEL
    PROPERTIES dateT 'Дата по' = VALUE(dt)

    TREE groupTree g = SkuGroup PARENT parent(g)
    PROPERTIES READONLY order(g), id(g), name(g)
    ORDERS order(g), name(g)
    FILTERGROUP inactive1 FILTER 'Активные' active(g) 'F6' DEFAULT

    OBJECTS isp = Sku
    PROPERTIES(isp) READONLY idBarcode, id, name
    PROPERTIES recalculateCost(isp)
    ORDERS name(isp)
    FILTERS isParent(g, isp)
    FILTERGROUP activeItem1 FILTER 'Активный' active(isp) DEFAULT
;

DESIGN recalulateParams {
    width = 1200;
    NEW dates {
        horizontal = TRUE;
        caption = 'Период пересчёта';
        MOVE PROPERTY (dateF);
        MOVE PROPERTY (dateT);
    }
    NEW sku {
        caption = 'Выбор товаров для пересчёта (если ничего не выбрать галочкой Вкл., то будет по всем)';
        horizontal = TRUE;
        fill = 1;
        MOVE BOX(TREE groupTree) {fill = 1;}
        MOVE BOX(isp){fill = 3;}
    }
    MOVE TOOLBARBOX;
}

recalulateCostLedger 'Пересчитать с/с в открытом периоде'() {
    DIALOG recalulateParams OBJECTS df = subtract(currentDate(), 14) INPUT, dt = currentDate() INPUT DO {
        recalculateAllSkus() <- TRUE IF NOT (GROUP SUM 1 IF recalculateCost(Sku sku));
        recalulateCostLedgerSelectedSkus(df, dt);
    }
}

EXTEND FORM closingDocuments
    PROPERTIES () recalulateCostLedger DRAW s TOOLBAR
;
MODULE RepricingCommittee;

REQUIRE EmployeeStock;

numberDisposal '№ распоряжения на переоценку' = DATA STRING[30] (Stock);

//--------------------------------Комиссия для переоценки----------------------------------//
CLASS RepricingCommittee 'Комиссия для переоценки' : Committee;

name 'Наименование' = DATA ISTRING[150](RepricingCommittee);

name(RepricingCommittee committee) += name(committee) IF committee IS RepricingCommittee;

repricingCommittee 'Комиссия для переоценки' = DATA RepricingCommittee (Stock);
nameRepricingCommittee 'Комиссия для переоценки' (Stock stock) = name(repricingCommittee(stock)) IN committee;
is 'По умолчанию' (RepricingCommittee repricingCommittee, Stock stock) = repricingCommittee(stock) == repricingCommittee;
inCommittee (Stock stock, Employee employee) = in(repricingCommittee (stock), employee);
nameEmployeeCommittee 'Члены комиссии' (Stock stock) = namePositionEmployee(repricingCommittee (stock));
shortNameEmployeeCommittee 'Члены комиссии' (Stock stock) = shortNamePositionEmployee(repricingCommittee (stock));
nameChairman 'Председатель комиссии' (Stock stock) = nameChairman(repricingCommittee(stock));
shortNameChairman 'Председатель комиссии' (Stock stock) = shortNameChairman(repricingCommittee(stock));
namePositionChairman 'Должность' (Stock stock) = namePositionChairman(repricingCommittee(stock));
CONSTRAINT repricingCommittee(Stock stock) AND NOT in(repricingCommittee(stock), stock)
    CHECKED BY repricingCommittee[Stock] MESSAGE 'Для отдела выбрана комиссия, которая для него не действует';

FORM repricingCommittee 'Комиссия для переоценки'
    OBJECTS c=RepricingCommittee PANEL
    PROPERTIES(c)      name, nameChairman

    TREE stockTree sg = StockGroup PARENT parent(sg)
    PROPERTIES READONLY sgTreeName = name(sg)

    OBJECTS ts=Stock
    PROPERTIES    READONLY tsTreeName = name(ts)
    PROPERTIES(c, ts) in , is
    FILTERGROUP inactiveStock FILTER 'Активный' active(ts) 'ctrl F10' DEFAULT
    
    FILTERS isParent(sg, ts)
    ORDER tsTreeName


    OBJECTS e=Employee
    PROPERTIES(e)      READONLY name[Contact], firstName, lastName, namePosition
    PROPERTIES(e)      NEWEDIT, EDIT, DELETE GRID

    PROPERTIES(c, e)   in
    FILTERS            (countStock (e, c) AND NOT allowStockEmployee()) OR (e IS Employee AND allowStockEmployee())
    FILTERGROUP filters6
        FILTER 'Показывать только членов комиссии' in(c, e) 'F10'

    FILTERGROUP filters5
        FILTER 'Показывать отделы только для данной комиссии' in(c, ts) 'F9'
    FILTERGROUP active FILTER 'Активные' active(e) 'F5'  DEFAULT 
    EDIT RepricingCommittee OBJECT c
;
@extendFormFilterStockAccess(ts, repricingCommittee);
@extendFormFilterStockGroupAccess(sg, repricingCommittee);

DESIGN repricingCommittee {
    BOX {
        size = (1024, 768);
        OBJECTS {
            NEW managers BEFORE BOX(e) {
                caption = 'Состав комиссии';
                MOVE PROPERTY (nameChairman(c));
            }
    
            NEW stocks BEFORE managers {
                fill = 1;
                type = SPLITH;
    
                MOVE BOX(TREE stockTree);
                MOVE BOX(ts) {
                    fill = 3;
                }
            }
        }
    }
}

FORM repricingCommitteeDialog 'Комиссии для переоценки'
    OBJECTS w=RepricingCommittee
    PROPERTIES(w)      READONLY name, nameStocks, nameEmployee, nameChairman
    PROPERTIES(w)      NEWSESSION NEW, EDIT, DELETE GRID

    LIST RepricingCommittee OBJECT w
;
// ----------------------------------- Макрос для задания комиссии для документов ------------------------------------------ //

META defineDocumentHeaderRepricingCommittee(object, stockProp)
    dataNumberDisposal '№ распоряжения на переоценку' (object) = DATA STRING[30] (###object) IN documentPrm;

    repricingCommittee (object) = DATA RepricingCommittee(###object);
    nameRepricingCommittee 'Комиссия для переоценки' (###object object) = name(repricingCommittee(object)) IN documentPrm CHARWIDTH 20;
    repricingCommittee (###object object) <- repricingCommittee(stockProp(object))
        WHEN CHANGED(stockProp(object));

    CONSTRAINT repricingCommittee (###object object) AND NOT in(repricingCommittee (object), stockProp(object))
        CHECKED BY repricingCommittee[###object] MESSAGE 'Для документа выбрана комиссия, которая не действует для склада документа';

    inCommittee (###object object, Employee employee) = in(repricingCommittee (object), employee);
    nameEmployeeCommittee 'Члены комиссии' (###object object) = namePositionEmployee(repricingCommittee (object));
    nameChairman 'Председатель комиссии' (###object object) = nameChairman(repricingCommittee(object));
    namePositionChairman 'Должность' (###object object) = namePositionChairman(repricingCommittee(object));
END
META defineDocumentHeaderAbstractRepricingCommittee(object)
    numberDisposal '№ распоряжения на переоценку' (object) = ABSTRACT STRING[30] (###object) IN documentPrm;

    repricingCommittee (object) = ABSTRACT RepricingCommittee(###object);
    nameRepricingCommittee 'Комиссия для переоценки' (###object object) = name(repricingCommittee(object)) IN documentPrm CHARWIDTH 20;

    inCommittee (###object object, Employee employee) = in(repricingCommittee (object), employee);
    nameEmployeeCommittee 'Члены комиссии' (###object object) = namePositionEmployee(repricingCommittee (object));
    shortNameEmployeeCommittee 'Члены комиссии' (###object object) = shortNameEmployee(repricingCommittee (object));
    nameChairman 'Председатель комиссии' (###object object) = nameChairman(repricingCommittee(object));
    shortNameChairman 'Председатель комиссии' (###object object) = shortNameChairman(repricingCommittee(object));
    namePositionChairman 'Должность' (###object object) = namePositionChairman(repricingCommittee(object));
END

META defineDocumentInterfaceHeaderRepricingCommittee (object, stockProp)
    @defineDocumentHeaderAbstractRepricingCommittee (object);
    @defineDocumentHeaderRepricingCommittee (user###object, stockProp);

    numberDisposal '№ распоряжения на переоценку' (User###object object) = OVERRIDE dataNumberDisposal(object), numberDisposal(stockProp(object)) IN documentPrm;
    changeNumberDisposal(User###object object)  { 
        INPUT s = STRING[30] DO
            dataNumberDisposal(object) <- s;        
    }
    
    repricingCommittee (User###object object) += repricingCommittee(object);
    numberDisposal (User###object object) += numberDisposal(object);    
END

NAVIGATOR {
    stockNavigator {
        NEW FOLDER revaluationNavigator 'Переоценка' {
            NEW repricingCommitteeDialog;
        }
    }
}

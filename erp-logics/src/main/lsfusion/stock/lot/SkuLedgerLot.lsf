MODULE SkuLedgerLot;

REQUIRE LotLedger, SkuLedger, Lot, LotAggregate;

NAMESPACE Stock;

quantity = ABSTRACT NUMERIC[16,5] (InSkuLedger, Lot);
quantity = ABSTRACT NUMERIC[16,5] (OutFIFOSkuLedger, Lot);

signedQuantity 'Кол-во' (SkuLedger ledger, Lot lot) = 
    MULTI quantity[InSkuLedger, Lot](ledger, lot),
         -quantity[OutFIFOSkuLedger, Lot](ledger, lot) CHARWIDTH 7 MATERIALIZED;

batch (Lot l) = GROUP LAST batch(SkuLedger sl) ORDER DESC dateTime(sl), sl WHERE signedQuantity(sl, l) AND batch(sl) MATERIALIZED INDEXED;
nameBatch 'Партия' (Lot l) = name(batch(l));

skuBalance = GROUP SUM signedQuantity(SkuLedger ledger, Lot lot) IF active(ledger) BY lot, stock(ledger) CHARWIDTH 7;

currentBalance 'Текущий остаток' (Lot l, Stock st) =
    GROUP SUM (onHand(Lot parent, st) (+) skuBalance(parent, st)) IF level(l, parent) CHARWIDTH 7 MATERIALIZED;
prevCurrentBalance (Lot l, Stock st) = PREV(currentBalance(l, st));

skuCurrentBalance (Lot l, Stock st) = sku(l) IF currentBalance(l, st) MATERIALIZED;
INDEX skuCurrentBalance(Lot l, Stock st), st;

currentBalance 'Текущий остаток' (Lot l) = GROUP SUM currentBalance(l, Stock st) MATERIALIZED;

currentBalanceLot 'Остаток марок' (Sku sk, Stock st) =
    GROUP SUM currentBalance(Lot l, st) * count(l) IF skuCurrentBalance(l, st) = sk AND NOT aggregated(l) MATERIALIZED;
prevCurrentBalanceLot (Sku sk, Stock st) = PREV(currentBalanceLot(sk, st));

// constraint
@defineOption(checkCurrentBalanceLot, 'Проверять на превышение остатка по маркам', stock);

skipCheckCurrentBalanceLot 'Не проверять на превышение остатка КМ' = DATA BOOLEAN (Employee);
EXTEND FORM employee
    PROPERTIES(e) skipCheckCurrentBalanceLot
;

DESIGN employee {
    access {
        MOVE PROPERTY(skipCheckCurrentBalanceLot(e));
    }
}

CONSTRAINT checkCurrentBalanceLot() AND NOT skipCheckCurrentBalanceLot(currentUser()) AND SET(currentBalanceLot(Sku sk, Stock st) > (OVERRIDE currentBalance(sk, st), 0))
    MESSAGE 'Количество КМ на остатках превышает текущий остаток по товару';

CONSTRAINT checkCurrentBalanceLot() AND NOT skipCheckCurrentBalanceLot(currentUser()) AND SET(currentBalance(Lot l, Stock st) < 0)
    MESSAGE 'Остаток по КМ на складе становится меньше 0';
    
EXTEND FORM currentBalanceSkuStock
    PROPERTIES (s, st) currentBalanceLot
    
    OBJECTS l = Lot
    PROPERTIES(l) READONLY id, aggregated, count, nameBatch
    PROPERTIES(l, st) READONLY currentBalance
    FILTERS currentBalance(l, st), sku(l) = s
    
    FILTERGROUP notAggregated
        FILTER 'Без агрегаций' NOT aggregated(l) DEFAULT
;

DESIGN currentBalanceSkuStock {
    ledgerBox {
        MOVE BOX(l);
    }
}

EXTEND FORM lots
    PROPERTIES(l) READONLY nameBatch, currentBalance

    OBJECTS sl = SkuLedger
    PROPERTIES(sl) READONLY date, dateTime, canonicalNameSkuGroup, nameSku, description, nameStock, nameBatch
    PROPERTIES(sl, l) READONLY signedQuantity
    PROPERTIES(sl) EDIT SHOWIF allowedEdit(sl) NEWSESSION, show SHOWIF allowedShow(sl)
    
    ORDERS date(sl) DESC
    FILTERS isPosted(sl),
            signedQuantity(sl, l)

    OBJECTS ll = LotLedger
    PROPERTIES(ll) READONLY dateTime, description, nameStock
    PROPERTIES(ll, l) READONLY quantity
    PROPERTIES(ll) EDIT
    
    ORDERS dateTime(ll) DESC
    FILTERS active(ll),
            quantity(ll, l)
;

DESIGN lots {
    tabbedPane {
        MOVE BOX(sl) { caption = 'Движения'; }
        MOVE BOX(ll) { caption = 'Операции'; }
    }
}
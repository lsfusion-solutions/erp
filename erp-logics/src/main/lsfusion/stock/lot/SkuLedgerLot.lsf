MODULE SkuLedgerLot;

REQUIRE LotLedger, SkuLedger, Lot, LotAggregate;

NAMESPACE Stock;

quantity = ABSTRACT NUMERIC[16,5] (InSkuLedger, Lot);
quantity = ABSTRACT NUMERIC[16,5] (OutFIFOSkuLedger, Lot);

TABLE lotSkuLedger(Lot, SkuLedger);
signedQuantity 'Кол-во' (SkuLedger ledger, Lot lot) = 
    MULTI quantity[InSkuLedger, Lot](ledger, lot),
         -quantity[OutFIFOSkuLedger, Lot](ledger, lot) CHARWIDTH 7 MATERIALIZED;
signedQuantityActive (SkuLedger ledger, Lot lot) = signedQuantity(ledger, lot) IF active(ledger);

stock (SkuLedger ledger, Lot lot) = stock(ledger) IF signedQuantityActive(ledger, lot) MATERIALIZED; 
dateTime (SkuLedger ledger, Lot lot) = dateTime(ledger) IF signedQuantityActive(ledger, lot) MATERIALIZED; 

INDEX stock(SkuLedger ledger, Lot lot), dateTime(ledger, lot), lot;

batch (Lot l) = GROUP LAST batch(SkuLedger sl) 
                               ORDER DESC dateTime(sl), sl 
                               WHERE signedQuantity(sl, l) AND batch(sl) AND sku(sl) = sku(l) // проверка на sku(sl) = sku(l) нужна для уценки, когда sku у lot меняется на лету 
                         MATERIALIZED INDEXED;

nameBatch 'Партия' (Lot l) = name(batch(l));

skuBalance = GROUP SUM signedQuantity(SkuLedger ledger, Lot lot) IF active(ledger) BY lot, stock(ledger) CHARWIDTH 7;

currentBalance 'Текущий остаток' (Lot l, Stock st) =
    GROUP SUM (onHand(Lot parent, st) (+) skuBalance(parent, st)) IF level(l, parent) CHARWIDTH 7 MATERIALIZED;
prevCurrentBalance (Lot l, Stock st) = PREV(currentBalance(l, st));

skuCurrentBalance (Lot l, Stock st) = sku(l) IF currentBalance(l, st) MATERIALIZED;
INDEX skuCurrentBalance(Lot l, Stock st), st;

currentBalance 'Текущий остаток' (Lot l) = GROUP SUM currentBalance(l, Stock st) MATERIALIZED;

currentBalanceLot 'Остаток марок' (Sku sk, Stock st) =
    GROUP SUM currentBalance(Lot l, st) * count(l) IF skuCurrentBalance(l, st) = sk AND NOT aggregated(l) MATERIALIZED;
prevCurrentBalanceLot (Sku sk, Stock st) = PREV(currentBalanceLot(sk, st));

// constraint
@defineOption(checkCurrentBalanceLot, 'Проверять на превышение остатка по маркам', stock);

// без учета текущей даты/времени
balanceB 'Остаток на начало дня'###caption (Lot lot, Stock stock, DATE date) = currentBalance(lot, stock)
    (-) [GROUP SUM signedQuantityActive(SkuLedger ledger, Lot l) IF dateTime(ledger, l) >= DATETIME(date) BY l, stock(ledger, l)](lot, stock)
    (-) [GROUP SUM quantity(LotLedger ledger, Lot l) IF active(ledger) AND date(ledger) >= date BY l, stock(ledger)](lot, stock) CHARWIDTH 7;
prevBalanceB 'Остаток (до)'###caption (Lot lot, Stock stock, DATE date) = PREV(balanceB(lot, stock, date) IF date IS DATE);

// с учетом текущей даты/времени
balanceA 'Остаток на конец дня'###caption (Lot lot, Stock stock, DATE date) = currentBalance(lot, stock)
    (-) [GROUP SUM signedQuantityActive(SkuLedger ledger, Lot l) IF dateTime(ledger, l) >= DATETIME(sum(date, 1)) BY l, stock(ledger, l)](lot, stock)
    (-) [GROUP SUM quantity(LotLedger ledger, Lot l) IF active(ledger) AND date(ledger) > date BY l, stock(ledger)](lot, stock) CHARWIDTH 7;
prevBalanceA 'Остаток (после)'###caption (Lot lot, Stock stock, DATE date) = PREV(balanceA(lot, stock, date) IF date IS DATE);

// без учета текущей даты/времени
balanceB 'Остаток (до)'###caption (Lot lot, Stock stock, DATETIME dateTime) = currentBalance(lot, stock)
    (-) [GROUP SUM signedQuantityActive(SkuLedger ledger, Lot l) IF dateTime(ledger, l) >= dateTime BY l, stock(ledger, l)](lot, stock) 
    (-) [GROUP SUM quantity(LotLedger ledger, Lot l) IF active(ledger) AND dateTime(ledger) >= dateTime BY l, stock(ledger)](lot, stock) CHARWIDTH 7;
prevBalanceB 'Остаток (до)'###caption (Lot lot, Stock stock, DATETIME dateTime) = PREV(balanceB(lot, stock, dateTime) IF dateTime IS DATETIME);

// с учетом текущей даты/времени
balanceA 'Остаток (после)'###caption (Lot lot, Stock stock, DATETIME dateTime) = currentBalance(lot, stock)
    (-) [GROUP SUM signedQuantityActive(SkuLedger ledger, Lot l) IF dateTime(ledger, l) > dateTime BY l, stock(ledger, l)](lot, stock)
    (-) [GROUP SUM quantity(LotLedger ledger, Lot l) IF active(ledger) AND dateTime(ledger) > dateTime BY l, stock(ledger)](lot, stock) CHARWIDTH 7;
prevBalanceA 'Остаток (после)'###caption (Lot lot, Stock stock, DATETIME dateTime) = PREV(balanceA(lot, stock, dateTime) IF dateTime IS DATETIME);

skipCheckCurrentBalanceLot 'Не проверять на превышение остатка КМ' = DATA BOOLEAN (Employee);
EXTEND FORM employee
    PROPERTIES(e) skipCheckCurrentBalanceLot
;

DESIGN employee {
    access {
        MOVE PROPERTY(skipCheckCurrentBalanceLot(e));
    }
}

CONSTRAINT checkCurrentBalanceLot() AND NOT skipCheckCurrentBalanceLot(currentUser()) AND SET(currentBalanceLot(Sku sk, Stock st) > (OVERRIDE currentBalance(sk, st), 0))
    MESSAGE 'Количество КМ на остатках превышает текущий остаток по товару';

CONSTRAINT checkCurrentBalanceLot() AND NOT skipCheckCurrentBalanceLot(currentUser()) AND SET(currentBalance(Lot l, Stock st) < 0)
    MESSAGE 'Остаток по КМ на складе становится меньше 0';

CONSTRAINT checkCurrentBalanceLot() AND NOT skipCheckCurrentBalanceLot(currentUser()) AND SET(currentBalance(Lot l) > 1)
    MESSAGE 'Остаток по КМ по всем складам становится больше 1';
    
EXTEND FORM currentBalanceSkuStock
    PROPERTIES (s, st) currentBalanceLot
    
    OBJECTS lot = Lot
    PROPERTIES(lot) READONLY id, aggregated, count, nameBatch
    PROPERTIES(lot, st) READONLY currentBalance
    FILTERS currentBalance(lot, st), sku(lot) = s
    
    FILTERGROUP notAggregated
        FILTER 'Без агрегаций' NOT aggregated(lot) DEFAULT
;

DESIGN currentBalanceSkuStock {
    ledgerBox {
        MOVE BOX(lot);
    }
}

EXTEND FORM lots
    PROPERTIES(l) READONLY nameBatch, currentBalance

    OBJECTS sl = SkuLedger
    PROPERTIES(sl) READONLY date, dateTime, canonicalNameSkuGroup, nameSku, description, nameStock, nameBatch
    PROPERTIES(sl, l) READONLY signedQuantity
    PROPERTIES(sl) EDIT SHOWIF allowedEdit(sl) NEWSESSION, show SHOWIF allowedShow(sl)
    
    ORDERS date(sl) DESC
    FILTERS isPosted(sl),
            signedQuantity(sl, l)

    OBJECTS ll = LotLedger
    PROPERTIES(ll) READONLY dateTime, description, nameStock
    PROPERTIES(ll, l) READONLY quantity
    PROPERTIES(ll) EDIT
    
    ORDERS dateTime(ll) DESC
    FILTERS active(ll),
            quantity(ll, l)
;

DESIGN lots {
    tabbedPane {
        MOVE BOX(sl) { caption = 'Движения'; }
        MOVE BOX(ll) { caption = 'Операции'; }
    }
}

FORM dialogStockLot 'Выбор марки'
    OBJECTS s = Stock PANEL

    OBJECTS l = Lot
    PROPERTIES(l) READONLY id, count, quantity
    PROPERTIES READONLY currentBalance(l, s)
    
    FILTERGROUP balance
        FILTER 'Есть остаток' currentBalance(l, s) DEFAULT
;
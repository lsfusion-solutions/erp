MODULE LotAggregate;

REQUIRE Lot;

NAMESPACE Stock;

aggregated 'Агрегация' = DATA BOOLEAN (Lot);
mixed 'Микс' = DATA BOOLEAN (Lot);
skipSku(Lot l) += mixed(l);

parent = DATA Lot (Lot) INDEXED;
idParent 'Родитель' (Lot l) = id(parent(l));

WHEN LOCAL SETCHANGED(parent(Lot l)) AND sku(parent(l)) DO sku(l) <- sku(parent(l));

TABLE parentLot (Lot, Lot);
level (Lot child, Lot parent) = RECURSION 1l IF child IS Lot AND parent == child
      STEP 2l IF parent == parent($parent) MATERIALIZED;

nestedLots 'Вложенных марок' (Lot l) = GROUP SUM 1 IF level(Lot child, l) AND NOT aggregated(child) MATERIALIZED;

count(Lot l) += nestedLots(l);

dataCount 'Штук' = DATA INTEGER (Lot);
count(Lot l) += dataCount(l);

EXTEND FORM lot
    PROPERTIES(l) aggregated, mixed, dataCount
    
    OBJECTS cl = Lot
    PROPERTIES(cl) READONLY id, nameSku
    PROPERTIES(cl) NEW, DELETE
    FILTERS parent(cl) = l
;

DESIGN lot {
    tabbedPane {
        NEW aggregate {
            caption = 'Агрегация';
            NEW aggregateHeader {
                horizontal = TRUE;
                MOVE PROPERTY(aggregated(l));
                MOVE PROPERTY(mixed(l));
                MOVE PROPERTY(dataCount(l));
            }
            MOVE BOX(cl);
        }
    }
}
 
EXTEND FORM lots
    PROPERTIES(l) READONLY aggregated, count
;

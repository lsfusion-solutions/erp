MODULE LotOrderInit;

REQUIRE LotOrder, LotInit;

NAMESPACE Lot;

countNotInitialized (LotOrder o) = GROUP SUM 1 IF quantity(LotOrderDetail d, Lot l) AND lotOrder(d) = o AND NOT lotInit(l) MATERIALIZED ;

lotOrderDetail = DATA LotOrderDetail (LotInitDetail) INDEXED;

initializedLot 'Введено в оборот' (LotOrderDetail od) = GROUP SUM quantity(LotInitDetail id) IF lotOrderDetail(id) = od AND NOT canceled(lotInit(id)) MATERIALIZED;
initializedLot 'Введено в оборот' (LotOrder o) = GROUP SUM initializedLot(LotOrderDetail d) IF lotOrder(d) = o MATERIALIZED; 

toInitialize (LotOrder o) = isPosted(o) AND quantityLot(o) AND NOT initializedLot(o) = quantityLot(o);

fill (LotInit i, LotOrder o) {
    FOR lotOrder(LotOrderDetail od) = o NEW id = LotInitDetail DO {
        lotInit(id) <- i;
        sku(id) <- sku(od);
        quantity(id) <- quantity(od);
        lotOrderDetail(id) <- od;
        
        quantity(id, Lot l) <- quantity(od, l);
    } 
}

fillFromOrder 'Заполнить из заказа' (LotInit i) {
    DIALOG lotOrders OBJECTS o INPUT FILTERS isPosted(o) AND toInitialize(o), type(o) = type(i), (stock(o) = stock(i) OR NOT stock(i)) DO {
        fill(i, o);
    }
}

EXTEND FORM lotInit
    PROPERTIES(i) fillFromOrder DRAW d TOOLBAR
;

newLotInit 'Ввести в оборот' (LotOrder o) {
    NEWSESSION { 
        NEW i = LotInit {
            type(i) <- type(o);
            stock(i) <- stock(o);
            fill(i, o);
            SHOW lotInit OBJECTS i = i DOCKED;            
        }
    }
}

EXTEND FORM lotOrders
    PROPERTIES(o) READONLY initializedLot BACKGROUND RGB(224,255,224) IF toInitialize(o)

    PROPERTIES(o) newLotInit TOOLBAR SHOWIF toInitialize(o)

    PROPERTIES(d) READONLY initializedLot
;

copy (LotInitDetail nd, LotInitDetail d) + {
    lotOrderDetail(nd) <- lotOrderDetail(d);
}
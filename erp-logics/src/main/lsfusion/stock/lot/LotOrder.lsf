MODULE LotOrder;

REQUIRE Lot, StockSkuDocument, SkuLedgerLot;

NAMESPACE Lot;

CLASS LotOrder 'Заказ марок';
CLASS LotOrderDetail 'Строка заказа марок';

id 'Код' = DATA STRING[100] (LotOrder) CHARWIDTH 36;
@defineDocument(lotOrder);

@defineDocumentHeaderNumber(LotOrder, STRING[10]);
@defineNumeratedDefault(LotOrder, 'Заказы марок', 'ЗМ');

@deriveDocumentHeaderTime(LotOrder);

@defineDocumentStock(lotOrder, stock, 'Склад');
CONSTRAINT stock(LotOrder o) AND NOT isCompany(stock(o))
    CHECKED BY stock[LotOrder]
    MESSAGE 'Для заказа марок выбран склад, который не принадлежит компании';
@defineDocumentStockAccess(LotOrder, stock, company);
@defineDocumentDialogStocks(lotOrder, stock, company, , );

@defineDocumentPosted(lotOrder);
canceled 'Отменен' = DATA BOOLEAN (LotOrder);

pending 'В ожидании' (LotOrder o) = o IS LotOrder AND NOT isPosted(o) AND NOT canceled(o);   

type 'Тип' = DATA LotType (LotOrder) NONULL;
nameType 'Тип' (LotOrder o) = name(type(o));
idType 'Код типа' (LotOrder o) = id(type(o));

// detail

@defineDocumentDetailSku(lotOrder, sku);
overGtin = ABSTRACT STRING[14] (LotOrderDetail);
gtin 'GTIN' (LotOrderDetail d) = OVERRIDE overGtin(d), gtin14(sku(d)) MATERIALIZED;

lotTypeCheckSkip = ABSTRACT VALUE BOOLEAN (LotOrder);
CONSTRAINT (SETCHANGED(sku(LotOrderDetail d)) OR SETCHANGED(type(lotOrder(d)))) AND NOT lotType(sku(d)) = type(lotOrder(d)) AND 
    NOT lotTypeCheckSkip(lotOrder(d))
    CHECKED BY sku[LotOrderDetail] 
    MESSAGE 'Тип маркировки товара не соответствует типу маркировки заказа';

@defineMovementSku(lotOrderDetail, stock);
@defineBalancesSku(lotOrderDetail);
@defineMovementPriceSku(lotOrderDetail, stock);
@defineInfoSku(lotOrderDetail);

@defineDocumentDetailQuantity(lotOrder);
@defineDocumentHeaderQuantity(lotOrder);
@defineDocumentHeaderSkuQuantity(lotOrder, sku);
quantity (LotOrder o, STRING[14] gtin) = GROUP SUM quantity(LotOrderDetail d) IF lotOrder(d) = o AND gtin = gtin(d); 

FORM lotOrder 'Заказ марок'
    OBJECTS o = LotOrder PANEL
    PROPERTIES(o) isPosted, canceled, nameNumerator, number, series, 
                  date, time, id, 
                  nameType, nameStock ON CHANGE changeStockCompany(o), note
    
    OBJECTS d = LotOrderDetail
    PROPERTIES(d) index READONLY, idBarcodeSku, idSku, nameSku, shortNameUOMSku, gtin, quantity, NEW, DELETE
    FILTERS lotOrder(d) = o
    
    EDIT LotOrder OBJECT o
;

DESIGN lotOrder {
    OBJECTS {
        NEW mainTabbed {
            tabbed = TRUE;
            fill = 1;
            NEW documentBox {
                caption = 'Заказ';
                NEW header {
                    NEW headerRow1 {
                        horizontal = TRUE;
                        alignment = STRETCH;
                        MOVE PROPERTY(isPosted(o));
                        MOVE PROPERTY(canceled(o));
                        MOVE PROPERTY(nameNumerator(o));
                        MOVE PROPERTY(number(o));
                        MOVE PROPERTY(series(o));
                    }
                    NEW headerRow2 {
                        horizontal = TRUE;
                        alignment = STRETCH;
                        MOVE PROPERTY(date(o));
                        MOVE PROPERTY(time(o));
                        MOVE PROPERTY(id(o));
                    }
                    NEW headerRow3 {
                        horizontal = TRUE;
                        alignment = STRETCH;
                        MOVE PROPERTY(nameType(o)) { notNull = TRUE; };
                        MOVE PROPERTY(nameStock(o));
                    }
                    MOVE PROPERTY(note(o));
                }
                MOVE BOX(d) {
                    caption = 'Спецификация';
                }
            }
        }
    }
}

// Подбор
@defineDocumentSku(lotOrder, sku, stock, );

price 'Цена' (Sku s, LotOrder o) = prevAccountPriceB(s, stock(o), dateTime(o));
@extendFormDocumentSku(lotOrder, lotOrder, o);

EXTEND FORM lotOrder
    PROPERTIES 'Марок' = prevCurrentBalanceLot(ks, stock(o)) AFTER prevCurrentBalance(ks, o)
    
    FILTERGROUP filterBalance
        FILTER 'Без марок' prevCurrentBalance(ks, o) > OVERRIDE prevCurrentBalanceLot(ks, stock(o)), 0

    FILTERGROUP filterLot
        FILTER 'Маркируется' lotType(ks) = type(o) DEFAULT

    FILTERGROUP filterSku
        FILTER 'С остатком' prevCurrentBalance(ks, stock(o)) 'F10' DEFAULT
        FILTER 'В документе' quantityLotOrderDetail(ks, o) 'F9'
;

@defineAddDetailDialogBarcodeCustomInner(lotOrder, lotOrderDetail, LotOrderDetail, sku);
EXTEND FORM lotOrder
    PROPERTIES(o) DRAW d addDetailInputBarcodeLotOrderDetail
;

FORM lotOrders 'Заказы марок'
    OBJECTS o = LotOrder
    PROPERTIES(o) READONLY isPosted, canceled, id, number, series, date, time, 
                           nameType, nameStock, note, countLotOrderDetail, quantityLotOrderDetail
    PROPERTIES(o) NEWSESSION NEW, EDIT, DELETE SHOWIF NOT isPosted(o)

    OBJECTS d = LotOrderDetail
    PROPERTIES(d) READONLY index, idBarcodeSku, idSku, nameSku, shortNameUOMSku, gtin, quantity
    FILTERS lotOrder(d) = o
;

DESIGN lotOrders {
    OBJECTS {
        NEW tabbedPane {
            tabbed = TRUE;
            fill = 1;
            MOVE BOX(d) { caption = 'Товары'; }
        }
    }
}

useLot (LotOrder o) = o IS LotOrder;
@defineDocumentLotIn(lotOrder, quantity, o, lotOrders, isPosted);

lotOrder (Lot l) = GROUP MAX lotOrder(LotOrderDetail d) IF quantity(d, l) MATERIALIZED INDEXED; 

@extendFormFilterAccessStock(LotOrder, o, lotOrders, stock, company);

NAVIGATOR {
    lotNavigator {
        NEW lotOrders;
    }
}

// copy
copy 'Копировать' (LotOrder o)  { 
    NEWSESSION {
        NEW no = LotOrder {
            type(no) <- type(o);
            stock(no) <- stock(o);
            FOR lotOrder(LotOrderDetail d) = o INLINE NEW nd = LotOrderDetail DO {
                lotOrder(nd) <- no;
                sku(nd) <- sku(d);
                quantity(nd) <- quantity(d);
            }
            SHOW lotOrder OBJECTS o = no DOCKED;
        }
    }
}
EXTEND FORM lotOrders PROPERTIES(o) TOOLBAR copy;
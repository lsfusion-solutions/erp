MODULE LotType;

REQUIRE Sku, Item;

NAMESPACE Stock;

// lot type

CLASS LotType 'Тип маркировки' {
    lp 'Одежда',
    perfumery 'Парфюмерия'
}

id 'Код' (LotType t) = getWord(staticName(t),'.',2) IF t IS LotType CHARWIDTH 20;
name 'Наименование' (LotType t) = staticCaption(t) IF t IS LotType CHARWIDTH 20;

FORM dialogLotTypes 'Выбор типа маркировки'
    OBJECTS t = LotType
    PROPERTIES(t) READONLY name
    
    LIST LotType OBJECT t
;

// sku group

dataLotType = DATA LotType (SkuGroup);
nearestLotType (SkuGroup child) = GROUP LAST dataLotType(SkuGroup parent) ORDER DESC level(child, parent) WHERE dataLotType(parent);

lotType (SkuGroup skuGroup) = OVERRIDE dataLotType(skuGroup), nearestLotType(skuGroup) MATERIALIZED;
nameLotType 'Тип маркировки' (SkuGroup skuGroup) = name(lotType(skuGroup)) IN paramsItemGroup;

EXTEND FORM itemGroup PROPERTIES(g) AFTER nameParent(g) nameLotType;
EXTEND FORM itemGroups PROPERTIES(g) AFTER canonicalName(g) READONLYIF isReadonly() nameLotType BACKGROUND RGB(190,190,190) IF dataLotType(g);

skipLotType = ABSTRACT BOOLEAN (Sku); 
lotType (Sku s) = lotType(skuGroup(s)) IF NOT skipLotType(s) MATERIALIZED;

// обязательность задания
dataExplicitLot = DATA BOOLEAN (SkuGroup);
nearestExplicitLot (SkuGroup child) = GROUP LAST dataExplicitLot(SkuGroup parent) ORDER DESC level(child, parent) WHERE dataExplicitLot(parent);

explicitLot 'Обязательное указание марки' (SkuGroup skuGroup) = OVERRIDE dataExplicitLot(skuGroup), nearestExplicitLot(skuGroup) MATERIALIZED IN paramsItemGroup;

EXTEND FORM itemGroup PROPERTIES(g) AFTER nameParent(g) explicitLot;
EXTEND FORM itemGroups PROPERTIES(g) AFTER canonicalName(g) READONLYIF isReadonly() explicitLot BACKGROUND RGB(190,190,190) IF dataExplicitLot(g);

explicitLot (Sku s) = explicitLot(skuGroup(s)) AND lotType(s) MATERIALIZED;
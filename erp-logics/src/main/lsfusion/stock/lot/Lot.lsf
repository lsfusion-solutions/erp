MODULE Lot;

REQUIRE EDIGTIN, Sku, LotType;

NAMESPACE Stock;

DESIGN options{
    pane{
        NEW lot {
            fill = 1;
            caption = 'Марки';
        } 
    }
}

dataGtin 'GTIN' (Sku s) = DATA STRING[14] (Sku) IN itemBase; // to delete
migratedDataGtin = DATA BOOLEAN();
onStarted() + {
    IF NOT migratedDataGtin() THEN {
        NEWSESSION {
            migratedDataGtin() <- TRUE;
            GTIN(Item i) <- dataGtin(i) WHERE NOT GTIN(i);
            APPLY;
        }
    }
}

overGtin = ABSTRACT STRING[14] (Sku);
gtin 'GTIN' (Sku s) = OVERRIDE GTIN(s), overGtin(s), STRING[13](idBarcode(s)) ;
gtin14 'GTIN' (Sku s) = STRING[14](lpad(gtin(s), 14, '0')) MATERIALIZED INDEXED;

skuFromGtin = GROUP MAX Sku s BY gtin14(s);

// lot
CLASS Lot 'Марка';
TABLE lot (Lot);

in 'Отм.' = DATA LOCAL BOOLEAN (Lot);
countInLot = GROUP SUM 1 IF in(Lot l);

id 'Код' = DATA STRING[200] (Lot) INDEXED IN id CHARWIDTH 15;
lot = GROUP AGGR Lot l BY id(l); 

extractGS1 = FORMULA STRING[200] 'split_part(($1),\'\',1)';
translateBarcode = FORMULA STRING[200] 'SELECT CASE WHEN ($1) ~* \'[а-яА-Я]\' THEN translate(($1), \'ЙЦУКЕНГШЩЗХЪФЫВАПРОЛДЖЭ/ЯЧСМИТЬБЮ,йцукенгшщзхъфывапролджэячсмитьбю.\', \'QWERTYUIOP{}ASDFGHJKL:"|ZXCVBNM<>?qwertyuiop[]asdfghjkl;\'\'zxcvbnm,./\') ELSE ($1) END';

barcodeToId = ABSTRACT CASE STRING[200] (STRING[200]);
barcodeToId (STRING[200] s) += WHEN s IS STRING[200] THEN translateBarcode(extractGS1(s));

tail 'Хвост' = DATA STRING[200] (Lot);

barcodeToTail = ABSTRACT CASE STRING[200] (STRING[200]);
barcodeToTail (STRING[200] s) += WHEN s IS STRING[200] THEN STRING[200](substrFrom(s, length(extractGS1(s)) + 1)); 

code 'КИ' (Lot l) = CONCAT '', id(l), tail(l);

idToGtin (STRING[200] s) = substr(s, 3, 14);
idToSeries (STRING[200] s) = substr(s, 19, 13);
 
gtin 'GTIN' (Lot l) = idToGtin(id(l));
series 'Серия' (Lot l) = idToSeries(id(l));

sku 'Товар' = DATA Sku (Lot) INDEXED;
skipSku = ABSTRACT BOOLEAN (Lot);
CONSTRAINT Lot l IS Lot AND NOT sku(l) AND NOT skipSku(l) MESSAGE 'Для марки должен быть задан товар';

idSku 'Код товара' (Lot l) = id(sku(l)) IN id;
nameSku 'Наименование товара' (Lot l) = name(sku(l)) IN id;
idBarcodeSku 'Штрихкод' (Lot l) = idBarcode(sku(l)) IN id;
note 'Описание' = DATA ISTRING[200] (Lot) IN id;

batch = ABSTRACT Batch (Lot) MATERIALIZED INDEXED;
currentBalance 'Текущий остаток' = ABSTRACT NUMERIC[16,5](Lot) MATERIALIZED;

overPreprocessedBarcode(STRING[200] s) += WHEN lotType(sku(lot(barcodeToId(s)))) THEN idBarcode(sku(lot(barcodeToId(s))));

changeSku 'Заменить товар' (Lot l) {
    DIALOG skus OBJECTS s INPUT DO {
        ASK 'Вы уверены, что хотите изменить товар для контрольной марки ' + id(l) + ' на ' + name(s) + '?\n Все ограничения будут временно отключены. Во всех существущих документах останется предыдущий товар.' DO {
            note(l) <- 'Замена товара с ' + idSku(l) + ' на ' + id(s);
            sku(l) <- s;
            setNoCancelInTransaction();
            APPLY;
        }
    }
}

count 'Штук' = ABSTRACT VALUE INTEGER (Lot);
count (Lot l) += 1 IF l IS Lot; 

FORM lot 'Марка'
    OBJECTS l = Lot PANEL
    PROPERTIES(l) id, tail, idSku, idBarcodeSku, nameSku, note
    
    EDIT Lot OBJECT l 
;

DESIGN lot {
    OBJECTS {
        NEW header {
            NEW ids {
                horizontal = TRUE;
                alignment = STRETCH;
                caption = 'Код';
                MOVE PROPERTY(id(l));
                MOVE PROPERTY(tail(l));
            }
            NEW sku {
                horizontal = TRUE;
                alignment = STRETCH;
                caption = 'Товар';
                MOVE PROPERTY(idSku(l));
                MOVE PROPERTY(idBarcodeSku(l));
                MOVE PROPERTY(nameSku(l));
                MOVE PROPERTY(note(l));
            }
            NEW params {
                horizontal = TRUE;
                alignment = STRETCH;
                caption = 'Параметры';
            }
        }
        NEW tabbedPane {
            tabbed = TRUE;        
            fill = 1;
        }
    }
}

FORM dialogLots 'Марки'
    OBJECTS l = Lot
    PROPERTIES(l) READONLY id, idSku, nameSku
    
    LIST Lot OBJECT l
;

FORM lots 'Марки'
    OBJECTS l = Lot
    PROPERTIES(l) READONLY id, idSku, nameSku
    PROPERTIES(l) NEWSESSION EDIT
    PROPERTIES(l) changeSku TOOLBAR
;

DESIGN lots {
    OBJECTS {
        NEW tabbedPane {
            tabbed = TRUE;
            fill = 1;
        }
    }
}

NAVIGATOR {
    stockNavigator {
        NEW FOLDER lotNavigator 'Марки' {
            NEW lots;
        }
    }
}

// dialogs

quantity 'Кол-во' = DATA LOCAL NUMERIC[16,5] (Lot);

skipShowQuantity = ABSTRACT BOOLEAN (Lot);
showQuantity (Lot lot) = lot IS Lot AND NOT skipShowQuantity(lot);

FORM dialogInLots 'Список марок'
    OBJECTS l = Lot
    PROPERTIES(l) READONLY id
    PROPERTIES(l) count
    PROPERTIES(l) READONLY quantity SHOWIF showQuantity(l)
    PROPERTIES(l) delete = DELETE
    FILTERS quantity(l)
;

removeLot 'Убрать' (Lot l) {
    quantity(l) <- NULL;
} TOOLBAR CHANGEKEY 'ctrl DELETE';

FORM dialogOutLots 'Список марок'
    OBJECTS l = Lot
    PROPERTIES(l) READONLY id, count
    PROPERTIES(l) quantity SHOWIF showQuantity(l),removeLot
    FILTERS quantity(l)
;

// meta
@defineOption(skipCheckDocumentLot, 'Не проверять КМ в документе', stock1);

META defineDocumentLot (doc, quantityProp, postedProp)
    TABLE doc##Detail##Lot(###doc##Detail, Lot);
    quantity 'Кол-во' = DATA NUMERIC[16,5] (###doc##Detail, Lot);

    skipCheckDocumentLot = ABSTRACT BOOLEAN (###doc##Detail);

    CONSTRAINT (SETCHANGED(quantity(###doc##Detail d, Lot l)) OR CHANGED(sku(d))) AND quantity(d,l) AND NOT sku(l) = sku(d) AND NOT skipCheckDocumentLot(d)
        MESSAGE 'Товар марки не соответствует товару строки документа';
    
    quantityLot 'Кол-во марок' (###doc##Detail d) = GROUP SUM quantity(d, Lot l) * count(l) MATERIALIZED;
    quantityLot 'Кол-во марок' (###doc i) = GROUP SUM quantityLot(###doc##Detail d) IF doc(d) = i MATERIALIZED;
    quantity 'Кол-во марок' (###doc i, Lot l) = GROUP SUM quantity(###doc##Detail d, l) IF doc(d) = i;
    
    
    CONSTRAINT SET((quantityProp(###doc##Detail d) OR quantityLot(d)) AND postedProp(d) AND NOT quantityProp(d) = OVERRIDE quantityLot(d), 0.0) 
               IF explicitLot(sku(d)) AND NOT skipCheckDocumentLot() AND NOT skipCheckDocumentLot(d)
        MESSAGE 'Для строки документа неправильно заданы марки';
    
    backgroundQuantityLot (###doc##Detail d) = (IF quantityLot(d) = quantityProp(d) THEN RGB(224,255,224) ELSE RGB(255,224,224)) 
                                                           IF lotType(sku(d));
END

META defineDocumentLotType(doc)
    in 'Вкл' = DATA BOOLEAN (###doc, LotType);
    
    countSku (###doc w, LotType t) = GROUP SUM 1 IF doc(###doc##Detail d) = w AND lotType(sku(d)) = t AND quantityLot(d);
    WHEN useLot(###doc w) AND (GROUP SUM 1 IF (CHANGED(sku(###doc##Detail d)) OR CHANGED(quantityLot(d))) AND doc(d) = w) DO {
        in(w, LotType t) <- TRUE IF countSku(w, t); 
    }
    
    recalculateLotType 'Заполнить типы марок' (###doc w) {
        NEWSESSION {
            in(w, LotType t) <- TRUE IF countSku(w, t);
            APPLY;
        } 
    }
END

lotBarcode() = DATA LOCAL STRING[200] ();

@defineOption(lotMinLength, 'Минимальная длина КМ', INTEGER, stock1);

META defineDocumentLotBarcodeIn (doc, quantityProp)
    EXTEND FORM doc
        OBJECTS cd = ###doc##Detail PANEL
        FILTERS cd = d
    ;
    
    isLotBarcode = ABSTRACT BOOLEAN (###doc##Detail, STRING[200]);
    local###doc##Detail = DATA LOCAL ###doc##Detail ();
    
    beforeInputBarcode###doc##Detail(###doc i, STRING[200] s) + {
        lotBarcode() <- NULL;
        consumedBarcode###doc##Detail() <- NULL;
        FOR [FILTER doc.cd](###doc##Detail d) DO {
            local###doc##Detail() <- d;
        }
        IF (length(s) >= OVERRIDE lotMinLength(), 20) OR isLotBarcode(local###doc##Detail(), s) THEN {
            lotBarcode() <- barcodeToId(s);
            FOR [FILTER doc.cd](###doc##Detail d) AND lotType(sku(d)) DO {
                FOR Lot l = lot(lotBarcode()) DO {
                    IF quantity(i, l) = 1.0 THEN {
                        MESSAGE 'Марка уже была отсканирована в текущем документе';
                        consumedBarcode###doc##Detail() <- TRUE;
                    } ELSE
                        IF sku(l) = sku(d) THEN {
                            quantity(d, l) <- 1.0;
                            IF NOT quantityProp(d) >= quantityLot(d) THEN
                                quantityProp(d) <- quantityLot(d);
                            consumedBarcode###doc##Detail() <- TRUE;
                        }
                } ELSE {
                    NEW l = Lot {
                        id(l) <- lotBarcode();
                        sku(l) <- sku(d);
                        quantity(d, l) <- 1.0;
                        IF NOT quantityProp(d) >= quantityLot(d) THEN
                            quantityProp(d) <- quantityLot(d);
                        consumedBarcode###doc##Detail() <- TRUE;
                    }
                }
            }
        }
    }
    
    afterInputBarcode###doc##Detail(###doc##Detail d, STRING[200] barcode) + {
        IF lotBarcode() THEN
            quantity(d, Lot l) <- 1.0 WHERE l = lot(lotBarcode());
    }
END

META defineDocumentLotIn(doc, quantityProp, obj, formList, postedProp)
    @defineDocumentLot(doc, quantityProp, postedProp);

    changeQuantityLot (###doc##Detail d) {
        quantity(Lot l) <- quantity(d, l);
        SHOW dialogInLots;    
    }
    
    EXTEND FORM doc
        PROPERTIES(d) quantityLot AFTER quantityProp(d) ON CHANGE changeQuantityLot(d) 
                      BACKGROUND backgroundQuantityLot(d) 
                      SHOWIF useLot(obj)
                      READONLYIF NOT lotType(sku(d))
    ;

    EXTEND FORM formList
        PROPERTIES(obj) READONLY quantityLot

        PROPERTIES(d) READONLY quantityLot AFTER quantityProp(d) 
                      BACKGROUND backgroundQuantityLot(d) 
                      SHOWIF useLot(obj)
                      READONLYIF NOT lotType(sku(d))
    ;
    
    @defineDocumentLotBarcodeIn(doc, quantityProp);
END

META defineDocumentLotBarcodeOut (doc, form, stock)
    EXTEND FORM form
        OBJECTS cdo = ###doc##Detail PANEL
        FILTERS cdo = d
    ;

    afterInputLotBarcode ABSTRACT LIST (###doc);
    beforeInputLotBarcode ABSTRACT LIST (###doc##Detail, STRING[200]);
    beforeInputBarcode###doc##Detail(###doc i, STRING[200] s) + {
        lotBarcode() <- NULL;
        consumedBarcode###doc##Detail() <- NULL;
        IF length(s) >= OVERRIDE lotMinLength(), 20 THEN {
            FOR [FILTER form.cdo](###doc##Detail d) AND lotType(sku(d)) DO
                beforeInputLotBarcode(d, s);
            FOR STRING[200] bc = barcodeToId(s) AND Lot l = lot(bc) DO {
                lotBarcode() <- bc;
                IF quantity(i, l) = 1.0 THEN {
                    MESSAGE 'Марка уже была отсканирована в текущем документе';
                    consumedBarcode###doc##Detail() <- TRUE;
                } ELSE {
                    IF explicitBatchLedger(stock(i)) AND batch(l) THEN {
                        FOR ###doc##Detail d = doc##Detail(batch(l), i) DO {
                            quantity(d, l) <- 1.0;
                            IF NOT quantity(d) >= quantityLot(d) THEN
                                quantity(d) <- quantityLot(d);
                            seek(d);
                            consumedBarcode###doc##Detail() <- TRUE;
                        } ELSE
                            NEW d = ###doc##Detail {
                                doc(d) <- i;
                                sku(d) <- sku(l);
                                batch(d) <- batch(l);
                                quantity(d, l) <- 1.0;
                                quantity(d) <- quantityLot(d);
                                seek(d);
                                consumedBarcode###doc##Detail() <- TRUE;
                            }
                    } ELSE
                        FOR ###doc##Detail d = doc##Detail(i, sku(l)) DO {
                            quantity(d, l) <- 1.0;
                            IF NOT quantity(d) >= quantityLot(d) THEN
                                quantity(d) <- quantityLot(d);
                            seek(d);
                            consumedBarcode###doc##Detail() <- TRUE;
                        } ELSE {
                            NEW d = ###doc##Detail {
                                doc(d) <- i;
                                sku(d) <- sku(l);
                                quantity(d, l) <- 1.0;
                                quantity(d) <- quantityLot(d);
                                seek(d);
                                consumedBarcode###doc##Detail() <- TRUE;
                            }
                        }
                }
            }
        }
        afterInputLotBarcode(i);
    }
    
    afterInputBarcode###doc##Detail(###doc##Detail d, STRING[200] barcode) + {
        IF lotBarcode() THEN
            quantity(d, Lot l) <- 1.0 WHERE l = lot(lotBarcode());
            IF explicitBatchLedger(stock(d)) THEN {
                batch(d) <- batch(lot(lotBarcode()));
            }
    }
END

META defineDocumentLotBarcodeOut (doc, stock)
    defineDocumentLotBarcodeOut(doc, doc, stock);
END
    
META defineDocumentLotOut(doc, form, stock, quantityProp, obj, postedProp)
    @defineDocumentLot(doc, quantityProp, postedProp);

    changeQuantityLot (###doc##Detail d) {
        quantity(Lot l) <- quantity(d, l);
        DIALOG dialogOutLots DO {
            quantity(d, Lot l) <- quantity(l);
        }    
    }

    dropLotBatch 'Сбросить марки' (###doc o) {
        quantity(###doc##Detail d, Lot l) <- NULL 
            WHERE doc(d) = o AND [FILTER form.d](d); 
    }
    
    EXTEND FORM form
        PROPERTIES(d) quantityLot AFTER quantity(d) ON CHANGE changeQuantityLot(d) 
                      ON CONTEXTMENU dropLotBatch(obj)  
                      BACKGROUND backgroundQuantityLot(d) 
                      SHOWIF useLot(obj)
        READONLYIF NOT lotType(sku(d))
    ;
    
    @defineDocumentLotBarcodeOut(doc, form, stock);
END
META defineDocumentLotOut(doc, stock, quantityProp, obj, postedProp)
    @defineDocumentLotOut(doc, doc, stock, quantityProp, obj, postedProp);
END
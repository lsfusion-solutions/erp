MODULE OrderPrint;

REQUIRE LegalEntity, Bank, Order;

NAMESPACE Order;

@defineDocumentAccount(order, supplier, ' поставщика');

@defineDocumentAccount(order, customer, ' покупателя');

//Рекламный блок

adImageOrder 'Рекламное изображение счет-фактуры' = DATA IMAGEFILE ();
loadImageOrder 'Загрузить рекламное изображение счет-фактуры' ()  { INPUT =adImageOrder() CHANGE; }
openImageOrder 'Просмотреть рекламное изображение счет-фактуры' ()  { open(adImageOrder()); }
deleteImageOrder 'Удалить рекламное изображение счет-фактуры' ()  { adImageOrder() <- NULL;} ASON CONTEXTMENU adImageOrder[];

adTextOrder 'Рекламное изображение счет-фактуры' = DATA RICHTEXT ();

EXTEND FORM options
    PROPERTIES() adImageOrder ON CHANGE loadImageOrder(), adTextOrder
;

DESIGN options {
    orders {
        NEW adContainer{
            type = CONTAINERH;
            fill = 1;
            caption = 'Рекламный блок';
            MOVE PROPERTY(adImageOrder()){
                panelCaptionAbove = TRUE;
                alignment = STRETCH;
            }
            MOVE PROPERTY(adTextOrder()){
                panelCaptionAbove = TRUE;
                fill = 3;
            }
        }
    }
}

FORM printOrder 'Счет-фактура (печать)'
    OBJECTS o = Order PANEL SUBREPORT
    PROPERTIES (o) READONLY nameSupplier, fullNameSupplier, nameSupplierStock, addressSupplier, taxNumberSupplier, 
                   numberAccountSupplier, nameBankSupplier, addressBankSupplier,
                   departmentBankSupplier, CBUBankSupplier, MFOBankSupplier, phoneSupplier,
                   nameCustomer, fullNameCustomer, nameCustomerStock, addressCustomer, 
                   numberAccountCustomer, nameBankCustomer, addressBankCustomer,
                   departmentBankCustomer, CBUBankCustomer, MFOBankCustomer, phoneCustomer,
                   number, series, seriesNumber, date, time, closeDate, currency, shortNameCurrency,
                   nameCurrency, note, countOrderDetail, quantityOrderDetail, sumOrderDetail,
                   VATSumOrderDetail, invoiceSumOrderDetail, nameAcquisitionTarget, descriptionContractSku
    PROPERTIES() adImage=adImageOrder, adText=adTextOrder

    OBJECTS d = OrderDetail
    PROPERTIES (d) READONLY index, nameSku, shortNameUOMSku,
                   idBarcodeSku, quantity,
                   price, sum, numberVAT,
                   valueVAT,  VATSum, invoiceSum
    FILTERS order(d) == o
;

print 'Счет-фактура' (Order order)  { PRINT printOrder OBJECTS o = order; }   IMAGE 'print.png' IN print;

FORM printOrderXLS 'Заказ'
    OBJECTS o = Order PANEL SUBREPORT
    PROPERTIES (o) READONLY nameSupplier, fullNameSupplier, nameSupplierStock, addressSupplier, 
                   numberAccountSupplier, nameBankSupplier, addressBankSupplier,
                   departmentBankSupplier, CBUBankSupplier, MFOBankSupplier, phoneSupplier,
                   nameCustomer, fullNameCustomer, nameCustomerStock, addressCustomer, 
                   numberAccountCustomer, nameBankCustomer, addressBankCustomer,
                   departmentBankCustomer, CBUBankCustomer, MFOBankCustomer, phoneCustomer,
                   number, series, seriesNumber, date, time, closeDate, currency,
                   nameCurrency, note, countOrderDetail, quantityOrderDetail, packQuantityOrderDetail, sumOrderDetail,
                   VATSumOrderDetail, invoiceSumOrderDetail, shipmentDateTime, shipmentDate, shipmentTime, 
                   addressCustomerStock, descriptionContractSku, createdNameUser

    OBJECTS d = OrderDetail
    PROPERTIES (d) READONLY index, nameSku, idSku, idBarcodeSku, shortNameUOMSku,
                   quantity, packQuantity,
                   price, sum, numberVAT,
                   valueVAT,  VATSum, invoiceSum, allBarcodesSku
    FILTERS order(d) == o
;

excel 'Заказ (xls)' (Order order)  { PRINT printOrderXLS OBJECTS o = order XLSX; } IN print;
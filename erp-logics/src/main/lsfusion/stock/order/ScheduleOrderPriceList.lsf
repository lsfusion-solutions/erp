MODULE ScheduleOrderPriceList;

REQUIRE ScheduleOrder, PriceList;

NAMESPACE  ScheduleOrder;

//--
META addScheduleOrder(filter, cap, filterStock) 
    countPriceLists 'Поставщики, у которых есть действующие прайсы' (filter, Stock stock, DATE date) = GROUP SUM 1 
        IF is###filter(filterStock(PriceList priceList)) AND isPosted(priceList) AND fromDate(priceList) <= (date AS DATE) 
        AND NOT (toDate(priceList) < (date AS DATE)) AND in(priceList, stock)
            BY filterStock(priceList);
        
    countScheduleOrders 'Поставщики, у которых есть действующие и будующие графики заказов' (filter, Stock stock, DATE date) = GROUP SUM 1 IF 
        startDate(ScheduleOrder order) AND NOT (endDate(order) < (date AS DATE)) AND in(order, stock)
         BY filter(order);  
          
    filterLegalEntity 'Поставщики, у которых есть прайсы и нет графика заказов' (LegalEntity filter, Stock stock, DATE date) = countPriceLists  (filter, stock, date) IF NOT countScheduleOrders (filter, stock, date);
    filterLegalEntity 'Поставщики, у которых есть прайсы и нет графика заказов'(LegalEntity filter, DATE date) = GROUP SUM 1 IF filterLegalEntity(filter, Stock stock, date);
    
    addScheduleOrder###filter 'Добавить'(LegalEntity filter, Stock stock)  { 
	NEWSESSION {
	        NEW o = ScheduleOrder {
	            filter(o) <- filter;
	            filter##Stock(o) <- defaultStock(filter(o)) WHERE countStocks(filter(o)) == 1;
	                                
	            customer(o) <- legalEntity(stock);
	            dataIn(o,stock) <- TRUE;                    
	            
	            SHOW scheduleOrder OBJECTS s=o DOCKED NOCANCEL;
	        }
	    }
} TOOLBAR IMAGE 'add.png';
    
    EXTEND FORM scheduleOrderDashboard
        
        OBJECTS l=LegalEntity
        PROPERTIES(l) READONLY name
        FILTERS is###filter(l)
        FILTERS filterLegalEntity(l,d)
        
        OBJECTS st=Stock
        PROPERTIES(st) READONLY name               
        PROPERTIES(l,st) addScheduleOrder###filter
        
        FILTERS filterLegalEntity(l,st,d)
    ;
    
    DESIGN scheduleOrderDashboard {
        headBox {
            NEW tabContainer BEFORE BOX(ss) {
                fill = 1;
                type = TABBED;
                MOVE BOX(s);
                NEW legalEntity {
                    caption = cap###', для которых есть прайс, но нет графика';
                    fill = 1;
                    type = SPLITH;
                    MOVE BOX(l) { caption = cap;}
                    MOVE BOX(st);
                }
            }
        }
    }
    @extendFormFilterStockAccess(st, scheduleOrderDashboard, filterStock);
END

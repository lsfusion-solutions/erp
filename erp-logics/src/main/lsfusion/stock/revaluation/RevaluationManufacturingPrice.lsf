MODULE RevaluationManufacturingPrice;

REQUIRE Revaluation, PurchaseManufacturingPrice;

NAMESPACE Revaluation;

//-- До
@defineDocumentInterfaceDetailPriceCustomPrefix(revaluationDetail, curManufacturing, ' изготовителя до');
//@deriveDocumentDetailPriceSystemLedgerPriceListTypeBatch(userRevaluation, manufacturingPriceStockPriceListType, curManufacturing, sku, stock);

curManufacturingPrice (UserRevaluationDetail detail)  <- round2(IF batch(detail)

                                                THEN prevPriceB(SystemLedgerPriceListType.manufacturingPriceStockPriceListType,
                                                                                               batch(detail),
                                                                                               stock(detail),
                                                                                               dateTime(detail))
                                                ELSE prevPriceB(SystemLedgerPriceListType.manufacturingPriceStockPriceListType,
                                                                                             sku(detail),
                                                                                             stock(detail),
                                                                                             dateTime(detail)))
                                                WHEN DO CHANGED(sku(detail)) OR
                                                     CHANGED(stock(detail)) OR
                                                     (CHANGED(dateTime(detail)) AND NOT isPosted(detail)) OR
                                                     CHANGED(batch(detail));
                                                         
@defineDocumentInterfaceDetailDataSumPrefix (revaluation, curManufacturing, ' изготовителя до');
@deriveDocumentDetailSumPrefix(userRevaluation, curManufacturing, currency, quantity);

//-- После
@defineDocumentInterfaceDetailPriceCustomPrefix(revaluationDetail, manufacturing, ' изготовителя после');
@defineDocumentInterfaceDetailDataSumPrefix (revaluation, manufacturing, ' изготовителя после');
@deriveDocumentDetailSumPrefix(userRevaluation, manufacturing, currency, quantity);

updatePriceManufacturing 'Изменять цену учетную на изготовителя' = DATA BOOLEAN (UserRevaluation) IN documentPrm;
EXTEND FORM userRevaluation PROPERTIES(p) updatePriceManufacturing;

updatePriceManufacturing(UserRevaluationDetail d) = updatePriceManufacturing(userRevaluation(d)); 
price(UserRevaluationDetail detail) <- manufacturingPrice(detail) 
    WHEN (CHANGED(manufacturingPrice(detail)) OR CHANGED(updatePriceManufacturing(detail))) AND updatePriceManufacturing(detail);

@defineOperationProperty(updatePriceManufacturing, 'Изменять цену учетную на изготовителя', showContainer);
@deriveDocumentOperationProperty(UserRevaluation, updatePriceManufacturing);

// -- Разница
@defineDocumentInterfaceDetailDiffSumPrefix (revaluation, manufacturingSum, , cur, ' изменения изготовителя');

@defineDocumentInterfaceHeaderSumPrefix (revaluation, diffManufacturing, ' изменения изготовителя');


EXTEND FORM userRevaluation
    PROPERTIES(p) BEFORE diffSumUserRevaluationDetail(p) diffManufacturingSumUserRevaluationDetail
    PROPERTIES(d) BEFORE curPrice(d) curManufacturingPrice, curManufacturingSum
    PROPERTIES(d) BEFORE price(d) manufacturingPrice, manufacturingSum
;
DESIGN userRevaluation {
    sumContainer{
        MOVE PROPERTY(diffManufacturingSumUserRevaluationDetail(p)) { charWidth = 13;}
    }    
}
EXTEND FORM revaluations
    PROPERTIES(p) READONLY BEFORE diffSumRevaluationDetail(p) diffManufacturingSumRevaluationDetail
    PROPERTIES(d) READONLY BEFORE curPrice(d) curManufacturingPrice, curManufacturingSum
    PROPERTIES(d) READONLY BEFORE price(d) manufacturingPrice, manufacturingSum
;

in (RevaluationDetail ledger, SystemLedgerPriceListType type) += WHEN manufacturingPrice(ledger) AND type == SystemLedgerPriceListType.manufacturingPriceStockPriceListType THEN TRUE;
price (RevaluationDetail ledger, SystemLedgerPriceListType type) += WHEN manufacturingPrice(ledger) AND type == SystemLedgerPriceListType.manufacturingPriceStockPriceListType THEN manufacturingPrice(ledger);


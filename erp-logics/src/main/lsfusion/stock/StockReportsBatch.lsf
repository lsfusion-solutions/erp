MODULE StockReportsBatch;

REQUIRE StockReports, PriceListLedgerAccount;

NAMESPACE StockReports;

accountSumB 'Сумма учетная' (Batch batch, DATETIME dateTime) =
    NUMERIC[32,9]((GROUP SUM accountSumB(batch, Stock st, dateTime) IF inBackSheet(st)));

//остатки по партиям
FORM printListBalanceBatchesStock 'Остатки по партиям'
    OBJECTS d = DATETIME PANEL SUBREPORT
    PROPERTIES(d) dateTime = VALUE

    PROPERTIES inBackSheetStocks()

    OBJECTS gt = GroupType PANEL
    PROPERTIES(gt) SELECTOR name
    PROPERTIES(gt) READONLY sessionConcatGroups
    PROPERTIES sumB(gt)

    OBJECTS           bt=Batch
    PROPERTIES(bt)    READONLY date, name, idBarcodeSku, nameSku, shortNameUOM//, shippedQuantityBatch
    FILTERS inSession(group(gt, sku(bt)))
    ORDERS          date(bt), name(bt), nameSku(bt)

    PROPERTIES(bt) READONLY balanceB
    PROPERTIES(bt, d) READONLY accountSumB
    FILTERS balanceB(bt)
;

printListBalanceBatches 'Списком' (DATETIME dateTime, GroupType gtype) 
    { PRINT printListBalanceBatchesStock OBJECTS d = dateTime, gt = gtype; }  IMAGE 'print.png' IN print;

accountSumBSkuBatch 'Учетная сумма на начало дня(по партиям)' (Group group, DATETIME dateTime) =
    NUMERIC[32,9]((GROUP SUM accountSumBSkuBatch(group, Stock st, dateTime) IF inBackSheet(st)));

FORM printBalanceBatchesStock 'Остатки по партиям'
    OBJECTS d = DATETIME PANEL SUBREPORT
    PROPERTIES(d) dateTime = VALUE

    PROPERTIES inBackSheetStocks()

    OBJECTS gt = GroupType PANEL
    PROPERTIES(gt) SELECTOR name
    PROPERTIES(gt) READONLY sessionConcatGroups
    PROPERTIES sumB(gt)

    OBJECTS sk = Group
    PROPERTIES READONLY canonicalName(sk)
    ORDERS canonicalName(sk)
    FILTERS inSession(sk) AND countSku(sk, gt)
    FILTERS groupType(sk) == gt
    PROPERTIES(sk, d) READONLY accountSumBSkuBatch
    FILTERGROUP inactive FILTER 'Активные' active(sk) 'F5' DEFAULT

    OBJECTS           bt=Batch
    PROPERTIES(bt)    READONLY date, name, idBarcodeSku, nameSku, shortNameUOM//, shippedQuantityBatch
    FILTERS           group(gt, sku(bt)) == sk
    ORDERS          date(bt), name(bt), nameSku(bt)

    PROPERTIES(bt) READONLY balanceB
    PROPERTIES(bt, d) READONLY accountSumB
    FILTERS balanceB(bt)
;

printBalanceBatches 'По группам' (DATETIME dateTime, GroupType gtype) 
    { PRINT printBalanceBatchesStock OBJECTS d = dateTime, gt = gtype; }  IMAGE 'print.png' IN print;

showAccountSumBatch 'Показывать учетные суммы по партиям в отчете по остаткам' = DATA BOOLEAN ();
EXTEND FORM options
    PROPERTIES() showAccountSumBatch
;
DESIGN options {
    stock1 {
        MOVE PROPERTY(showAccountSumBatch());
    }
}

accountSumRecBSkuBatch 'Учетная сумма (по партиям)' (Group group, DATETIME dateTime) =
    NUMERIC[32,9]((GROUP SUM accountSumRecBSkuBatch(group, Stock st, dateTime) IF inBackSheet(st)));
accountSumBatchB 'Сумма учетная по партиям' (Sku sku, DATETIME dateTime) =
    NUMERIC[32,9]((GROUP SUM accountSumBatchB(sku, Stock st, dateTime) IF inBackSheet(st)));

EXTEND FORM reportBalanceStock
    PROPERTIES(sk, d) READONLY accountSumRecBSkuBatch SHOWIF showAccountSumBatch()

    PROPERTIES(s, d) READONLY accountSumBatchB SHOWIF showAccountSumBatch()

    FILTERGROUP incorrectSum FILTER 'Неправильная сумма по партиям' abs(sumB(s) (-) accountSumBatchB(s, d)) > 0.0001 'F9'   

    PROPERTIES(bt, d) READONLY accountSumB
    PROPERTIES(d, gt) printListBalanceBatches, printBalanceBatches
;

DESIGN reportBalanceStock {
    print1 {
        MOVE PROPERTY(printListBalanceBatches(d,gt));
        MOVE PROPERTY(printBalanceBatches(d,gt));
    }
}
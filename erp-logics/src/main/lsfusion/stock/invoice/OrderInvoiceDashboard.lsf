MODULE OrderInvoiceDashboard;

REQUIRE Dashboard, OrderInvoice;

NAMESPACE Invoice;

excludeFilterInvoiced = ABSTRACT BOOLEAN (Order);
filterInvoiced = TRUE IF toInvoice(Order o) AND NOT excludeFilterInvoiced(o);  
background = ABSTRACT CASE COLOR (Order);

FORM orderInvoiceDashboard 'Приемка по заказам' AUTOREFRESH 60

    OBJECTS d = DATE PANEL
    PROPERTIES(d) date = VALUE
    
    OBJECTS o=Order GRID
    PROPERTIES (o) selected
    PROPERTIES (o) READONLY BACKGROUND background(o) isPosted, number, series, date, nameFrom, nameFromStock, nameTo, nameToStock, 
                   nameOperation, quantityOrderDetail, sumOrderDetail, VATSumOrderDetail, invoiceSumOrderDetail, note,
                   shipmentDate, shipmentTime, objectClassName    
    FILTERS in(o), isPosted(o)
    FILTERGROUP filters FILTER 'На дату' shipmentDate(o) <= d 'F5' DEFAULT
    FILTERGROUP filterInvoiced 
        FILTER 'Не оприходованы' filterInvoiced(o) 'F7' DEFAULT
        FILTER 'Оприходованы' NOT filterInvoiced(o) 'F6'
    
    OBJECTS od=OrderDetail GRID
    PROPERTIES (od) READONLY index, idBarcodeSku, nameSku, shortNameUOMSku,
                    packQuantity
    PROPERTIES (od) READONLY quantity, price, sum,
                   valueVAT, VATSum, invoiceSum,
                   nameSupplierStock, shipmentDate, shipmentTime, toInvoice  
    FILTERS order(od)==o
    ORDERS index(od)
    
    OBJECTS i=Invoice GRID
    PROPERTIES (i) READONLY isPosted, number, series, date, time,                                 
                   nameFrom, nameFromStock, nameTo, nameToStock,
                   nameCurrency, countInvoiceDetail, quantityInvoiceDetail, sumInvoiceDetail,
                   VATSumInvoiceDetail, invoiceSumInvoiceDetail

    PROPERTIES (i) NEWSESSION EDIT SHOWIF overShowEdit(i), deletei = DELETE SHOWIF overShowDelete(i)  
    FILTERS in(i)
    FILTERGROUP filters1 FILTER 'На дату' date(i) == d 'F5' DEFAULT
    
    PROPERTIES(o) createMultiUserInvoice  
;

DESIGN orderInvoiceDashboard {
    BOX {

        NEW dash BEFORE TOOLBARBOX{
            fill = 1;
            type = SPLITV;
            NEW header {
                fill = 1;                         
                MOVE BOX(o);

            }
            NEW pickings {
                fill = 1;
                type = TABBED;
                MOVE BOX(i) {
                    PANEL(i) {
                        type = CONTAINERH;
                    }
                }
                MOVE BOX(od) {
                    caption = 'Детализация заказа';
                }
            }
        }          
        NEW secondContainer BEFORE dash {
            type = CONTAINERH;
            caption = 'Шапка';
            MOVE PROPERTY(date);
        }
        PROPERTY(packQuantity(od)) { background = #D4FFD4; }
        PROPERTY(quantity(od)) { background = #D4FFD4; }
        PROPERTY(price(od)) { background = #D4FFD4; }
    }
}
 
@defineFilterIsOpened (order, orderInvoiceDashboard, o);
@defineFilterIsOpened (invoice, orderInvoiceDashboard, i);
@extendFormFilterAccessStockNoNull(Order, o, orderInvoiceDashboard, toStock, company);
@extendFormFilterAccessStockNoNull(Invoice, i, orderInvoiceDashboard, toStock, company);
@extendFormFilterRoleAccessNS(order, o, orderInvoiceDashboard, Operation); 
@extendFormFilterRoleAccessNS(invoice, i, orderInvoiceDashboard, Operation); 
NAVIGATOR {
    purchaseDashboardNavigator {
        NEW orderInvoiceDashboard;
    }
}



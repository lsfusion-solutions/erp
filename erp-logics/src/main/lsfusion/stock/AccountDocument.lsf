MODULE AccountDocument;

REQUIRE StockSkuDocument, SkuLedgerCost, Item;


CLASS AccountDocument 'Установка учетных цен';
TABLE accountDocument (AccountDocument);

@defineDocumentHeaderTime(AccountDocument);

toDate(AccountDocument a) = lastDayOfMonth(date(a)) MATERIALIZED;

@defineDocumentHeaderNote(AccountDocument);

@defineDocumentHeaderCreated(AccountDocument);
@defineExternalizable(accountDocument, STRING[100]);
WHEN LOCAL SET(AccountDocument o IS AccountDocument) INLINE DO {
    IF o IS AccountDocument AND NOT date(o) THEN date(o) <- firstDayOfMonth(currentDate());
    IF o IS AccountDocument AND NOT time(o) THEN time(o) <- 00:00;
};

@defineDocumentHeaderNumber(AccountDocument, BPSTRING[2]);
@defineNumeratedDefault(AccountDocument, 'Установка учетных Цен', 'ПП');

@defineDocumentHeaderPosted(AccountDocument);

@defineDocumentHeaderClosed(AccountDocument);
@defineDocumentHeaderDescription(AccountDocument, 'Установка учетных Цен');

@defineDocumentHeaderStock(accountDocument, stock, 'Склад', );
@defineDocumentDialogStocks(accountDocument, stock, company, , );

CONSTRAINT AccountDocument m IS AccountDocument AND NOT stock(m)
    MESSAGE 'Не задан склад для установки учетных цен';

accountPrice 'Цена (учетная)' = DATA NUMERIC[16,5](AccountDocument, Sku);

accountPriceB 'Цена'  =
    GROUP LAST accountPrice(AccountDocument a, Sku s)
    ORDER date(a), a
    WHERE date(a) < DATE date AND NOT toDate(a) < date
    BY s, stock(a), date COMPLEX CHARWIDTH 7;

accountPriceA 'Цена'  =
    GROUP LAST accountPrice(AccountDocument a, Sku s)
    ORDER date(a), a
    WHERE date(a) <= DATE date AND NOT toDate(a) < date
    BY s, stock(a), date COMPLEX CHARWIDTH 7;

costPrice 'Цена (себестоимость)' = DATA NUMERIC[16,5](AccountDocument, Sku);

costPriceB 'Цена'  =
    GROUP LAST costPrice(AccountDocument a, Sku s)
    ORDER date(a), a
    WHERE date(a) < DATE date AND NOT toDate(a) < date
    BY s, stock(a), date COMPLEX CHARWIDTH 7;

costPriceA 'Цена'  =
    GROUP LAST costPrice(AccountDocument a, Sku s)
    ORDER date(a), a
    WHERE date(a) <= DATE date AND NOT toDate(a) < date
    BY s, stock(a), date COMPLEX CHARWIDTH 7;


FORM accountDocument 'Установка учетных цен'
    OBJECTS m = AccountDocument PANEL

    PROPERTIES(m) isPosted, nameStock ON CHANGE changeStockCompany(m), nameNumerator, number, series, date, time,
        note

    OBJECTS d = Sku
    PROPERTIES(d) idBarcode, id, name, shortNameUOM
    PROPERTIES(m, d) accountPrice, costPrice
    FILTERGROUP detail
        FILTER 'В документе' accountPrice(m, d) 

    EVENTS
        ON OK prePost(m)

    EDIT AccountDocument OBJECT m
;

DESIGN accountDocument {
    OBJECTS {
        NEW mainTabbed {
            fill = 1;
            tabbed = TRUE;
            NEW documentBox {
                fill = 1;
                caption = 'Установка учетных цен';

                NEW headerBox {
                    horizontal = TRUE;

                    NEW headerRow1 {
                        fill = 1;
                        MOVE GROUP(documentHeader,m) {
                            lines = 1;
                            NEW doc1 {
                                horizontal = TRUE;
                                MOVE PROPERTY(isPosted(m)) { charWidth = 10; }
                                MOVE PROPERTY(nameStock(m)) { notNull = TRUE; };
                            }
                            NEW doc2 {
                                horizontal = TRUE;
                                MOVE PROPERTY(nameNumerator(m));
                                MOVE PROPERTY(number(m));
                                MOVE PROPERTY(series(m));
                                MOVE PROPERTY(date(m));
                                MOVE PROPERTY(time(m));
                            }
                        }

                        NEW headerTabbedPane {
                            alignment = STRETCH;
                            tabbed = TRUE;
                            NEW headerMainParams {
                                caption = 'Основные параметры';
                                MOVE GROUP(documentPrm,m) {
                                    lines = 3;
                                }
                            }
                            NEW headerExtraParams {
                                caption = 'Дополнительные параметры';
                            }
                            NEW headerCreateDetail {
                                caption = 'Основание';
                            }
                            NEW headerCreateDocuments {
                                caption = 'Производные документы';
                            }
                        }
                    }
                }
                NEW specificationBox {
                    fill = 1;
                    tabbed = TRUE;
                    MOVE BOX(d) {
                        caption = 'Спецификация';
                    }
                }
            }
        }
        PROPERTY(formOk()) {
            caption = 'Провести';
        }
    }
}

fixedCostSumCost (Sku sku, Stock stock, DATE dFrom, DATE dTo) =
    GROUP SUM signedQuantity(SkuLedger l) * fixedCostPrice(l) IF fixedCost(l) AND NOT fromStock(l) AND
        sku(l) = sku AND stock(l) = stock AND
        date(l) >= dFrom AND date(l) <= dTo;

recalculateCostGaussCost (DATE dFrom, DATE dTo) {
    NEWSESSION NESTED(recalculateCost[Sku], recalculateCost[Stock]) {
        calculatedCost(Sku sku, Stock stock) <- NULL;
        recalculateAllSkus() <- TRUE IF NOT (GROUP SUM 1 IF recalculateCost(Sku sku));
        recalculateAllStocks() <- TRUE IF NOT (GROUP SUM 1 IF recalculateCost(Stock stock));

        calcCostQuantity(Sku sku, Stock stock) <- balanceB(sku, stock, dFrom) IF (NOT positiveBalance() OR balanceB(sku, stock, dFrom) > 0) AND calcRecalculateCost(sku) AND calcRecalculateCost(stock);

        logToFile('cost', CONCAT '; ', 'Cost. Посчитаны начальные остатки по товарам', GROUP SUM 1 IF calcCostQuantity(Sku sku, Stock stock));

        calcCostSum (Sku sku, Stock stock) <- NUMERIC[18,4](calcCostQuantity(sku, stock) * costPriceB(sku, stock, dFrom)) IF calcCostQuantity(sku, stock) AND calcRecalculateCost(sku) AND calcRecalculateCost(stock);

        logToFile('cost', 'Cost. Посчитаны начальные суммы по товарам');

        calcCostQuantity (Sku sku, Stock stock) <- calcCostQuantity(sku, stock) (+) fixedCostQuantity(sku, stock, dFrom, dTo) WHERE fixedCostQuantity(sku, stock, dFrom, dTo) AND calcRecalculateCost(sku) AND calcRecalculateCost(stock);

        logToFile('cost', 'Cost. Посчитаны приходы количеств по товарам');

        calcCostSum (Sku sku, Stock stock) <- calcCostSum(sku, stock) (+) fixedCostSumCost(sku, stock, dFrom, dTo) WHERE fixedCostSumCost(sku, stock, dFrom, dTo) AND calcRecalculateCost(sku) AND calcRecalculateCost(stock);

        logToFile('cost', 'Cost. Посчитаны приходы сумм по товарам');

        calcCostIntra (Sku sku, Stock from, Stock to) <- NULL;
        calcCostIntra (Sku sku, Stock from, Stock to) <- intraCostQuantity(sku, from, to, dFrom, dTo) WHERE intraCostQuantity(sku, from, to, dFrom, dTo) AND calcRecalculateCost(sku) AND calcRecalculateCost(to) AND calcRecalculateCost(from);
        calcCostIntra (Sku sku, Stock from, Stock to) <- NULL WHERE calcCostIntra(sku, from, to) <= 0;

        WHILE TRUE DO {
            calcCostIntraTo(Sku sku, Stock to) <- GROUP SUM calcCostIntra(sku, Stock from, to);

            logToFile('cost', CONCAT '; ', 'Найдены склады/sku, на которые есть внутренние перемещения', GROUP SUM 1 IF calcCostIntraTo(Sku sku, Stock to));

            IF NOT (GROUP SUM 1 IF calcCostIntra(Sku sku, Stock from, Stock to) AND NOT calcCostIntraTo(sku, from)) THEN
                BREAK;

            calcCostSum (Sku sku, Stock to) <- (OVERRIDE calcCostSum(sku, to), 0.0) + NUMERIC[16,4](GROUP SUM (calcCostIntra (sku, Stock from, to) * calcCostSum(sku, from) / calcCostQuantity(sku, from)) IF NOT calcCostIntraTo(sku, from)) WHERE
            GROUP SUM (calcCostIntra (sku, Stock from, to) * calcCostSum(sku, from) / calcCostQuantity(sku, from)) IF NOT calcCostIntraTo(sku, from);
            calcCostQuantity (Sku sku, Stock to) <- (OVERRIDE calcCostQuantity(sku, to), 0.0) + (GROUP SUM calcCostIntra (sku, Stock from, to) IF NOT calcCostIntraTo(sku, from)) WHERE
            (GROUP SUM calcCostIntra (sku, Stock from, to) IF NOT calcCostIntraTo(sku, from));

            calcCostIntra(Sku sku, Stock from, Stock to) <- NULL WHERE calcCostIntra(sku, from, to) AND NOT calcCostIntraTo(sku, from);
        }


        calculatedCost(Sku sku, Stock stock) <- NUMERIC[16,4](calcCostSum(sku, stock) / calcCostQuantity(sku, stock)) WHERE calcCostQuantity(sku, stock) != 0.0;

        logToFile('cost', CONCAT '; ', 'Cost. Рассчитана себестоимость по товарам без циклов', GROUP SUM 1 IF calculatedCost(Sku sku, Stock stock));

        logToFile('cost', CONCAT '; ', 'Cost. Запуск алгоритма Гаусса', GROUP SUM 1 IF calcCostIntra(Sku sku, Stock from, Stock to));

        calculateCostGauss();

        IF calculatedErrors() != '' THEN {
            logToFile('cost', 'Не удалось посчитать себестоимость для sku (внутренний id) : \n' + calculatedErrors());
            MESSAGE 'Не удалось посчитать себестоимость для sku (внутренний id) : \n' + calculatedErrors();
        }

        logToFile('cost', 'Cost. Закончен подсчет по методу Гаусса');
    }
}


accountDocument = GROUP AGGR AccountDocument a BY stock(a), date(a);
 
calculateAndCreateDocument(DATE d, LegalEntity l ) {
    documentsClosedDate(Stock st) <- d;
    APPLY;

    //упр цена
    recalculateCost(Stock st) <- legalEntity(st) == l;
    recalculateCost(Sku s) <- active(s) AND NOT restricted(s);
    recalculateCostGauss(d, lastDayOfMonth(d));
    
    FOR [GROUP SUM 1 IF calculatedCost(Sku s, Stock st) BY st](Stock st) AND NOT accountDocument(st, d) NEW a = AccountDocument DO {
        stock(a) <- st;
        date(a) <- d;
    }
    
    FOR legalEntity(Stock st) == l AND AccountDocument a == accountDocument(st, d) DO {
        accountPrice(a, Sku s) <- NULL; 
    }
    FOR NUMERIC[16,5] p = calculatedCost(Sku s, Stock st) AND AccountDocument a == accountDocument(st, d) DO {
        accountPrice(a, s) <- p;
    }

    //упр цена 2
    recalculateCost(Stock st) <- legalEntity(st) == l;
    recalculateCost(Sku s) <- active(s) AND restricted(s);
    recalculateCostGauss(d, lastDayOfMonth(d));

    FOR [GROUP SUM 1 IF calculatedCost(Sku s, Stock st) BY st](Stock st) AND NOT accountDocument(st, d) NEW a = AccountDocument DO {
        stock(a) <- st;
        date(a) <- d;
    }

    FOR NUMERIC[16,5] p = calculatedCost(Sku s, Stock st) AND AccountDocument a == accountDocument(st, d) DO {
        accountPrice(a, s) <- p;
    }
    
    //себестоимость
    recalculateCost(Stock st) <- legalEntity(st) == l;
    recalculateCost(Sku s) <- active(s) AND NOT restricted(s);
    recalculateCostGaussCost(d, lastDayOfMonth(d));

    FOR [GROUP SUM 1 IF calculatedCost(Sku s, Stock st) BY st](Stock st) AND NOT accountDocument(st, d) NEW a = AccountDocument DO {
        stock(a) <- st;
        date(a) <- d;
    }

    FOR legalEntity(Stock st) == l AND AccountDocument a == accountDocument(st, d) DO {
        costPrice(a, Sku s) <- NULL;
    }
    FOR NUMERIC[16,5] p = calculatedCost(Sku s, Stock st) AND AccountDocument a == accountDocument(st, d) DO {
        costPrice(a, s) <- p;
    }
    
    //себестоимость 2
    recalculateCost(Stock st) <- legalEntity(st) == l;
    recalculateCost(Sku s) <- active(s) AND restricted(s);
    recalculateCostGaussCost(d, lastDayOfMonth(d));

    FOR [GROUP SUM 1 IF calculatedCost(Sku s, Stock st) BY st](Stock st) AND NOT accountDocument(st, d) NEW a = AccountDocument DO {
        stock(a) <- st;
        date(a) <- d;
    }

    FOR NUMERIC[16,5] p = calculatedCost(Sku s, Stock st) AND AccountDocument a == accountDocument(st, d) DO {
        costPrice(a, s) <- p;
    }
    //

    documentsClosedDate(Stock st) <- sum(lastDayOfMonth(d), 1);
    APPLY;
}

calculateAndCreateDocument 'Пересчитать учетные цены' (DATE d) {
    NEWSESSION {
        ASK 'Период закрыт. Выполнить пересчет документов в закрытом периоде?' IF documentsClosedDate() >= firstDayOfMonth(d) DO {
            documentsClosedDate() <- firstDayOfMonth(d);
            APPLY;

            FOR isCompany(LegalEntity l) DO {
                calculateAndCreateDocument(firstDayOfMonth(d),l);
            }

            documentsClosedDate() <- sum(lastDayOfMonth(d), 1);
            APPLY;
        }
    }
}

FORM accountDocuments 'Установка учетных цен'
    OBJECTS m = AccountDocument
    PROPERTIES(m) READONLYIF isReadonly() isClosed, isPosted, number, series, date, time, nameStock
    PROPERTIES(m) READONLY createdName = createdNameUser
    PROPERTIES(m) READONLYIF isReadonly() note

    PROPERTIES(m) READONLY PANEL createdNameUser, createdTime, createdHostnameComputer,
        postedNameUser, postedTime, postedHostnameComputer

    PROPERTIES(m) NEWSESSION NEW, editM = EDIT
    PROPERTIES(m) close SHOWIF isOpened(m), open SHOWIF isClosed(m)

    PROPERTIES(m) NEWSESSION deleted = DELETE

    OBJECTS d = Sku
    PROPERTIES(d) READONLY idBarcode, id, name, shortNameUOM
    PROPERTIES(m, d) READONLY accountPrice, costPrice

    FILTERS accountPrice(m, d)
    
    OBJECTS df = DATE PANEL 
    PROPERTIES (df) VALUE, calculateAndCreateDocument
;
@extendFormFilterAccessStock(AccountDocument, m, accountDocuments, stock, company);

DESIGN accountDocuments {
    OBJECTS {
        NEW filter {
            caption = 'Фильтры';
            horizontal = TRUE;
        }
        NEW documentContainer  {
            fill = 1;

            MOVE BOX(m) {
                fill = 2;
            }
            NEW documentDetail {
                fill = 1;
                tabbed = TRUE;

                MOVE BOX(d) {
                    caption = 'Спецификация';
                }
                NEW documentHistory {
                    caption = 'История';

                    MOVE GROUP(created,m);
                    MOVE GROUP(posted,m);
                }
                NEW printTab {
                    caption = 'Печатные формы';
                    NEW printContainer {
                        caption = 'Печать';
                    }
                }
                NEW actionContainer {
                    caption = 'Действия';
                    horizontal = TRUE;
                }
            }
        }
    }
}

@extendFormEditable(accountDocuments);

@defineFilterIsOpened (accountDocument, accountDocuments, m);
NAVIGATOR {
    stockCost {
        NEW accountDocuments;
    }
}
MODULE StockTax;

REQUIRE System, Stock, Sku, Tax, Country;

NAMESPACE Stock;

//------------------------- НДС -------------------------//

VAT 'НДС' = ABSTRACT Range (Sku, Country);
valueVAT 'НДС, %' (Sku sku, Country country, DATE date) = valueRate(VAT(sku, country), date);

valueVAT (Sku sku, Country country) = valueRate(VAT(sku, country)) MATERIALIZED;

VAT 'НДС' (Sku s, Stock st) = VAT(s, country(st));
valueVAT 'НДС, %' (Sku s, Stock st, DATE d) = valueVAT(s, country(st), d);

valueVAT 'НДС, %' (Sku s, Stock st) = valueVAT(s, country(st)); 

VAT 'НДС' (Sku s) = VAT(s, defaultCountry()) MATERIALIZED;

valueVAT 'НДС, %' (Sku s, DATE date) = valueRate(VAT(s), date);
valueVAT 'НДС, %' (Sku s) = valueVAT(s, defaultCountry()) MATERIALIZED;

META deriveDocumentDetailVAT (concrete, prefixP, dateProp,  skuProp, stockProp)
    prefixP##VAT(###concrete##Detail detail) <- VAT (skuProp(detail), country(stockProp(detail)))
        WHEN DO CHANGED(skuProp(detail)) OR CHANGED (stockProp(detail));
END
META deriveDocumentDetailVAT (concrete, prefixP, dateProp)
    @deriveDocumentDetailVAT (concrete, prefixP, dateProp,  sku, departmentStore);
END
META deriveDocumentDetailVAT (concrete, prefixP)
    @deriveDocumentDetailVAT (concrete, prefixP, date);
END

//НДС расчетный
META defineDocumentHeaderTax (object, taxSID, taxCaption, prefix)
    taxSID = DATA Range(###object);
    number##taxSID taxCaption (###object object) = number(taxSID(object)) IN documentPrm;
    value##taxSID taxCaption (###object object) = valueRate (taxSID(object),  date(object)) IN documentPrm;
END
META defineDocumentAbstractHeaderTax (object, taxSID, taxCaption, prefix)
    taxSID = ABSTRACT Range(###object);
    number##taxSID taxCaption (###object object) = number(taxSID(object)) IN documentPrm;
    value##taxSID taxCaption (###object object) = valueRate(taxSID(object), date(object)) IN documentPrm;
END
META defineDocumentInterfaceHeaderTAX (object, taxSID, taxCaption, prefix)
    @defineDocumentAbstractHeaderTax (object, taxSID, taxCaption, prefix);
    @defineDocumentHeaderTax (user###object, taxSID, taxCaption, prefix);
    taxSID (User###object object) += taxSID(object);
END

META defineDocumentHeaderTAX(object, taxSID, taxCaption)
    @defineDocumentHeaderTax(object, taxSID, taxCaption, );
END
META defineDocumentAbstractHeaderTAX(object, taxSID, taxCaption)
    @defineDocumentAbstractHeaderTax(object, taxSID, taxCaption, );
END
META defineDocumentInterfaceHeaderTAX(object, taxSID, taxCaption)
    @defineDocumentInterfaceHeaderTAX(object, taxSID, taxCaption, );
END
META deriveDocumentDetailVATHeader(object, taxSID, stockProp, skuProp, dateProp)
    taxSID###object###Detail (object###Detail) <-
        IF taxSID###object(object###object###Detail(object###Detail))
            THEN taxSID###object(object###object###Detail(object###Detail))
            ELSE VATSkuCountry (skuProp###object##Detail(object###detail), countryStock(stockProp###object##Detail(object###detail)))
        WHEN DO CHANGED(taxSID###object(object###object###Detail(object###Detail))) OR
             CHANGED(stockProp###object##Detail(object###detail)) OR
             CHANGED(skuProp###object##Detail(object###detail));
END
META deriveDocumentDetailVATHeader(object, taxSID, stockProp)
    @deriveDocumentDetailVATHeader(object, taxSID, stockProp, sku, date);
END

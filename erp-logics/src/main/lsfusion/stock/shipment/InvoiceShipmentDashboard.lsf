MODULE InvoiceShipmentDashboard;

REQUIRE Dashboard, InvoiceShipment;

NAMESPACE Shipment;

notCreateShipment (Invoice invoice) = invoice IS Invoice AND NOT createShipment(invoice);

showDeleteNotUser (Shipment shipment) = shipment IS Shipment AND NOT isUser(shipment) AND overShowDelete(shipment);

showDeleteUser(Shipment shipment) = isUser(shipment) AND overShowDelete(shipment);
  
FORM invoiceShipmentDashboard 'Приемка по накладным' AUTOREFRESH 60

    OBJECTS dt = DATE PANEL
    PROPERTIES(dt) date = VALUE
    
    OBJECTS i = Invoice
    PROPERTIES (i) READONLY statusShipped BACKGROUND backgroundShip(i)
    PROPERTIES (i) READONLY number, series, date, nameSupplier, nameSupplierStock, 
                            nameCustomer, nameCustomerStock, 
                            nameCurrency, countInvoiceDetail, quantityInvoiceDetail, sumInvoiceDetail,
                            VATSumInvoiceDetail, invoiceSumInvoiceDetail, note
    PROPERTIES (i) TOOLBAR createMultiShipment SHOWIF notShipped(i), addUserShipment SHOWIF notCreateShipment(i)
    ORDERS date(i)
    FILTERS isPosted(i),
            in(i)
        
    FILTERGROUP shipped 
        FILTER 'Не оприходованы' NOT isShipped(i) 'F11' DEFAULT     
        FILTER 'Оприходованы' isShipped(i) 'F10' 
        
    OBJECTS d = InvoiceDetail
    PROPERTIES (d) READONLY index, idBarcodeSku, nameSku, shortNameUOMSku
    PROPERTIES (d) READONLY quantity, price, sum,
                   valueVAT, VATSum, invoiceSum, toShip        
    FILTERS invoice(d)==i
        
    OBJECTS s = Shipment
    PROPERTIES (s) READONLY isPosted 
    PROPERTIES (s) READONLYIF isReadonly() number, series, date, nameSupplier,
                            nameSupplierStock, nameCustomer, nameCustomerStock 
    PROPERTIES (s) READONLY countShipmentDetail, quantityShipmentDetail, sumShipmentDetail, 
                   note, invoices 
    
    PROPERTIES (s) deleteMulti SHOWIF showDeleteNotUser(s) TOOLBAR
    PROPERTIES (s) NEWSESSION EDIT SHOWIF overShowEdit(s), deletes = DELETE SHOWIF showDeleteUser(s)   
    ORDERS date(s)
    
    FILTERS in(s)
    
    FILTERGROUP filters0 FILTER 'Накладные на дату' date(i)<=dt 'F5' DEFAULT
    FILTERGROUP filters1 FILTER 'Поставки на дату' date(s)==dt 'F5' DEFAULT
;    

DESIGN invoiceShipmentDashboard {
    BOX {
        OBJECTS {
            NEW dash {
                type = SPLITV;
                fill = 1;
                
                MOVE BOX(i) {PROPERTY(addUserShipment(i)){ caption = 'Принять частично';}}
                NEW details {
                    fill = 1;
                    type = TABBED;
                    MOVE BOX(s);
                    MOVE BOX(d) {
                        caption = 'Детализация';
                    }                    
                }
            }
            NEW date BEFORE dash {
                type = CONTAINERH;
                caption = 'Шапка';
                MOVE PROPERTY(date);
            }
        }
    }
}

@extendFormFilterAccessStock(Invoice, i, invoiceShipmentDashboard, customerStock, company);

@extendFormFilterAccessStock(Shipment, s, invoiceShipmentDashboard, customerStock, company);

@extendFormFilterRoleAccessNS(invoice, i, invoiceShipmentDashboard, Operation); 
@extendFormFilterRoleAccessNS(shipment, s, invoiceShipmentDashboard, Operation);
 
NAVIGATOR {
    purchaseDashboardNavigator {
        NEW invoiceShipmentDashboard;
    }
}    
MODULE StockManager;

REQUIRE Stock, LegalEntityManager;

NAMESPACE Stock;

manager = DATA Employee (LegalEntity, Stock);
nameManager 'Менеджер' (LegalEntity l, Stock s) = shortName(manager(l, s));
overManager(LegalEntity l, Stock s) = OVERRIDE manager(l, s), manager(l) IF s IS Stock;

inManager 'Отм' (Employee e, LegalEntity l, Stock s) = e ==  manager(l, s);

EXTEND FORM legalEntity 
    TREE stockTreeM bm=BPSTRING[3], sgm = StockGroup PARENT parent
    PROPERTIES READONLY VALUE(bm), sgmTreeName = name(sgm)
    ORDER sgmTreeName
    FILTERS stringEqualsAll(bm)

    OBJECTS stm=Stock
    PROPERTIES(stm) READONLY name
    PROPERTIES nameManager(l, stm)
    FILTERS isParent(sgm, stm) OR (stm IS Stock AND NOT sgm),
            isCompany(stm)
;

DESIGN legalEntity {
    managerContainer {
        NEW managerStock{
            type = SPLITH;
            fill = 1;
            MOVE BOX(TREE stockTreeM) {
                fill = 1;
                caption = 'Группа складов';
            }
            MOVE BOX(stm) {
                fill = 3;
            }
        }
    }
}

EXTEND FORM employee
    OBJECTS stm=Stock PANEL 
    PROPERTIES READONLY name(stm) SELECTOR
    FILTERS isCompany(stm)
    
    TREE legalEntityTreeMS lgms = LegalEntityGroup PARENT parent
    PROPERTIES READONLY  lgmsTreeName = name(lgms)
    ORDER lgmsTreeName

    OBJECTS lms = LegalEntity GRID
    PROPERTIES READONLY name(lms), nameManager(lms, stm)
    PROPERTIES inManager(e, lms, stm)
    ORDER name(lms)
    FILTERS isParent(lgms, lms) OR (lms IS LegalEntity AND NOT lgms)
;

DESIGN employee {
    managerTabContainer {
        NEW managerStockContainer {
            caption = 'По складу';
            fill = 1;
            MOVE BOX(stm);
            NEW managerStockSplit {
                fill = 1;
                type = SPLITH;
                MOVE BOX(TREE legalEntityTreeMS) {fill = 1;};
                MOVE BOX(lms) {fill = 3;};
            }
        }
    }
}
MODULE WBPriceTransaction;

REQUIRE WBItemArticleMapping, WBBalance;

NAMESPACE WB;

price 'Цена в ERP' = DATA INTEGER (Item);
discount 'Размер скидки в ERP, %' = DATA INTEGER (Item);
clubDiscount 'Скидка WB Клуба в ERP, %' = DATA INTEGER (Item);
priceWB 'Цена, загружена на WB' = DATA INTEGER (Item);
discountWB 'Размер скидки, загружена на WB, %' = DATA INTEGER (Item);
clubDiscountWB 'Скидка WB Клуба, загружена на WB, %' = DATA INTEGER (Item);
editableSizePrice 'Задавать цены для размеров' = DATA BOOLEAN (Item);
quarantine 'В карантине' = DATA BOOLEAN (Item);

WHEN LOCAL CHANGED(price(Item i)) AND price(i) <= 0.0 DO {
    MESSAGE 'Цена должна быть положительной' NOWAIT;
    price(i) <- PREV(price(i));
}

WHEN LOCAL CHANGED(discount(Item i)) AND discount(i) < 0.0 DO {
    MESSAGE 'Размер скидки не может быть отрицательным' NOWAIT;
    discount(i) <- PREV(discount(i));
}

WHEN LOCAL CHANGED(clubDiscount(Item i)) AND clubDiscount(i) < 0.0 DO {
    MESSAGE 'Размер скидки WB клуба не может быть отрицательным' NOWAIT;
    clubDiscount(i) <- PREV(clubDiscount(i));
}

price 'Цена в ERP' = DATA INTEGER (Size);
priceWB 'Цена, загружена на WB' = DATA INTEGER (Size);

EXTEND FORM item 
    PROPERTIES(i) price, discount, clubDiscount
    PROPERTIES(size) price
;

DESIGN item {
    itemHeader {
        MOVE PROPERTY(price(i));
        MOVE PROPERTY(discount(i));
        MOVE PROPERTY(clubDiscount(i));
    }
}

CLASS PriceTransaction 'Загрузка цен и скидок';
id 'ID' = DATA INTEGER (PriceTransaction);
priceTransaction (INTEGER id) = GROUP MAX PriceTransaction t BY id(t);
dateTime 'Дата/время' = DATA DATETIME (PriceTransaction);
user = DATA CustomUser (PriceTransaction);
nameUser 'Загружено пользователем' (PriceTransaction t) = shortName(user(t));

in = DATA BOOLEAN (PriceTransaction, Item);
price 'Цена товара' = DATA INTEGER (PriceTransaction, Item);
discount 'Размер скидки, %' = DATA INTEGER (PriceTransaction, Item);
clubDiscount 'Скидка WB Клуба, %' = DATA INTEGER (PriceTransaction, Item);

in = DATA BOOLEAN (PriceTransaction, Size);
price 'Цена размера' = DATA INTEGER (PriceTransaction, Size);

CLASS PriceTransactionType 'Тип загрузки' {
    itemPrice 'Установить цены и скидки для товаров',
    sizePrice 'Установить цены для размеров',
    wbDiscount 'Установить скидки WB Клуба'
}

type = DATA PriceTransactionType (PriceTransaction);
nameType 'Тип загрузки' (PriceTransaction t) = staticCaption(type(t));

CLASS PriceTransactionStatus 'Статус загрузки' {
    processing 'в обработке',
    processed 'обработана, в товарах нет ошибок, цены и скидки обновились',
    canceled 'отменена',
    partlyProcessed 'обработана, но в товарах есть ошибки; для товаров без ошибок цены и скидки обновились',
    error 'обработана, но во всех товарах есть ошибки'
}

status = DATA PriceTransactionStatus (PriceTransaction);
nameStatus 'Статус загрузки цен' (PriceTransaction t) = staticCaption(status(t));

CLASS PriceTransactionItemStatus 'Статус загрузки товара' {
    processing 'в обработке',
    success 'товар без ошибок, цена и/или скидка обновилась',
    error 'есть ошибки, данные не обновились'
}

status = DATA PriceTransactionItemStatus (PriceTransaction, Item);
nameStatus 'Статус загрузки товара' (PriceTransaction t, Item i) = staticCaption(status(t, i));
errorText 'Описание ошибки' = DATA STRING (PriceTransaction, Item);

lastPriceTransactionItem = 
    GROUP LAST PriceTransaction t IF in(t, Item i) AND type(t)==PriceTransactionType.itemPrice ORDER t BY i MATERIALIZED;
lastSuccessPriceTransactionItem = 
    GROUP LAST PriceTransaction t IF in(t, Item i) AND type(t)==PriceTransactionType.itemPrice AND status(t, i)==PriceTransactionItemStatus.success ORDER t BY i MATERIALIZED;

nameStatusLastPriceTransactionItem 'Статус последней загрузки цен и скидок для товаров' (Item i) = 
    nameStatus(lastPriceTransactionItem(i)) CHARWIDTH 10;

priceLastSuccessPriceTransactionItem 'Цена последней успешной загрузки' (Item i)= price(lastSuccessPriceTransactionItem(i), i);
discountLastSuccessPriceTransactionItem 'Скидка последней успешной загрузки' (Item i)= discount(lastSuccessPriceTransactionItem(i), i);

status = DATA PriceTransactionItemStatus (PriceTransaction, Size);
nameStatus 'Статус загрузки товара' (PriceTransaction t, Size i) = staticCaption(status(t, i));
errorText 'Описание ошибки' = DATA STRING (PriceTransaction, Size);

lastPriceTransactionSize = 
    GROUP LAST PriceTransaction t IF in(t, Size s) AND type(t)==PriceTransactionType.sizePrice ORDER t BY s MATERIALIZED;
lastSuccessPriceTransactionSize =
    GROUP LAST PriceTransaction t IF in(t, Size s) AND type(t)==PriceTransactionType.sizePrice AND status(t, s)==PriceTransactionItemStatus.success ORDER t BY s MATERIALIZED;

nameStatusLastPriceTransactionSize 'Статус последней загрузки цен для размеров' (Size s) = 
    nameStatus(lastPriceTransactionSize(s)) CHARWIDTH 10;

priceLastSuccessPriceTransactionSize 'Цена последней успешной загрузки' (Size s)= price(lastSuccessPriceTransactionSize(s), s);

lastPriceTransactionWBClub =
    GROUP LAST PriceTransaction t IF in(t, Item i) AND type(t)==PriceTransactionType.wbDiscount ORDER t BY i MATERIALIZED;
lastSuccessPriceTransactionWBClub =
    GROUP LAST PriceTransaction t IF in(t, Item i) AND type(t)==PriceTransactionType.wbDiscount AND status(t, i)==PriceTransactionItemStatus.success ORDER t BY i MATERIALIZED;

nameStatusLastPriceTransactionWBClub 'Статус последней загрузки скидок WB клуба' (Item i) =
    nameStatus(lastPriceTransactionWBClub(i)) CHARWIDTH 10;

clubDiscountLastSuccessPriceTransactionItem 'Скидка WB клуба последней успешной загрузки' (Item i)= clubDiscount(lastSuccessPriceTransactionWBClub(i), i);

overAllGoodsNumber 'Всего товаров' = DATA INTEGER (PriceTransaction);
successGoodsNumber 'Товаров без ошибок' = DATA INTEGER (PriceTransaction);

dataUpdatePricesDiscountsLimit = DATA INTEGER ();
updatePricesDiscountsLimit 'Лимит для установки цен и скидок' = OVERRIDE dataUpdatePricesDiscountsLimit(), 1000;

id = DATA LOCAL NUMERIC();
alreadyExists = DATA LOCAL STRING();

FORM priceTransaction
    PROPERTIES() errorText, error
    PROPERTIES() IN data id, alreadyExists
;

//карантин

sizeID = DATA LOCAL INTEGER(INTEGER);
newDiscount = DATA LOCAL NUMERIC(INTEGER);
techSizeName = DATA LOCAL STRING(INTEGER);
oldPrice = DATA LOCAL NUMERIC(INTEGER);
nmID = DATA LOCAL NUMERIC(INTEGER);
priceDiff = DATA LOCAL NUMERIC(INTEGER);
oldDiscount = DATA LOCAL NUMERIC(INTEGER);
currencyIsoCode4217 = DATA LOCAL STRING(INTEGER);
newPrice = DATA LOCAL NUMERIC(INTEGER);

FORM quarantine
    PROPERTIES() errorText, error

    OBJECTS data = INTEGER
    PROPERTIES(data) sizeID, newDiscount, techSizeName, oldPrice, nmID, priceDiff, id, oldDiscount, currencyIsoCode4217, newPrice
    FILTERS imported(data)
;

dataQuarantineLimit = DATA INTEGER ();
quarantineLimit 'Лимит по товарам в карантине' = OVERRIDE dataQuarantineLimit(), 1000;

quarantineItems 'Получить товары в карантине' () {
    NEWSESSION {
        setHttpHeadersIS();

        LOCAL message = STRING ();
        LOCAL response = FILE();

        logToFile('WB', 'GET: ' + url(CategoryRequest.content) + '/api/v2/quarantine/goods');

        LOCAL offset = INTEGER ();
        offset() <- 0;

        quarantine(Item i) <- NULL;
        LOCAL stop = BOOLEAN ();

        WHILE NOT stop() DO {
            TRY {
                EXTERNAL HTTP GET url(CategoryRequest.pricesDiscounts) + '/api/v2/quarantine/goods?limit=' + quarantineLimit() +'&offset=' + offset()
                    HEADERS httpHeaders TO response;
                fileToString(response());
                logToFile('WB', resultString());

                message() <- messageStatusHTTP(STRING(statusHttp()));
            } CATCH {
                logToFile('WB', messageCaughtException());
                message() <- CONCAT '\n', messageStatusHTTP(STRING(statusHttp())), messageCaughtException();
                stop() <- TRUE;
            } FINALLY {
                afterSendRequestIS();
                MESSAGE '<h3>Получить товары в карантине</h3>' NOWAIT;
                MESSAGE message() NOWAIT;

                IF statusHttp() == 200 THEN {
                    offset() <- offset() + quarantineLimit();
                    IMPORT quarantine JSON FROM response();

                    FOR nmID(INTEGER i) AND Item item == itemNM(nmID(i)) DO {
                        quarantine(item) <- TRUE;
                    } ELSE {
                        stop() <- TRUE;
                    }
                }

                APPLY NESTED LOCAL;
            }
        }
    }
}

dataItemPricesLimit = DATA INTEGER ();
itemPricesLimit 'Лимит по получению товаров с ценами' = OVERRIDE dataItemPricesLimit(), 100;

clubDiscount = DATA LOCAL NUMERIC(INTEGER);
listGoodsSizes = DATA LOCAL INTEGER(INTEGER);
discountedPrice = DATA LOCAL NUMERIC(INTEGER);
price = DATA LOCAL NUMERIC(INTEGER);
clubDiscountedPrice = DATA LOCAL NUMERIC(INTEGER);
editableSizePrice = DATA LOCAL STRING(INTEGER);
discount = DATA LOCAL NUMERIC(INTEGER);

FORM itemPrices
    PROPERTIES() errorText, error

    OBJECTS listGoods = INTEGER IN data
    PROPERTIES(listGoods) clubDiscount, editableSizePrice, nmID, discount, currencyIsoCode4217, vendorCode
    FILTERS imported(listGoods)

    OBJECTS sizes = INTEGER
    FILTERS listGoodsSizes(sizes) == listGoods
    PROPERTIES(sizes) sizeID, techSizeName, discountedPrice, price, clubDiscountedPrice
    FILTERS imported(sizes)
;

getItemPrices 'Получить цены товаров' () {
    NEWSESSION {
        setHttpHeadersIS();

        LOCAL message = STRING ();
        LOCAL response = FILE();

        logToFile('WB', 'GET: ' + url(CategoryRequest.content) + '/api/v2/list/goods/filter');

        LOCAL limit = INTEGER ();
        limit() <- itemPricesLimit();
        LOCAL offset = INTEGER ();
        offset() <- 0;

        LOCAL stop = BOOLEAN ();

        WHILE NOT stop() DO {
            TRY {
                EXTERNAL HTTP GET url(CategoryRequest.pricesDiscounts) + '/api/v2/list/goods/filter?limit=' + limit() +'&offset=' + offset()
                    HEADERS httpHeaders TO response;
                fileToString(response());
                logToFile('WB', resultString());

                message() <- messageStatusHTTP(STRING(statusHttp()));
            } CATCH {
                logToFile('WB', messageCaughtException());
                message() <- OVERRIDE messageStatusHTTP(STRING(statusHttp())), messageCaughtException();
                stop() <- TRUE;
            } FINALLY {
                afterSendRequestIS();
                MESSAGE '<h3>Получить цены товаров</h3>' NOWAIT;
                MESSAGE message() NOWAIT;

                IF statusHttp() == 200 THEN {
                    limit() <- limit() + itemPricesLimit();
                    offset() <- offset() + itemPricesLimit();
                    IMPORT itemPrices JSON FROM response();

                    FOR nmID(INTEGER i) AND Item item == itemNM(nmID(i)) DO {
                        priceWB(item) <- price(GROUP MAX INTEGER j IF listGoodsSizes(j) == i);
                        discountWB(item) <- discount(i);
                        clubDiscountWB(item) <- clubDiscount(i);
                        editableSizePrice(item) <- IF NOT editableSizePrice(i) == 'false' THEN TRUE;

                        FOR sizeID(INTEGER j) AND Size s == size(sizeID(j)) AND listGoodsSizes(j) == i DO {
                            priceWB(s) <- price(j);
                        }
                    } ELSE {
                        stop() <- TRUE;
                    }
                }

                APPLY NESTED LOCAL;
            }
        }
    }
}

getItemPrices 'Получить цены товара' (Item i) {
    NEWSESSION {
        setHttpHeadersIS();

        LOCAL message = STRING ();
        LOCAL response = FILE();

        logToFile('WB', 'GET: ' + url(CategoryRequest.content) + '/api/v2/list/goods/filter');

        TRY {
            EXTERNAL HTTP GET url(CategoryRequest.pricesDiscounts) + '/api/v2/list/goods/filter?limit=1000&offset=0&filterNmID=' + nmID(i)
                HEADERS httpHeaders TO response;
            fileToString(response());
            logToFile('WB', resultString());

            message() <- messageStatusHTTP(STRING(statusHttp()));
        } CATCH {
            logToFile('WB', messageCaughtException());
            message() <- CONCAT '\n', messageStatusHTTP(STRING(statusHttp())), messageCaughtException();
        } FINALLY {
            afterSendRequestIS();
            MESSAGE '<h3>Получить цены товара</h3>' NOWAIT;
            MESSAGE message() NOWAIT;

            IF statusHttp() == 200 THEN {
                IMPORT itemPrices JSON FROM response();

                FOR nmID(INTEGER k) == nmID(i) DO {
                    priceWB(i) <- price(GROUP MAX INTEGER j IF listGoodsSizes(j) == k);
                    discountWB(i) <- discount(k);
                    clubDiscountWB(i) <- clubDiscount(k);
                    editableSizePrice(i) <- IF NOT editableSizePrice(k) == 'false' THEN TRUE;

                    FOR sizeID(INTEGER j) AND Size s == size(sizeID(j)) AND listGoodsSizes(j) == k DO {
                        //price(s) <- price(j);
                        priceWB(s) <- price(j);
                    }
                }

                APPLY NESTED LOCAL;
            }
        }
    }
}

uploadPricesDiscounts 'Установить цены и скидки' () {
    NEWSESSION {
        setHttpHeadersIS();

        LOCAL message = STRING ();
        LOCAL response = FILE();
        LOCAL toUpload = BOOLEAN (Item);
        
        quarantineItems();
        getItemPrices();
        
        toUpload(Item i) <- TRUE WHERE nmID(i) AND (price(i) AND NOT price(i) == priceWB(i) OR 
            discount(i) AND NOT discount(i) == discountWB(i)) AND NOT isTrash(i) AND NOT quarantine(i) AND NOT editableSizePrice(i);

        IF (GROUP SUM 1 IF toUpload(Item item)) THEN {
            logToFile('WB', 'POST: ' + url(CategoryRequest.content) + '/api/v2/upload/task');

            LOCAL index = INTEGER (Item);
            index(Item i) <- PARTITION SUM 1 IF toUpload(i) ORDER i;

            LOCAL exported = INTEGER ();
            exported() <- 0;

            WHILE exported() < (GROUP MAX index(Item i)) DO {
                EXPORT JSON FROM 'nmID' = nmID(Item i), 'price' = price(i), 'discount' = discount(i) WHERE 
                    index(i) > exported() AND index(i) <= exported() + updatePricesDiscountsLimit();
                
                fileToString(exportFile());
                resultString() <- '\{"data":' + resultString() + '\}';
                stringToFile(resultString(), 'UTF-8', 'json');

                TRY {
                    EXTERNAL HTTP POST url(CategoryRequest.pricesDiscounts) + '/api/v2/upload/task' HEADERS httpHeaders PARAMS resultFile() TO response;
                    fileToString(response());
                    logToFile('WB', resultString());

                    message() <- messageStatusHTTP(STRING(statusHttp()));
                } CATCH {
                    logToFile('WB', messageCaughtException());
                    message() <- CONCAT '\n', messageStatusHTTP(STRING(statusHttp())), messageCaughtException();
                } FINALLY {
                    afterSendRequestIS();
                    MESSAGE '<h3>Установить цены и скидки</h3>' NOWAIT;
                    MESSAGE message() NOWAIT;
                    IF statusHttp() == 200 THEN {
                        IMPORT priceTransaction JSON FROM response();
                        NEW t = PriceTransaction {
                            id(t) <- id();
                            dateTime(t) <- currentDateTime();
                            user(t) <- currentUser();
                            status(t) <- PriceTransactionStatus.processing;
                            
                            in(t, Item i) <- TRUE WHERE index(i) > exported() AND index(i) <= exported() + updatePricesDiscountsLimit();
                            price(t, Item i) <- price(i) WHERE in(t, i);
                            discount(t, Item i) <- discount(i) WHERE in(t, i);
                            status(t, Item i) <- PriceTransactionItemStatus.processing WHERE in(t, i);

                            type(t) <- PriceTransactionType.itemPrice;
                        }

                        APPLY NESTED LOCAL;
                    }
                }

                exported() <- exported() + updatePricesDiscountsLimit();
            }
        } ELSE {
            MESSAGE 'Отсутствуют данные для выгрузки';
        }
    }
}

dataUpdatePricesSizesLimit = DATA INTEGER ();
updatePricesSizesLimit 'Лимит для установки цен для размеров' = OVERRIDE dataUpdatePricesSizesLimit(), 1000;

uploadPricesSizes 'Установить цены для размеров' () {
    NEWSESSION {
        setHttpHeadersIS();

        LOCAL message = STRING ();
        LOCAL response = FILE();
        LOCAL toUpload = BOOLEAN (Size);

        quarantineItems();
        getItemPrices();
        
        toUpload(Size s) <- TRUE WHERE nmID(itemWB(s)) AND (price(s) AND NOT price(s)==priceWB(s)) AND chrtID(s) AND NOT isTrash(itemWB(s)) AND NOT quarantine(itemWB(s)) AND editableSizePrice(itemWB(s));

        IF (GROUP SUM 1 IF toUpload(Size size)) THEN {
            logToFile('WB', 'POST: ' + url(CategoryRequest.content) + '/api/v2/upload/task/size');

            LOCAL index = INTEGER (Size);
            index(Size i) <- PARTITION SUM 1 IF toUpload(i) ORDER i;

            LOCAL exported = INTEGER ();
            exported() <- 0;

            WHILE exported() < (GROUP MAX index(Size i)) DO {
                EXPORT JSON FROM 'nmID' = nmID(itemWB(Size i)), 'sizeID' = chrtID(i), 'price' = price(i) WHERE
                    index(i) > exported() AND index(i) <= exported() + updatePricesSizesLimit();

                fileToString(exportFile());
                resultString() <- '\{"data":' + resultString() + '\}';
                stringToFile(resultString(), 'UTF-8', 'json');

                TRY {
                    EXTERNAL HTTP POST url(CategoryRequest.pricesDiscounts) + '/api/v2/upload/task/size' HEADERS httpHeaders PARAMS resultFile() TO response;
                    fileToString(response());
                    logToFile('WB', resultString());

                    message() <- messageStatusHTTP(STRING(statusHttp()));
                } CATCH {
                    logToFile('WB', messageCaughtException());
                    message() <- CONCAT '\n', messageStatusHTTP(STRING(statusHttp())), messageCaughtException();
                } FINALLY {
                    afterSendRequestIS();
                    MESSAGE '<h3>Установить цены для размеров</h3>' NOWAIT;
                    MESSAGE message() NOWAIT;
                    IF statusHttp() == 200 THEN {
                        IMPORT priceTransaction JSON FROM response();
                        NEW t = PriceTransaction {
                            id(t) <- id();
                            dateTime(t) <- currentDateTime();
                            user(t) <- currentUser();
                            status(t) <- PriceTransactionStatus.processing;

                            in(t, Size i) <- TRUE WHERE index(i) > exported() AND index(i) <= exported() + updatePricesSizesLimit();
                            price(t, Size i) <- price(i) WHERE in(t, i);
                            status(t, Size i) <- PriceTransactionItemStatus.processing WHERE in(t, i);

                            type(t) <- PriceTransactionType.sizePrice;
                        }

                        APPLY NESTED LOCAL;
                    }
                }

                exported() <- exported() + updatePricesSizesLimit();
            }
        } ELSE {
            MESSAGE 'Отсутствуют данные для выгрузки';
        }
    }
}

dataUpdateClubDiscountsLimit = DATA INTEGER ();
updateClubDiscountsLimit 'Лимит для установки скидок WB Клуба' = OVERRIDE dataUpdateClubDiscountsLimit(), 1000;

uploadClubDiscounts 'Установить скидки WB Клуба' () {
    NEWSESSION {
        setHttpHeadersIS();

        LOCAL message = STRING ();
        LOCAL response = FILE();
        LOCAL toUpload = BOOLEAN (Item);

        quarantineItems();
        getItemPrices();
        
        toUpload(Item i) <- TRUE WHERE nmID(i) AND (clubDiscount(i) AND NOT clubDiscount(i) == clubDiscountWB(i)) AND NOT isTrash(i) AND NOT quarantine(i);

        IF (GROUP SUM 1 IF toUpload(Item item)) THEN {
            logToFile('WB', 'POST: ' + url(CategoryRequest.content) + '/api/v2/upload/task/club-discount');

            LOCAL index = INTEGER (Item);
            index(Item i) <- PARTITION SUM 1 IF toUpload(i) ORDER i;

            LOCAL exported = INTEGER ();
            exported() <- 0;

            WHILE exported() < (GROUP MAX index(Item i)) DO {
                EXPORT JSON FROM 'nmID' = nmID(Item i), 'clubDiscount' = clubDiscount(i) WHERE
                    index(i) > exported() AND index(i) <= exported() + updateClubDiscountsLimit();

                fileToString(exportFile());
                resultString() <- '\{"data":' + resultString() + '\}';
                stringToFile(resultString(), 'UTF-8', 'json');

                TRY {
                    EXTERNAL HTTP POST url(CategoryRequest.pricesDiscounts) + '/api/v2/upload/task/club-discount' HEADERS httpHeaders PARAMS resultFile() TO response;
                    fileToString(response());
                    logToFile('WB', resultString());

                    message() <- messageStatusHTTP(STRING(statusHttp()));
                } CATCH {
                    logToFile('WB', messageCaughtException());
                    message() <- CONCAT '\n', messageStatusHTTP(STRING(statusHttp())), messageCaughtException();
                } FINALLY {
                    afterSendRequestIS();
                    MESSAGE '<h3>Установить скидки WB Клуба</h3>' NOWAIT;
                    MESSAGE message() NOWAIT;
                    IF statusHttp() == 200 THEN {
                        IMPORT priceTransaction JSON FROM response();
                        NEW t = PriceTransaction {
                            id(t) <- id();
                            dateTime(t) <- currentDateTime();
                            user(t) <- currentUser();
                            status(t) <- PriceTransactionStatus.processing;

                            in(t, Item i) <- TRUE WHERE index(i) > exported() AND index(i) <= exported() + updateClubDiscountsLimit();
                            clubDiscount(t, Item i) <- clubDiscount(i) WHERE in(t, i);
                            status(t, Item i) <- PriceTransactionItemStatus.processing WHERE in(t, i);

                            type(t) <- PriceTransactionType.wbDiscount;
                        }

                        APPLY NESTED LOCAL;
                    }
                }

                exported() <- exported() + updateClubDiscountsLimit();
            }
        } ELSE {
            MESSAGE 'Отсутствуют данные для выгрузки';
        }
    }
}

FORM sizePrices
    PROPERTIES() errorText, error

    OBJECTS listGoods = INTEGER IN data
    PROPERTIES(listGoods) sizeID, techSizeName, clubDiscount, discountedPrice, price, editableSizePrice, nmID, discount, 
        clubDiscountedPrice, currencyIsoCode4217, vendorCode
    FILTERS imported(listGoods)
;

getSizePrices 'Получить цены размеров' (Item i) {
    NEWSESSION {
        setHttpHeadersIS();

        LOCAL message = STRING ();
        LOCAL response = FILE();

        logToFile('WB', 'GET: ' + url(CategoryRequest.content) + '/api/v2/list/goods/size/nm');

        TRY {
            EXTERNAL HTTP GET url(CategoryRequest.pricesDiscounts) + '/api/v2/list/goods/size/nm?limit=1000&offset=0&nmID=' + nmID(i)
                HEADERS httpHeaders TO response;
            fileToString(response());
            logToFile('WB', resultString());

            message() <- messageStatusHTTP(STRING(statusHttp()));
        } CATCH {
            logToFile('WB', messageCaughtException());
            message() <- CONCAT '\n', messageStatusHTTP(STRING(statusHttp())), messageCaughtException();
        } FINALLY {
            afterSendRequestIS();
            MESSAGE '<h3>Получить цены размеров</h3>' NOWAIT;
            MESSAGE message() NOWAIT;

            IF statusHttp() == 200 THEN {
                IMPORT sizePrices JSON FROM response();

                priceWB(i) <- price(GROUP MAX INTEGER j IF nmID(j) == nmID(i));
                discountWB(i) <- discount(GROUP MAX INTEGER j IF nmID(j) == nmID(i));
                clubDiscountWB(i) <- clubDiscount(GROUP MAX INTEGER j IF nmID(j) == nmID(i));
                editableSizePrice(i) <- IF NOT editableSizePrice(GROUP MAX INTEGER j IF nmID(j) == nmID(i)) == 'false' THEN TRUE;

                FOR nmID(INTEGER k) == nmID(i) AND size(sizeID(k)) == Size s DO {
                    priceWB(s) <- price(k);
                }

                APPLY NESTED LOCAL;
            }
        }
    }
}


uploadID = DATA LOCAL INTEGER();
uploadDate = DATA LOCAL ZDATETIME();
successGoodsNumber = DATA LOCAL NUMERIC();
overAllGoodsNumber = DATA LOCAL NUMERIC();
activationDate = DATA LOCAL ZDATETIME();
status = DATA LOCAL INTEGER();

FORM processedStatus
    PROPERTIES() errorText, error
    PROPERTIES() IN data uploadID, uploadDate, successGoodsNumber, overAllGoodsNumber, activationDate, status
;

getProcessedStatus 'Состояние обработанной загрузки' (PriceTransaction t) {
    NEWSESSION {
        setHttpHeadersIS();

        LOCAL message = STRING ();
        LOCAL response = FILE();

        logToFile('WB', 'GET: ' + url(CategoryRequest.content) + '/api/v2/history/tasks');

        TRY {
            EXTERNAL HTTP GET url(CategoryRequest.pricesDiscounts) + '/api/v2/history/tasks?uploadID=' + id(t)
                HEADERS httpHeaders TO response;
            fileToString(response());
            logToFile('WB', resultString());

            message() <- messageStatusHTTP(STRING(statusHttp()));
        } CATCH {
            logToFile('WB', messageCaughtException());
            message() <- CONCAT '\n', messageStatusHTTP(STRING(statusHttp())), messageCaughtException();
        } FINALLY {
            afterSendRequestIS();
            MESSAGE '<h3>Состояние обработанной загрузки</h3>' NOWAIT;
            MESSAGE message() NOWAIT;

            IF statusHttp() == 200 THEN {
                IMPORT processedStatus JSON FROM response();
                IF uploadID() == id(t) THEN {
                    status(t) <- CASE
                                    WHEN status() == 3 THEN PriceTransactionStatus.processed
                                    WHEN status() == 4 THEN PriceTransactionStatus.canceled
                                    WHEN status() == 5 THEN PriceTransactionStatus.partlyProcessed
                                    WHEN status() == 6 THEN PriceTransactionStatus.error;
                    overAllGoodsNumber(t) <- overAllGoodsNumber();
                    successGoodsNumber(t) <- successGoodsNumber();
                }

                APPLY NESTED LOCAL;
            }
        }
    }
}

dataProcessedStatusSpecificationLimit = DATA INTEGER ();
processedStatusSpecificationLimit 'Лимит по детализации обработанной загрузки' = OVERRIDE dataProcessedStatusSpecificationLimit(), 100;

errorText1 = DATA LOCAL STRING(INTEGER);
status = DATA LOCAL INTEGER(INTEGER);

FORM processedStatusSpecification
    PROPERTIES() errorText, error
    PROPERTIES() IN data uploadID

    OBJECTS historyGoods = INTEGER IN data
    PROPERTIES(historyGoods) sizeID, errorText1 EXTID 'errorText', techSizeName, clubDiscount, price, nmID, discount, currencyIsoCode4217, vendorCode, status
    FILTERS imported(historyGoods)
;

processedStatusSpecification 'Детализация обработанной загрузки' (PriceTransaction t) {
    NEWSESSION {
        setHttpHeadersIS();

        LOCAL message = STRING ();
        LOCAL response = FILE();

        logToFile('WB', 'GET: ' + url(CategoryRequest.content) + '/api/v2/history/goods/task');

        LOCAL limit = INTEGER ();
        limit() <- processedStatusSpecificationLimit();
        LOCAL offset = INTEGER ();
        offset() <- 0;

        LOCAL stop = BOOLEAN ();

        WHILE NOT stop() DO {
            TRY {
                EXTERNAL HTTP GET url(CategoryRequest.pricesDiscounts) + '/api/v2/history/goods/task?limit=' + limit() + '&offset=' + offset() + '&uploadID=' + id(t)
                    HEADERS httpHeaders TO response;
                fileToString(response());
                logToFile('WB', resultString());

                message() <- messageStatusHTTP(STRING(statusHttp()));
            } CATCH {
                logToFile('WB', messageCaughtException());
                message() <- CONCAT '\n', messageStatusHTTP(STRING(statusHttp())), messageCaughtException();
                stop() <- TRUE;
            } FINALLY {
                afterSendRequestIS();
                MESSAGE '<h3>Детализация обработанной загрузки</h3>' NOWAIT;
                MESSAGE message() NOWAIT;

                IF statusHttp() == 200 THEN {
                    limit() <- limit() + processedStatusSpecificationLimit();
                    offset() <- offset() + processedStatusSpecificationLimit();
                    IMPORT processedStatusSpecification JSON FROM response();

                    IF uploadID() == id(t) THEN {
                        FOR nmID(INTEGER i) AND Item item == itemNM(nmID(i)) DO {
                            IF editableSizePrice(item) AND size(sizeID(i)) AND in(t, size(sizeID(i))) THEN {
                                status(t, size(sizeID(i))) <- CASE
                                    WHEN status(i) == 2 THEN PriceTransactionItemStatus.success
                                    WHEN status(i) == 3 THEN PriceTransactionItemStatus.error;
                                errorText(t, size(sizeID(i))) <- errorText1(i);
                            } ELSE IF in(t, item) THEN {
                                status(t, item) <- CASE 
                                    WHEN status(i) == 2 THEN PriceTransactionItemStatus.success
                                    WHEN status(i) == 3 THEN PriceTransactionItemStatus.error;
                                errorText(t, item) <- errorText1(i);
                            }
                        } ELSE {
                            stop() <- TRUE;
                        }
                    } ELSE {
                        stop() <- TRUE;
                    }

                    APPLY NESTED LOCAL;
                } ELSE {
                    stop() <- TRUE;
                }
            }
        }
    }
}

getProcessedStatus 'Получить данные необработанных загрузок цен и скидок' () {
    FOR PriceTransaction t IS PriceTransaction AND status(t)==PriceTransactionStatus.processing NOINLINE DO {
        getProcessedStatus(t);
        processedStatusSpecification(t);
    }
}

EXTEND FORM WBPricesBalances
    PROPERTIES() uploadPricesDiscounts, uploadPricesSizes, uploadClubDiscounts, quarantineItems, getItemPrices
    
    PROPERTIES(i) READONLY AFTER isTrash(i) quarantine, editableSizePrice
    PROPERTIES(i) READONLY price, priceWB, discount,  discountWB, clubDiscount, clubDiscountWB
    PROPERTIES getItemPrices(i) GRID, getSizePrices(i) SHOWIF editableSizePrice(i)
    
    PROPERTIES(i) READONLY priceLastSuccessPriceTransactionItem, discountLastSuccessPriceTransactionItem,
        clubDiscountLastSuccessPriceTransactionItem, nameStatusLastPriceTransactionItem, nameStatusLastPriceTransactionWBClub 
    PROPERTIES(s) READONLY price, priceWB, priceLastSuccessPriceTransactionSize, nameStatusLastPriceTransactionSize  

    OBJECTS t = PriceTransaction LAST
    PROPERTIES(t) READONLY id, dateTime, nameUser, nameStatus, overAllGoodsNumber, successGoodsNumber, nameType
    PROPERTIES(t) GRID getProcessedStatus, processedStatusSpecification 
    PROPERTIES getProcessedStatus() DRAW t TOOLBAR 
    FILTERGROUP processing
        FILTER 'Необработанные' status(t)==PriceTransactionStatus.processing DEFAULT 
    
    OBJECTS item = Item
    PROPERTIES READONLY id(item), nmID(item), name(item), price(t, item), discount(t, item), clubDiscount(t, item),
        nameStatus(t, item), errorText(t, item) PANEL
    FILTERS in(t, item)
    
    OBJECTS size = Size
    PROPERTIES READONLY chrtID(size), techSize(size), wbSize(size), price(t, size), nameStatus(t, size), 
        errorText(t, size) PANEL
    FILTERS in(t, size)
;

DESIGN WBPricesBalances {
    tabbed {
        actionRow2 {
            MOVE PROPERTY(uploadPricesDiscounts());
            MOVE PROPERTY(uploadPricesSizes());
            MOVE PROPERTY(uploadClubDiscounts());
            MOVE PROPERTY(quarantineItems());
            MOVE PROPERTY(getItemPrices());
        }
        NEW priceTransactions {
            fill = 1;
            caption = 'Загрузки цен и скидок';
            MOVE BOX(t) { fill = 2; }
            NEW details {
                fill = 1;
                horizontal = TRUE;
                NEW itemContainer{
                    fill=1;
                    MOVE BOX(item);
                    NEW errorItemContainer{
                        align = STRETCH;
                        caption = 'Текст ошибки';
                        MOVE PROPERTY(errorText(t, item)) {
                            caption = '';
                            align = STRETCH;
                            height = 50;
                            fontSize = 14;
                        }
                    }
                }
                NEW sizeContainer{
                    fill=1;
                    MOVE BOX(size);
                    NEW errorSizeContainer{
                        align = STRETCH;
                        caption = 'Текст ошибки';
                        MOVE PROPERTY(errorText(t, size)) {
                            caption = '';
                            align = STRETCH;
                            height = 50;
                            fontSize = 14;
                        }
                    }
                }
            }
        }
    }
}

EXTEND FORM integrationData
    PROPERTIES() updatePricesDiscountsLimit, updatePricesSizesLimit, updateClubDiscountsLimit, quarantineLimit, itemPricesLimit,
        processedStatusSpecificationLimit
;

DESIGN integrationData {
    requestLimits {
        MOVE PROPERTY(updatePricesDiscountsLimit());
        MOVE PROPERTY(updatePricesSizesLimit());
        MOVE PROPERTY(updateClubDiscountsLimit());
        MOVE PROPERTY(quarantineLimit());
        MOVE PROPERTY(itemPricesLimit());
        MOVE PROPERTY(processedStatusSpecificationLimit());
    }
}
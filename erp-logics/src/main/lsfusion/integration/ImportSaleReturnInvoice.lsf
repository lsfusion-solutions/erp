MODULE ImportSaleReturnInvoice;

REQUIRE System,
        ImportDocument,
        Item,
        SaleReturnInvoice;

NAMESPACE SaleReturn; 


original 'Файл оригинала накладной'  = DATA CUSTOMFILE (Invoice);

saveOriginal 'Загрузить файл оригинала накладной' (Invoice i)  { INPUT =original(i) CHANGE; }
openOriginal 'Просмотреть оригинал накладной' (Invoice i)  { open(original(i)); }

countDaysClearSaleReturnInvoiceCustomfiles 'За сколько дней хранить файлы импорта(накладная-продажа(возврат))' = DATA INTEGER (); 

EXTEND FORM options 
    PROPERTIES countDaysClearSaleReturnInvoiceCustomfiles();
    
DESIGN options { 
    log { 
        MOVE PROPERTY(countDaysClearSaleReturnInvoiceCustomfiles());
    }
}
        
clearApplicationLog () + { 
    NEWSESSION {
        IF countDaysClearSaleReturnInvoiceCustomfiles() THEN {
            original(SaleReturn.Invoice i) <- NULL IF date(i) <= subtract(currentDate(), countDaysClearSaleReturnInvoiceCustomfiles());
        }
        apply();
    } 
}

EXTEND FORM userInvoice
    PROPERTIES(i) saveOriginal, openOriginal 
;

DESIGN userInvoice{
    import {
        caption = 'Импорт';
        type = CONTAINERH;
        NEW origin {
            caption = 'Оригинал накладной';
            MOVE PROPERTY(saveOriginal(i));
            MOVE PROPERTY(openOriginal(i));
        }
    }
}





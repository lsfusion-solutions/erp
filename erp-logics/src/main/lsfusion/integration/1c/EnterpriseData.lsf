MODULE EnterpriseData;

REQUIRE Time, Utils, MasterData;

NAMESPACE EnterpriseData;

dateTimeToString = FORMULA STRING 'to_char($1, \'YYYY-MM-DD"T"HH24:MI:SS\')';
dateToString = FORMULA STRING 'to_char($1, \'YYYY-MM-DD\')';


idSended 'Номер переданного сообщения' = DATA INTEGER ();
isSended 'Сообщение передано' = DATA BOOLEAN ();
idTicket 'Получена квитанция' = DATA INTEGER ();

WHEN LOCAL CHANGED(idSended()) AND NOT idSended() > idTicket() DO idTicket() <- idSended();

idReceived 'Номер принятого сообщения' = DATA INTEGER ();
isReceived 'Сообщение принято' = DATA BOOLEAN ();

dateStartExchange 'Дата начала обмена' = DATA DATE ();
 
toBase 'Префикс базы-получателя' = DATA STRING[2] () CHARWIDTH 4;
fromBase 'Префикс базы-отправителя' = DATA STRING[2] () CHARWIDTH 4;

currentDateTimeString() = dateTimeToString(currentDateTime());
availableVersion () = '1.5';
format () = 'http://v8.1c.ru/edi/edi_stnd/EnterpriseData/1.5';
exchangePlan () = 'СинхронизацияДанныхЧерезУниверсальныйФормат';
messageNo () = OVERRIDE idSended(), 0;
receivedNo () = OVERRIDE idReceived(), 0;
prefix () = fromBase();

path 'Путь обмена' () = DATA STRING ();
sendPath () = path() + 'Message_' + fromBase() + '_' + toBase();
receivePath () = path() + 'Message_' + toBase() + '_' + fromBase();

GROUP header EXTID 'msg:Header';
GROUP confirmation EXTID 'msg:Confirmation' : header;
GROUP body EXTID '=http://v8.1c.ru/edi/edi_stnd/EnterpriseData/1.5:Body';

GROUP keyProperties EXTID 'КлючевыеСвойства';

GROUP product EXTID 'Товары';
GROUP service EXTID 'Услуги';
GROUP paymentCard EXTID 'ОплатаПлатежнымиКартами';
GROUP reason EXTID 'ДокументыОснования';
GROUP reasonDocument EXTID 'ДокументОснование';


FORM sendMessage FORMEXTID 'Message'
    PROPERTIES ATTR = 'http://www.1c.ru/SSL/Exchange/Message' EXTID 'xmlns:msg'
    PROPERTIES ATTR = 'http://www.w3.org/2001/XMLSchema' EXTID 'xmlns:xs'
    PROPERTIES ATTR = 'http://www.w3.org/2001/XMLSchema-instance' EXTID 'xmlns:xsi'
    
    PROPERTIES() IN header format EXTID 'msg:Format', currentDateTimeString EXTID 'msg:CreationDate'
    PROPERTIES() IN confirmation exchangePlan EXTID 'msg:ExchangePlan', toBase EXTID 'msg:To', fromBase EXTID 'msg:From', messageNo EXTID 'msg:MessageNo', receivedNo EXTID 'msg:ReceivedNo'
    PROPERTIES() IN header availableVersion EXTID 'msg:AvailableVersion', prefix EXTID 'msg:Prefix'
;

idReceivedTemp = DATA LOCAL INTEGER ();

FORM receiveMessage FORMEXTID 'Message'
    PROPERTIES() IN confirmation idReceivedTemp EXTID 'msg:MessageNo', idTicket EXTID 'msg:ReceivedNo'
;

// exchange properties
CLASS ABSTRACT EnterpriseData 'Универсальный обмен 1С';

uid 'UID' = DATA STRING[36] (EnterpriseData) INDEXED;
object 'Объект ЕД' (STRING[36] id) = GROUP AGGR EnterpriseData ed BY uid(ed);

skipUid = ABSTRACT BOOLEAN (EnterpriseData);
sendUid (EnterpriseData ed) = uid(ed) IF NOT skipUid(ed);

packet 'Пакет' = DATA INTEGER (EnterpriseData) INDEXED;

isDeleted 'Пометка удаления' = ABSTRACT BOOLEAN (EnterpriseData);
WHEN CHANGED (isDeleted(EnterpriseData ed)) AND ed IS EnterpriseData DO packet(ed) <- NULL;

WHEN SET (EnterpriseData ed IS EnterpriseData) AND NOT uid(ed) DO {
    generateUUID();
    uid(ed) <- generatedUUID();
}

overReceive ABSTRACT LIST ();
overImport ABSTRACT LIST ();

receiveMessage 'Получить' () {
    NEWSESSION {
        LOCAL f = XMLFILE ();
        TRY {
            READ receivePath() + '.xml' TO f;
            IMPORT receiveMessage XML FROM f();
            overImport();
            IF idReceivedTemp() AND NOT idReceivedTemp() <= idReceived() THEN {
                idReceived() <- idReceivedTemp();
                isReceived() <- TRUE;
                overReceive();
            } ELSE {
                isReceived() <- NULL;    
            }
            APPLY; 
        } CATCH {
            NEWSESSION APPLY { isReceived() <- NULL; }
        }
    }
}

overSend ABSTRACT LIST ();

sendMessage 'Передать' () {
    NEWSESSION {
        TRY {
            idSended() <- messageNo() (+) 1;
            LOCAL f = XMLFILE ();
            EXPORT sendMessage XML TO f;
            WRITE f() TO sendPath();
            isSended() <- TRUE;
            overSend();
            APPLY;
        } CATCH {
            MESSAGE System.messageCaughtException();
            NEWSESSION APPLY { isSended() <- NULL; }
            CANCEL;
        }
    }
}

EXTEND FORM integrationData
    PROPERTIES () idSended BACKGROUND (IF isSended() THEN RGB(225,255,225) ELSE RGB(255, 225, 225)),
                  idReceived BACKGROUND (IF isReceived() THEN RGB(225,255,225) ELSE RGB(255, 225, 225)),
                  dateStartExchange, toBase, fromBase, path,
                  sendMessage, receiveMessage
;

DESIGN integrationData {
    pane {
        NEW enterpriseData {
            caption = 'Обмен 1С';
            NEW status {
                type = CONTAINERH;
                MOVE PROPERTY (idSended());
                MOVE PROPERTY (idReceived());
            }
            NEW basic {
                type = CONTAINERH;      
                MOVE PROPERTY (dateStartExchange());
                MOVE PROPERTY (toBase());
                MOVE PROPERTY (fromBase());
            }
            MOVE PROPERTY (path());
            NEW buttons {
                type = CONTAINERH;
                MOVE PROPERTY (sendMessage());
                MOVE PROPERTY (receiveMessage());
            }
        }
    }
}

GROUP link EXTID 'СсылкаНаОбъект';
GROUP linkInLink EXTID 'СсылкаНаОбъект' : link;

META defProp(name, caption, type, class)
    name caption = ABSTRACT type (###class);
    WHEN CHANGED(name(###class o)) DO packet(o) <- NULL;
END

META defPropDetail(name, caption, type, class)
    name caption = ABSTRACT type (###class##Detail);
    WHEN CHANGED(name(###class##Detail d)) DO packet(class(d)) <- NULL;
END

META defPropObject(name, caption, type, class)
    @defProp(name, caption, type, ###class);
    @def###type(name, class);
END

META defPropObject(name, caption, type, class, table)
    @defPropDetail(name, caption, type, class);
    @def###type(name, class, table);
END

META defPropId(name, caption, type, class)
    @defProp(name, caption, type, ###class);
    id###name###class caption (###class class) = staticCaption(name(class));
    EXTEND FORM sendMessage
        PROPERTIES (class) id###name###class EXTID caption
    ;
END

META defPropId(name, caption, type, class, table)
    @defPropDetail(name, caption, type, class);
    id###name###class###table caption (###class###table class###table) = staticCaption(name(class###table));
    EXTEND FORM sendMessage
        PROPERTIES (class###table) id###name###class###table EXTID caption
    ;
END

META defPropSimple(name, caption, type, class)
    @defProp(name, caption, type, ###class);
    EXTEND FORM sendMessage
        PROPERTIES (class) name EXTID caption
    ;
END

META defPropSimple(name, caption, type, class, table)
    @defPropDetail(name, caption, type, class);
    EXTEND FORM sendMessage
        PROPERTIES (class###table) name EXTID caption
    ;
END

META defPropBoolean(name, caption, class);
    @defProp(name, caption, BOOLEAN, ###class);
    ##name##BooleanString (###class class) = IF name(class) THEN 'true' ELSE 'false';
    EXTEND FORM sendMessage
        PROPERTIES (class) ##name##BooleanString EXTID caption
    ;
END

META extKeyProperties (nm, type, caption, group, obj, detail)
    EXTEND FORM sendMessage
        OBJECTS nm###obj###detail = ###type EXTID caption IN group
        PROPERTIES(nm###obj###detail) sendUid EXTID 'Ссылка', dateTimeString EXTID 'Дата', id EXTID 'Номер'
        FILTERS nm###obj###detail = nm(obj###detail)
    
        OBJECTS company###obj###detail = Company EXTID 'Организация'
        PROPERTIES(company###obj###detail) sendUid EXTID 'Ссылка', name EXTID 'Наименование', fullName EXTID 'НаименованиеПолное', inn EXTID 'ИНН', kpp EXTID 'КПП', idType EXTID 'ЮридическоеФизическоеЛицо'
        FILTERS company###obj###detail = company(nm###obj###detail)
    ;   
END

META extKeyProperties (type, caption, group, obj, detail)
    @extKeyProperties (type, ###type, caption, group, obj, detail);
END

META extDeleteObject(objtype, caption)
    EXTEND FORM sendMessage
        OBJECTS objtype##Deleted = ###objtype EXTID 'УдалениеОбъекта' IN body
        PROPERTIES(objtype##Deleted) IN link IN linkInLink sendUid EXTID caption
        FILTERS sendUid(objtype##Deleted), EnterpriseData.isDeleted(objtype##Deleted)
        
        FILTERS NOT DATE(dateTime(objtype##Deleted)) < dateStartExchange(), NOT packet(objtype##Deleted) <= idTicket()
    ;
END

META extTable(class, group)
    CLASS ABSTRACT ###class###group : ###class##Detail;
    
    is###group = ABSTRACT BOOLEAN (###class###group);
    
    EXTEND FORM sendMessage  
        OBJECTS class###group = ###class###group EXTID 'Строка' IN group
        FILTERS in(class, class###group), is###group(class###group)
    ;
END

META extDocument(doctype, caption)
    CLASS ABSTRACT ###doctype 'Документ.'##caption : Document;
    
    CLASS ABSTRACT ###doctype##Detail 'Строка';
    
    ##doctype = ABSTRACT ###doctype (###doctype##Detail);
    in (###doctype ##doctype, ###doctype##Detail ##doctype##Detail) = ##doctype(##doctype##Detail) == ##doctype; 
    
    @defProp(company, 'Организация', Company, ###doctype);
    
    EXTEND FORM sendMessage
        OBJECTS doctype = ###doctype EXTID 'Документ.'##caption IN body
        PROPERTIES(doctype) IN keyProperties sendUid EXTID 'Ссылка', dateTimeString EXTID 'Дата', id EXTID 'Номер'
        FILTERS dateTimeString(doctype), id(doctype), NOT EnterpriseData.isDeleted(doctype)
       
        OBJECTS company###doctype = Company EXTID 'Организация' IN keyProperties
        PROPERTIES(company###doctype) sendUid EXTID 'Ссылка', name EXTID 'Наименование', fullName EXTID 'НаименованиеПолное', inn EXTID 'ИНН', kpp EXTID 'КПП', idType EXTID 'ЮридическоеФизическоеЛицо'
        FILTERS company###doctype = company(doctype)
        
        FILTERS NOT DATE(dateTime(doctype)) < dateStartExchange(), NOT packet(doctype) <= idTicket()
    ;
    
    overSend () + { 
        FOR ###doctype doctype IS ###doctype AND NOT packet(doctype) AND NOT DATE(dateTime(doctype)) < dateStartExchange() DO {
            packet(doctype) <- messageNo();
        }
}

    @extDeleteObject(doctype, caption##'Ссылка');
END

// static types
META defStaticType (id, caption)
    @defineStaticObject(###id, caption);
    id (STRING s) = GROUP AGGR ###id id BY name(id);
END

META defStaticTypeValue (class, id, caption)
    EXTEND CLASS ###class { id caption };
    is###id###class (###class class) = class = ###class.id;
    is###id###class (STRING s) = is###id###class(class(s));
END

@defStaticType (typePartner, 'ЮридическоеФизическоеЛицо');
@defStaticTypeValue (typePartner, legalEntity, 'ЮридическоеЛицо');
@defStaticTypeValue (typePartner, individual, 'ФизическоеЛицо');

@defStaticType(typeStock, 'ВидыСкладов');
@defStaticTypeValue(typeStock, nonAutomatedShop, 'НеавтоматизированнаяТорговаяТочка');
@defStaticTypeValue(typeStock, warehouse, 'Оптовый');
@defStaticTypeValue(typeStock, retail, 'Розничный');

@defStaticType(typeContract, 'ВидыДоговоров');
@defStaticTypeValue(typeContract, other, 'Прочее');
@defStaticTypeValue(typeContract, agent, 'СКомиссионером');
@defStaticTypeValue(typeContract, purchaseAgent, 'СКомиссионеромНаЗакупку');
@defStaticTypeValue(typeContract, consignor, 'СКомитентом');
@defStaticTypeValue(typeContract, purchaseConsignor, 'СКомитентомНаЗакупку');
@defStaticTypeValue(typeContract, customer, 'СПокупателем');
@defStaticTypeValue(typeContract, supplier, 'СПоставщиком');

@defStaticType(typePurchaseInvoice, 'ВидыОперацийПоступленияТоваров');
@defStaticTypeValue(typePurchaseInvoice, import, 'ЗакупкаПоИмпорту');
@defStaticTypeValue(typePurchaseInvoice, delivery, 'ПокупкаУПоставщика');
@defStaticTypeValue(typePurchaseInvoice, comission, 'ПриемНаКомиссию');

@defStaticType(typeSaleInvoice, 'ВидыОперацийРеализацияТоваровУслуг');
@defStaticTypeValue(typeSaleInvoice, sale, 'РеализацияКлиенту');
@defStaticTypeValue(typeSaleInvoice, comission, 'ПередачаНаКомиссию');
@defStaticTypeValue(typeSaleInvoice, shipmentTransit, 'ОтгрузкаБезПереходаПравСобственности_ВПути');
@defStaticTypeValue(typeSaleInvoice, shipmentSale, 'ОтгрузкаБезПереходаПравСобственности_Реализовано');

@defStaticType(typeInventory, 'ТипЗапасов');

@defStaticTypeValue(typeInventory, revolvingPackage, 'ОборотнаяТара');
@defStaticTypeValue(typeInventory, finishedProduct, 'ГотоваяПродукция');
@defStaticTypeValue(typeInventory, contractorMaterial, 'ДавальческиеСырьеИМатериалы');
@defStaticTypeValue(typeInventory, sparePart, 'ЗапасныеЧасти');
@defStaticTypeValue(typeInventory, comissionItem, 'КомиссионныеТовары');
@defStaticTypeValue(typeInventory, purchaseItem, 'ПокупныеИзделия');
@defStaticTypeValue(typeInventory, purchaseSemifinishedProduct, 'ПокупныеПолуфабрикатыИКомплектующие');
@defStaticTypeValue(typeInventory, goods, 'СобственныеТовары');
@defStaticTypeValue(typeInventory, uniform, 'СпецоснасткаИСпецодеждаНаСкладе');
@defStaticTypeValue(typeInventory, buildingMaterial, 'СтроительныеМатериалы');
@defStaticTypeValue(typeInventory, material, 'СырьеИМатериалы');
@defStaticTypeValue(typeInventory, fuel, 'Топливо');

@defStaticType(typePurchaseReturnInvoice, 'ВидыОперацийВозвратПоставщику');
@defStaticTypeValue(typePurchaseReturnInvoice, delivery, 'ВозвратПоставщику');
@defStaticTypeValue(typePurchaseReturnInvoice, comission, 'ВозвратКомитенту');

@defStaticType(typeSaleReturnInvoice, 'ВидыОперацийВозвратОтПокупателя');
@defStaticTypeValue(typeSaleReturnInvoice, customer, 'ВозвратОтКлиента');
@defStaticTypeValue(typeSaleReturnInvoice, comission, 'ВозвратОтКомиссионера');
@defStaticTypeValue(typeSaleReturnInvoice, retailCustomer, 'ВозвратОтРозничногоПокупателя');

@defStaticType(typeOutcomeCashOrderBank, );
@defStaticTypeValue(typeOutcomeCashOrderBank, collection, 'Инкассация');
@defStaticTypeValue(typeOutcomeCashOrderBank, cash, 'ВзносНаличными');

@defStaticType(typeVat, );
@defStaticTypeValue(typeVat, vat4, 'НДС4');
@defStaticTypeValue(typeVat, vat4d104, 'НДС4_104');
@defStaticTypeValue(typeVat, vat18, 'НДС18');
@defStaticTypeValue(typeVat, vat18d118, 'НДС18_118');
@defStaticTypeValue(typeVat, vat2, 'НДС2');
@defStaticTypeValue(typeVat, vat2d102, 'НДС2_102');
@defStaticTypeValue(typeVat, vat10, 'НДС10');
@defStaticTypeValue(typeVat, vat10d110, 'НДС10_110');
@defStaticTypeValue(typeVat, vat0, 'НДС0');
@defStaticTypeValue(typeVat, vat20, 'НДС20');
@defStaticTypeValue(typeVat, vat20d120, 'НДС20_120');
@defStaticTypeValue(typeVat, withoutVat, 'БезНДС');

@defStaticType(typeVatInvoice, 'ВидыСчетаФактуры');
@defStaticTypeValue(typeVatInvoice, sale, 'НаРеализацию');
@defStaticTypeValue(typeVatInvoice, adjustment, 'Корректировочный');
@defStaticTypeValue(typeVatInvoice, advance, 'НаАванс');
@defStaticTypeValue(typeVatInvoice, advanceComission, 'НаАвансКомитента');
@defStaticTypeValue(typeVatInvoice, amountDifference, 'СуммовыеРазницы');
@defStaticTypeValue(typeVatInvoice, taxOfficial, 'НалоговыйАгент');
@defStaticTypeValue(typeVatInvoice, purchase, 'НаПоступление');

@defStaticType(typeInvoiced, 'СпособыВыставленияСчетаФактуры');
@defStaticTypeValue(typeInvoiced, eDoc, 'ВЭлектронномВиде');
@defStaticTypeValue(typeInvoiced, paper, 'НаБумажномНосителе');
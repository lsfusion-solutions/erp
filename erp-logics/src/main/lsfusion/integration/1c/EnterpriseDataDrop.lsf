MODULE EnterpriseDataDrop;

REQUIRE EnterpriseData;

NAMESPACE EnterpriseData;

CLASS EnterpriseDataDrop 'Универсальный обмен 1С (drop)' : EnterpriseData;
TABLE enterpriseDataDrop (EnterpriseDataDrop);

desc 'Описание' = DATA STRING ( EnterpriseDataDrop);
desc(EnterpriseDataDrop e,Configuration c) += desc(e) IF c IS Configuration;
createdTime '{historizable.created.time}'= DATA DATETIME (EnterpriseDataDrop);
hostnameComputer 'Имя компьютера' = DATA ISTRING[100] (EnterpriseDataDrop);
userLogin 'Логин пользователя' = DATA STRING[100] (EnterpriseDataDrop);
nameContact 'Имя пользователя' =  DATA STRING[100] (EnterpriseDataDrop) IN id;

isEnterpriseData (EnterpriseData e) = e IS EnterpriseData AND NOT e IS EnterpriseDataDrop;
objectExchangeDrop = DATA ObjectExchange(EnterpriseDataDrop);
objectExchange(EnterpriseDataDrop e) +=  objectExchangeDrop(e) IF e IS EnterpriseDataDrop;
isDeleted(EnterpriseDataDrop e) +=  TRUE IF e IS EnterpriseDataDrop;

inSending (Configuration c, EnterpriseDataDrop drop) += inSending(objectExchange(drop), c) IF drop IS EnterpriseDataDrop  AND NOT skip(c, drop);

WHEN DROPPED(EnterpriseData ed AS EnterpriseData)  AND PREV(isEnterpriseData(ed)) AND PREV(uid(ed)) DO {
    NEW d = EnterpriseDataDrop {
        uid(d) <- PREV(uid(ed));
        desc(d,Configuration c) <- CONCAT ' ', 'УдалениеОбъекта:', name(PREV(objectExchange(ed)));
        objectExchangeDrop(d) <- PREV(objectExchange(ed));
        createdTime(d) <- currentDateTime();
        hostnameComputer(d) <- hostnameCurrentComputer();
        userLogin(d) <- login(currentUser());
        nameContact(d) <- STRING[100](name(currentUser()));
    }
}

nameLink(EnterpriseDataDrop d) = getWord(staticCaption(objectExchangeDrop(d)), '.',2)+'Ссылка';
sendUidConcat(EnterpriseDataDrop d, Configuration c) = '<'+nameLink(d)+' xmlns="http://v8.1c.ru/edi/edi_stnd/EnterpriseData/'+(nameVersion(c))+'">'+ sendUid(d,c)+ '</'+nameLink(d)+'>';

META extDeleteObjectExtra( ver)
    EXTEND FORM sendMessage
        OBJECTS obj##DeletedExtra = EnterpriseDataDrop EXTID 'УдалениеОбъекта' IN bodySend###ver
        PROPERTIES(obj##DeletedExtra, configuration) IN link IN linkInLink sendUidConcat EXTID 'value'
        FILTERS sendUid(obj##DeletedExtra, configuration), 
                EnterpriseData.isDeleted(obj##DeletedExtra)

        FILTERS inLocalSending(configuration, obj##DeletedExtra), NOT skip(configuration, obj##DeletedExtra)
    ;
END




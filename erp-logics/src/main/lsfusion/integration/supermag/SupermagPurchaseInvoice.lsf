MODULE SupermagPurchaseInvoice;

REQUIRE SupermagSettings, SupermagExportForms, PurchaseManufacturingPrice, PurchaseInvoiceConsignmentBy, PurchaseInvoice,
        PricingPurchase, PricingSumPurchase, PurchaseCreditNote, PurchaseInvoiceContract, PriceListOver3, Utils, SupermagPricing, PricingPurchaseInvoiceDashboard, Range, PurchaseInvoiceExtraPack;

NAMESPACE Purchase;

@settingOperationSupermag(Purchase);

exportPurchaseInvoice ABSTRACT LIST (Purchase.Invoice);

EXTEND FORM Purchase.invoices
    PROPERTIES (i) exportPurchaseInvoice TOOLBAR 
    PROPERTIES (i) id SHOWIF showIDs()
;

DESIGN Purchase.invoices {
    actionContainer{
        NEW mag {
            caption = 'Супермаг';
            MOVE PROPERTY (exportPurchaseInvoice(i));
        }        
    }
}

userInvoiceApply 'Сохранить ' (UserInvoice doc) {
    apply();
    IF NOT canceled() THEN {
        IF series(doc) THEN { number(doc) <- seriesNumber(doc); series(doc) <- NULL;}
        id(doc) <- CONCAT '', STRING[10](getWord(docTypeSupermag(operation(doc)),';',1)), seriesNumber(doc); 
        dataIndex(InvoiceDetail docd) <- index(docd) WHERE invoice(docd) = doc; 
        id(InvoiceDetail docd) <- (CONCAT '~',id(doc),index(docd)) WHERE invoice(docd) = doc;      
        APPLY;
        exportPurchaseInvoice(doc);
    }
}

EXTEND FORM userInvoice  PROPERTIES userInvoiceApply(i) TOOLBAR;

DESIGN userInvoice {
    TOOLBARRIGHT {
        MOVE PROPERTY (userInvoiceApply(i)) BEFORE PROPERTY (formClose()) { imagePath = 'apply.png';} 
        PROPERTY (formOk()) { hide = TRUE; }
        PROPERTY (formApply()) { hide = TRUE; }
    }
}

//-- предполагаем, что дата оплаты для накладной только одна
lastInvoiceContractLedger 'Дата оплаты' = GROUP LAST Purchase.InvoiceContractLedger l ORDER date(l),l BY inContractLedger(l);// MATERIALIZED;
lastPaymentPeriod = GROUP MAX PaymentPeriod p BY paymentCondition(p);                            

@settingIntegration(Purchase.Invoice, Purchase.InvoiceDetail);

dateTimeChange[StockDocumentLedger](InvoiceShipment doc) += dateTimeChange(invoice(doc));
bornin[StockDocumentLedger](InvoiceShipment doc) += bornin(invoice(doc));

overInvoiceDocSum = ABSTRACT NUMERIC[18,4] (Invoice);

calcendDate(Purchase.Invoice doc) = IF bankingDays(paymentCondition(doc)) THEN
                                      toDateTime(jumpWorkdays(country(supplier(doc)),date(doc),countDays(lastPaymentPeriod(paymentCondition(doc))))) ELSE
                                      sumDay(shipmentDateTime(doc),countDays(lastPaymentPeriod(paymentCondition(doc))));

//---------------------------------------------------------------------------экспорт-----------------------------------------------------------------------------------
overExportPurchaseInvoiceWI ABSTRACT LIST (Purchase.Invoice, DocMain);

exportPurchaseInvoiceWI 'Экспорт в супермаг' (Invoice doc) {
    inc(DocMain d) <- NULL;
    IF docTypeSupermag(operation(doc)) AND opcodeSupermag(operation(doc)) THEN {        
        LOCAL NESTED dt = DATETIME();
        NEW expMain = DocMain {
                inc(expMain) <- TRUE;          
                doctype(expMain) <- STRING[10] (getWord(docTypeSupermag(operation(doc)),';',1));
                id(doc) <- CONCAT '', doctype(expMain), seriesNumber(doc);               
                id(expMain) <- id(doc); 
                docid(expMain) <- seriesNumber(doc);            
                bornin(expMain) <- OVERRIDE bornin(doc), baseIDSupermag();
                opcode(expMain) <- STRING[10] (getWord(opcodeSupermag(operation(doc)),';',1));
                userop(expMain) <- STRING[10] (getWord(useropSupermag(operation(doc)),';',1));
                creatdate(expMain) <- shipmentDateTime(doc);                
                docstate(expMain) <- CASE WHEN isPosted(doc) THEN 3 
                                      WHEN createShipment(doc) THEN 2 
                                      WHEN isClosed(doc) THEN 0 ELSE 1;
                invoiceDocCreate(expMain) <- dateTime(doc);
                invoiceDocSum(expMain) <- OVERRIDE overInvoiceDocSum(doc), sumInvoiceConsignmentDetail(doc);
                invoiceDocNumber(expMain) <- CONCAT ' ', invoiceNumber(doc), invoiceSeries(doc); //OVERRIDE  (CONCAT ' ', invoiceNumber(doc), invoiceSeries(doc)), seriesNumber(doc);      
                priceRoundMode(expMain) <- 4 IF doc IS Invoice;                                                
                createFinobligation(expMain)  <- createFinobligation(operation(doc));                               
                clientIndex(expMain) <- id(supplier(doc));
                locationTo(expMain) <- id(customerStock(doc));
                ourselfclient(expMain) <- id(legalEntity(customerStock(doc)));
                                                
                FOR invoice(InvoiceDetail docd) = doc DO {
                    NEW expDet = DocDetail {
                        dataIndex(docd) <- index(docd); 
                        id(docd) <- (CONCAT '~',id(doc),index(docd));
                        index(expDet) <- index(docd);
                        docMain(expDet) <- expMain;
                        id(expDet) <- (CONCAT '~',id(expMain),index(expDet));
                        skuId(expDet) <- id(sku(docd));                                                                           
                        quantity(expDet) <- OVERRIDE creditQuantity(docd), shipmentQuantity(docd), 0; //quantity(docd);
                        blankquantity(expDet) <- OVERRIDE quantity(docd), 0;
                        //blankquantity(expDet) <- OVERRIDE quantityOrder(docd), quantity(docd), 0;                        
                        priceWithTax(expDet) <- CASE WHEN isExtraPack(docd) THEN round(invoicePrice(docd)/amountPack(docd),4) ELSE invoicePrice(docd);
                        valueTax(expDet) <- OVERRIDE valueVAT(docd), valueVAT(sku(docd));
                        priceNoTax(expDet) <- CASE WHEN isExtraPack(docd) THEN round(price(docd)/amountPack(docd),4) ELSE price(docd);
                        priceCurr(expDet) <- round(priceWithTax(expDet)/curRate(expMain),4);
                        sumWithTax(expDet) <- OVERRIDE abs(creditInvoiceSum(docd)), round2(invoicePrice(docd)*shipmentQuantity(docd)), abs(invoiceSum(docd));
                        sumNoTax(expDet) <- OVERRIDE abs(creditSum(docd)), round2(price(docd)*shipmentQuantity(docd)), abs(sum(docd));
                        sumTax(expDet) <- OVERRIDE creditVATSum(docd), (sumWithTax(expDet)-sumNoTax(expDet)), VATSum(docd);
                        sumCurr(expDet) <- round(sumWithTax(expDet)/curRate(expMain),4);
                        priceManufacturer(expDet) <- CASE WHEN isExtraPack(docd) AND manufacturingPrice(docd) >= 0.0001  THEN round(manufacturingPrice(docd)/amountPack(docd),4) WHEN manufacturingPrice(docd) >= 0.0001 THEN manufacturingPrice(docd) ELSE priceNoTax(expDet);
                        extracharge(expDet) <-(OVERRIDE manufacturingMarkup(docd),0);
                        priceRetail(expDet) <- (OVERRIDE retailPrice(docd), 0);                    
                        expiryDate(expDet) <- expiryDate(docd); 
                    }
                    
                }           
                sumWithTax(expMain) <- GROUP SUM abs(sumWithTax(DocDetail expDet)) IF docMain(expDet) = expMain;                
                sumCurr(expMain) <- GROUP SUM sumCurr(DocDetail expDet) IF docMain(expDet) = expMain; 
                countSpec(expMain) <- GROUP SUM 1 IF docMain(DocDetail expDet) = expMain;
                                
                acceptsum(expMain)  <- creditInvoiceSumInvoiceDetail(doc);
                basesum(expMain) <- invoiceSumInvoiceDetail(doc);
                calcenddate(expMain) <- OVERRIDE dateTime(lastInvoiceContractLedger(doc)), calcendDate(doc);
                paymentdelay(expMain) <- countDays(lastPaymentPeriod(paymentCondition(doc)));
                finagent(expMain) <- id(supplier(doc)); 
                iscalendardelay(expMain) <- IF bankingDays(paymentCondition(doc)) THEN '0' ELSE '1'; 
                
                FOR include(Order o, doc) DO NEW b = DocBase {
                    basisDoc(expMain, b) <- TRUE;
                    docid(b) <- seriesNumber(o);
                    doctype(b) <- 'OR';
                }
                
                FOR ContractSku c = contractSku(doc) DO NEW b = DocBase {
                    basisDoc(expMain, b) <- TRUE;
                    docid(b) <- seriesNumber(c);
                    doctype(b) <- 'CO';
                }                                
                
                overExportPurchaseInvoiceWI(doc,expMain); 
                
                IF countSpec(expMain) > 0 THEN {
                    dt () <- currentDateTime();        
                    TRY EXPORT exportWI XML TO System.exportFile;
                    CATCH logToFile('supermag', CONCAT '\n', 'Приходная накладная', 'file://' + exportDirectorySupermag() + '/' + 'WI_' +STRING[30](LONG(doc))+'_'+(OVERRIDE seriesNumber(doc), '')+'_'+ formulaDateT(dt()) + ' ParseMessage error:', messageCaughtException(), javaStackTraceCaughtException(), lsfStackTraceCaughtException());
                    TRY {
                            WRITE System.exportFile() TO 'file://' + exportDirectorySupermag() + '/' + doctype(expMain) +'_' +STRING[30](LONG(doc))+'_'+(OVERRIDE id(expMain), '')+'_'+ formulaDateT(dt());
                            WRITE System.exportFile() TO 'file://' + exportDirectorySupermag() + '/COPY/' + doctype(expMain) +'_' +STRING[30](LONG(doc))+'_'+(OVERRIDE id(expMain), '')+'_'+ formulaDateT(dt());
                        }
                    CATCH logToFile('supermag', CONCAT '\n', 'Приходная накладная', 'file://' + exportDirectorySupermag() + '/' + doctype(expMain) +'_' + STRING[30](LONG(doc)) + '_' + (OVERRIDE seriesNumber(doc), '') + '_' + formulaDateT(dt()) + ' ParseMessage error:', messageCaughtException(), javaStackTraceCaughtException(), lsfStackTraceCaughtException());
                    
                    sleep(pauseBetweenInvoiceFI());
                    
                    dt () <- currentDateTime();        
                    TRY EXPORT exportFI XML TO System.exportFile;
                    CATCH logToFile('supermag', CONCAT '\n', 'Финансовое обязательство', 'file://' + exportDirectorySupermag() + '/' + 'FI_' +STRING[30](LONG(doc))+'_'+(OVERRIDE seriesNumber(doc), '')+'_'+ formulaDateT(dt()) + ' ParseMessage error:', messageCaughtException(), javaStackTraceCaughtException(), lsfStackTraceCaughtException());
                    TRY {
                            WRITE System.exportFile() TO 'file://' + exportDirectorySupermag() + '/' + 'FI_' +STRING[30](LONG(doc))+'_'+(OVERRIDE id(expMain), '')+'_'+ formulaDateT(dt());
                            WRITE System.exportFile() TO 'file://' + exportDirectorySupermag() + '/COPY/' + 'FI_' +STRING[30](LONG(doc))+'_'+(OVERRIDE id(expMain), '')+'_'+ formulaDateT(dt());
                        }
                    CATCH logToFile('supermag', CONCAT '\n', 'Финансовое обязательство', 'file://' + exportDirectorySupermag() + '/' + 'FI_' + STRING[30](LONG(doc)) + '_' + (OVERRIDE seriesNumber(doc), '') + '_' + formulaDateT(dt()) + ' ParseMessage error:', messageCaughtException(), javaStackTraceCaughtException(), lsfStackTraceCaughtException());
                }                             
                  
                inc(expMain) <- NULL;
        }         
    }
    APPLY;
}

exportPurchaseInvoice (Purchase.Invoice doc) + {
    IF (getWord(docTypeSupermag(operation(doc)),';',1)) = 'WI' AND NOT id(operation(doc))='initial' AND (inExport(supplierStock(doc)) OR inExport(customerStock(doc))) AND (isPosted(doc) OR createShipment(doc)) THEN exportPurchaseInvoiceWI(doc);    
}

overExportPurchaseInvoiceAC ABSTRACT LIST (Invoice, DocMain);
overExportPurchaseInvoiceDetailAC ABSTRACT LIST (InvoiceDetail, DocDetail);

exportPurchaseInvoiceAC 'Экспорт в супермаг' (Invoice doc) {
    inc(DocMain d) <- NULL;
    IF createPricing(doc) THEN {   
        LOCAL NESTED dt = DATETIME();
        NEW expMain = DocMain {
                inc(expMain) <- TRUE;
                doctype(expMain) <- 'AC';
                docid(expMain) <- seriesNumber(doc);                 
                id(expMain) <- CONCAT '', doctype(expMain), seriesNumber(doc);                 
                opcode(expMain) <- '-1';
                docstate(expMain) <- CASE WHEN isPosted(doc) THEN 3 
                                      WHEN isClosed(doc) THEN 0 ELSE 2;
                creatdate(expMain) <- shipmentDateTime(doc);
                execDate(expMain) <- postedTime(doc);
                execTime(expMain) <- postedTime(doc);                
                priceType(expMain) <- idSupermagPriceType(customerStock(doc)); //STRING[10](replace( (OVERRIDE id(retailPriceListType(customerStock(doc))),  id(baseBPriceListLedger(retailPriceListType(customerStock(doc))))),'D',''));
                lastStatusSupermag(doc) <- OVERRIDE lastStatusSupermag(doc), docstate(expMain); 
                bornin(expMain) <- OVERRIDE bornin(doc), baseIDSupermag();
                location(expMain)  <- id(customerStock(doc));
                reasonPricing(expMain) <- '0';     
                priceRoundMode(expMain) <- 0;
                FOR invoice(InvoiceDetail docd) = doc AND NOT skipChangeLedger(docd)  DO {
                    NEW expDet = DocDetail {
                        index(expDet) <- index(docd);
                        docMain(expDet) <- expMain;
                        id(expDet) <- (CONCAT '~',id(expMain),index(expDet));
                        skuId(expDet) <- id(sku(docd));                                                                           
                        quantity(expDet) <- pricingQuantity(docd);
                        valueTax(expDet) <- valueRetailVAT(docd);
                        priceWithTax(expDet) <- retailPrice(docd);
                        priceNoTax(expDet) <- NUMERIC[18,4] ( abs(round( priceWithTax(expDet) / (1 + valueTax(expDet)/100),4)));
                        priceCurr(expDet) <- round(priceWithTax(expDet)/curRate(expMain),4);
                        sumWithTax(expDet) <- retailSum(docd);
                        sumNoTax(expDet) <- NUMERIC[18,4] ( abs(round( sumWithTax(expDet) / (1 + valueTax(expDet)/100),2)));
                        sumCurr(expDet) <-round(sumWithTax(expDet)/curRate(expMain),4);
                        sumTax(expDet) <- retailVATSum(docd);
                        oldPrice(expDet) <- retailPriceB(docd);
                        flags(expDet) <- '0';
                        revalSum(expDet) <- 0.00;
                        revalOperQuantity(expDet) <- 0.00;
                        overExportPurchaseInvoiceDetailAC(docd, expDet);
                    }
                }    

                overExportPurchaseInvoiceAC(doc,expMain);
                
                sumWithTax(expMain) <- GROUP SUM abs(sumWithTax(DocDetail expDet)) IF docMain(expDet) = expMain;                
                sumCurr(expMain) <- GROUP SUM sumCurr(DocDetail expDet) IF docMain(expDet) = expMain; 
                countSpec(expMain) <- GROUP SUM 1 IF docMain(DocDetail expDet) = expMain;                
                
                NEW b = DocBase {
                    basisDoc(expMain, b) <- TRUE;
                    docid(b) <- seriesNumber(doc);
                    doctype(b) <- 'WI';
                }
                               
                IF countSpec(expMain) > 0 THEN {
                    dt () <- currentDateTime();        
                    TRY EXPORT exportAC XML TO System.exportFile;
                    CATCH logToFile('supermag', CONCAT '\n', 'Расценка на приходе', 'file://' + exportDirectorySupermag() + '/' + doctype(expMain) +  '_' +STRING[30](LONG(doc))+'_'+(OVERRIDE seriesNumber(doc), '')+'_'+ formulaDateT(dt()) + ' ParseMessage error:', messageCaughtException(), javaStackTraceCaughtException(), lsfStackTraceCaughtException());
                    TRY {
                            WRITE System.exportFile() TO 'file://' + exportDirectorySupermag() + '/' + doctype(expMain) +'_' +STRING[30](LONG(doc))+'_'+(OVERRIDE id(expMain), '')+'_'+ formulaDateT(dt());
                            WRITE System.exportFile() TO 'file://' + exportDirectorySupermag() + '/COPY/' + doctype(expMain) +'_' +STRING[30](LONG(doc))+'_'+(OVERRIDE id(expMain), '')+'_'+ formulaDateT(dt());
                        }
                    CATCH logToFile('supermag', CONCAT '\n', 'Расценка на приходе', 'file://' + exportDirectorySupermag() + '/' + doctype(expMain) +'_' + STRING[30](LONG(doc)) + '_' + (OVERRIDE seriesNumber(doc), '') + '_' + formulaDateT(dt()) + ' ParseMessage error:', messageCaughtException(), javaStackTraceCaughtException(), lsfStackTraceCaughtException());
                }    
                inc(expMain) <- NULL;
        }                                    
    }       
    APPLY;    
}

exportPurchaseInvoice (Purchase.Invoice doc) + {
    IF (getWord(docTypeSupermag(operation(doc)),';',1)) = 'WI' AND NOT id(operation(doc))='initial' AND (inExport(supplierStock(doc)) OR inExport(customerStock(doc))) AND (isPosted(doc) OR createShipment(doc)) AND createPricing(doc) THEN exportPurchaseInvoiceAC(doc);    
    IF id(operation(doc))='purchaseSPPrices' AND (inExport(supplierStock(doc)) OR inExport(customerStock(doc))) AND (isPosted(doc) OR createShipment(doc)) AND createPricing(doc) THEN exportPurchaseInvoiceAC(doc);    
}

//--------------------------------------------------------------------------------------------импорт--------------------------------------------------------------
overImportPurchaseInvoice ABSTRACT LIST ();
overImportPurchaseInvoice2 ABSTRACT LIST ();
overImportPurchaseInvoiceStatus ABSTRACT LIST ();
overImportPurchaseInvoiceDetail ABSTRACT LIST (Purchase.UserInvoiceDetail, INTEGER);

importPurchaseInvoice 'Импорт накладные (закупка)' () {

    LOCAL paymentCondition = PaymentCondition (STRING);
    imported (Purchase.Invoice i) <- NULL;         
    imported (Purchase.InvoiceDetail d) <- NULL;            
       
    filterSql() <- 'NOT d.DOCTYPE = \'IW\' AND b.DocType(+)= d.DocType AND b.id(+) = d.id AND d.locationto IN ('+ stocksImport() + ')  AND d.CreateDat >= TO_DATE(\'' + toDateISO(dateFromImportDocument()) + '\',\'YYYY-MM-DD\') AND ( ';
    LOCAL filterDoc = STRING ();
    LOCAL operDoc = STRING ();
    filterDoc() <-'';
    FOR Operation op IF op IS Operation AND getWord(docTypeSupermag(op),';',1) DO {
       operDoc() <- CONCAT ' AND ', ('d.DOCTYPE = \'' + getWord(docTypeSupermag(op),';',1) + '\''), ('d.OPCODE = ' + getWord(opcodeSupermag(op),';',1)), ('d.USEROP = ' + getWord(useropSupermag(op),';',1)) ; 
       filterDoc() <- CONCAT ' OR ',filterDoc(), '(' + operDoc() + ')';
    }
         
    filterDoc() <- substrFrom(filterDoc(),4);
    filterSql() <- CONCAT '', filterSql(), filterDoc(),')'; 
         
    IF length(numberImportDocument()) > 0 THEN filterSql() <- 'd.DocType = \'WI\' AND b.DocType(+)= d.DocType AND b.id(+) = d.id AND d.id = \'' + numberImportDocument() + '\''; 
    
    sqlString() <- 'Select d.ID, d.doctype, d.opcode, d.userop, UTL_RAW.CAST_TO_VARCHAR2(UTL_ENCODE.BASE64_ENCODE(d.bornin)), d.createdat, d.docstate, d.clientindex, d.locationto, d.Commentary, b.suppldocsum, b.SUPPLIERDOC, b.SUPPLIERDOCCREATE, f.paymentdelay, f.iscalendardelay, f.isadmitted from Supermag.SMDocuments d, Supermag.SMWayBillsIn b, Supermag.SMFinobligation f where ' + filterSql() + ' AND f.docid(+)=d.id AND f.doctype(+)=d.doctype';    

    IF enableLogImport() THEN logToFile('supermag', CONCAT '\n', 'Импорт накладные (закупка)', sqlString());
    
    EXTERNAL SQL connectionString() EXEC sqlString() TO SupermagSettings.file;
       
    IMPORT TABLE FROM SupermagSettings.file() TO docNumber, docType, operCode, userOp, bornIn, docDate, docState, clientIndex, locationTo, comment, suppldocSum, suppldocNumber, suppldocDate, paymentDelay, isCalendarDelay, isAdmitted ;
    
    idDoc(INTEGER i) <- STRING[50](CONCAT '', STRING(docType(i)), STRING(docNumber(i)));
    currentVersion (INTEGER i) <-  (CONCAT '~', STRING(idDoc(i)), STRING(operCode(i)), STRING(userOp(i)), STRING(bornIn(i)), STRING(docDate(i)), STRING(docState(i)), STRING(clientIndex(i)), STRING(locationTo(i)), STRING(comment(i)), STRING(suppldocSum(i)), STRING(suppldocNumber(i)), STRING(suppldocDate(i)), STRING(paymentDelay(i)), STRING(isCalendarDelay(i)), STRING(isAdmitted(i)) ) IF idDoc(i);                 
    codeOperation (INTEGER i) <- CONCAT '~', STRING(docType(i)), STRING(operCode(i)), STRING(userOp(i)) IF idDoc(i);
    
    isBankDays(INTEGER i) <- (CASE WHEN isCalendarDelay(i) = '1' THEN 0 WHEN isCalendarDelay(i) = '0' THEN 0 ELSE NULL) WHERE idDoc(i);   
    
    FOR [GROUP SUM 1 BY idDoc(INTEGER i) IF operation1Supermag(codeOperation(i)) AND NOT docState(i) = 1](STRING[50] newId) AND NOT userInvoice(newId) DO NEW o = UserInvoice {
        id(o) <- newId;
    }

    FOR Purchase.UserInvoice doc == Purchase.userInvoice(idDoc(INTEGER i)) AND NOT currentVersion(i) = Purchase.lastVersion(doc) DO {
        isPosted(doc) <- docState(i) == 3;
        createShipment(doc) <- docState(i) == 2 OR docState(i) == 3; 
        isClosed(doc) <- docState(i) == 0;           
    }
//    IF length(numberImportDocument()) > 0 THEN { //для отладки
//        MESSAGE sqlString();
//    }
      
    FOR Purchase.UserInvoice doc == Purchase.userInvoice(idDoc(INTEGER i)) AND NOT currentVersion(i) = Purchase.lastVersion(doc) AND NOT importOnlyStatus(Purchase.operation1Supermag(codeOperation(i))) DO {       
        number[Document](doc) <- STRING[28](docNumber(i));
        bornin(doc) <- bornIn(i);
        operation(doc) <- operation1Supermag(codeOperation(i));
        supplier(doc) <- legalEntity(STRING(clientIndex(i)));
        supplierStock(doc) <- stock(STRING[100] (clientIndex(i)));
        customer(doc) <- legalEntity(stock(ISTRING[100] (locationTo(i))));
        customerStock(doc) <- stock(ISTRING[100] (locationTo(i)));
        date(doc) <- DATE (docDate(i));  
        time(doc) <- OVERRIDE time(doc), 09:00;
        imported(doc) <- TRUE;
        paymentCondition(doc) <- paymentCondition(paymentPeriod(paymentDelay(i), isBankDays(i))) WHERE paymentDelay(i) > 0;              
        lastVersion(doc) <- currentVersion(i);
    }  
    
    overImportPurchaseInvoice();
    
    //импорт заказов-оснований и договоров для накладных. импорт всех оснований через overImportPurchaseInvoice()
    sqlString() <- 'Select d.ID, d.doctype, d.opcode, d.userop, bs.baseId, bs.baseDoctype from Supermag.SMDocuments d, Supermag.SMWayBillsIn b, Supermag.SMCommonBases bs where ' + filterSql() + ' AND bs.id(+)=d.id AND bs.doctype(+)=d.doctype';    

    IF enableLogImport() THEN logToFile('supermag', CONCAT '\n', 'Импорт накладные (закупка)', sqlString());
    
    EXTERNAL SQL connectionString() EXEC sqlString() TO SupermagSettings.file;
     
    IMPORT TABLE FROM SupermagSettings.file() TO docNumber, docType, operCode, userOp, baseNumber, baseDoctype ;

    idDoc(INTEGER i) <- STRING[50](CONCAT '', STRING(docType(i)), STRING(docNumber(i)));
    idBase(INTEGER i) <- STRING[50](CONCAT '', STRING(baseDoctype(i)), STRING(baseNumber(i)));

    IF length(numberImportDocument()) > 0 THEN { //для отладки
        MESSAGE sqlString();
        MESSAGE idDoc(0)  +  ' ' + idBase(0);
    }

    FOR Purchase.UserInvoice doc == Purchase.userInvoice(idDoc(INTEGER i)) AND userContractSku(idBase(i)) AND NOT importOnlyStatus(Purchase.operation1Supermag(codeOperation(i))) DO {    
        contractSku(doc) <- userContractSku(idBase(i));        
    }    

    FOR Purchase.UserInvoice doc == Purchase.userInvoice(idDoc(INTEGER i)) AND userOrder(idBase(i)) AND baseDoctype(i) = 'OR' AND NOT importOnlyStatus(Purchase.operation1Supermag(codeOperation(i))) DO {
        include(Order orS, doc) <- NULL;    
        include(Order orS, doc) <- TRUE WHERE orS = userOrder(idBase(i));
    }        

    overImportPurchaseInvoice2();
    
    //импорт спецификаций
    sqlString() <- 'SELECT d.ID, d.DOCTYPE, d.LOCATIONTO, ds.SPECITEM, ds.ARTICLE, ds.QUANTITY, ds.ITEMPRICE, ds.ITEMPRICENOTAX, ds.TOTALPRICE, ds.TOTALPRICENOTAX, dt.TAXRATE, dt.TAXSUM, dby.MANUFACTURERSPRICE, dby.extracharge, dby.RETAILPRICE, dmis.QUANTBYDOC ' + 
    'FROM Supermag.SMDocuments d, Supermag.SMWayBillsIn b, Supermag.SMSpec ds, Supermag.SMSPECTAX dt, Supermag.SMSPECBY dby, Supermag.SLSPECQMISMATCH dmis ' + 
    'WHERE ds.DocType = \'WI\' AND ds.DocId = d.id AND ' + 
    'dt.DocType(+) = \'WI\' AND dt.DocId(+) = ds.DocId AND dt.SPECITEM(+) = ds.SPECITEM AND ' + 
    'dby.DocType(+) = \'WI\' AND dby.DocId(+) = ds.DocId AND dby.SPECITEM(+) = ds.SPECITEM AND ' + 
    'dmis.DocType(+) = \'WI\' AND dmis.DocId(+) = ds.DocId AND dmis.SPECITEM(+) = ds.SPECITEM AND ' + 
    filterSql()+' ORDER BY ds.doctype, ds.docid, ds.specitem';    

    IF enableLogImport() THEN logToFile('supermag', CONCAT '\n', 'Импорт накладные (закупка)', sqlString());
    
    EXTERNAL SQL connectionString() EXEC sqlString() TO SupermagSettings.file;

    IMPORT TABLE FROM SupermagSettings.file() TO docNumber, docType, locationTo, specItem, idItem, quantity, itemPrice, itemPriceNoTax, totalPrice, totalPriceNoTax, taxRate, taxSum, priceManufacturer, extracharge, retailPrice, blankquantity;
    
    idDoc(INTEGER i) <- STRING[50](CONCAT '', docType(i), docNumber(i)) WHERE docNumber(i) AND docType(i);
    idDet(INTEGER i) <- STRING[50](CONCAT '~', idDoc(i), specItem(i)) WHERE idDoc(i) AND specItem(i);
    currentVersion (INTEGER i) <- ( CONCAT '~', STRING(idDoc(i)), STRING(locationTo(i)), STRING(specItem(i)), STRING(idItem(i)), STRING(quantity(i)), 
                                                STRING(itemPrice(i)), STRING(itemPriceNoTax(i)), STRING(totalPrice(i)), STRING(totalPriceNoTax(i)),  
                                                STRING(taxSum(i)), STRING(taxRate(i)), STRING(priceManufacturer(i)), STRING(extracharge(i)), STRING(retailPrice(i)), STRING(blankquantity(i)) ) IF idDet(i);                  

    IF length(numberImportDocument()) > 0 THEN { //для отладки
        MESSAGE sqlString();    
        MESSAGE idDet(0);
        MESSAGE currentVersion(0);        
    }

    FOR Purchase.userInvoice(idDoc(INTEGER i)) AND NOT Purchase.userInvoiceDetail(idDet(i)) AND NOT importOnlyStatus(Purchase.operation1Supermag(codeOperation(i))) DO NEW det = Purchase.UserInvoiceDetail {
        Purchase.userInvoice(det) <- Purchase.userInvoice(idDoc(i));
        id(det) <- idDet(i);
    }
       
    FOR Purchase.UserInvoiceDetail det = Purchase.userInvoiceDetail(idDet(INTEGER i)) AND NOT currentVersion(i) = Purchase.lastVersion(det) AND NOT importOnlyStatus(Purchase.operation1Supermag(codeOperation(i))) DO {
        sku(det) <- item(idItem(i));
        shipmentQuantity(det) <- quantity(i);
        quantity(det) <- OVERRIDE blankquantity(i), quantity(i);
        invoicePrice(det) <- NUMERIC[16,2](round(itemPrice(i),2));
        price(det) <- NUMERIC[16,2](round(itemPriceNoTax(i),2));
////          поменяли логику теперь "количество" в СМ = "принято" в Fusion, "количество по док" в СМ = "количество" в Fusion
////          а сумм по документу в СМ нет :(, считаем сами
////        invoiceSum(det) <- NUMERIC[16,2](round(quantity(det)*invoicePrice(det),2));
////        sum(det) <- NUMERIC[16,2](round(quantity(det)*price(det),2));
        VAT(det) <- OVERRIDE valueCurrentVAT(countrySupplierStock(det), taxRate(i)), VAT(sku(det));
        dataIndex(det) <- specItem(i);
        manufacturingPrice(det) <- priceManufacturer(i);
        manufacturingMarkup(det) <- CASE WHEN calcManufacturingMarkupFromPrices(det) THEN NUMERIC[8,3]( (calcBaseManufacturingPrice(det) / manufacturingPrice(det) - 1) *100)      
                                                                                     ELSE calcManufacturingMarkup(det);        
        overImportPurchaseInvoiceDetail(det,i);
        imported(det) <- TRUE;
        connectOrder(det);        
        lastVersion(det) <- currentVersion(i);
    }
    
    FOR Purchase.UserInvoiceDetail det = Purchase.userInvoiceDetail(idDet(INTEGER i)) DO imported(det) <- TRUE;
//    
    DELETE Purchase.UserInvoiceDetail det WHERE det IS UserInvoiceDetail AND imported(userInvoice(det)) AND NOT imported(det);
    
    executeLocalEvents('Purchase.UserInvoice');
    numberImportDocument() <- NULL;
   
    APPLY;           
}

EXTEND FORM integrationData PROPERTIES importPurchaseInvoice();

importPurchaseInvoiceStatus 'Импорт накладные (закупка)' (STRING indexDoc) {
               
    filterSql() <- 'd.DocType = \'' + getWord(indexDoc,'~',1) + '\' AND d.id = \'' + getWord(indexDoc,'~',2) + '\''; 
    
    sqlString() <- 'Select d.ID, d.doctype, d.opcode, d.userop, UTL_RAW.CAST_TO_VARCHAR2(UTL_ENCODE.BASE64_ENCODE(d.bornin)), d.docstate, f.isadmitted from Supermag.SMDocuments d, Supermag.SMFinobligation f WHERE ' + filterSql() + ' AND f.docid(+)=d.id AND f.doctype(+)=d.doctype';    
    
    IF enableLogImport() THEN logToFile('supermag', CONCAT '\n', 'Импорт накладные (закупка)', sqlString());
    
    TRY
    EXTERNAL SQL connectionString() EXEC sqlString() TO SupermagSettings.file;
    CATCH MESSAGE sqlString();
       
    IMPORT TABLE FROM SupermagSettings.file() TO docNumber, docType, operCode, userOp, bornIn, docState, isAdmitted;
    
    idDoc(INTEGER i) <- STRING[50](CONCAT '', STRING(docType(i)), STRING(docNumber(i)));
    codeOperation (INTEGER i) <- CONCAT '~', STRING(docType(i)), STRING(operCode(i)), STRING(userOp(i)) IF idDoc(i);

    FOR Purchase.UserInvoice doc == Purchase.userInvoice(idDoc(INTEGER i)) DO {       
        CASE WHEN docState(0) = 0 THEN {
            isPosted(doc) <- NULL;
            isClosed(doc) <- TRUE;                
        }
        WHEN docState(0) = 1 THEN {
            isPosted(doc) <- NULL;
            isClosed(doc) <- NULL;                        
        } 
        WHEN docState(0) = 2 THEN {
            isPosted(doc) <- IF (docType(0) = 'FA' OR docType(0) = 'LA') THEN TRUE ELSE NULL;
            isClosed(doc) <- NULL;;                
        } 
        WHEN docState(0) = 3 THEN {
            isPosted(doc) <- TRUE;
//            isClosed(doc) <- TRUE;        
        } 
    }      
            
    overImportPurchaseInvoiceStatus();
    
    APPLY;
}

importPurchaseInvoiceFull 'Импорт накладные (закупка)' (STRING indexDoc) {

    imported (Purchase.Invoice i) <- NULL;         
    imported (Purchase.InvoiceDetail d) <- NULL;            

    filterSql() <- 'd.DocType = \'' + getWord(indexDoc,'~',1) + '\' AND b.DocType(+)= d.DocType AND b.id(+) = d.id AND d.id = \'' + getWord(indexDoc,'~',2) + '\''; 
    
    sqlString() <- 'Select d.ID, d.doctype, d.opcode, d.userop, UTL_RAW.CAST_TO_VARCHAR2(UTL_ENCODE.BASE64_ENCODE(d.bornin)), d.createdat, d.docstate, d.clientindex, d.locationto, d.Commentary, b.suppldocsum, b.SUPPLIERDOC, b.SUPPLIERDOCCREATE, f.paymentdelay, f.iscalendardelay, f.isadmitted from Supermag.SMDocuments d, Supermag.SMWayBillsIn b, Supermag.SMFinobligation f where ' + filterSql() + ' AND f.docid(+)=d.id AND f.doctype(+)=d.doctype';    

    IF enableLogImport() THEN logToFile('supermag', CONCAT '\n', 'Импорт накладные (закупка)', sqlString());
    
    EXTERNAL SQL connectionString() EXEC sqlString() TO SupermagSettings.file;
       
    IMPORT TABLE FROM SupermagSettings.file() TO docNumber, docType, operCode, userOp, bornIn, docDate, docState, clientIndex, locationTo, comment, suppldocSum, suppldocNumber, suppldocDate, paymentDelay, isCalendarDelay, isAdmitted ;
    
    idDoc(INTEGER i) <- STRING[50](CONCAT '', STRING(docType(i)), STRING(docNumber(i)));
    codeOperation (INTEGER i) <- CONCAT '~', STRING(docType(i)), STRING(operCode(i)), STRING(userOp(i)) IF idDoc(i);
    
    isBankDays(INTEGER i) <- (CASE WHEN isCalendarDelay(i) = '1' THEN 0 WHEN isCalendarDelay(i) = '0' THEN 0 ELSE NULL) WHERE idDoc(i);   
    
    IF NOT userInvoice(idDoc(0)) THEN NEW o = UserInvoice {  id(o) <- idDoc(0); }
                                
    FOR Purchase.UserInvoice doc == Purchase.userInvoice(idDoc(INTEGER i)) DO {       
        number[Document](doc) <- STRING[28](docNumber(i));
        bornin(doc) <- bornIn(i);
        operation(doc) <- operation1Supermag(codeOperation(i));
        lastStatusSupermag(doc) <- docState(i);
        supplier(doc) <- legalEntity(STRING(clientIndex(i)));
        supplierStock(doc) <- stock(STRING[100] (clientIndex(i)));
        customer(doc) <- legalEntity(stock(ISTRING[100] (locationTo(i))));
        customerStock(doc) <- stock(ISTRING[100] (locationTo(i)));
        date(doc) <- DATE (docDate(i));  
        time(doc) <- OVERRIDE time(doc), 09:00;
        imported(doc) <- TRUE;
        paymentCondition(doc) <- paymentCondition(paymentPeriod(paymentDelay(i), isBankDays(i))) WHERE paymentDelay(i) > 0;              
    }  
    
    FOR Purchase.UserInvoice doc == Purchase.userInvoice(idDoc(INTEGER i)) DO {       
        CASE WHEN docState(0) = 0 THEN {
            isPosted(doc) <- NULL;
            isClosed(doc) <- TRUE;                
        }
        WHEN docState(0) = 1 THEN {
            isPosted(doc) <- NULL;
            isClosed(doc) <- NULL;                        
        } 
        WHEN docState(0) = 2 THEN {
            isPosted(doc) <- IF (docType(0) = 'FA' OR docType(0) = 'LA') THEN TRUE ELSE NULL;
            isClosed(doc) <- NULL;;                
        } 
        WHEN docState(0) = 3 THEN {
            isPosted(doc) <- TRUE;
//            isClosed(doc) <- TRUE;        
        } 
    }      
    
    overImportPurchaseInvoice();

    //импорт заказов-оснований и договоров для накладных. импорт всех оснований через overImportPurchaseInvoice()
    sqlString() <- 'Select d.ID, d.doctype, d.opcode, d.userop, bs.baseId, bs.baseDoctype from Supermag.SMDocuments d, Supermag.SMWayBillsIn b, Supermag.SMCommonBases bs where ' + filterSql() + ' AND bs.id(+)=d.id AND bs.doctype(+)=d.doctype';    

    IF enableLogImport() THEN logToFile('supermag', CONCAT '\n', 'Импорт накладные (закупка)', sqlString());

    EXTERNAL SQL connectionString() EXEC sqlString() TO SupermagSettings.file;

    IMPORT TABLE FROM SupermagSettings.file() TO docNumber, docType, operCode, userOp, baseNumber, baseDoctype ;

    idDoc(INTEGER i) <- STRING[50](CONCAT '', STRING(docType(i)), STRING(docNumber(i)));
    idBase(INTEGER i) <- STRING[50](CONCAT '', STRING(baseDoctype(i)), STRING(baseNumber(i)));

    FOR Purchase.UserInvoice doc == Purchase.userInvoice(idDoc(INTEGER i)) AND userContractSku(idBase(i)) DO {    
        contractSku(doc) <- userContractSku(idBase(i));        
    }    

    FOR Purchase.UserInvoice doc == Purchase.userInvoice(idDoc(INTEGER i)) AND userOrder(idBase(i)) AND baseDoctype(i) = 'OR' DO {
        include(Order orS, doc) <- NULL;    
        include(Order orS, doc) <- TRUE WHERE orS = userOrder(idBase(i));
    }        

    overImportPurchaseInvoice2();

    //импорт спецификаций
    sqlString() <- 'SELECT d.ID, d.DOCTYPE, d.LOCATIONTO, ds.SPECITEM, ds.ARTICLE, ds.QUANTITY, ds.ITEMPRICE, ds.ITEMPRICENOTAX, ds.TOTALPRICE, ds.TOTALPRICENOTAX, dt.TAXRATE, dt.TAXSUM, dby.MANUFACTURERSPRICE, dby.extracharge, dby.RETAILPRICE, dmis.QUANTBYDOC ' + 
    'FROM Supermag.SMDocuments d, Supermag.SMWayBillsIn b, Supermag.SMSpec ds, Supermag.SMSPECTAX dt, Supermag.SMSPECBY dby, Supermag.SLSPECQMISMATCH dmis ' + 
    'WHERE ds.DocType = d.DocType AND ds.DocId = d.id AND ' + 
    'dt.DocType(+) = ds.DocType AND dt.DocId(+) = ds.DocId AND dt.SPECITEM(+) = ds.SPECITEM AND ' + 
    'dby.DocType(+) = ds.DocType AND dby.DocId(+) = ds.DocId AND dby.SPECITEM(+) = ds.SPECITEM AND ' + 
    'dmis.DocType(+) = ds.DocType AND dmis.DocId(+) = ds.DocId AND dmis.SPECITEM(+) = ds.SPECITEM AND ' + 
    filterSql()+' ORDER BY ds.doctype, ds.docid, ds.specitem';    

    IF enableLogImport() THEN logToFile('supermag', CONCAT '\n', 'Импорт накладные (закупка)', sqlString());

    EXTERNAL SQL connectionString() EXEC sqlString() TO SupermagSettings.file;

    IMPORT TABLE FROM SupermagSettings.file() TO docNumber, docType, locationTo, specItem, idItem, quantity, itemPrice, itemPriceNoTax, totalPrice, totalPriceNoTax, taxRate, taxSum, priceManufacturer, extracharge, retailPrice, blankquantity;

    idDoc(INTEGER i) <- STRING[50](CONCAT '', docType(i), docNumber(i)) WHERE docNumber(i) AND docType(i);
    idDet(INTEGER i) <- STRING[50](CONCAT '~', idDoc(i), specItem(i)) WHERE idDoc(i) AND specItem(i);

    FOR Purchase.userInvoice(idDoc(INTEGER i)) AND NOT Purchase.userInvoiceDetail(idDet(i)) DO NEW det = Purchase.UserInvoiceDetail {
        Purchase.userInvoice(det) <- Purchase.userInvoice(idDoc(i));
        id(det) <- idDet(i);
    }

    FOR Purchase.UserInvoiceDetail det = Purchase.userInvoiceDetail(idDet(INTEGER i))  DO {
        sku(det) <- item(idItem(i));
////          поменяли логику теперь "количество" в СМ = "принято" в Fusion, "количество по док" в СМ = "количество" в Fusion
        shipmentQuantity(det) <- quantity(i);
        quantity(det) <- OVERRIDE blankquantity(i), quantity(i);
        invoicePrice(det) <- NUMERIC[16,2](round(itemPrice(i),2));
        price(det) <- NUMERIC[16,2](round(itemPriceNoTax(i),2));
        VAT(det) <- OVERRIDE valueCurrentVAT(countrySupplierStock(det), taxRate(i)), VAT(sku(det));
        dataIndex(det) <- specItem(i);
        manufacturingPrice(det) <- priceManufacturer(i);
        manufacturingMarkup(det) <- CASE WHEN calcManufacturingMarkupFromPrices(det) THEN NUMERIC[8,3]( (calcBaseManufacturingPrice(det) / manufacturingPrice(det) - 1) *100)      
                                                                                     ELSE calcManufacturingMarkup(det);        
        overImportPurchaseInvoiceDetail(det,i);   // для Бигзз определяем retailPriceListType(det)
        connectOrder(det);        
        imported(det) <- TRUE;
    }       

    DELETE Purchase.UserInvoiceDetail det WHERE det IS UserInvoiceDetail AND imported(userInvoice(det)) AND NOT imported(det);

    executeLocalEvents('Purchase.UserInvoice');

    APPLY;               
}

importPurchaseInvoices () + {
    LOCAL indexDoc = STRING ();
    
    FOR [GROUP SUM 1 BY indexDoc(INTEGER i) IF operation1Supermag(codeOperation(i)) IS Purchase.Operation AND importOnlyStatus(operation1Supermag(codeOperation(i))) AND userInvoice(idDoc(i))](STRING indexDoc) NOINLINE DO  {           
        indexDoc() <- indexDoc;     
        NEWSESSION NESTED (indexDoc){            
            importPurchaseInvoiceStatus(indexDoc());
        } 
    }
    
    FOR [GROUP SUM 1 BY indexDoc(INTEGER i) IF operation1Supermag(codeOperation(i)) IS Purchase.Operation AND NOT (importOnlyStatus(operation1Supermag(codeOperation(i))) AND userInvoice(idDoc(i)))](STRING indexDoc) NOINLINE DO {       
        indexDoc() <- indexDoc; 
        NEWSESSION NESTED (indexDoc){
            importPurchaseInvoiceFull(indexDoc());
        } 
    }
}

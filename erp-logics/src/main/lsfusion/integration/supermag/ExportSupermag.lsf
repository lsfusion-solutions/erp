MODULE ExportSupermag;
REQUIRE StockDocument, LogTools;

@defineLogRotate(supermag, 5, weekly);

exportSupermag 'Экспорт в супермаг' ABSTRACT (StockDocumentLedger);
exportTime (DATETIME dt)=  [FORMULA STRING[20] 'to_char(($1),\'YYYY-MM-DD\') || \'T\' || to_char(($1),\'HH24:MI:SS\')'](dt);
formulaDateT =  [FORMULA STRING[20] 'to_char(($1),\'YYYYMMDDHH24MISSMS\')']( DATETIME dt);

idSupermag 'Код в супермаг' = DATA STRING[100] (Operation.Operation); 
idSupermagReturn 'Код в супермаг (возврат)' = DATA STRING[100] (Operation.Operation); 
idSupermag 'Код в супермаг (BORNIN)' = DATA STRING[100] (Stock); // base64


CLASS SupermagLog 'История' ;
TABLE supermagLog (SupermagLog);
id 'Код' = DATA STRING[100] (SupermagLog);
dateTime 'Дата/время' = DATA DATETIME (SupermagLog);
userLogin 'Логин пользователя' = DATA STRING[100] (SupermagLog) CHARWIDTH 15; 
nameContact 'Имя пользователя' =  DATA STRING[100] (SupermagLog) IN id CHARWIDTH 15;
stockDocumentLedger 'Дата/время' = DATA StockDocumentLedger (SupermagLog);
dateTimeAnswer 'Дата/время ответа' = DATA DATETIME (SupermagLog);
errorAnswerFile 'Ошибка файла' = DATA BOOLEAN (SupermagLog);
errorAnswer 'Ошибка обработки' = DATA BOOLEAN (SupermagLog);
textError 'Текст ошибки' = DATA TEXT (SupermagLog);

supermagLog = GROUP MAX SupermagLog log BY ISTRING[100](id(log));

lastSupermagLog (StockDocumentLedger l) = GROUP LAST SupermagLog d
          ORDER dateTime(d), d 
          BY stockDocumentLedger(d);
lastDateTimeSupermagLog 'Последняя выгрузка' = dateTime(lastSupermagLog (StockDocumentLedger l));     
dateTimeAnswerSupermagLog 'Дата/время ответа' = dateTimeAnswer(lastSupermagLog (StockDocumentLedger l));     
errorAnswerFileSupermagLog 'Ошибка файла' = errorAnswerFile(lastSupermagLog (StockDocumentLedger l));     
errorAnswerSupermagLog 'Ошибка обработки' = errorAnswer(lastSupermagLog (StockDocumentLedger l));   
    
exportDirectorySupermag 'Путь для экспорта файлов в супермаг' = DATA STRING[500] ();
exportDirectorySupermagReturn 'Путь для ответов о состояни загрузки в супермаг' = DATA STRING[500] ();

EXTEND FORM integrationData 
    PROPERTIES() exportDirectorySupermag, exportDirectorySupermagReturn
;

DESIGN integrationData {
    pane {
        NEW supermag {
            caption = 'Супермаг документы';
            MOVE PROPERTY (exportDirectorySupermag());  
            MOVE PROPERTY (exportDirectorySupermagReturn());        
        }
    }
}

//select 'Отм.' = DATA LOCAL BOOLEAN (StockDocumentLedger);
//exportSupermagSelect 'Экспорт в супермаг (отм.)'() {
//    FOR select(StockDocumentLedger l) DO {
//        exportSupermag(l);
//    }
//    select(StockDocumentLedger l) <- NULL;
//} 
//
//EXTEND FORM sumStockDocumentLedger
//    PROPERTIES (il) exportSupermag TOOLBAR, select 
//    PROPERTIES (ol) exportSupermag TOOLBAR, select
//    PROPERTIES exportSupermagSelect() TOOLBAR DRAW ol SHOWIF (GROUP SUM 1 IF select(StockDocumentLedger hhh))
//;
//exportSupermagInterval 'Экспорт в супермаг'(DATE df, DATE dt) {
//    FOR active(StockDocumentLedger l) AND date(l)>= df AND date(l) <= dt DO {
//        exportSupermag(l);
//    }
//} 

isExport 'Загружен' (StockDocumentLedger d)= lastDateTimeSupermagLog(d) AND dateTimeAnswerSupermagLog(d) AND NOT (errorAnswerFileSupermagLog(d) OR errorAnswerSupermagLog(d));
FORM exportSupermag 'Экспорт в супермаг' 
    OBJECTS dates = (dFrom = DATE, dTo = DATE) PANEL
    PROPERTIES valFrom = VALUE(dFrom), valTo = VALUE(dTo)
    
    OBJECTS ol = StockDocumentLedger
    PROPERTIES(ol) READONLY isClosed, isPosted, date, nameStock,
                   oObjName = objectClassName, description,
                   nameOperation, nameLegalEntity, nameLegalEntityStock, 
                   sumItem, sumContainer, sum, lastDateTimeSupermagLog,
                   dateTimeAnswerSupermagLog,errorAnswerFileSupermagLog,errorAnswerSupermagLog, isExport
                   
    FILTERS active(ol),            
            date(ol) >= dFrom,
            date(ol) <= dTo
    ORDER date(ol), nameStock(ol)    
    FILTERGROUP open FILTER 'Открыт' isOpened(ol) 'F6'   
    
    OBJECTS l = SupermagLog
    PROPERTIES (l) READONLY id, dateTime, userLogin, nameContact, dateTimeAnswer, errorAnswerFile, errorAnswer, textError
    FILTERS stockDocumentLedger(l)==ol
;
DESIGN exportSupermag {
    BOX {        
        OBJECTS {
            MOVE BOX (dates);
            NEW body {
                fill = 1;
                type = SPLITH;
                MOVE BOX(ol){
                    fill = 2;
                }
                MOVE BOX(l);
            }
        }
    }

}
exportSupermagFilter 'Экспорт в супермаг (фильтр)'() {
    IF exportDirectorySupermag() THEN {
        FOR [ FILTER exportSupermag.ol](StockDocumentLedger l) DO {
            exportSupermag(l);
        }
    } ELSE {
        MESSAGE 'Не задан путь для экспорта файлов в супермаг';
    }
}
EXTEND FORM exportSupermag
    PROPERTIES (ol) exportSupermag TOOLBAR 
    PROPERTIES () exportSupermagFilter TOOLBAR DRAW ol
;

NAVIGATOR {
    stockReports {
        NEW exportSupermag;
    }
}

GROUP POSTOBJECT;
GROUP TOTALPACKAGE;

id = DATA LOCAL STRING();
ERROR = DATA LOCAL STRING(INTEGER);
POSTOBJECTERROR = DATA LOCAL INTEGER(INTEGER);
ERROR1 = DATA LOCAL STRING(INTEGER);
TOTALPACKAGEERROR1 = DATA LOCAL INTEGER(INTEGER);
name = DATA LOCAL STRING();
packageType = DATA LOCAL STRING();

FORM REPLY
PROPERTIES() name ATTR, packageType ATTR
PROPERTIES() IN POSTOBJECT id EXTID 'Id'

OBJECTS ERROR = INTEGER IN POSTOBJECT
PROPERTIES(ERROR) ERROR EXTID 'value'

OBJECTS ERROR1 = INTEGER EXTID 'ERROR' IN TOTALPACKAGE
PROPERTIES(ERROR1) ERROR1 EXTID 'value';

//-- В планировщик
exported = DATA LOCAL NESTED BOOLEAN (StockDocumentLedger);

exportSupermag'Экспорт в супермаг' () {   
    FOR exported(StockDocumentLedger d) DO {
        exportSupermag(d);
    }
}
exportSupermag (DATE dt) {
    IF exportDirectorySupermag() THEN {        
        exported(StockDocumentLedger d) <- NULL;   
        exported(StockDocumentLedger d) <- TRUE WHERE active(d) AND date(d)==dt;   
        exportSupermag(); 
    } ELSE {
        MESSAGE 'Не задан путь для экспорта файлов в супермаг' NOWAIT;
    }

}

importAnswer 'Импорт ответов из супермаг'()  { 
    IF   exportDirectorySupermagReturn() THEN {  
        LOCAL importName = ISTRING[100] (INTEGER ); 
        
        fileName(INTEGER i)<- NULL;    
        listFiles('file://' + exportDirectorySupermagReturn());
        
        fileName(INTEGER i)<- NULL WHERE fileName(i) AND NOT isISubstring(fileName(i),'.Reply');    
        importName(INTEGER i)<- ISTRING[100](replace(replace(upper(fileName(i)),'.REPLY',''), '.XML','')) WHERE fileName(i) ;    
            
        FOR importName(INTEGER j) AND NOT fileIsDirectory(j) AND supermagLog(importName(j)) == SupermagLog log  DO NEWSESSION NESTED (fileName[INTEGER], importName[INTEGER]) {
            dateTimeAnswer(log) <- NULL;
            errorAnswerFile(log) <- NULL;
            errorAnswer(log) <- NULL;
            textError(log) <- NULL;
            LOCAL importFile = XMLFILE (); 
            
            TRY {
                READ 'file://' + exportDirectorySupermagReturn() + '/' + fileName(j) TO importFile;
                IMPORT REPLY XML FROM importFile();
            } CATCH {
                dateTimeAnswer(log)<- currentDateTime();
                errorAnswerFile(log) <- TRUE;
                logToFile('supermagAnswer', CONCAT '\n', 'file://' + fileName(j) + ' ParseMessage error:', messageCaughtException(), javaStackTraceCaughtException(), lsfStackTraceCaughtException());
                APPLY;
                RETURN;    
            }
            dateTimeAnswer(log)<- currentDateTime();
            IF (GROUP SUM 1 IF ERROR(INTEGER ii)) OR (GROUP SUM 1 IF ERROR1(INTEGER ii)) THEN {            
                errorAnswer(log)<- TRUE;                      
                textError(log) <- CONCAT '\n', (GROUP CONCAT ERROR(INTEGER ii) IF ERROR(ii), '\n'), 
                                                (GROUP CONCAT ERROR1(INTEGER ii) IF ERROR1(ii), '\n');  
            } 
            APPLY NESTED (fileName[INTEGER], importName[INTEGER]); 
          
            delete('file://' + exportDirectorySupermagReturn() + '/' + fileName(j));
            fileName(j) <- NULL;
            importName(j) <- NULL;                
            
        }
    } ELSE {
        MESSAGE 'Не задан путь для выгрузки в супермаг' NOWAIT ;
    }
}
EXTEND FORM exportSupermag
    PROPERTIES () importAnswer TOOLBAR DRAW l
;
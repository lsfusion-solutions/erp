MODULE ImportAxaptaRange;

REQUIRE ImportAxapta, RangeLevel;

NAMESPACE ImportAxapta;

importRange 'Импорт ассортиментных матриц' () {
    NEWSESSION {
        LOCAL file = FILE ();    
        LOCAL shopGroupId = STRING[20](INTEGER);
        LOCAL itemId = STRING[10](INTEGER);
        
        IF csvImport() THEN {
            INPUT f = CSVFILE DO {
                IMPORT CSV ';' HEADER NOESCAPE FROM f TO shopGroupId, itemId;
            }
        } ELSE {
            EXTERNAL SQL connectionString() 
                EXEC 'SELECT SHOPGROUPID, ITEMID FROM RETAILSHOPGROUPITEM' TO file;
                IMPORT TABLE FROM file() TO shopGroupId, itemId;
        }
        FOR name(StoreType st) AND NOT range(id(st)) DO NEW range = Range.Range {
            name(range) <- name(storeType(id(st)));
            id(range) <- id(st);
            in(range, rangeLevel('1')) <- TRUE;
            inData(range, SkuGroup sg) <- NOT parent(sg);
            dataLevel(range, st, DATE date) <- rangeLevel('1') IF date = 2020_01_01;
            NEW rr = RangeRev {
                range(rr)<- range;
                dateTime(rr) <- DATETIME (2020_01_01);
            }
        }
        FOR STRING[100] id = id(Range.range(RangeRev rr)) NOINLINE DO {
            level(rr, Item item) <- rangeLevel('1') IF [GROUP SUM 1 BY 'F'+shopGroupId(INTEGER i), itemId(i)](id, id(item));
        }
        APPLY;
    }
}

EXTEND FORM integrationData
    PROPERTIES() importRange
;
    
DESIGN integrationData {
    axaptaButtons {
        NEW importItemGroupCont {
            type = CONTAINERH;
            MOVE PROPERTY (importRange());
        }
    }
}
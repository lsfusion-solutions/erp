MODULE ImportAxaptaRange;

REQUIRE ImportAxapta, RangeLevel;

NAMESPACE ImportAxapta;

importRange 'Импорт ассортиментных матриц' () {
    NEWSESSION {
        LOCAL file = FILE ();    
        LOCAL shopGroupId = STRING[20](INTEGER);
        LOCAL itemId = STRING[10](INTEGER);
        
        IF csvImport() THEN {
            INPUT f = CSVFILE DO {
                IMPORT CSV ';' HEADER NOESCAPE FROM f TO shopGroupId, itemId;
            }
        } ELSE {
            EXTERNAL SQL connectionString() 
                EXEC 'SELECT SHOPGROUPID, ITEMID FROM RETAILSHOPGROUPITEM' TO file;
                IMPORT TABLE FROM file() TO shopGroupId, itemId;
        }
        FOR name(StoreType st) AND NOT range(substr(id(st),2,50)) DO NEW range = Range.Range {
            name(range)<- substr(id(st),2,50);
            id(range)<- substr(id(st),2,50);
            in(range, rangeLevel('1')) <- TRUE;
            inData(range, SkuGroup sg) <- TRUE IF sg = itemGroup('07Отд00000001');
            dataLevel(range, st, DATE date) <- rangeLevel('1') IF date = currentDate();
            NEW rr = RangeRev {
                range(rr)<- range;
                dateTime(rr) <- DATETIME (2020_01_01);
            }
        }
        FOR id(Range.range(RangeRev rr)) DO {
            level (rr, Item item) <- NULL;
            level (rr, Item item) <- rangeLevel('1') WHERE [GROUP SUM 1 BY shopGroupId(INTEGER i), itemId(i)](id(range(rr)), id(item));
        }
        APPLY;
    }
}

EXTEND FORM integrationData
    PROPERTIES() importRange
;
    
DESIGN integrationData {
    axaptaButtons {
        NEW importItemGroupCont {
            type = CONTAINERH;
            MOVE PROPERTY (importRange());
        }
    }
}
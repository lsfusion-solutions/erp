MODULE ImportAxaptaRetail;

REQUIRE ImportAxaptaPriceList, PriceListOver3, PriceListStoreSkip, PriceListOperationDefault, PriceListBefore;

NAMESPACE ImportAxapta;

//импорт видов цен
importPriceType 'Импорт видов цен' () {
    NEWSESSION {
        LOCAL file = FILE ();    
        LOCAL id = STRING[100](INTEGER);
        LOCAL name = ISTRING[50](INTEGER);
                
        IF csvImport() THEN {
            INPUT f = CSVFILE DO {
                IMPORT CSV ';' HEADER NOESCAPE FROM f TO id, name;
            }
        } ELSE {
            EXTERNAL SQL connectionString() 
                EXEC 'SELECT PRICELISTID, NAME FROM PRICELISTTABLE WHERE DATAAREAID = $1' 
                PARAMS areaId() TO file;
                IMPORT TABLE FROM file() TO id, name;
        }

        //настройка операций для импорта
        ////добавление операции
        //не уверен что нужно здесь, можно вынести отдельно
        IF NOT operation('individual') THEN {
            createDefaultOperation('Розничная (магазин)', 'individual');
            FOR id(PriceList.Operation o) == 'individual' DO {
                groupType(o) <- SystemGroupType.skuGroupType;
                
                allRoles(o) <- TRUE;
        
                needStock(o) <- TRUE;                           
                                
                //из-за из-за привязки к магазинам индивид. прайсов для операции         
                doNotSetStocksUserPriceList(o) <- TRUE;
                
                //chainStores не  проходит при создании связок с прайсами пример ГУРМАН2                
                //dataIn(StockGroup g1, o) <- TRUE WHERE id(g1) == 'chainStores';
                allStocks(o) <- TRUE;
                                
                show(DataPriceListType pt, o) <- TRUE WHERE id(pt) == 'supplier';    

                in(Currency c, o) <- TRUE WHERE c == defaultCurrency();
            }  
        }
        
        FOR id(PriceList.Operation o) = 'promotion' DO {
            change(DataPriceListType t, o) <- TRUE WHERE id(t) = 'promotion' AND NOT change(t, o);
            show(DataPriceListType t, o) <- TRUE WHERE id(t) = 'promotion' AND NOT show(t, o);         
            skipConstraintPriceBefore(o) <- TRUE WHERE  NOT skipConstraintPriceBefore(o);         
            doNotSetStocksUserPriceList(o) <- TRUE WHERE NOT doNotSetStocksUserPriceList(o);
            //chainStores не  проходит при создании связок с прайсами пример ГУРМАН2
            dataIn(StockGroup g1, o) <- NULL;
            allStocks(o) <- TRUE WHERE NOT allStocks(o);                  
        }
        
        allowRepeatSku(PriceList.Operation o) <- TRUE WHERE id(o) = 'retail' AND NOT allowRepeatSku(o);       
        ////////////

        IF NOT dataPriceListType('individual') THEN NEW g = DataPriceListType {
            id(g) <- 'individual';
            name(g) <- 'Розничная (магазин)';
            includeVAT(g) <- TRUE;
            change(g, PriceList.operation('individual')) <- TRUE;;
            show(g, PriceList.operation('individual')) <- TRUE;
        }
        
        //пользовательские виды цен
        FOR id(INTEGER i) AND NOT dataPriceListType(id(i)) DO NEW g = DataPriceListType {
            id(g) <- id(i);    
        }
        
        FOR id(INTEGER i) AND DataPriceListType g == dataPriceListType(id(i))  DO {
            name(g) <- name(i);
            skipUnusedStock(g) <- TRUE;
            includeVAT(g) <- TRUE;
            
            change(g, PriceList.Operation o) <- TRUE WHERE o = operation('retail');
            show(g, PriceList.Operation o) <- TRUE WHERE o = operation('retail');                        
        }

        //перегруженные виды цен
        IF NOT dataPriceListType('promotion') THEN NEW g = DataPriceListType {
            id(g) <- 'promotion';
            name(g) <- 'Розничная (акционная)'; 
            includeVAT(g) <- TRUE;          
        }
        
        FOR id(INTEGER i) AND DataPriceListType d = dataPriceListType(id(i)) AND NOT overPriceListType('O' + id(i)) DO NEW g = OverPriceListType {
            id(g) <- 'O' + id(i);
            baseAPriceListLedger(g) <- dataPriceListType('promotion');
            baseBPriceListLedger(g) <- dataPriceListType('individual');
            baseCPriceListLedger(g) <- d;
        }          
                        
        FOR id(INTEGER i) AND OverPriceListType g = overPriceListType('O' + id(i)) DO {
            name(g) <- name(i);
        }        

        //расчетный вид цены
        FOR id(INTEGER i) AND NOT calcPriceListType('C' + id(i)) DO NEW g = CalcPriceListType {
            id(g) <- 'C' + id(i);
               
        }
        
        FOR id(INTEGER i) AND CalcPriceListType g = calcPriceListType('C' + id(i)) DO {
            name(g) <- name(i);  
            includeVAT(g) <- TRUE;
            basePriceListType(g) <- dataPriceListType('supplier'); 
            
            currency(g) <- defaultCurrency();
            roundCondition(g) <- defaultRoundCondition(currency(g));
            country(g) <- defaultCountry();

            groupType(g) <- SystemGroupType.skuGroupType;
            //country(g) <- defaultCountry();
            //currency(t) <- defaultCurrency();
        }       
     
        APPLY;
    }
}

EXTEND FORM integrationData
    PROPERTIES() importPriceType
;

DESIGN integrationData {
    axaptaButtons {
        NEW importPriceTypeCont {
            type = CONTAINERH;
            MOVE PROPERTY (importPriceType());
        }
    }
}

//Импорт привязок видов цен к форматам магазинов
importStoreTypePriceType 'Импорт привязок видов цен к форматам магазинов' () {
    NEWSESSION {
        LOCAL file = FILE ();    
        LOCAL idPriceType = STRING[100](INTEGER);
        LOCAL idStoreType = STRING[100](INTEGER);
                
        IF csvImport() THEN {
            INPUT f = CSVFILE DO {
                IMPORT CSV ';' HEADER NOESCAPE FROM f TO idPriceType, idStoreType;
            }
        } ELSE {
            EXTERNAL SQL connectionString() 
                EXEC 'SELECT PRICELISTID, SHOPGROUPID FROM RETAILSHOPGROUP WHERE DATAAREAID = $1' 
                PARAMS areaId() TO file;
                IMPORT TABLE FROM file() TO idPriceType, idStoreType;
        }
        
        FOR idPriceType(INTEGER i) AND StoreType st = storeType('F' + idStoreType(i))  DO {
            retailCalcPriceListType(st) <- priceListType('C' + idPriceType(i)); 
            retailPriceListType(st) <- priceListType('O' + idPriceType(i));   
        }        
        
//        retailCalcPriceListType(StoreType st) <- NULL WHERE NOT (GROUP MAX INTEGER i IF storeType('F' + idStoreType(i)) = st);
//        retailPriceListType(StoreType st) <- NULL WHERE NOT (GROUP MAX INTEGER i IF storeType('F' + idStoreType(i)) = st);
        
        APPLY;
    }
}

EXTEND FORM integrationData
    PROPERTIES() importStoreTypePriceType
;

DESIGN integrationData {
    axaptaButtons {
        NEW importStoreTypePriceTypeCont {
            type = CONTAINERH;
            MOVE PROPERTY (importStoreTypePriceType());
        }
    }
}

//Импорт надбавок по группам
importGroupMarkup 'Импорт надбавок по группам товаров' () {
    NEWSESSION {
        LOCAL file = FILE ();    
        LOCAL idPriceType = STRING[100](INTEGER);
        LOCAL idItemGroup = STRING[100](INTEGER);
        LOCAL sumMarkup = NUMERIC[8,2](INTEGER);

        IF csvImport() THEN {
            INPUT f = CSVFILE DO {
                IMPORT CSV ';' HEADER NOESCAPE FROM f TO idPriceType, idItemGroup, sumMarkup;
            }
        } ELSE {
            EXTERNAL SQL connectionString() 
                EXEC 'PRICELISTID, ITEMRANGEID, EXTRAPCT FROM PRICEEXTRAITEMRANGE WHERE EXTRAPCT > 0 AND DATAAREAID = $1' 
                PARAMS areaId() TO file;
                IMPORT TABLE FROM file() TO idPriceType, idItemGroup, sumMarkup;
        }

        FOR CalcPriceListType p = calcPriceListType('C' + idPriceType(INTEGER i)) AND ItemGroup g = itemGroup(idItemGroup(i))  DO {
            dataMarkup(p, g) <-  sumMarkup(i); 
        }     
        
//        dataMarkup(CalcPriceListType p, ItemGroup g) <- NULL 
//            WHERE NOT (GROUP MAX INTEGER i IF calcPriceListType('C' + idPriceType(i)) = p AND itemGroup(idItemGroup(i)) = g);
        
        APPLY;
    }
}

EXTEND FORM integrationData
    PROPERTIES() importGroupMarkup
;

DESIGN integrationData {
    axaptaButtons {
        NEW importGroupMarkupCont {
            type = CONTAINERH;
            MOVE PROPERTY (importGroupMarkup());
        }
    }
}

//Импорт надбавок по товарам
importItemMarkup 'Импорт надбавок по товарам' () {
    NEWSESSION {
        LOCAL file = FILE ();    
        LOCAL idPriceType = STRING[100](INTEGER);
        LOCAL idItem = STRING[100](INTEGER);
        LOCAL sumMarkup = NUMERIC[8,2](INTEGER);

        IF csvImport() THEN {
            INPUT f = CSVFILE DO {
                IMPORT CSV ';' HEADER NOESCAPE FROM f TO idPriceType, idItem, sumMarkup;
            }
        } ELSE {
            EXTERNAL SQL connectionString() 
                EXEC 'SELECT PRICELISTID, ITEMID, EXTRAPCT FROM PRICELISTITEM WHERE EXTRAPCT > 0 AND DATAAREAID = $1' 
                PARAMS areaId() TO file;
                IMPORT TABLE FROM file() TO idPriceType, idItem, sumMarkup;
        }

        FOR CalcPriceListType p = calcPriceListType('C' + idPriceType(INTEGER i)) AND Item item = item(idItem(i))  DO {
            dataMarkup(p, item) <-  sumMarkup(i); 
        }     
        
//        dataMarkup(CalcPriceListType p, Item g) <- NULL 
//            WHERE NOT (GROUP MAX INTEGER i IF calcPriceListType('C' + idPriceType(i)) = p AND item(idItem(i)) = g);
        
        APPLY;
    }
}

EXTEND FORM integrationData
    PROPERTIES() importItemMarkup
;

DESIGN integrationData {
    axaptaButtons {
        NEW importItemMarkupCont {
            type = CONTAINERH;
            MOVE PROPERTY (importItemMarkup());
        }
    }
}

//Импорт базовых прайс листов
importRetailPrice 'Импорт базовых прайс листов' () {
    NEWSESSION {
        LOCAL file = FILE ();    
        LOCAL id = STRING[100] (INTEGER);
        LOCAL description = ISTRING[500] (INTEGER); 
        LOCAL isPosted = INTEGER (INTEGER); 
        LOCAL date = DATETIME (INTEGER);         
        LOCAL type = INTEGER (INTEGER);

        IF csvImport() THEN {
            INPUT f = CSVFILE DO {
                IMPORT CSV ';' HEADER NOESCAPE FROM f TO id, description, isPosted, date, type;
            }
        } ELSE {
            EXTERNAL SQL connectionString() 
                EXEC 'SELECT JOURNALID, DESCRIPTION, POSTED, JOURNALDATE, JOURTYPE FROM RETAILESTIMATIONJOUR WHERE DATAAREAID = $1' 
                    PARAMS areaId() TO file;

            IMPORT TABLE FROM file() TO id, description, isPosted, date, type;
        }

        FOR INTEGER i1 = [GROUP MAX INTEGER i BY id(i)](STRING id) AND NOT userPriceList(id) NEW p = UserPriceList DO {
            id(p) <- id;            
            number(p) <- STRING[48](id(i1));
        }

        FOR INTEGER i1 = [GROUP MAX INTEGER i BY id(i)](STRING id) AND UserPriceList p = userPriceList(id) DO {
            PriceList.operation(p) <- IF type(i1) = 1 THEN operation('retail') ELSE operation('individual');
            date(p) <- DATE(date(i1));
            time(p) <- TIME(date(i1));
            fromDate(p) <- DATE(date(i1));
            fromTime(p) <- 00:00;
            isPosted(p) <- TRUE WHERE isPosted(i1) = 1;
            note(p) <- description(i1);
        }

//        DELETE UserPriceList g WHERE operation(g) = operation('retail') AND NOT [GROUP SUM 1 BY id(INTEGER i)](id(g));                      
//        DELETE UserPriceList g WHERE operation(g) = operation('individual') AND NOT [GROUP SUM 1 BY id(INTEGER i)](id(g));               

        APPLY;
    }
}

EXTEND FORM integrationData
    PROPERTIES() importRetailPrice
;

DESIGN integrationData {
    axaptaButtons {
        NEW importRetailPriceCont {
            type = CONTAINERH;
            MOVE PROPERTY (importRetailPrice());
        }
    }
}

//Импорт строк базовых прайс листов
importRetailPriceDetail 'Импорт строк базовых прайс листов' () {
    NEWSESSION {
        LOCAL file = FILE ();    
        LOCAL id = STRING[100] (INTEGER);
        LOCAL idPrice = STRING[100] (INTEGER);
        LOCAL idItem = STRING[100] (INTEGER);

        IF csvImport() THEN {
            INPUT f = CSVFILE DO {
                IMPORT CSV ';' HEADER NOESCAPE FROM f TO idPrice, idItem, id;
            }
        } ELSE {
            EXTERNAL SQL connectionString() 
                EXEC 'SELECT JOURNALID, ITEMID, TRANSID FROM RETAILESTIMATIONTRANS WHERE DATAAREAID = $1' 
                    PARAMS areaId() TO file;

            IMPORT TABLE FROM file() TO idPrice, idItem, id;
        }
        
        FOR id(INTEGER i) AND UserPriceList p = userPriceList(idPrice(i)) AND NOT userPriceListDetail(STRING(id(i))) DO NEW d = UserPriceListDetail {
            id(d) <- STRING(id(i));            
        }
        
        FOR id(INTEGER i) AND UserPriceList p = userPriceList(idPrice(i)) AND UserPriceListDetail d = userPriceListDetail(id(i)) DO {
            userPriceList(d) <- p;
            sku(d) <- item(idItem(i));
        }
        
        DELETE UserPriceListDetail g WHERE operation(g) = operation('retail') AND NOT [GROUP SUM 1 BY id(INTEGER i)](id(g));
        DELETE UserPriceListDetail g WHERE operation(g) = operation('individual') AND NOT [GROUP SUM 1 BY id(INTEGER i)](id(g));

        APPLY;
    }
}

EXTEND FORM integrationData
    PROPERTIES() importRetailPriceDetail
;

DESIGN integrationData {
    axaptaButtons {
        NEW importRetailPriceDetailCont {
            type = CONTAINERH;
            MOVE PROPERTY (importRetailPriceDetail());
        }
    }
}

//Импорт цен базовых прайс листов (есть и retail и  десятков ненулевых строк individual)
importSumRetailPriceDetail 'Импорт цен базовых прайс листов' () {
    NEWSESSION {
        LOCAL file = FILE ();    
        LOCAL idPriceDetail = STRING[100] (INTEGER);
        LOCAL idPriceListType = STRING[100] (INTEGER);
        LOCAL sum = NUMERIC[16,4] (INTEGER);

        IF csvImport() THEN {
            INPUT f = CSVFILE DO {
                IMPORT CSV ';' HEADER NOESCAPE FROM f TO idPriceDetail, idPriceListType, sum;
            }
        } ELSE {
            EXTERNAL SQL connectionString() 
                EXEC 'SELECT TRANSID, PRICELISTID, PRICE FROM RETAILESTIMATIONTRANSPRICE WHERE PRICELISTID != \'\' AND DATAAREAID = $1' 
                    PARAMS areaId() TO file;

            IMPORT TABLE FROM file() TO idPriceDetail, idPriceListType, sum;
        }
        
       
        FOR UserPriceListDetail d = userPriceListDetail(idPriceDetail(INTEGER i)) AND DataPriceListType t = dataPriceListType(idPriceListType(i)) DO {
            price(d, t) <- sum(i) IF sum(i) > 0;
        }
        
//        price(UserPriceListDetail d, DataPriceListType t) <- NULL WHERE price(d, t) AND operation(d) = operation('retail')
//            AND NOT t = dataPriceListType('retail')
//            AND NOT (GROUP MAX INTEGER i IF userPriceListDetail(idPriceDetail(i)) = d AND dataPriceListType(idPriceListType(i)) = t);
// 
//        price(UserPriceListDetail d, DataPriceListType t) <- NULL WHERE price(d, t) AND operation(d) = operation('individual')
//            AND NOT t = dataPriceListType('individual')
//            AND NOT (GROUP MAX INTEGER i IF userPriceListDetail(idPriceDetail(i)) = d AND dataPriceListType(idPriceListType(i)) = t);
        
        APPLY;
    }
}

EXTEND FORM integrationData
    PROPERTIES() importSumRetailPriceDetail
;

DESIGN integrationData {
    axaptaButtons {
        NEW importSumRetailPriceDetailCont {
            type = CONTAINERH;
            MOVE PROPERTY (importSumRetailPriceDetail());
        }
    }
}

//Импорт цен индивидуальных прайсов и привязок к складам (не все строки из файла есть в  файле импорта базовых строк)
importSumIndividualPriceDetail 'Импорт цен индивидуальных прайсов и привязок к складам' () {
    NEWSESSION {
        LOCAL file = FILE ();    
        LOCAL idPriceDetail = STRING[100] (INTEGER);
        LOCAL idStock = STRING[100] (INTEGER);
        LOCAL sum = NUMERIC[16,4] (INTEGER);

        IF csvImport() THEN {
            INPUT f = CSVFILE DO {
                IMPORT CSV ';' HEADER NOESCAPE FROM f TO idPriceDetail, idStock, sum;
            }
        } ELSE {
            EXTERNAL SQL connectionString() 
                EXEC 'SELECT TRANSID, INVENTLOCATIONID_SHOP, PRICE FROM RETAILESTIMATIONTRANSPRICE WHERE INVENTLOCATIONID_SHOP != \'\' AND DATAAREAID = $1' 
                    PARAMS areaId() TO file;

            IMPORT TABLE FROM file() TO idPriceDetail, idStock, sum;
        }
           
        FOR [GROUP MAX idPriceDetail(INTEGER i) BY userPriceList(userPriceListDetail(idPriceDetail(i))),  stock(idStock(i))](UserPriceList p, Stock s) DO {
            dataIn(p, s) <- TRUE;
        }
        
        FOR NUMERIC[16,4] s = [GROUP MAX sum(INTEGER i) BY idPriceDetail(i)](STRING[100] id) 
            AND UserPriceListDetail d = userPriceListDetail(id)  DO {
            price(d, dataPriceListType('individual')) <- s IF s > 0;
        }

//        dataIn(UserPriceList p, Stock s) <- NULL WHERE dataIn(p, s) AND operation(p) = operation('individual')
//            AND NOT (GROUP MAX INTEGER i IF userPriceList(userPriceListDetail(idPriceDetail(i))) = p AND stock(idStock(i)) = s);
//
//        price(UserPriceListDetail d, dataPriceListType('individual')) <- NULL WHERE price(d, dataPriceListType('individual')) AND operation(d) = operation('individual')
//            AND NOT (GROUP MAX INTEGER i IF userPriceListDetail(idPriceDetail(i)) = d AND sum(i) > 0);
        
        APPLY;
        
        }
}

EXTEND FORM integrationData
    PROPERTIES() importSumIndividualPriceDetail
;

DESIGN integrationData {
    axaptaButtons {
        NEW importSumIndividualPriceDetailCont {
            type = CONTAINERH;
            MOVE PROPERTY (importSumIndividualPriceDetail());
        }
    }
}

//Импорт акционных прайс листов
importPromotionPrice 'Импорт акционных прайс листов' () {
    NEWSESSION {
        LOCAL file = FILE ();    
        LOCAL NESTED id = STRING[100] (INTEGER);
        LOCAL number = STRING[100] (INTEGER); 
        LOCAL NESTED isPosted = INTEGER (INTEGER); 
        LOCAL name = STRING[100] (INTEGER); 
        LOCAL fromDate = DATETIME (INTEGER);
        LOCAL toDate = DATETIME (INTEGER);

        IF csvImport() THEN {
            INPUT f = CSVFILE DO {
                IMPORT CSV ';' HEADER NOESCAPE FROM f TO id, number, isPosted, name, fromDate, toDate;
            }
        } ELSE {
            EXTERNAL SQL connectionString() 
                EXEC 'SELECT acg.ADVERTCAMPAIGNID + \'/\' + ADVERTLOCATIONLISTID, ' + 
                            'acg.ADVERTCAMPAIGNID, ac.HASTRANS, ac.ADVERTCAMPAIGNNAME, ' + 
                            'ac.DATEBEGIN, ac.DATEEND' + 
                      'FROM (SELECT ADVERTCAMPAIGNID, ADVERTLOCATIONLISTID FROM ADVERTCAMPAIGNLINES ' + 
                            'WHERE RETAILPRICEADVERT > 0 GROUP BY ADVERTCAMPAIGNID, ADVERTLOCATIONLISTID) acg' +
                      'JOIN ADVERTCAMPAIGN ac ON ac.ADVERTCAMPAIGNID = acg.ADVERTCAMPAIGNID '+ 
                      'WHERE DATAAREAID = $1' 
                    PARAMS areaId() TO file;

            IMPORT TABLE FROM file() TO id, number, isPosted, name, fromDate, toDate;
        }

        FOR INTEGER i1 = [GROUP MAX INTEGER i BY id(i)](STRING id) AND NOT userPriceList(id) NEW p = UserPriceList DO {
            id(p) <- id;            
            number(p) <- (number(i1));
            PriceList.operation(p) <- operation('promotion');            
        }

        FOR INTEGER i1 = [GROUP MAX INTEGER i BY id(i)](STRING id) AND UserPriceList p = userPriceList(id) DO {
            isPosted(p) <- TRUE IF isPosted(i1) = 1;
            note(p) <- name(i1);
            fromDate(p) <- DATE(fromDate(i1));
            fromTime(p) <- 00:00;
            toDate(p) <- DATE(toDate(i1));
            toTime(p) <- 00:00; 
            //из-за constraint-а Дата с (Цена до) и Дата по (Цена до)не могут быть в интервале самого прайса
            fromPriceBeforeDate(p) <- sum(fromDate(p), -1);
            fromPriceBeforeTime(p) <- 23:59; 
        }

//        DELETE UserPriceList g WHERE PriceList.operation(g) = PriceList.operation('promotion') AND NOT [GROUP SUM 1 BY id(INTEGER i)](id(g));                      

        APPLY;
        
        //альтернатива sum(fromDate(p), -1);
        // проставляется вторым APPLY  из-за constraint-а Дата с (Цена до) и Дата по (Цена до)не могут быть в интервале самого прайса
        ///можно проставлять в импорте строк (но выходит медленно)
//        FOR INTEGER i1 = [GROUP MAX INTEGER i BY id(i)](STRING id) AND UserPriceList p = userPriceList(id) DO {
//            isPosted(p) <- TRUE IF isPosted(i1) = 1;
//            toPriceBeforeDate(p) <- NULL;
//            toPriceBeforeTime(p) <- NULL;            
//            fromPriceBeforeDate(p) <- NULL;
//            fromPriceBeforeTime(p) <- NULL;        
//        }
//        APPLY;
    }
}

EXTEND FORM integrationData
    PROPERTIES() importPromotionPrice
;

DESIGN integrationData {
    axaptaButtons {
        NEW importPromotionPriceCont {
            type = CONTAINERH;
            MOVE PROPERTY (importPromotionPrice());
        }
    }
}

//Импорт строк акционных прайс листов
importPromotionPriceDetail 'Импорт строк акционных прайс листов' () {
    NEWSESSION {
        LOCAL file = FILE ();    
        LOCAL id = STRING[100] (INTEGER);
        LOCAL idPrice = STRING[100] (INTEGER);
        LOCAL idItem = STRING[100] (INTEGER);
        
        LOCAL sum = NUMERIC[16,4] (INTEGER);
        LOCAL sumBefore = NUMERIC[16,4] (INTEGER);

        IF csvImport() THEN {
            INPUT f = CSVFILE DO {
                IMPORT CSV ';' HEADER NOESCAPE FROM f TO id, idPrice, idItem, sum, sumBefore;
            }
        } ELSE {
            EXTERNAL SQL connectionString() 
                EXEC 'SELECT ADVERTLINEID, acl.ADVERTCAMPAIGNID + \'/\' + ADVERTLOCATIONLISTID,' + 
                             'ITEMID, CASE WHEN ac.ADVERTCLNSTATUS = 3 THEN 0.0 ELSE RETAILPRICEADVERT END, RETAILPRICEREGULAR ' + 
                      'FROM ADVERTCAMPAIGNLINES acl JOIN ADVERTCAMPAIGN ac ON ac.ADVERTCAMPAIGNID = acl.ADVERTCAMPAIGNID WHERE RETAILPRICEADVERT > 0 AND '+ 
                      'WHERE DATAAREAID = $1'
                    PARAMS areaId() TO file;

            IMPORT TABLE FROM file() TO id, idPrice, idItem, sum, sumBefore;
        }

        FOR id(INTEGER i) AND UserPriceList p = userPriceList(idPrice(i)) AND NOT userPriceListDetail(STRING(id(i))) DO NEW d = UserPriceListDetail {
            id(d) <- STRING(id(i));            
        }
        
        FOR id(INTEGER i) AND UserPriceList p = userPriceList(idPrice(i)) AND UserPriceListDetail d = userPriceListDetail(id(i)) DO {
            userPriceList(d) <- p;
            sku(d) <- item(idItem(i));
            price(d, dataPriceListType('promotion')) <- sum(i) IF sum(i) > 0;
            priceBefore(d) <- sumBefore(i);
        }
        
        //DELETE UserPriceListDetail g WHERE PriceList.operation(g) = PriceList.operation('promotion') AND NOT [GROUP SUM 1 BY id(INTEGER i)](id(g));

        APPLY;
    }
}

EXTEND FORM integrationData
    PROPERTIES() importPromotionPriceDetail
;

DESIGN integrationData {
    axaptaButtons {
        NEW importPromotionPriceDetailCont {
            type = CONTAINERH;
            MOVE PROPERTY (importPromotionPriceDetail());
        }
    }
}

//Импорт привязки акций к складам
importPromotionPriceStock 'Импорт привязки акций к складам' () {
    NEWSESSION {
        LOCAL file = FILE ();    
        LOCAL idPrice = STRING[100] (INTEGER);
        LOCAL idStock = STRING[100] (INTEGER);

        IF csvImport() THEN {
            INPUT f = CSVFILE DO {
                IMPORT CSV ';' HEADER NOESCAPE FROM f TO idPrice, idStock;
            }
        } ELSE {
            EXTERNAL SQL connectionString() 
                EXEC 'SELECT ADVERTCAMPAIGNID + \'/\' + ADVERTLOCATIONLISTID, INVENTLOCATIONID FROM ADVERTLOCATIONLISTLINES WHERE DATAAREAID = $1' 
                    PARAMS areaId() TO file;

            IMPORT TABLE FROM file() TO idPrice, idStock;
        }
           
        FOR [GROUP MAX idPrice(INTEGER i) BY userPriceList(idPrice(i)),  stock(idStock(i))](UserPriceList p, Stock s) DO {
            dataIn(p, s) <- TRUE;
        }

//        dataIn(UserPriceList p, Stock s) <- NULL WHERE dataIn(p, s) AND operation(p) = operation('promotion')
//            AND NOT (GROUP MAX INTEGER i IF userPriceList(userPriceListDetail(idPriceDetail(i))) = p AND stock(idStock(i)) = s);
        
        APPLY;        
    }
}

EXTEND FORM integrationData
    PROPERTIES() importPromotionPriceStock
;

DESIGN integrationData {
    axaptaButtons {
        NEW importPromotionPriceStockCont {
            type = CONTAINERH;
            MOVE PROPERTY (importPromotionPriceStock());
        }
    }
}
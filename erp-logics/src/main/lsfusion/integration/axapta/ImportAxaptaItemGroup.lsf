MODULE ImportAxaptaItemGroup;

REQUIRE ImportAxapta, Item;

NAMESPACE ImportAxapta;

sqlImportItemGroup 'Импорт из SQL' = DATA BOOLEAN ();

importItemGroup 'Импорт товарных групп' () {
    NEWSESSION {
        LOCAL file = FILE ();    
        LOCAL id = STRING[100](INTEGER);
        LOCAL idParent = STRING[100](INTEGER);
        LOCAL prefix = ISTRING[250] (INTEGER);
        LOCAL name = ISTRING[250] (INTEGER);
        
        IF sqlImportItemGroup() THEN {
            EXTERNAL SQL connectionString() 
                EXEC 'SELECT RANGEID, RANGEIDPARENT, PREFIX, NAME FROM INVENTITEMRANGE WHERE DATAAREAID = $1' 
                PARAMS areaID() TO file; 
        } ELSE {
            INPUT f = CSVFILE DO {
                IMPORT CSV ';' FROM f TO id, idParent, prefix, name;
            }
        }
               
        FOR id(INTEGER i) AND NOT itemGroup(id(i)) DO NEW g = ItemGroup {
            id(g) <- id(i);
        }
        
        FOR id(INTEGER i) AND ItemGroup g == itemGroup(id(i)) DO {
            name(g) <- CONCAT ' ', prefix(i), name(i);
            parent(g) <- itemGroup(idParent(i));
        }     
        DELETE ItemGroup g WHERE g IS ItemGroup AND NOT [GROUP SUM 1 BY id(INTEGER i)](id(g));                   
        APPLY;
    }
}

EXTEND FORM integrationData
PROPERTIES() importItemGroup, sqlImportItemGroup;
DESIGN integrationData{
    axaptaButtons{
        NEW importItemGroupCont {
            type = CONTAINERH;
            MOVE PROPERTY (importItemGroup());
            MOVE PROPERTY (sqlImportItemGroup());
        }
    }
}
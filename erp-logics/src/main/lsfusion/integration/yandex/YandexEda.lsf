MODULE YandexEda;

REQUIRE SaleInternetOrder, DeliveryOption, ItemInternet, ItemNutrition, ItemStorage;

NAMESPACE YandexEda;

TABLE settings() NODEFAULT;

@defineLogRotate(yandex, 5, weekly);
//-------------------- АУТЕНТИФИКАЦИЯ ------------------------//
CLASS YandexEdaAuth 'Аутентификация ЯндексЕда';

id 'Код' = DATA STRING (YandexEdaAuth);
clientId 'Client ID' = DATA STRING[100] (YandexEdaAuth);
clientSecret 'Client Secret'= DATA STRING[100] (YandexEdaAuth);
grantType = DATA STRING[100] (YandexEdaAuth);
scope = DATA STRING[20] (YandexEdaAuth);
activeToken = DATA STRING (YandexEdaAuth);

//сессии
CLASS YandexEdaSession 'Сессии';
TABLE YandexEdaSession(YandexEdaSession);

auth = DATA YandexEdaAuth (YandexEdaSession);
dateTime 'Дата/время' = DATA DATETIME (YandexEdaSession);
expired 'Истёк'= DATA BOOLEAN (YandexEdaSession);
id 'Токен' = DATA STRING (YandexEdaSession) IN id CHARWIDTH 10;
INDEX id(YandexEdaSession ys), expired(ys);
yandexEdaSession = GROUP AGGR YandexEdaSession yandexEdaSession BY id(yandexEdaSession) 
    IF NOT expired(yandexEdaSession);

beforeAuthorization ABSTRACT LIST ();

EXTEND FORM integrationData
    OBJECTS yandex = YandexEdaAuth
    PROPERTIES (yandex) id, clientId, clientSecret
    PROPERTIES (yandex) NEW, EDIT, DELETE
    
    OBJECTS yandexSession = YandexEdaSession LAST
    PROPERTIES (yandexSession) READONLY dateTime, expired, id
    FILTERS auth(yandexSession) = yandex
    FILTERGROUP expired
        FILTER 'Активные' NOT expired(yandexSession) DEFAULT 
;


DESIGN integrationData{
    pane{
        delivery{
            NEW yandexEda{
                caption = 'YandexEda';
                MOVE BOX (yandex);
                MOVE BOX (yandexSession);
            }
        }
    }
}


//-------------------- /security/oauth/token------------------------//
security_oauth_token (STRING client_id, STRING client_secret, STRING grant_type, STRING scope) {
    IF NOT (GROUP MAX YandexEdaAuth u IF client_id == clientId(u) AND clientSecret(u) == client_secret) THEN {
        statusHttpTo() <- 401;
        EXPORT JSON FROM code = 100,
            description = 'Не найден пользователь';
    }
    ELSE {
        generateUUID();
        NEW y = YandexEdaSession {
            auth(y) <- GROUP MAX YandexEdaAuth u IF client_id == clientId(u) AND clientSecret(u) == client_secret;
            id(y) <- generatedUUID();
            dateTime(y) <- currentDateTime();
        }
        APPLY NESTED LOCAL;
        beforeAuthorization();
        EXPORT JSON FROM access_token = generatedUUID();
    }
}@@noauth;

//-------------------- v1/security/oauth/token------------------------//
v1_security_oauth_token (STRING client_id, STRING client_secret, STRING grant_type, STRING scope) {
    security_oauth_token (client_id, client_secret, grant_type, scope);
}@@noauth;

//-------------------- НОМЕНКЛАТУРА------------------------//

yandexName (Item i) = ABSTRACT ISTRING;

GROUP description;

parentId (Group g) = id(parent(g));
categoryId (Item i) = ABSTRACT CASE STRING[100];
modiffersGroup (Item i) = ABSTRACT STRING;

//description
general (Item i) = ABSTRACT STRING;
composition (Item i) = ABSTRACT STRING;
nutritionalValue (Item i) = ABSTRACT STRING;
purpose (Item i) = ABSTRACT STRING; 
packageInfo (Item i) = ABSTRACT STRING;
vendorName (Item i) = ABSTRACT STRING;
isCatchWeight (Sku i) = IF (id(UOM(i)) = 'кг.') OR nameUOM(i) = 'КГ' OR nameUOM(i) = 'кг' OR shortNameUOM(i) = 'кг' OR shortNameUOM(i) = 'кг.' THEN TTRUE ELSE TFALSE;
quantum (Item i) = ABSTRACT NUMERIC[16,5];
oldPrice (Item i, DepartmentStore d) = ABSTRACT NUMERIC[16,4];
customOldPrice(Item i, DepartmentStore d) = ABSTRACT NUMERIC[16,4];
available(Item i, DepartmentStore s) = ABSTRACT NUMERIC[16,5];

inOnlineYandex (Item i, DepartmentStore d) = ABSTRACT CASE BOOLEAN;

stock (YandexEdaAuth client, STRING id) = ABSTRACT DepartmentStore;

oldPrice (Item i, DepartmentStore d) += customOldPrice(i, d);

CLASS ModiffersGroup 'Группы модификаторов';

GROUP barcode;

//измерения
GROUP measure;
GROUP volume;

initcap (TEXT p1) = FORMULA 'initcap($1)';
nameCat (STRING name) = replace(lower(name), getWord(lower(name), ' ', 1), initcap(getWord(lower(name), ' ', 1)));

limitPerOrder = ABSTRACT INTEGER (Item);

FORM nomenclature 'Номенклатура ЯндексЕда'
    OBJECTS s = DepartmentStore PANEL
    
    OBJECTS g = Group EXTID 'categories'
    PROPERTIES id(g), = nameCat(name(g)) EXTID 'name', parentId(g), positionOver(g) EXTID 'sortOrder'
    FILTERS onlineCategory(g)
    
    OBJECTS i = Item EXTID 'items'
    PROPERTIES id(i), oldPrice(i,s), yandexName(i) EXTID 'name', categoryId(i), limitPerOrder(i),
         = INTEGER(valueVAT(i)) EXTID 'vat', = OVERRIDE id(i), '' EXTID 'vendorCode',
         isCatchWeight(i), currentPrice(i,s) EXTID 'price'
    PROPERTIES IN measure = INTEGER(netWeight(i) * 1000) EXTID 'value', = IF netWeight(i) THEN 'GRM' EXTID 'unit', quantum(i)
    PROPERTIES IN volume = round0(volume(i)) EXTID 'value', = IF volume(i) THEN 'DMQ' EXTID 'unit'
    PROPERTIES IN barcode idBarcode(i) EXTID 'value', = IF idBarcode(i) THEN 'ean13' EXTID 'type', 
        = IF isCatchWeight(i) == TTRUE THEN 'ean13-tail-gram-5' ELSE 'none' EXTID 'weightEncoding'
    PROPERTIES IN description general(i), composition(i), nutritionalValue(i), purpose(i), conditionsText(i) EXTID 'storageRequirements',
        = OVERRIDE expiryText(i), STRING(expiryDays(i)) EXTID 'expiresIn', = OVERRIDE nameCountry(i), '' EXTID 'vendorCountry', packageInfo(i), 
        vendorName(i)
    
    OBJECTS images = Item EXTID 'images'
    PROPERTIES urlHttpAccess(images) EXTID 'url', hashImage(images) EXTID 'hash'
    FILTERS images = i
        
    FILTERS inOnlineYandex(i,s)
;  

    
FORM availability 'Наличие Яндекс.Еда'
    OBJECTS s = DepartmentStore PANEL
    
    OBJECTS i = Item EXTID 'items'
    PROPERTIES id(i), available(i, s) EXTID 'stock'
    FILTERS inOnlineYandex(i, s)
; 

FORM prices 'Цены Яндекс.Еда'
    OBJECTS s = DepartmentStore PANEL

    OBJECTS i = Item EXTID 'items'
    PROPERTIES id(i), currentPrice(i, s) EXTID 'price', = INTEGER(valueVAT(i)) EXTID 'vat', oldPrice(i, s)
    FILTERS id(i), currentPrice(i, s), inOnlineYandex(i, s)
;

carrierSelect ABSTRACT LIST(YandexEdaAuth);
beforeExportData ABSTRACT LIST (YandexEdaAuth);
afterExportAvailability ABSTRACT LIST (YandexEdaAuth);
afterExport ABSTRACT LIST (YandexEdaAuth, DepartmentStore, STRING );

//блокировка запросов
useLockRequests 'Использовать блокировку более одного запроса' = DATA BOOLEAN () TABLE settings;
EXTEND FORM integrationData
    PROPERTIES useLockRequests()
;
DESIGN integrationData{
    yandexEda{
        MOVE PROPERTY (useLockRequests());
    }
}

lockRequest = DATA BOOLEAN () TABLE settings;
lockRequestResult = DATA LOCAL NESTED BOOLEAN ();

WHEN CHANGED (useLockRequests()) DO lockRequest() <- NULL;

lockRequest() {
    IF useLockRequests() THEN NEWSESSION {
        IF lockRequest() THEN {
            lockRequestResult() <- TRUE;
            statusHttpTo() <- 500;
            logToFile('yandex', CONCAT ' : ', 'WARNING Запрос заблокирован');
        } ELSE {
            lockRequest() <- TRUE;
            APPLY NESTED LOCAL; 
        } 
    }
}
unlockRequest(){
    IF useLockRequests() THEN NEWSESSION{
        lockRequest() <- NULL;
        APPLY;
    }
}
onStarted()+{
    unlockRequest();
}

//-------------------- /v1/nomenclature/{placeId}/composition(availability)------------------------//
v1_nomenclature(FILE f){
    logToFile('yandex', CONCAT ' : ', 'Поступил запрос ', servletPath() + pathInfo());
    LOCAL tokenClient = STRING ();
    tokenClient() <- OVERRIDE getWord(headers('Authorization'), ' ', 2), getWord(headers('authorization'), ' ', 2);
    LOCAL aggr = YandexEdaAuth();
    aggr() <- auth(yandexEdaSession(tokenClient()));
    IF NOT (aggr()) THEN {
        statusHttpTo() <- 401;
        logToFile('yandex', CONCAT ' : ', 'ERROR Не найден токен авторизации', tokenClient());
        EXPORT JSON FROM code = 100, description = 'Не найден токен авторизации';
    }
    ELSE {
        lockRequest();
        IF lockRequestResult() THEN {
            EXPORT JSON FROM code = 100, description = 'Запросы заблокированны, повторите попытку позже';
            RETURN;
        }
        carrierSelect(aggr());
        LOCAL method = STRING ();
        method() <- replace(servletPath() + pathInfo(), TEXT('/exec/v1/nomenclature/'), TEXT (''));
        LOCAL store = STRING ();
        LOCAL param = STRING ();
        store() <- getWord(method(), '/', 1);
        param() <- getWord(method(), '/', 2);
        LOCAL s = DepartmentStore();
        s() <- stock(aggr(), store());
        IF NOT s() THEN {
            statusHttpTo() <- 404;
            logToFile('yandex', CONCAT ' : ', ' ERROR Не найден магазин', store());
            EXPORT FROM error = 'Не найден магазин', storeId = store();
        }
        LOCAL countItems = INTEGER ();
        countItems() <- GROUP SUM 1 IF inOnlineYandex(Item i, s());
        IF param() == 'availability' THEN {
            logToFile('yandex', CONCAT ' : ', 'Запрос остатков', countItems() + ' товаров');
            EXPORT availability OBJECTS s = s() JSON;
            afterExportAvailability(aggr());
        }
        IF param() == 'composition' THEN{
            beforeExportData(aggr());
            logToFile('yandex', CONCAT ' : ', 'Запрос номенклатуры', countItems() + ' товаров');
            EXPORT nomenclature OBJECTS s = s() JSON;    
        }
        IF param() == 'prices' THEN {
            logToFile('yandex', CONCAT ' : ', 'Запрос цен', countItems() + ' товаров');
            EXPORT prices OBJECTS s = s() JSON;
        }
        afterExport(aggr(), s(), param());
        unlockRequest();
    }
}@@noauth;

closeExpiriedToken(){
    expired(YandexEdaSession y) <- TRUE IF NOT expired(y) AND DATE(dateTime(y)) < currentDate();
    DELETE YandexEdaSession y WHERE expired(y);
}

//-------------------- /v1/menu/{placeId}/promos------------------------//
v1_menu(FILE f){
    EXPORT JSON FROM result = 'ok';
}
MODULE YandexEdaOrder;

REQUIRE YandexEda, SaleInternetOrderIntegration, SaleInternetPicking;

NAMESPACE YandexEda;

GROUP deliveryInfo;
GROUP deliveryAddress : deliveryInfo;
GROUP paymentInfo;

eatsId = DATA LOCAL STRING();
persons = DATA LOCAL STRING();
phoneNumber = DATA LOCAL STRING();
clientName = DATA LOCAL STRING();
courierArrivementDate = DATA LOCAL STRING();
comment = DATA LOCAL STRING();
restaurantId = DATA LOCAL STRING();
quantity = DATA LOCAL NUMERIC(INTEGER);
price = DATA LOCAL NUMERIC(INTEGER);
name = DATA LOCAL STRING(INTEGER);
id = DATA LOCAL STRING(INTEGER);
itemsPromos = DATA LOCAL INTEGER(INTEGER);
discount = DATA LOCAL STRING(INTEGER);
type = DATA LOCAL STRING(INTEGER);
itemsModifications = DATA LOCAL INTEGER(INTEGER);
quantity1 = DATA LOCAL STRING(INTEGER);
price1 = DATA LOCAL NUMERIC(INTEGER);
name1 = DATA LOCAL STRING(INTEGER);
id1 = DATA LOCAL STRING(INTEGER);
discount1 = DATA LOCAL NUMERIC(INTEGER);
type1 = DATA LOCAL STRING(INTEGER);
itemsCost = DATA LOCAL NUMERIC();
paymentType = DATA LOCAL STRING();
discriminator = DATA LOCAL STRING();

FORM yandexorder FORMEXTID 'null'
    PROPERTIES() eatsId, persons, comment, restaurantId, discriminator
    PROPERTIES() IN deliveryInfo phoneNumber, clientName, courierArrivementDate

    OBJECTS items = INTEGER
    PROPERTIES(items) quantity, price, name, id
    FILTERS imported(items)

    OBJECTS promos = INTEGER
    FILTERS itemsPromos(promos) == items
    PROPERTIES(promos) discount, type
    FILTERS imported(promos)

    OBJECTS modifications = INTEGER
    FILTERS itemsModifications(modifications) == items
    PROPERTIES(modifications) quantity1 EXTID 'quantity', price1 EXTID 'price', name1 EXTID 'name', id1 EXTID 'id'
    FILTERS imported(modifications)

    OBJECTS promos1 = INTEGER EXTID 'promos'
    PROPERTIES(promos1) discount1 EXTID 'discount', type1 EXTID 'type'
    FILTERS imported(promos1)
    PROPERTIES() IN paymentInfo itemsCost, paymentType
;

itemById (STRING id) = GROUP LAST Item i ORDER id(i) WHERE id(i) == id;
stockYandexOrder (STRING id) = ABSTRACT DepartmentStore;
restarauntId (DepartmentStore s) = ABSTRACT STRING;
carrierOrder (YandexEdaAuth a) = ABSTRACT Carrier;


yandexCarrier = GROUP MAX Carrier r IF id(r) = 'yandex';
paymentTypeOrder = ABSTRACT CASE PaymentTypeInternetOrder (STRING);
paymentTypeOrder(STRING type) += WHEN type = 'CASH' THEN PaymentTypeInternetOrder.inFact;
paymentTypeOrder(STRING type) += WHEN type = 'CARD' THEN PaymentTypeInternetOrder.prepayment;

//paymentTypeOrder (STRING type) = CASE 
//    WHEN type = 'CASH' THEN PaymentTypeInternetOrder.
 
//статусы заказа
useCustomOrderStatus = ABSTRACT BOOLEAN ();
yandexEdaOrderStatusCustom = ABSTRACT STRING (InternetOrder);
yandexEdaOrderStatusDefault (InternetOrder o) = CASE 
    WHEN status(o) = StatusInternetOrder.new THEN 'ACCEPTED_BY_RESTAURANT'
    WHEN status(o) = StatusInternetOrder.isPicking THEN 'COOKING'
    WHEN status(o) = StatusInternetOrder.picked THEN 'READY'
    WHEN status(o) = StatusInternetOrder.delivery THEN 'TAKEN_BY_COURIER'
    WHEN status(o) = StatusInternetOrder.delivered THEN 'DELIVERED'
    WHEN status(o) = StatusInternetOrder.closed THEN 'CANCELLED'
;
yandexEdaOrderStatus (InternetOrder o) = IF useCustomOrderStatus() THEN yandexEdaOrderStatusCustom(o) ELSE yandexEdaOrderStatusDefault(o);

httpHeadersYandex(TEXT name) = CASE
    WHEN name == 'Content-Type' THEN 'application/vnd.eats.order.v2+json';
courierArrivementDateText = DATA STRING (InternetOrder);
courierArrivementDate (InternetOrder o) += WHEN carrier(o) == yandexCarrier() THEN DATETIME (courierArrivementDateText(o));

afterCreateOrder ABSTRACT LIST (InternetOrder);
priceAggrRound(NUMERIC[16,4] price, Item s)  = IF mod(price*quantum(s)*1000, 10) > 0 AND isCatchWeight(s) 
    THEN price + 0.01 ELSE price;
priceQuantum(NUMERIC[18,4] price, Item i) = IF quantum(i) THEN (price/quantum(i)) ELSE price;
qtyQuantum(NUMERIC[18,4] qty, Item i) = IF quantum(i) THEN qty*quantum(i) ELSE qty;

priceYandex 'Цена Яндекс' = DATA NUMERIC[18,4] (InternetOrderDetail);
qtyYandex 'Кол-во Яндекс' = DATA NUMERIC[18,4] (InternetOrderDetail);

EXTEND FORM internetOrder
    PROPERTIES (d) priceYandex, qtyYandex SHOWIF carrier(o) = yandexCarrier()
;
afterCreateDetails ABSTRACT LIST (INTEGER, InternetOrderDetail);
defaultNameCustomer = ABSTRACT STRING[255] ();
notUseRoundPrice = ABSTRACT CASE BOOLEAN (Carrier);
logOrder ABSTRACT LIST (InternetOrder, FILE);

createOrder(FILE f){
    LOCAL tokenClient = STRING ();
    tokenClient() <- OVERRIDE getWord(headers('Authorization'), ' ', 2), getWord(headers('authorization'), ' ', 2);;
    logToFile('yandex', CONCAT ' : ', 'Create order');
    LOCAL aggr = YandexEdaAuth();
    aggr() <- auth(yandexEdaSession(tokenClient()));//GROUP MAX YandexEdaAuth u IF activeToken(u) == tokenClient();
    IF NOT (aggr()) THEN {
        statusHttpTo() <- 401;
        EXPORT JSON FROM code = 100, description = 'Не найден токен авторизации';
    }
    ELSE {
        IMPORT yandexorder FROM JSONFILE (f);//f;
        LOCAL yandexOrder = InternetOrder ();
        yandexOrder() <- GROUP MAX InternetOrder o IF idAggregator(o) = eatsId() AND id(carrier(o)) = id(aggr());
        IF yandexOrder() THEN {
            EXPORT JSON FROM result = 'OK', orderId = uuid(yandexOrder());
            RETURN;
        }
        NEW order = InternetOrder {
            logOrder(order, f);
            carrier(order) <- carrierOrder(aggr());
            number(order) <- eatsId();
            idAggregator(order) <- eatsId();
            supplierStock(order) <- stock(aggr(), restaurantId());
            customerComment(order) <- comment();
            nameInternetCustomer(order) <- STRING[255](OVERRIDE defaultNameCustomer(), clientName());
            phoneInternetCustomer(order) <- STRING[100](phoneNumber());
            courierArrivementDateText(order) <- courierArrivementDate();
            isPosted(order) <- TRUE;
            deliveryDate(order) <- currentDate();
            uuid(order) <- STRING(LONG(order));
            FOR id(INTEGER items) NEW detail = InternetOrderDetail DO{
                priceYandex(detail) <- NUMERIC[18,4](price(items));
                qtyYandex(detail) <- NUMERIC[18,4](quantity(items));
                sku(detail) <- itemById(id(items));
                quantity(detail) <- NUMERIC[16,5](quantity(items));
                price(detail) <- IF notUseRoundPrice(carrier(order)) THEN NUMERIC[16,4](price(items)) 
                    ELSE priceAggrRound(NUMERIC[16,4](price(items)), sku(detail));
                internetOrder(detail) <- order;
                afterCreateDetails(items, detail);
            }
            paymentTypeInternetOrder(order) <- paymentTypeOrder(paymentType());
            paymentCardSumInternetOrder(order) <- NUMERIC[18,4](itemsCost()) WHERE paymentType() = 'CARD';
            paymentCashSumInternetOrder(order) <- NUMERIC[18,4](itemsCost()) WHERE paymentType() = 'CASH';
            paymentERIPSumInternetOrder(order) <- NUMERIC[18,4](itemsCost()) WHERE paymentType() = 'ONLINE';
            afterCreateOrder(order);
            APPLY ;
            IF canceled() THEN {
                statusHttpTo() <- 500;
                EXPORT JSON FROM result = 'error', description = applyMessage();
            }
            EXPORT JSON FROM result = 'OK', orderId = uuid(order);
        }
    } 
}@@noauth;  

internetOrderByUuid (STRING id) = GROUP LAST InternetOrder order ORDER order 
    WHERE uuid(order) == id;

orderNotFound(){
    statusHttpTo() <- 404;
    EXPORT JSON FROM code = 100, description = 'Не найден заказ';
}

extraUpdateOrder ABSTRACT LIST (InternetOrder);
afterUpdateDetails ABSTRACT LIST (INTEGER, InternetOrderDetail);
afterUpdateDetailsChange ABSTRACT LIST (INTEGER, InternetOrderDetail);

statusUpdate = DATA LOCAL STRING();

FORM closeOrder
    PROPERTIES() statusUpdate EXTID 'status', eatsId, comment;

customDeleteOrderStatus = ABSTRACT CASE StatusInternetOrder (InternetOrder);

customCancelOrder = ABSTRACT BOOLEAN ();
overCancelOrder ABSTRACT LIST (InternetOrder);

updateOrder(FILE f){
    LOCAL order = STRING ();
    order() <- replace(replace(servletPath() + pathInfo(), TEXT('/exec/v1/order/'), TEXT ('')), '/exec/order/', '');
    logToFile('yandex', CONCAT ' : ', 'Update order', order());
    LOCAL tokenClient = STRING ();
    tokenClient() <- OVERRIDE getWord(headers('Authorization'), ' ', 2), getWord(headers('authorization'), ' ', 2);;
    LOCAL aggr = YandexEdaAuth();
    aggr() <- auth(yandexEdaSession(tokenClient()));
    IF NOT (aggr()) THEN {
        statusHttpTo() <- 401;
        EXPORT JSON FROM code = 100, description = 'Не найден токен авторизации';
    }
    ELSE {
        //добавляем отмену заказа через PUT /order/uuid/status
        IF getWord(order(), '/', 2) = 'status' AND internetOrderByUuid(getWord(order(), '/', 1)) THEN{
            LOCAL orderLoc = STRING ();
            orderLoc() <- getWord(order(), '/', 1);
            IMPORT closeOrder JSON FROM f;
            customerComment(internetOrderByUuid(orderLoc())) <- comment();
            IF customCancelOrder() THEN {
                overCancelOrder(internetOrderByUuid(orderLoc()));
                RETURN;
            } ELSE {
                IF statusUpdate() = 'DELIVERED' THEN{
                    status(internetOrderByUuid(orderLoc())) <- StatusInternetOrder.delivered;
                } ELSE {
                    status(internetOrderByUuid(orderLoc())) <- OVERRIDE customDeleteOrderStatus(internetOrderByUuid(orderLoc())), StatusInternetOrder.closed;
                }
                APPLY ;
                IF canceled() THEN {
                    statusHttpTo() <- 500;
                    EXPORT JSON FROM result = 'error', description = applyMessage();
                }
                EXPORT JSON FROM order = uuid(internetOrderByUuid(order())), status = 'Отменён';
                RETURN;
            }
        }
        IMPORT yandexorder FROM JSONFILE(f);
        FOR InternetOrder io =  internetOrderByUuid(order()) DO{
            carrier(io) <- carrierOrder(aggr());
            customerComment(io) <- comment();
            extraUpdateOrder(io);
            nameInternetCustomer(io) <- STRING[255](clientName());
            phoneInternetCustomer(io) <- STRING[100](phoneNumber());
            courierArrivementDateText(io) <- courierArrivementDate();
            isPosted(io) <- TRUE;
            LOCAL detailLoc = InternetOrderDetail (INTEGER);
            detailLoc(INTEGER i) <- GROUP MAX InternetOrderDetail d IF sku(d) = sku(id(i)) AND internetOrder(d) = io;
            FOR id(INTEGER items) AND InternetOrderDetail detail = detailLoc(items) DO{
                pickingQuantity(detail) <- NUMERIC[16,5](quantity(items));
                afterUpdateDetails(items, detail);
            }

            FOR id(INTEGER items) AND NOT detailLoc(items)  NEW change = InternetOrderDetail DO {

                internetOrder(change) <- io;
                sku(change) <- sku(id(items));
                quantity(change) <- NUMERIC[16,5](quantity(items));
                pickingQuantity(change) <- NUMERIC[16,5](quantity(items));
                price(change) <- NUMERIC[16,4](price(items));
                afterUpdateDetailsChange(items, change);

            }
            paymentTypeInternetOrder(io) <- paymentTypeOrder(paymentType());
            paymentCardSumInternetOrder(io) <- NUMERIC[18,4](itemsCost()) IF paymentType() = 'CARD';
            paymentCashSumInternetOrder(io) <- NUMERIC[18,4](itemsCost()) IF paymentType() = 'CASH';
            paymentERIPSumInternetOrder(io) <- NUMERIC[18,4](itemsCost()) IF paymentType() = 'ONLINE';
            afterCreateOrder(io);
            APPLY ;
            IF canceled() THEN {
                statusHttpTo() <- 500;
                EXPORT JSON FROM result = 'error', description = applyMessage();
            }
            ELSE EXPORT JSON FROM result = 'OK';
        }
            ELSE orderNotFound();
    }
}

deleteOrder(FILE f){
    LOCAL tokenClient = STRING ();
    tokenClient() <- OVERRIDE getWord(headers('Authorization'), ' ', 2), getWord(headers('authorization'), ' ', 2);
    LOCAL aggr = YandexEdaAuth();
    aggr() <- auth(yandexEdaSession(tokenClient()));
    IF NOT (aggr()) THEN {
        statusHttpTo() <- 401;
        EXPORT JSON FROM code = 100, description = 'Не найден токен авторизации';
    }
    ELSE {
        LOCAL order = STRING ();
        order() <- replace(servletPath() + pathInfo(), TEXT('/exec/order/'), TEXT (''));
        IF NOT order() THEN { orderNotFound(); RETURN; }
        IF internetOrderByUuid(order()) THEN{
            IMPORT closeOrder FROM f;
            customerComment(internetOrderByUuid(order())) <- comment();
            status(internetOrderByUuid(order())) <- OVERRIDE customDeleteOrderStatus(internetOrderByUuid(order())), StatusInternetOrder.closed;
            APPLY ;
            IF canceled() THEN {
                statusHttpTo() <- 500;
                EXPORT JSON FROM result = 'error', description = applyMessage();
            }
            EXPORT JSON FROM order = uuid(internetOrderByUuid(order())), status = 'Отменён';
        }
    }
}

updatedAt 'Дата и время обновления заказа' = DATA ZDATETIME (InternetOrder);

WHEN SETCHANGED (status(InternetOrder o)) DO updatedAt(o) <- currentZDateTime();

//форма для экспорта статуса заказа
FORM statusYandex FORMEXTID 'null'
    OBJECTS o = InternetOrder PANEL
    PROPERTIES (o) yandexEdaOrderStatus EXTID 'status'
    PROPERTIES = toChar(updatedAt(o), 'YYYY-MM-DD"T"HH24:MI:SS.USOF:00') EXTID 'updatedAt'
; 

quantityOrderDetail (InternetOrderDetail d) = IF status(internetOrder(d)) = StatusInternetOrder.new 
    OR status(internetOrder(d)) = StatusInternetOrder.isPicking THEN quantity(d) 
        ELSE pickingQuantity(d);
sumItemCost 'Итоговая стоимость' (InternetOrder o) = IF status(o) = StatusInternetOrder.new 
    OR status(o) = StatusInternetOrder.isPicking THEN paymentCardSumInternetOrder(o)
    ELSE pickingSumInternetOrderDetail(o); 
    
// форма информации о заказе
FORM getOrder FORMEXTID 'null'
    OBJECTS o = InternetOrder PANEL 
    PROPERTIES idAggregator(o) EXTID 'eatsId', = 1 EXTID 'persons', = OVERRIDE customerComment(o), '' EXTID 'comment', 
       = IF supplierStock(o) IS DepartmentStore THEN restarauntId(supplierStock(o)) EXTID 'restaurantId', 
       = 'yandex' EXTID 'discriminator', = '[]' EXTID 'promos'
    PROPERTIES IN deliveryInfo phoneInternetCustomer(o) EXTID 'phoneNumber', 
        nameInternetCustomer(o) EXTID 'clientName', = toChar(courierArrivementDate(o), 'YYYY-MM-DD"T"HH24:MI:SS.US+03:00') EXTID 'courierArrivementDate'
    PROPERTIES IN paymentInfo = OVERRIDE sumItemCost(o), paymentCardSumInternetOrder(o)  EXTID 'itemsCost',
        = IF paymentTypeInternetOrder(o) = PaymentTypeInternetOrder.inFact THEN 'CASH' ELSE 'CARD' EXTID 'paymentType'
 
    OBJECTS items = InternetOrderDetail
    PROPERTIES quantityOrderDetail(items) EXTID 'quantity', price(items) EXTID 'price', nameSku(items) EXTID 'name', idSku(items) EXTID 'id',
        = OVERRIDE '[]', idSku(items) EXTID 'modifications', = OVERRIDE '[]', idSku(items) EXTID 'promos'
    FILTERS internetOrder(items) = o AND quantityOrderDetail(items)
;
logInfoOrder = DATA BOOLEAN ();

getOrder (FILE f) {
    LOCAL tokenClient = STRING ();
    tokenClient() <- OVERRIDE getWord(headers('Authorization'), ' ', 2), getWord(headers('authorization'), ' ', 2);;
    LOCAL aggr = YandexEdaAuth();
    aggr() <- auth(yandexEdaSession(tokenClient()));
    IF NOT (aggr()) THEN {
        statusHttpTo() <- 401;
        EXPORT JSON FROM code = 100, description = 'Не найден токен авторизации';
    }
    ELSE{
        LOCAL path = STRING ();
        path() <- replace(replace(servletPath() + pathInfo(), TEXT('/exec/v1/order/'), TEXT ('')), '/exec/order/', '');
        LOCAL orderNumber = STRING ();
        LOCAL param = STRING ();
        orderNumber() <- getWord(path(), '/', 1);
        param() <- getWord(path(), '/', 2);
        logToFile('yandex', CONCAT ' : ', 'IN order method', method(), 'path', path(), 'order', orderNumber(), 'param', param());
        LOCAL order = InternetOrder();
        order() <- internetOrderByUuid(orderNumber());
        IF NOT order() THEN { orderNotFound(); RETURN; }
        IF (param() = 'status') THEN 
        {
            EXPORT statusYandex OBJECTS o = order() JSON;
            headersTo('Content-Type') <- 'application/json';
        }
        ELSE 
        {
            headersTo(TEXT h) <- httpHeadersYandex(h);
            EXPORT getOrder OBJECTS o = order() JSON;
            IF logInfoOrder() THEN {
                fileToString(exportFile());
                logToFile('yandex', CONCAT ' : ', 'INFO order', orderNumber(), resultString());
            }
        }
    }
}


//основной метод api для работы с заказами
order (FILE f) {
    fileToString(f);
    logToFile('yandex', (CONCAT ' : ', 'IN order method', method(), resultString()));
    CASE 
        WHEN method() = 'POST' THEN createOrder(f);
        WHEN method() = 'PUT' THEN updateOrder(f);
        WHEN method() = 'DELETE' THEN deleteOrder(f);
        WHEN method() = 'GET' THEN getOrder(f);
}@@noauth;

v1_order(FILE f){
    order(f);
}@@noauth;
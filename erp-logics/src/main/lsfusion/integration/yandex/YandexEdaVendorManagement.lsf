MODULE YandexEdaVendorManagement;

REQUIRE YandexEda;

NAMESPACE YandexEda;

baseUrlVendor 'Base url' = DATA STRING () TABLE settings;
clientIdVendor 'ClientId' = DATA STRING () TABLE settings;
clientSecretVendor 'ClientSecret' = DATA STRING() TABLE settings;
tokenVendor 'Токен' = DATA LOCAL STRING();
partnerName 'Имя партнера'= DATA STRING () TABLE settings;


EXTEND FORM integrationData
    PROPERTIES() baseUrlVendor, clientIdVendor, clientSecretVendor,
        partnerName, tokenVendor
;
DESIGN integrationData{
    yandexEda{
       NEW yandexEdaVendor{
           caption = 'Vendor Management';
           MOVE PROPERTY (baseUrlVendor());
           MOVE PROPERTY (clientIdVendor());
           MOVE PROPERTY (clientSecretVendor());
           MOVE PROPERTY (partnerName());
           MOVE PROPERTY (tokenVendor());
       }
    }
}

headersAuthorization (TEXT name) = CASE
    WHEN name = 'Content-Type' THEN 'application/x-www-form-urlencoded'
;

getToken 'Получить токен'(){
    LOCAL f = FILE();
    EXTERNAL HTTP POST baseUrlVendor() + '/oauth2/token'
        HEADERS headersAuthorization PARAMS
        CONCAT '&',
            'client_id=' + clientIdVendor(),
            'client_secret=' + clientSecretVendor()
        TO f
    ;
    LOCAL access_token = STRING ();
    IMPORT JSON FROM f() TO() access_token = access_token;

    IF access_token() THEN {
        tokenVendor() <- access_token();
    }
}

getTokenJSON 'Поулчить токен (JSON)'(){
    LOCAL f = FILE();
    EXTERNAL HTTP POST baseUrlVendor() + '/oauth2/token'
        HEADERS headersAuthorization PARAMS 
            JSON FROM client_id = clientIdVendor(),
                      client_secret = clientSecretVendor()
        TO f
    ;
    LOCAL access_token = STRING ();
    IMPORT JSON FROM f() TO() access_token = access_token;

    IF access_token() THEN {
        tokenVendor() <- access_token();
    }
}

from = DATA LOCAL STRING();
deliveryType = DATA LOCAL STRING(STRING);
vendorId = DATA LOCAL STRING(STRING);
to = DATA LOCAL STRING();
tag = DATA LOCAL STRING();
message = DATA LOCAL STRING();

FORM blockRequest
    PROPERTIES() from, to, tag, message

    OBJECTS vendorInfo = STRING
    PROPERTIES(vendorInfo) deliveryType, vendorId
;

blockHeaders (TEXT name) = CASE 
    WHEN name = 'Authorization' THEN tokenVendor()
    WHEN name = 'Partner-Name' THEN partnerName()
;


block(STRING ds, DATETIME df, DATETIME dt, STRING reason, STRING tag){
    getToken();
    IF tokenVendor() THEN{
        
        tag() <- tag;
        from() <- toChar(df, 'YYYY-MM-DD"T"HH24:MI:SS+0300');
        to() <- toChar(dt, 'YYYY-MM-DD"T"HH24:MI:SS+0300');
        message() <- reason;
        deliveryType(ds) <- 'ddc';
        vendorId(ds) <- ds;
        
        LOCAL f = FILE();
        
        EXPORT blockRequest FILTERS vendorInfo = ds JSON;
        
        EXTERNAL HTTP POST baseUrlVendor() + '/block' HEADERS blockHeaders PARAMS exportFile() TO f;
        MESSAGE CONCAT '\n', 'Успешная блокировка', 'id ' + ds, 'tag ' + tag, 'reason ' + reason, 'to ' + to() NOWAIT;
    }
}

blockTechnicalProblem 'Блок по тех.проблемам'(STRING ds, DATETIME df, DATETIME dt, STRING reason){
    block(ds, df, dt, reason, 'technical_problem');
}

blockTooBusy 'Блок по загруженности'(STRING ds, DATETIME df, DATETIME dt, STRING reason){
    block(ds, df, dt, reason, 'too_busy');
}

statuses = DATA LOCAL STRING (STRING);

FORM statusRequest
    OBJECTS statuses = STRING
    PROPERTIES(statuses) statuses EXTID 'value'
    
    OBJECTS vendorInfo = STRING
    PROPERTIES(vendorInfo) deliveryType, vendorId
;

//
blockId = DATA LOCAL STRING(INTEGER);
availableToCancel = DATA LOCAL BOOLEAN (INTEGER);
deliveryType = DATA LOCAL STRING(INTEGER);
vendorId = DATA LOCAL STRING(INTEGER);
from = DATA LOCAL STRING(INTEGER);
to = DATA LOCAL STRING(INTEGER);
source = DATA LOCAL STRING(INTEGER);
externalBlockId = DATA LOCAL STRING(INTEGER);
message = DATA LOCAL STRING(INTEGER);
status = DATA LOCAL STRING(INTEGER);

FORM blockAnswer
    OBJECTS items = INTEGER
    PROPERTIES(items) blockId, availableToCancel, 
        deliveryType, vendorId, from, to, source, externalBlockId, 
        message, status
    FILTERS imported(items)
;


getStatusBlock(STRING ds){
    IF tokenVendor() THEN {

        deliveryType(ds) <- 'ddc';
        vendorId(ds) <- ds;
        statuses(ds) <- 'enabled';

        LOCAL f = FILE();
        EXPORT statusRequest FILTERS vendorInfo = ds, statuses = ds JSON;
        
        EXTERNAL HTTP POST baseUrlVendor() + '/v2/status' HEADERS blockHeaders PARAMS exportFile() TO f;
        IMPORT blockAnswer JSON FROM f();
    }
}


FORM unblockRequest
    OBJECTS blockIds = INTEGER
    PROPERTIES(blockIds) blockId EXTID 'value'
    FILTERS blockId(blockIds)

    OBJECTS vendorInfo = STRING
    PROPERTIES(vendorInfo) deliveryType, vendorId
;

unblock 'Разблокировать' (STRING ds) {
    getToken();
    IF tokenVendor() THEN {

        deliveryType(ds) <- 'ddc';
        vendorId(ds) <- ds;

        getStatusBlock(ds);
        
        LOCAL f = FILE();
        EXPORT unblockRequest FILTERS vendorInfo = ds JSON;


        EXTERNAL HTTP POST baseUrlVendor() + '/unblock' HEADERS blockHeaders PARAMS exportFile() TO f;
        MESSAGE CONCAT '\n', 'Успешно разблокировано', 'id ' + ds NOWAIT;

        blockId(INTEGER i) <- NULL;
    }
}

unblock 'Разблокировать' (STRING ds, DATETIME df, DATETIME dt) {
    getToken();
    IF tokenVendor() THEN {

        deliveryType(ds) <- 'ddc';
        vendorId(ds) <- ds;

        getStatusBlock(ds);

        blockId(INTEGER i) <- NULL WHERE blockId(i) AND NOT 
            (from(i) == toChar(df, 'YYYY-MM-DD"T"HH24:MI:SS+0300') AND to(i) == toChar(dt, 'YYYY-MM-DD"T"HH24:MI:SS+0300'));

        LOCAL f = FILE();
        EXPORT unblockRequest FILTERS vendorInfo = ds JSON;
        
        EXTERNAL HTTP POST baseUrlVendor() + '/unblock' HEADERS blockHeaders PARAMS exportFile() TO f;
        MESSAGE CONCAT '\n', 'Успешно разблокировано', 'id ' + ds, 'from ' + toChar(df, 'YYYY-MM-DD"T"HH24:MI:SS+03:00'), 'to ' + toChar(dt, 'YYYY-MM-DD"T"HH24:MI:SS+03:00') NOWAIT;

        blockId(INTEGER i) <- NULL;
    }
}
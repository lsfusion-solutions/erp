MODULE RetailCRMArticle;

REQUIRE RetailCRM, ItemArticle, StockArticle;

PRIORITY Stock;

// article
TABLE promotionConditionArticle (PromotionCondition, Article);
inData 'В условии' = DATA BOOLEAN (PromotionCondition, Article);

 
in 'В условии' (PromotionCondition condition, Article article) = OVERRIDE
    inData(condition, article),                                                                            
    TRUE IF inParent(condition, itemGroup(article)); 

useFullNamePromotionCondition 'Использовать полное имя акции' = DATA BOOLEAN ();

EXTEND FORM options PROPERTIES() useFullNamePromotionCondition;
DESIGN options {
    retailCRM {
        MOVE PROPERTY(useFullNamePromotionCondition());
    }
}

fullName(PromotionCondition c) = IF useFullNamePromotionCondition() THEN CONCAT '/', namePromotion(c), name(c) ELSE namePromotion(c);
promotionConditions 'В акциях' (Article a) = GROUP
                                              CONCAT ',', name(PromotionCondition c) IF in(c, a)
                                              ORDER c;
promotionConditions 'В других акциях' (PromotionCondition pc, Article a) =
                                        GROUP
                                              CONCAT fullName(PromotionCondition c) IF 
                                                     inData(c, a) AND 
                                                     dateTo(c) >= dateFrom(pc) AND
                                                     dateTo(pc) >= dateFrom(c) AND 
                                                     c != pc AS PromotionCondition, 
                                              ','
                                              ORDER c CHARWIDTH 10;

overIn (PromotionCondition condition, Item sku) += inData(condition, article(sku));

nameBalance (Stock stock)= name(stock) + '(остаток)' CHARWIDTH 15;    

backgroundBalance (Promotion p)= RGB(204,204,255) IF p IS Promotion;

prevCurrentBalance 'Итого (остаток)' (Article ar, Promotion sh) = GROUP SUM prevCurrentBalance(ar,DepartmentStore ddd) IF in(sh, store(ddd));

copyProperties(PromotionCondition promotionConditionTo, PromotionCondition promotionConditionFrom) + { 
    IF copyItems() THEN {
        inData(promotionConditionTo, Article article) <- inData(promotionConditionFrom, article);
    }
}

// ---------------- Импорт из XLS ------------------ //

importArticleXLS 'Импорт (xlsx)' (PromotionCondition c)  { 

    LOCAL article = STRING[100] (INTEGER);
    LOCAL price = NUMERIC[16,4] (INTEGER);
    
    INPUT f = EXCELFILE DO {
        IMPORT XLS NOHEADER FROM f AS EXCELFILE TO article = A, price = B;
        FOR imported(INTEGER r) DO {
            in(c, Article a) <- TRUE WHERE id(a) == article(r);
            resultPrice(c, Sku s) <- price(r) WHERE id(article(s)) == article(r) AND price(r) > 0.0;
        }
        
        MESSAGE 'Импортированы ' + (OVERRIDE (GROUP SUM 1 IF article(article(INTEGER r))), 0) + ' записей';
    }
}
          
removeArticleXLS 'Исключить из акции (xlsx)' (PromotionCondition c)  { 

    LOCAL article = STRING[100] (INTEGER);
    LOCAL count = INTEGER(); 
    
    INPUT f = EXCELFILE DO {
        IMPORT XLS NOHEADER FROM f AS EXCELFILE TO article = A;
        count() <- (GROUP SUM 1 IF in(c, article(article(INTEGER r))) AND imported(r));

        FOR imported(INTEGER r) DO {
            in(c, Article a) <- NULL WHERE id(a) == article(r);
        }
        
        MESSAGE 'Исключены ' + (OVERRIDE count(), 0) + ' записей';
    }
}                
                
EXTEND FORM promotion
    OBJECTS params = (dFrom = DATE, dTo = DATE) PANEL
    PROPERTIES objFrom = VALUE(dFrom), objTo = VALUE(dTo)

    OBJECTS ddd = Stock GRID
    FILTERGROUP inactiveStock FILTER 'Активный' active(ddd) 'ctrl F10' DEFAULT
    FILTERS     in(sh, store(ddd))
    
    OBJECTS ar = Article
    PROPERTIES(ar)     READONLY idItemGroup, id, caption, prevCurrentBalance, canonicalNameItemGroup,
                       image PANEL
    PROPERTIES(sg, ar) READONLY promotionConditions
    PROPERTIES(sg, ar)          in
    PROPERTIES READONLY prevCurrentBalance(ar,ddd) COLUMNS 'astock' (ddd) HEADER nameBalance(ddd) BACKGROUND backgroundBalance(sh),
                       prevCurrentBalance(ar,sh)
    FILTERS                     isParent(skg, ar)
    
    PROPERTIES(sg) DRAW ar TOOLBAR importArticleXLS, removeArticleXLS 

    FILTERGROUP filterArticle
        FILTER 'В акции' in(sg, ar) 'F10'                        
;

DESIGN promotion {
    REMOVE BOX(ddd);
    row212 {
        type = SPLITH;
        NEW row213 {
            fill = 1;
            type = SPLITV;
            MOVE BOX(TREE skuTree);            
        
            NEW imageBox {
                fill = 1;
                caption = 'Изображение';                     
                MOVE PROPERTY(image(ar)) {
                    caption = '';
                    fill = 1;
                }
            }                        
        }
        NEW filterBox {
            type = CONTAINERV;
            fill = 3.5;
            MOVE BOX(params) {
                type = CONTAINERH;
                caption = 'Реализация за период'; 
            } 
            MOVE skuPane {
                type = TABBED;
                NEW articleTab {
                    caption = 'Артикул';
                    fill = 1;
                    type = CONTAINERV;
                    MOVE BOX(ar);                    
                }
                MOVE BOX(sk);
            } 
        }
        
    }         
    PROPERTY(prevCurrentBalance(ar,ddd)) { charWidth = 2;} 
    PROPERTY(prevCurrentBalance(ar,sh)) { charWidth = 4;}  
}
                
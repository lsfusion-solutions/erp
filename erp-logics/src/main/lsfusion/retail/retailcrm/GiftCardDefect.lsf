MODULE GiftCardDefect;

REQUIRE GiftCard;

NAMESPACE GiftCard;

//-- 
CLASS DefectGiftCard 'Списание испорченных ПС';
CLASS DefectGiftCardDetail 'Строка списания испорченных ПС' : GiftCardLedger;

@defineDocument(defectGiftCard);
@deriveDocumentHeaderTimePrefix(DefectGiftCard, );

@defineDocumentHeaderNumber(DefectGiftCard, BPSTRING[2]);
@defineNumeratedDefault(DefectGiftCard, 'Списание испорченных ПС', 'ИП');

@defineDocumentDataStock(defectGiftCard, departmentStore, 'Склад', );
@defineDocumentDialogStocks(defectGiftCard, departmentStore, company, , );

@defineDocumentDescription (defectGiftCard, 'Списание испорченных ПС');


fromDate 'Дата с' = DATA DATE (DefectGiftCard) IN documentHeader;
fromDate 'Дата с' (DefectGiftCardDetail d)= fromDate(defectGiftCard(d));
toDate 'Дата по' = DATA DATE (DefectGiftCard) IN documentHeader;
toDate 'Дата по' (DefectGiftCardDetail d)= toDate(defectGiftCard(d));

giftCard = DATA GiftCard (DefectGiftCardDetail) INDEXED NONULL;
seriesNumberGiftCard 'Серия/Номер' (DefectGiftCardDetail d) = seriesNumber(giftCard(d)) IN id;
priceGiftCard 'Цена' (DefectGiftCardDetail d) = price(giftCard(d));
expiryDaysGiftCard 'Срок действия' (DefectGiftCardDetail d) = expiryDays(giftCard(d));
defectGiftCardDetail  = GROUP AGGR (DefectGiftCardDetail d) BY giftCard(d);

allowUsed 'Разрешить использованные' = DATA BOOLEAN (DefectGiftCardDetail);

CONSTRAINT giftCard(DefectGiftCardDetail d) AND used(giftCard(d)) AND NOT allowUsed(d)
    CHECKED BY giftCard[DefectGiftCardDetail]
    MESSAGE 'Списывать можно только непогашенные подарочные сертификаты';

//nameDepartmentStoreffff (DefectGiftCardDetail d) = nameDepartmentStore(giftCard(d));

FORM defectGiftCard 'Списание испорченных ПС'

    OBJECTS r = DefectGiftCard PANEL
    PROPERTIES (r) nameDepartmentStore ON CHANGE changeDepartmentStoreCompany(r),  nameNumerator, 
                   number, series, date, time, note, fromDate, toDate
                        
                
    PROPERTIES (r) countDefectGiftCardDetail

    OBJECTS d = DefectGiftCardDetail
    PROPERTIES (d) index
    PROPERTIES (d) seriesNumberGiftCard, priceGiftCard, expiryDaysGiftCard //, nameDepartmentStoreffff                   
    PROPERTIES (d) NEW, deleteid=DELETE GRID
       
    ORDERS index(d)

    PROPERTIES(r) DRAW d deleteDefectGiftCardDetail
    
    FILTERS defectGiftCard(d) == r

    EDIT DefectGiftCard OBJECT r
;

DESIGN defectGiftCard {
    BOX {
        size = (1024, 768);
        NEW headerBox {
            type = CONTAINERH;

            NEW headerParams {
                fill = 1;
                type = CONTAINERV;
                MOVE GROUP(documentHeader,r) {
                    columns = 1;
                    type = CONTAINERV;
                    NEW first {
                        type = CONTAINERH;
                        MOVE PROPERTY(fromDate(r));
                        MOVE PROPERTY(toDate(r));
                        MOVE PROPERTY(nameDepartmentStore(r));               
                    }
                    NEW second { 
                        type = CONTAINERH;
                        MOVE PROPERTY(nameNumerator(r));
                        MOVE PROPERTY(number(r));
                        MOVE PROPERTY(series(r));
                        MOVE PROPERTY(date(r));
                        MOVE PROPERTY(time(r));
                    }   
                                                                                                                   
                }
            }
            MOVE GROUP(documentSum,r) {
                columns = 1;
            }
        }
        MOVE GROUP(documentPrm,r);
        NEW specificationBox {
            fill = 1;
            MOVE BOX(d) {
                caption = 'Спецификация';
            }
        }
        MOVE TOOLBARBOX;

    }
}


FORM defectGiftCards 'Списания испорченных ПС'

    OBJECTS r = DefectGiftCard 
    PROPERTIES (r) READONLY nameDepartmentStore, 
                   number, series, date, time,
                   note, fromDate, toDate

                
    PROPERTIES (r) READONLY countDefectGiftCardDetail
    PROPERTIES (r) NEWSESSION NEW, EDIT, deleter=DELETE
    PROPERTIES (r) READONLY PANEL createdNameUser, createdTime, createdHostnameComputer 
        
    OBJECTS d = DefectGiftCardDetail
    PROPERTIES (d) READONLY index, seriesNumberGiftCard, priceGiftCard, expiryDaysGiftCard
    
    ORDERS index(d)

    FILTERS defectGiftCard(d) == r
                
    LIST DefectGiftCard OBJECT r
;

DESIGN defectGiftCards {
    BOX {
        size = (1024, 768);

        NEW documentContainer BEFORE TOOLBARBOX {
            fill = 1;

            type = SPLITV;
            MOVE BOX(r);    

            NEW documentDetail {
                fill = 1;
                type = TABBED;

                MOVE BOX(d) {
                    fill = 1;
                    caption = 'Спецификация';
                }
                NEW documentHistory {
                    caption = 'История';
                    type = CONTAINERV;

                    MOVE GROUP(created,r);                       
                }
                NEW printTab {
                    caption = 'Печатные формы';
                    NEW printContainer {
                        caption = 'Печать';
                        type = CONTAINERV;
                    }
                }
            }
        }
    }
}

NAVIGATOR {
    retailReports  {
        NEW defectGiftCards;
    }
}

dateDefect 'Дата порчи' (GiftCard g) = DATE(dateTime(defectGiftCardDetail(g))) MATERIALIZED;
departmentStoreDefect 'Где испорчен' (GiftCard g) = departmentStore(defectGiftCardDetail(g));
nameDepartmentStoreDefect 'Где испорчен' (GiftCard g) = name(departmentStoreDefect(g));
seriesNumberDefect 'Номер (списание исп.)' (GiftCard g) = seriesNumber(defectGiftCard(defectGiftCardDetail(g)));

EXTEND FORM giftCards
    PROPERTIES (g) READONLY dateDefect, nameDepartmentStoreDefect, seriesNumberDefect
    FILTERGROUP filter3
        FILTER 'Испорчен' defectGiftCardDetail(g) 'F5' 
        FILTER 'Не испорчен' sold(g) AND NOT defectGiftCardDetail(g) 'F4'     
;

//имплемент в GiftCardLedger
giftCard(DefectGiftCardDetail detail) += giftCard(detail);
dateTime(DefectGiftCardDetail detail) += dateTime(detail);
date(DefectGiftCardDetail detail) += date(detail);
sum(DefectGiftCardDetail detail) += 0.0 IF detail IS DefectGiftCardDetail;
description(DefectGiftCardDetail detail) += STRING[100]('Испорчен') IF detail IS DefectGiftCardDetail;
numberDocument(DefectGiftCardDetail detail) += STRING[50](seriesNumber(defectGiftCard(detail)));
departmentStore(DefectGiftCardDetail detail) += departmentStore(detail);


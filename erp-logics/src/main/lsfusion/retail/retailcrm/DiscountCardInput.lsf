MODULE DiscountCardInput;

REQUIRE DiscountCard, ZReportDiscountCard;


defaultDiscountLegalEntityGroup  = DATA LegalEntityGroup ();
nameDefaultDiscountLegalEntityGroup 'Группа организация по умолчанию' = name(defaultDiscountLegalEntityGroup());

EXTEND FORM options 
    PROPERTIES nameDefaultDiscountLegalEntityGroup();
    
DESIGN options {
    discountCards {
        MOVE PROPERTY(nameDefaultDiscountLegalEntityGroup());    
    }
}

prevLegalEntity(DiscountCard d) = PREV (legalEntity(d));

add(LegalEntity l, DiscountCard disc)  { 
    legalEntity(disc) <- l;
    ownership(l) <- Ownership.individual;
    legalEntityGroup(l) <- defaultDiscountLegalEntityGroup(); 
}

changeFirstName(DiscountCard disc)  { 
    INPUT s = STRING[30] DO {
        FOR NOT legalEntity(disc) NEW l = LegalEntity DO add(l, disc);
        firstName(LegalEntity l) <- s WHERE l == legalEntity(disc);
    }
}

changeLastName(DiscountCard disc)  { 
    INPUT s = STRING[30] DO {
        FOR NOT legalEntity(disc) NEW l = LegalEntity DO add(l, disc);
        lastName(LegalEntity l) <- s WHERE l == legalEntity(disc);
    }
}

changeAdress(DiscountCard disc)  { 
    INPUT s = STRING[150] DO {
        FOR NOT legalEntity(disc) NEW l = LegalEntity DO add(l, disc);
        dataAddress(LegalEntity l, DATE d) <- s WHERE l == legalEntity(disc) AND d==currentDate();
    }
}

changeEmail(DiscountCard disc)  { 
    INPUT s = STRING[100] DO {
        FOR NOT legalEntity(disc) NEW l = LegalEntity DO add(l, disc);
        email(LegalEntity l) <- s WHERE l == legalEntity(disc);
    }
}

changePhone(DiscountCard disc)  { 
    INPUT s = STRING[100] DO {
        FOR NOT legalEntity(disc) NEW l = LegalEntity DO add(l, disc);
        dataPhone(LegalEntity l, DATE d) <- s WHERE l == legalEntity(disc) AND d==currentDate();
    }
}

changeBirthday(DiscountCard disc)  { 
    INPUT d = DATE DO {
        FOR NOT legalEntity(disc) NEW l = LegalEntity DO add(l, disc);
        birthday(LegalEntity l) <- d WHERE l == legalEntity(disc);
    }
}

FORM discoundCardInput 'Ввод анкет дисконтных карт'

    OBJECTS d = DiscountCard
    PROPERTIES (d) number READONLY, series READONLY, id SHOWIF showIDs() READONLY
    PROPERTIES (d) firstNameContact ON CHANGE changeFirstName(d)  ON CHANGEWYS changeFirstName(d), 
                   lastNameContact ON CHANGE changeLastName(d) ON CHANGEWYS changeLastName(d), 
                   addressLegalEntity ON CHANGE changeAdress(d) ON CHANGEWYS changeAdress(d),
                   emailLegalEntity ON CHANGE changeEmail(d) ON CHANGEWYS changeEmail(d), 
                   phoneLegalEntity ON CHANGE changePhone(d) ON CHANGEWYS changePhone(d), 
                   birthdayContact ON CHANGE changeBirthday(d) ON CHANGEWYS changeBirthday(d)                 
    FILTERGROUP noneLE
        FILTER 'Нет держателя' NOT prevLegalEntity(d) DEFAULT
;

NAVIGATOR {
    retailDashboardNavigator {
        NEW discoundCardInput;
    }
}
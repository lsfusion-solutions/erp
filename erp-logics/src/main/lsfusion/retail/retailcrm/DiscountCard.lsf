MODULE DiscountCard;

REQUIRE Retail, LegalEntity, Numerator, IndividualLegalEntity;

NAMESPACE RetailCRM;

CLASS DiscountCard 'Дисконтная карта';
TABLE discountCard (DiscountCard);
@defineExternalizable(discountCard, STRING[100]);

number 'Номер' = DATA STRING[28] (DiscountCard) IN numbered INDEXED CHARWIDTH 13;
series 'Серия' = DATA BPSTRING[2] (DiscountCard) IN numbered CHARWIDTH 3 NOFLEX;
 
WHEN useLoweredNumber() AND CHANGED(number(DiscountCard o))
     INLINE DO number(o) <- lower(number(o));
     
WHEN useUpperedSeries() AND CHANGED(series(DiscountCard o))
     INLINE DO series(o) <- upper(series(o));

WHEN CHANGED(number(DiscountCard o)) AND NOT keepNumberSpaces()
    INLINE DO number(o) <- replace(number(o), ' ', '');

seriesNumber 'Серия/Номер' (DiscountCard o) = 
    CONCAT '', series(o), number(o) 
    CHARWIDTH 7 INDEXED MATERIALIZED;

@defineNumeratedDefault(DiscountCard, 'Дисконтные карты', 'ДС');

discountSeriesNumber (discountCard) = GROUP AGGR DiscountCard ddiscountCard WHERE ddiscountCard IS DiscountCard BY seriesNumber (ddiscountCard);
discountNumber (discountCard) = GROUP MAX DiscountCard ddiscountCard BY number(ddiscountCard) IF ddiscountCard IS DiscountCard;

discountString (STRING[28] discountCard) = OVERRIDE discountSeriesNumber(discountCard), discountNumber(discountCard);  
toLong = FORMULA NUMERIC[18,0] 'to_number(REPLACE(($1),\',\',\'.\'), \'999999999999999999\')';
longNumber (DiscountCard d) = toLong(number(d));

// Держатель
legalEntity (discountCard) = DATA LegalEntity(DiscountCard) INDEXED;
stringLegalEntity = ABSTRACT BOOLEAN (DiscountCard);

overNameLegalEntity = ABSTRACT ISTRING[150] (DiscountCard);
nameLegalEntity 'Держатель дисконтной карты' (DiscountCard dc) = OVERRIDE overNameLegalEntity(dc), name(legalEntity(dc)) IN id CHARWIDTH 30;

overFirstNameContact = ABSTRACT ISTRING[100] (DiscountCard);
firstNameContact 'Имя' (DiscountCard dc) = OVERRIDE overFirstNameContact(dc), firstName(legalEntity(dc)) IN id CHARWIDTH 15;

overLastNameContact = ABSTRACT ISTRING[100] (DiscountCard);
lastNameContact 'Фамилия' (DiscountCard dc) = OVERRIDE overLastNameContact(dc), lastName(legalEntity(dc)) IN id CHARWIDTH 15;

overBirthdayContact = ABSTRACT DATE (DiscountCard);
birthdayContact 'День рождения' (DiscountCard dc) = OVERRIDE overBirthdayContact(dc), birthday(legalEntity(dc)) IN id;

overPhoneLegalEntity = ABSTRACT ISTRING[100] (DiscountCard);
phoneLegalEntity 'Телефон' (DiscountCard dc) = OVERRIDE overPhoneLegalEntity(dc), phone(legalEntity(dc)) CHARWIDTH 15;

overEmailLegalEntity = ABSTRACT ISTRING[400] (DiscountCard);
emailLegalEntity 'E-mail' (DiscountCard dc) = OVERRIDE overEmailLegalEntity(dc), email(legalEntity(dc)) CHARWIDTH 15;

overAddressLegalEntity = ABSTRACT ISTRING[150] (DiscountCard);
addressLegalEntity 'Адрес' (DiscountCard dc) = OVERRIDE overAddressLegalEntity(dc), address(legalEntity(dc)) IN id CHARWIDTH 30;

changeLegalEntity (DiscountCard d) {
        DIALOG dialogLegalEntities OBJECTS l INPUT DO {
            legalEntity(d) <- l;
        }
}
changeNameLegalEntity (DiscountCard d) {
    IF (stringLegalEntity(d)) THEN {
        INPUT =overNameLegalEntity(d) CHANGE;
    } ELSE {
        changeLegalEntity(d);
    }
}
changeFirstNameContact (DiscountCard d) {
    IF (stringLegalEntity(d)) THEN {
        INPUT =overFirstNameContact(d) CHANGE;
    } ELSE {
        changeLegalEntity(d);
    }
}
changeLastNameContact (DiscountCard d) {
    IF (stringLegalEntity(d)) THEN {
        INPUT =overLastNameContact(d) CHANGE;
    } ELSE {
        changeLegalEntity(d);
    }
}
changeBirthdayContact (DiscountCard d) {
    IF (stringLegalEntity(d)) THEN {
        INPUT =overBirthdayContact(d) CHANGE;
    } ELSE {
        changeLegalEntity(d);
    }
}
changeEmailLegalEntity (DiscountCard d) {
    IF (stringLegalEntity(d)) THEN {
        INPUT =overEmailLegalEntity(d) CHANGE;
    } ELSE {
        changeLegalEntity(d);
    }
}
changePhoneLegalEntity (DiscountCard d) {
    IF (stringLegalEntity(d)) THEN {
        INPUT =overPhoneLegalEntity(d) CHANGE;
    } ELSE {
        changeLegalEntity(d);
    }
}
changeAddressLegalEntity (DiscountCard d) {
    IF (stringLegalEntity(d)) THEN {
        INPUT =overAddressLegalEntity(d) CHANGE;
    } ELSE {
        changeLegalEntity(d);
    }
}

middleNameHttpServerContact = ABSTRACT ISTRING[100] (DiscountCard);
numberSexHttpServerContact = ABSTRACT INTEGER (DiscountCard);
skipLoad = ABSTRACT BOOLEAN (DiscountCard);

//тип карты
CLASS DiscountCardType 'Тип дисконтной карты' : StaticObject;
TABLE discountCardType (DiscountCardType);

dataName = DATA ISTRING[100](DiscountCardType);
name 'Тип карты' (DiscountCardType t) = OVERRIDE dataName(t), staticCaption(t);
changeName (DiscountCardType t) {
    INPUT s = ISTRING[100] DO dataName(t) <- s;
}

discountCardType = DATA DiscountCardType(DiscountCard) INDEXED;
nameDiscountCardType 'Тип карты' = name(discountCardType(DiscountCard d));

@defineExternalizable(discountCardType, STRING[10]);
idDiscountCardType 'Тип дисконтной карты' (DiscountCard d) = id(discountCardType(d));

extInfo = ABSTRACT TEXT (DiscountCard);

FORM discountCardType 'Тип дисконтной карты'
    OBJECTS t = DiscountCardType PANEL 
    PROPERTIES(t) name ON CHANGE changeName(t), id    
    EDIT DiscountCardType OBJECT t
;

FORM discountCardTypes 'Типы дисконтных карт'
    OBJECTS t = DiscountCardType
    PROPERTIES(t) READONLY name, id
    PROPERTIES(t) NEWSESSION NEW, EDIT, deletet = DELETE 
    
    LIST DiscountCardType OBJECT t
;

NAVIGATOR {
    retailMasterData {
        NEW discountCardTypes;
    }
}

// Сроки действия
notFillDate 'Не заполнять дату выдачи' = DATA BOOLEAN ();

GROUP discountCardDate 'Срок действия' : base;
date 'Дата выдачи' (discountCard) = DATA DATE (DiscountCard) IN discountCardDate;
date (DiscountCard discountCard) <- currentDate() WHEN SET(discountCard IS DiscountCard) AND NOT notFillDate();

dateTo 'Дата окончания действия' (discountCard) = DATA DATE (DiscountCard) IN discountCardDate;

name 'Наименование' = DATA STRING[100] (DiscountCard);
initialSum 'Начальная сумма' (discountCard) = DATA NUMERIC[18,4] (DiscountCard);

percent 'Процент скидки' (discountCard) = DATA NUMERIC[18,4] (DiscountCard);

inactive 'Неактивный' = DATA BOOLEAN (DiscountCard);
isActive 'Активный' (DiscountCard g) = g IS DiscountCard AND NOT inactive(g);

// Генерация дисконтных карт
FORM generationDiscountCards 'Генерация дисконтных карт'
    OBJECTS n=Numerator  PANEL
    PROPERTIES(n) SELECTOR name

    OBJECTS quantityCards=INTEGER PANEL
    PROPERTIES(quantityCards) intValueQuantityCards = VALUE

    OBJECTS t=DiscountCardType PANEL
    PROPERTIES(t) SELECTOR name
    
    OBJECTS p=NUMERIC[18,4] PANEL
    PROPERTIES(p) percent = VALUE
;

DESIGN generationDiscountCards {
    BOX {
        PROPERTY(intValueQuantityCards) {
            caption = 'Количество дисконтных карт';
        }
        PROPERTY(name(t)) {
            caption = 'Наименование';
        }
        PROPERTY(percent) {
            caption = 'Процент скидки';
        }
    }
}

iterateLong(LONG i, LONG from, LONG to) = RECURSION i==from AND from IS LONG AND to IS LONG STEP i==$i+1 AND i<=to CYCLES IMPOSSIBLE;

overGenerateDiscountCard  ABSTRACT LIST ( DiscountCard);
generateDiscountCards 'Сгенерировать дисконтные карты'()  { 
    DIALOG generationDiscountCards OBJECTS n=defaultNumeratorDiscountCard(), n INPUT, quantityCards INPUT, t INPUT, p INPUT CHECK DO {
        IF quantityCards <= 100000 THEN { 
            FOR iterateLong(LONG i, curValue(n), curValue(n) + quantityCards - 1) AND NOT discountSeriesNumber(CONCAT '', series(n), lpadStringWithZero(i, stringLength(n))) NEW d = DiscountCard DO {
                numerator(d) <- n;
                number(d) <- lpadStringWithZero(i, stringLength(n));
                series(d) <- series(n);
                discountCardType(d) <- t;
                percent(d) <- p;
                overGenerateDiscountCard(d);
            }
        
            curValue(Numerator numerator) <- curValue(numerator) + quantityCards WHERE numerator == n;
            APPLY;
        } ELSE {
            MESSAGE 'Слишком большой интервал';
        }
    }
}

// Формы по работе с дисконтными картами
FORM discountCard 'Дисконтная карта'
    OBJECTS d=DiscountCard PANEL
    PROPERTIES(d) id SHOWIF showIDs(), name, initialSum, percent, nameNumerator, number, series, date, dateTo, inactive,
                  firstNameContact ON CHANGE changeFirstNameContact(d), lastNameContact ON CHANGE changeLastNameContact(d), 
                  phoneLegalEntity ON CHANGE changePhoneLegalEntity(d), emailLegalEntity ON CHANGE changeEmailLegalEntity(d),          
                  addressLegalEntity ON CHANGE changeAddressLegalEntity(d), birthdayContact ON CHANGE changeBirthdayContact(d), 
                  nameLegalEntity ON CHANGE changeNameLegalEntity(d), nameDiscountCardType

    EDIT DiscountCard OBJECT d
;

DESIGN discountCard {
    BOX {
        size = (1024, 768);
        BOX(d) {
            PANEL(d) {
                type = CONTAINERV;
                NEW row1 {
                    NEW common {
                        type = CONTAINERH;
                        caption = 'Общие';
                        MOVE PROPERTY(id(d));
                        MOVE PROPERTY(name(d));
                        MOVE PROPERTY(initialSum(d));
                        MOVE PROPERTY(percent(d));
                        MOVE PROPERTY(nameDiscountCardType(d));
                        MOVE PROPERTY(inactive(d));
                    }
                }
                NEW row2 {
                    type = CONTAINERH;
                    MOVE GROUP(numbered,d);
                    MOVE GROUP(discountCardDate,d);
                }
                NEW row3 {
                    caption = 'Держатель';
                    MOVE PROPERTY (nameLegalEntity(d)) { caption = 'Полное имя'; }
                    NEW row3columns {
                        type = COLUMNS;
                        columns = 3;
                        MOVE PROPERTY(firstNameContact(d));
                        MOVE PROPERTY(lastNameContact(d));
                        MOVE PROPERTY(phoneLegalEntity(d));
                        MOVE PROPERTY(emailLegalEntity(d));
                        MOVE PROPERTY(addressLegalEntity(d));
                        MOVE PROPERTY(birthdayContact(d)) {
                            pattern='dd.MM.yyyy';
                        }
                    }
                }
            }
        }
        NEW tabContainer {
            fill = 1;
            type = TABBED;
        }
        MOVE TOOLBARBOX;
    }
}

FORM discountCardDialog 'Дисконтные карты'

    OBJECTS d = DiscountCard
    PROPERTIES(d) READONLY number, series, id SHOWIF showIDs(), nameDiscountCardType, nameLegalEntity, 
                           phoneLegalEntity, emailLegalEntity,  addressLegalEntity,
                           firstNameContact, lastNameContact, birthdayContact, 
                           name, initialSum, percent, date, dateTo
    FILTERGROUP active
        FILTER 'Активные' isActive(d) DEFAULT 

    LIST DiscountCard OBJECT d
;

FORM discountCards 'Дисконтные карты'
    OBJECTS d=DiscountCard
    PROPERTIES(d) READONLYIF isReadonly() number, series, id SHOWIF showIDs(), nameDiscountCardType, nameLegalEntity ON CHANGE changeNameLegalEntity(d), 
                  firstNameContact ON CHANGE changeFirstNameContact(d), lastNameContact ON CHANGE changeLastNameContact(d), 
                  phoneLegalEntity ON CHANGE changePhoneLegalEntity(d), emailLegalEntity ON CHANGE changeEmailLegalEntity(d),          
                  addressLegalEntity ON CHANGE changeAddressLegalEntity(d), birthdayContact ON CHANGE changeBirthdayContact(d), 
                  date, dateTo, name, initialSum, percent
    PROPERTIES(d) NEWSESSION NEW, EDIT, DELETE 
    
    FILTERGROUP active
        FILTER 'Активные' isActive(d) DEFAULT 

    ORDER number(d)

    PROPERTIES() generateDiscountCards DRAW d TOOLBAR 
;
@extendFormEditable(discountCards);

DESIGN discountCards {
    BOX {
            MOVE BOX(d) {
                PROPERTY(birthdayContact(d)) {
                    pattern='dd.MM.yyyy';
                }
            }
            NEW tabbed {
                fill = 1;
                type = TABBED;
            };
            MOVE TOOLBARBOX;
        }
}

NAVIGATOR {
    retailMasterData {
        NEW discountCards;
    }
}
EXTEND FORM options
    PROPERTIES notFillDate()
;
DESIGN options {
    pane {        
        NEW discountCards {
            caption = 'Дисконтные карты';
            MOVE PROPERTY (notFillDate());
        }
    }
}
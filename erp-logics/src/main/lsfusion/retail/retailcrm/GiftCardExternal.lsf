MODULE GiftCardExternal;

REQUIRE GiftCardLegalEntity;

NAMESPACE GiftCard;


CLASS ExternalGiftCard 'Оплата внешним сертификатом': GiftCard, SaleGiftCard;

EXTEND CLASS PaymentMeans { paymentMeansExternalGiftCard 'Внешний подарочный сертификат'};
order(PaymentMeans p) += WHEN p == PaymentMeans.paymentMeansExternalGiftCard THEN 3;

overLoadDefaultPaymentTypes() + { 
    loadDefaultType('Внешний подарочный сертификат', PaymentMeans.paymentMeansExternalGiftCard, 'giftcardEx');
}

sumExternalGiftCardPayment 'Сумма продаж (внешний сертификат)' (receipt) = GROUP SUM sum(Payment payment) IF paymentMeans(payment) == PaymentMeans.paymentMeansExternalGiftCard BY receipt(payment);
sumExternalGiftCardPayment 'Сумма продаж (внешний сертификат)' (zReport) = GROUP SUM sumExternalGiftCardPayment(Receipt r) BY zReport(r) IN documentSum;

minExternalGiftCardPaymentType() = GROUP MIN PaymentType paymentType IF paymentMeans(paymentType) == PaymentMeans.paymentMeansExternalGiftCard;

EXTEND FORM zReports
    PROPERTIES (z) READONLY BEFORE sumNegativeCashPayment(z) sumExternalGiftCardPayment
    PROPERTIES (b) READONLY AFTER sumCardPayment(b) sumExternalGiftCardPayment    
;

//имплемент в GiftCardLedger
isSold(ExternalGiftCard payment) += payment IS ExternalGiftCard;

giftCard(ExternalGiftCard payment) += payment AS ExternalGiftCard;
dateTime(ExternalGiftCard payment) += toDateTime(date[GiftCard](payment)) IF payment IS ExternalGiftCard;
date(ExternalGiftCard payment) += date[GiftCard](payment) IF payment IS ExternalGiftCard;
//sum(ExternalGiftCard payment) += sum[Payment](payment) IF payment IS ExternalGiftCard;
description(ExternalGiftCard payment) += 'Продажа ВС' IF payment IS ExternalGiftCard;
//numberDocument(ExternalGiftCard payment) += VARSTRING[30](number(receipt(payment))) IF payment IS ExternalGiftCard;
//departmentStore(ExternalGiftCard payment) += departmentStore(receipt(payment)) IF payment IS ExternalGiftCard;
skipSoldConstraint(ExternalGiftCard payment) += payment IS ExternalGiftCard AND NOT used(payment);

allowNegativeBalance(ExternalGiftCard payment) += TRUE IF payment IS ExternalGiftCard;

createExternalGiftCard 'Создать внешний подарочный сертификат' () {
    NEWSESSION {
        NEW pg = ExternalGiftCard{
            SHOW giftCard OBJECTS g=pg DOCKED;
        }    
    }
} 

EXTEND FORM giftCards
    PROPERTIES createExternalGiftCard() DRAW g TOOLBAR        
;
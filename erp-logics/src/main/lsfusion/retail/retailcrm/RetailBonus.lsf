MODULE RetailBonus;

REQUIRE RetailCRM;

@defineOption(blockDaysBonus, 'Блокировать бонусы на столько дней', INTEGER, discountCards);

@defineOption(useBonus, 'Использовать бонусы', BOOLEAN, discountCards);

minBonusSum 'Минимальная сумма строки чека при оплате бонусами' = DATA NUMERIC[16,4] () COMPLEX;
EXTEND FORM options PROPERTIES() minBonusSum;
DESIGN options { discountCards { MOVE PROPERTY(minBonusSum()); } }

//Бонусы
useBonus 'Использовать бонусы' = DATA BOOLEAN (Promotion);
useBonus 'Использовать бонусы' (PromotionCondition c) = useBonus(promotion(c)) MATERIALIZED;

EXTEND FORM promotion
    PROPERTIES(sh) SHOWIF useBonus() useBonus
;

DESIGN promotion {
    row01 {
        MOVE PROPERTY(useBonus(sh));
    }
}

EXTEND FORM promotions
    PROPERTIES(sh) READONLY SHOWIF useBonus() useBonus 
;

CLASS TypeBonusLedger 'Тип начисления бонусов' {
    user 'Пользовательский',
    registration 'За регистрацию'
}

FORM typeBonusLedger 'Тип начисления бонусов' 
    OBJECTS t = TypeBonusLedger
    PROPERTIES(t) READONLY staticCaption
    
    LIST TypeBonusLedger OBJECT t
;

CLASS BonusLedger 'Начисление бонусов';
TABLE bonusLedger(BonusLedger);

date 'Дата' = DATA DATE (BonusLedger);
date (BonusLedger ledger) <- currentDate() WHEN SET(ledger IS BonusLedger);
sum 'Сумма' = DATA NUMERIC[18,4] (BonusLedger);
note 'Описание' = DATA STRING[100] (BonusLedger);
discountCard = DATA DiscountCard (BonusLedger) NONULL DELETE;
typeBonusLedger = DATA TypeBonusLedger (BonusLedger) NONULL;
nameTypeBonusLedger 'Тип начисления бонусов' (BonusLedger bl) = staticCaption(typeBonusLedger(bl));

sumBonusLedger 'Начислено бонусов' = GROUP SUM sum(BonusLedger l) BY discountCard(l);

createdUser = DATA CustomUser (BonusLedger);
createdUser (BonusLedger ledger) <- currentUser() WHEN SET(ledger IS BonusLedger);
createdNameUser 'Создан пользователем' (BonusLedger ledger) = name[Contact](createdUser(ledger)) CHARWIDTH 10;
createdTime 'Время создания' = DATA DATETIME (BonusLedger);
createdTime (BonusLedger ledger) <- currentDateTime() WHEN SET(ledger IS BonusLedger);
createdComputer = DATA Computer (BonusLedger);
createdComputer (BonusLedger ledger) <- currentComputer() WHEN SET(ledger IS BonusLedger);
createdHostnameComputer 'Создан на компьютере' (BonusLedger ledger) = hostname(createdComputer(ledger)) CHARWIDTH 10;

addBonusLedger 'Добавить' (DiscountCard dc)  { 
    NEW bl = BonusLedger {
        discountCard(bl) <- dc;
        typeBonusLedger(bl) <- TypeBonusLedger.user;
    }    
}

EXTEND FORM discountCard
    PROPERTIES(d) READONLY sumBonusLedger

    OBJECTS l = BonusLedger
    PROPERTIES(l) date, sum ,note, nameTypeBonusLedger
    PROPERTIES(l) READONLY createdNameUser, createdTime, createdHostnameComputer
    PROPERTIES(d) addBonusLedger DRAW l TOOLBAR 
    PROPERTIES(l) NEWSESSION DELETE
    FILTERS discountCard(l) == d
;

DESIGN discountCard {
    tabContainer {
        MOVE BOX(l);
    }
}

EXTEND FORM discountCards
    PROPERTIES(d) READONLY sumBonusLedger
;

// Перевод бонусов

EXTEND CLASS TypeBonusLedger {
    transfer 'Перевод с другой карты'
}

transferCard = DATA DiscountCard (BonusLedger) INDEXED;
seriesNumberTransferCard 'Номер карты перевода' (BonusLedger l) = seriesNumber(transferCard(l));

CONSTRAINT typeBonusLedger(BonusLedger l) == TypeBonusLedger.transfer AND NOT transferCard(l)
    MESSAGE 'Не выбрана карта для начисления бонусов при переводе с карты на карту';

EXTEND FORM discountCard
    PROPERTIES(l) READONLY seriesNumberTransferCard
;

transferBonusLedger (DiscountCard c) = GROUP AGGR BonusLedger l BY transferCard(l);
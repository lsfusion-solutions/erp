MODULE RepricingManufacturingPrice;

REQUIRE Repricing, PriceListLedger, PurchaseManufacturingPrice, PricingManufacturingPrice;

manufacturingPrice 'Цена изготовителя после' = ABSTRACT NUMERIC[16,4] (RepricingDetail) MATERIALIZED;
manufacturingPrice 'Цена изготовителя после' = DATA NUMERIC[16,4] (UserRepricingDetail);

manufacturingPrice (UserRepricingDetail repricingDetail) += manufacturingPrice(repricingDetail);

@defineDocumentInterfaceDetailPriceCustomPrefix(repricingDetail, curManufacturing, ' изготовителя до');
//@deriveDocumentDetailPriceSystemLedgerPriceListType(userRepricing, manufacturingPriceStockPriceListType, curManufacturing, sku, departmentStore);
@deriveDocumentDetailPriceSystemLedgerPriceListTypeBatch (userRepricing, manufacturingPriceStockPriceListType, curManufacturing, sku, departmentStore);
overRecalculatePrice(UserRepricingDetail d) +  { 
    curManufacturingPrice (d)  <- IF batch(d)
        THEN prevPriceB(SystemLedgerPriceListType.manufacturingPriceStockPriceListType,
                    batch(d),
                    departmentStore(d),
                    dateTime(d))
        ELSE prevPriceB(SystemLedgerPriceListType.manufacturingPriceStockPriceListType,
                    sku(d),
                    departmentStore(d),
                    dateTime(d));
}  
                                                       
overConfirmRecalculatePrice(UserRepricingDetail d) +  { 
    manufacturingPrice (d)  <- curManufacturingPrice(d);
}                                                       

skipChangeDateManufacturingPrice = ABSTRACT BOOLEAN (UserRepricingDetail);

manufacturingPrice(UserRepricingDetail d) <- curManufacturingPrice(d) WHEN 
    CHANGED(sku(d)) OR
    CHANGED(departmentStore(d)) OR
    (CHANGED(dateTime(d)) AND NOT skipChangeDateManufacturingPrice(d)) OR
    CHANGED(batch(d));
curManufacturingExtraPackagingCost(UserRepricingDetail d) = curRetailExtraPackagingCost(d);
curManufacturingExtraPackagingCostSkip(UserRepricingDetail d) = d IS UserRepricingDetail;
@changeDocumentDetailPricePrefix(userRepricingDetail, curManufacturing, cur, cur, curManufacturing, cur, curRetail);
manufacturingExtraPackagingCost(UserRepricingDetail d) = retailExtraPackagingCost(d);
manufacturingExtraPackagingCostSkip(UserRepricingDetail d) = d IS UserRepricingDetail;
@changeDocumentDetailPricePrefix(userRepricingDetail, manufacturing, newRetail, , manufacturing, , retail);

useManufacturingCalcMarkup = ABSTRACT BOOLEAN (UserRepricingDetail);
useCurManufacturingCalcMarkup = ABSTRACT BOOLEAN (UserRepricingDetail);

skipCalcPricingManufacturingPrice 'Делать расценку от входной цены, а не цены изготовителя' = ABSTRACT BOOLEAN (UserRepricingDetail);
overSkipCalcPricingManufacturingPrice 'Делать расценку от входной цены, а не цены изготовителя' (UserRepricingDetail d) = OVERRIDE skipCalcPricingManufacturingPrice(d), skipCalcPricingManufacturingPrice();

overRepricingPrice[UserRepricingDetail](UserRepricingDetail detail) += manufacturingPrice(detail) IF (NOT overSkipCalcPricingManufacturingPrice(detail) OR useManufacturingCalcMarkup(detail));
overCurRepricingPrice[UserRepricingDetail](UserRepricingDetail detail) += curManufacturingPrice(detail) IF (NOT overSkipCalcPricingManufacturingPrice(detail) OR useCurManufacturingCalcMarkup(detail));

EXTEND FORM userRepricing
    PROPERTIES(d) manufacturingPrice ON CHANGE changeManufacturingPrice(d) BEFORE price(d),
                  curManufacturingPrice ON CHANGE changeCurManufacturingPrice(d) BEFORE curPrice(d)

;
DESIGN userRepricing{
    PROPERTY(manufacturingPrice(d)) { background = #FFFFCC; }
}

EXTEND FORM repricings
    PROPERTIES(d) READONLY manufacturingPrice BEFORE price(d), curManufacturingPrice BEFORE curPrice(d)
;

overChangePriceUser(UserRepricingDetail userRepricingDetail) + { 
    manufacturingPrice(userRepricingDetail) <- curManufacturingPrice(userRepricingDetail);
}


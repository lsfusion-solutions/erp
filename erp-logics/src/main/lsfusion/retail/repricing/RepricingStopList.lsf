MODULE  RepricingStopList;

REQUIRE StopList, MachineryPriceTransaction, Repricing;

NAMESPACE StopList;

statusStopList 'Запрет продаж' (RepricingDetail d)= IF skipMachineryPriceTransaction(sku(d), departmentStore(d), currentDateTime())
    THEN 'В стоп-листе'
    ELSE 'Нет запрета' CHARWIDTH 10;
backgroundStopList 'Цвет' (RepricingDetail d) = RGB (255,0,0) IF skipMachineryPriceTransaction(sku(d), departmentStore(d), currentDateTime());

countInStopList 'Количество товаров в стоп-листе ' (i,s)= GROUP SUM 1 
    IF skipMachineryPriceTransaction(sku(RepricingDetail d), departmentStore(d), currentDateTime()) 
          BY repricing(d), sku(d);

countSkuInStopList 'Количество товаров в стоп-листе ' (Repricing i) = GROUP SUM 1 
    IF countInStopList(i,Sku s);
            
createReversStopList 'Исключить из стоп-листа'(Repricing repricing)   { 

    NESTEDSESSION {   
        NEW sl = StopList {    
            in(DepartmentStore st, sl) <- TRUE WHERE st == departmentStore(repricing);  
            exclude(sl) <-TRUE;
            isPosted(sl) <-TRUE;
                   
            FOR countInStopList(repricing, Sku sku) NEW d = StopListDetail DO {
                
                stopList(d) <- sl;
                sku(d) <- sku;            
            }   
            SHOW stopList OBJECTS sl =sl DOCKED;
        }        
    }
};

EXTEND FORM userRepricing
    PROPERTIES (p) READONLY PANEL SHOWIF countSkuInStopList(p) DRAW d countSkuInStopList,
                   createReversStopList CHANGEABLE 
    PROPERTIES (d) READONLY statusStopList BACKGROUND backgroundStopList(d) AFTER nameSku(d)
    
;
DESIGN userRepricing {
    PANEL(d) {
        NEW stopList {
            horizontal = TRUE;
            caption = 'Стоп-лист';            
            MOVE PROPERTY (countSkuInStopList(p)) {background = #FF0000;}
            MOVE PROPERTY (createReversStopList(p));
        }        
    }    
}  
EXTEND FORM repricings
    PROPERTIES (d) READONLY statusStopList BACKGROUND backgroundStopList(d) AFTER nameSku(d)
;
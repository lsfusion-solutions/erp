MODULE ReceiptReports;

REQUIRE ZReport, PurchaseLedger;

NAMESPACE ZReport;


CLASS ReceiptReport 'Отчет по чекам';
CLASS ReceiptReportDetail 'Строка отчета по чекам';

CLASS IntervalReport 'Диапазон суммы по чеку';
TABLE intervalReport (IntervalReport);

CLASS IntervalReportDetail 'Диапазон';
TABLE intervalReportDetail (IntervalReportDetail);

@defineDocument(receiptReport);
@deriveDocumentHeaderTimePrefix(ReceiptReport, );

@defineDocumentHeaderNumber(ReceiptReport);
@defineNumeratedDefault(ReceiptReport, 'Отчет по чекам', 'ОЧ');


@defineDocumentDescription (receiptReport, 'Отчет по чекам');

//--

sum 'Верхний порог суммы' = DATA NUMERIC[18,4] (IntervalReport) NONULL DELETE;

@defineDocumentRelation (receiptReport, IntervalReport, '');

intervalPrev (IntervalReport interval) = PARTITION PREV interval ORDER sum(interval) 
    BY receiptReport(interval) MATERIALIZED;
fromSumIntervalReportReceipt 'Сумма с' (IntervalReport interval)= sum(intervalPrev (interval));   

@defineDocumentRelation (receiptReportDetail, IntervalReportDetail, '');
@defineDocumentDetailActions(receiptReportDetail, IntervalReportDetail);

intervalReport  = DATA IntervalReport (IntervalReportDetail);
sum 'Верхний порог суммы'  = sum(intervalReport(IntervalReportDetail d));

fromDate 'Дата с' = DATA DATE (ReceiptReport) IN documentPrm;
fromDate 'Дата с' (ReceiptReportDetail d)= fromDate(receiptReport(d));

toDate 'Дата по' = DATA DATE (ReceiptReport) IN documentPrm;
toDate 'Дата по' (ReceiptReportDetail d)= toDate(receiptReport(d));

fromTime 'Время с' = DATA TIME (ReceiptReport) IN documentPrm;
fromTime 'Время с' (ReceiptReportDetail d)= fromDate(receiptReport(d));

toTime 'Время по' = DATA TIME (ReceiptReport) IN documentPrm;
toTime 'Время по' (ReceiptReportDetail d)= toDate(receiptReport(d));

fromDate (ReceiptReport o) <- currentDate() WHEN SET(o IS ReceiptReport);
toDate (ReceiptReport o) <- currentDate() WHEN SET(o IS ReceiptReport);
fromTime (ReceiptReport o) <- 00:00 WHEN SET(o IS ReceiptReport);
toTime (ReceiptReport o) <- 23:59 WHEN SET(o IS ReceiptReport);


TABLE storeReceiptReport (Store, ReceiptReport);
in 'Вкл.' = DATA BOOLEAN (Store, ReceiptReport);

TABLE groupReceiptReport (Group, ReceiptReport);
inData 'Вкл.' = DATA BOOLEAN (Group, ReceiptReport);

countInDataGroup (ReceiptReport report)= GROUP SUM 1 IF inData(Group group, report);

levelParent (Group group, ReceiptReport report) = GROUP
    MIN level(group, Group parent) IF inData(parent, report);

inParent (Group group, ReceiptReport report) = TRUE IF levelParent(group, report);

in 'Вкл.' (Group group, ReceiptReport report) = OVERRIDE
    inData(group, report),
    inParent(group, report);
    
TABLE legalEntityGroupReceiptReport (LegalEntityGroup, ReceiptReport);
inData 'Вкл.' = DATA BOOLEAN (LegalEntityGroup, ReceiptReport);

TABLE legalEntityReceiptReport (LegalEntity, ReceiptReport);
inData 'Вкл.' = DATA BOOLEAN (LegalEntity, ReceiptReport);

levelParent (LegalEntityGroup group, ReceiptReport report) = GROUP
    MIN level(group, LegalEntityGroup parent) IF inData(parent, report);
inParent (LegalEntityGroup group, ReceiptReport report) = TRUE IF levelParent(group, report);

in 'Вкл.' (LegalEntityGroup legalEntityGroup, ReceiptReport report) = OVERRIDE
    inData(legalEntityGroup, report),
    inParent(legalEntityGroup, report);

in 'Вкл.' (LegalEntity company, ReceiptReport report) = OVERRIDE
    inData(company, report),    
    in(legalEntityGroup(company), report);

countInLegalEntity (ReceiptReport report)= GROUP SUM 1 IF in(LegalEntity company, report);

groupType = DATA GroupType (ReceiptReport);
nameGroupType 'Классификатор' (ReceiptReport g) =  name(groupType(g));

groupType (ReceiptReport o) <- SystemGroupType.skuGroupType WHEN SET(o IS ReceiptReport);

countBon 'Кол-во чеков' = DATA NUMERIC[14,3] (ReceiptReport) IN documentSum;
countBonDetail 'Ср. кол-во позиций в чеке' = DATA NUMERIC[14,3] (ReceiptReport) IN documentSum;
countSelectedBonDetail 'Ср. кол-во выб. позиций в чеке' = DATA NUMERIC[14,3] (ReceiptReport) IN documentSum;
sumBon 'Ср. сумма чека' = DATA NUMERIC[18,4] (ReceiptReport) IN documentSum;
sumSelectedBonDetail 'Ср. сумма выб. позиций в чеке' = DATA NUMERIC[18,4] (ReceiptReport) IN documentSum;

store = DATA Store (ReceiptReportDetail);
nameStore 'Магазин' (ReceiptReportDetail d) = name(store(d));

countBon 'Кол-во чеков' = DATA NUMERIC[14,3] (ReceiptReportDetail);
countBonDetail 'Ср. кол-во позиций в чеке' = DATA NUMERIC[14,3] (ReceiptReportDetail);
countSelectedBonDetail 'Ср. кол-во выб. позиций в чеке' = DATA NUMERIC[14,3] (ReceiptReportDetail);
sumBon 'Ср. сумма чека' = DATA NUMERIC[18,4] (ReceiptReportDetail);
sumSelectedBonDetail 'Ср. сумма выб. позиций в чеке' = DATA NUMERIC[18,4] (ReceiptReportDetail);

countBon 'Кол-во чеков' = DATA NUMERIC[14,3] (IntervalReportDetail);
countBonDetail 'Ср. кол-во позиций в чеке' = DATA NUMERIC[14,3] (IntervalReportDetail);
countSelectedBonDetail 'Ср. кол-во выб. позиций в чеке' = DATA NUMERIC[14,3] (IntervalReportDetail);
sumBon 'Ср. сумма чека' = DATA NUMERIC[18,4] (IntervalReportDetail);
sumSelectedBonDetail 'Ср. сумма выб. позиций в чеке' = DATA NUMERIC[18,4] (IntervalReportDetail);
            
            
in (ReceiptReport r, ReceiptDetail d) = 
    date(d) >= fromDate(r) AND date(d) <= toDate(r)
    AND toTime(dateTime(d)) >= fromTime(r) AND toTime(dateTime(d)) <= toTime(r)  
    AND in(store(d), r) AND sku(d) AND isPosted(d)
    AND (in(group(groupType(r), sku(d)), r) OR (sku(d) AND NOT countInDataGroup(r)))
    AND (in(supplierLastOrderBatch(sku(d), departmentStore(d)),r) OR (sku(d) AND NOT countInLegalEntity(r)));

in (ReceiptReport report, receipt) = GROUP SUM 1 IF in (report, ReceiptDetail d) BY receipt(d);

countBon 'Кол-во чеков' = GROUP SUM 1 IF in(ReceiptReport report, Receipt receipt) BY store(receipt), report;
countBonDetail 'Кол-во позиций в выбранных чеках' = GROUP SUM countReceiptDetail(Receipt receipt) IF in (ReceiptReport report, receipt) BY store(receipt), report;
in 'Кол-во выбранных позиций' = GROUP SUM 1 IF in (ReceiptReport report, ReceiptDetail d) BY report, store(d);
sumBonDetail 'Сумма выбранных чеков' = GROUP SUM sumReceiptDetail(Receipt receipt) IF in (ReceiptReport report, receipt) BY store(receipt), report;
sumSelectedBonDetail 'Сумма выбранных позиций' = GROUP SUM sum(ReceiptDetail d) IF in (ReceiptReport report, d) BY store(d), report;

//-- С диапазонами сумм
in (ReceiptReport report, Receipt receipt, IntervalReport interval) = in (report, receipt) IF
    receiptReport(interval) == report AND sumReceiptDetail(receipt) <= sum(interval) AND 
    ((fromSumIntervalReportReceipt(interval) < sumReceiptDetail(receipt)) OR  (NOT fromSumIntervalReportReceipt(interval)));

countBon 'Кол-во чеков' = GROUP SUM 1 IF
    in(ReceiptReport report, Receipt receipt, IntervalReport interval) 
        BY store(receipt), report, interval;
countBonDetail 'Кол-во позиций в выбранных чеках' = GROUP SUM countReceiptDetail(Receipt receipt) IF 
    in(ReceiptReport report, receipt, IntervalReport interval) 
        BY store(receipt), report, interval;        
        
in 'Кол-во выбранных позиций' = GROUP SUM 1 IF 
    in (ReceiptReport report, ReceiptDetail d) AND in(report, receipt(d), IntervalReport interval)  
        BY report, store(d), interval;
        
sumBonDetail 'Сумма выбранных чеков' = GROUP SUM sumReceiptDetail(Receipt receipt) IF
     in(ReceiptReport report, receipt, IntervalReport interval) 
        BY store(receipt), report, interval;
sumSelectedBonDetail 'Сумма выбранных позиций' = GROUP SUM sum(ReceiptDetail d) IF
    in (ReceiptReport report, d) AND in(report, receipt(d), IntervalReport interval)  
        BY store(d), report, interval;


//--
calcCountBon 'Кол-во чеков' (ReceiptReport report) = GROUP SUM 1 IF in(report, Receipt receipt);
calcCountBonDetail 'Кол-во позиций в выбранных чеках' (ReceiptReport report) = GROUP SUM countReceiptDetail(Receipt receipt) IF in (report, receipt);
calcIn 'Кол-во выбранных позиций' (ReceiptReport report) = GROUP SUM 1 IF in (report, ReceiptDetail d);
calcSumBonDetail 'Сумма выбранных чеков' (ReceiptReport report) = GROUP SUM sumReceiptDetail(Receipt receipt) IF in (report, receipt);
calcSumSelectedBonDetail 'Сумма выбранных позиций' (ReceiptReport report) = GROUP SUM sum(ReceiptDetail d) IF in (report, d);


fillData 'Заполнить данные'(ReceiptReport report)  { 
    DELETE ReceiptReportDetail d WHERE d IS ReceiptReportDetail AND receiptReport(d)== report;

    
    countBon(report) <- NUMERIC[14,3](calcCountBon(report));
    countBonDetail(report) <- NUMERIC[14,3](calcCountBonDetail(report) /(countBon(report)));        
    countSelectedBonDetail(report) <- NUMERIC[14,3](calcIn(report) /(countBon(report)));        
    sumBon(report) <- NUMERIC[18,4](calcSumBonDetail(report)/(countBon(report)));        
    sumSelectedBonDetail(report) <- NUMERIC[18,4](calcSumSelectedBonDetail(report)/(calcIn(report)));    
    
    
    FOR in(Store store,report) NEW d = ReceiptReportDetail DO {
        receiptReport(d) <- report;
        store(d) <- store;
        
        countBon(d) <- NUMERIC[14,3](countBon(store, report));
        countBonDetail(d) <- NUMERIC[14,3](countBonDetail(store, report) /(countBon(d)));
        countSelectedBonDetail(d) <- NUMERIC[14,3](in(report, store) /(countBon(d)));        
        sumBon(d) <- NUMERIC[18,4](sumBonDetail(store, report)/(countBon(d)));
        sumSelectedBonDetail(d) <- NUMERIC[18,4](sumSelectedBonDetail(store, report)/(in(report, store)));
        
        FOR receiptReport(IntervalReport interval) == report NEW dd = IntervalReportDetail DO {
            intervalReport(dd) <- interval;
            receiptReportDetail(dd) <- d;
//            todo: Валится ассоршен, без -еа- работает. Разобраться. Начиная со свойства (countBonStoreReceiptReportIntervalReport(store, report, interval))

            countBon(dd) <- NUMERIC[14,3](countBon(store, report, interval));
            countBonDetail(dd) <- NUMERIC[14,3](countBonDetail(store, report, interval) /(countBon(dd)));
            countSelectedBonDetail(dd) <- NUMERIC[14,3](in(report, store,interval) /(countBon(dd)));        
            sumBon(dd) <- NUMERIC[18,4](sumBonDetail(store, report, interval)/(countBon(dd)));
            sumSelectedBonDetail(dd) <- NUMERIC[18,4](sumSelectedBonDetail(store, report,interval)/(in(report, store,interval)));            
            
        }        
    }        
}

FORM receiptReport 'Отчет по чекам'

    OBJECTS r = ReceiptReport PANEL
    PROPERTIES (r) nameNumerator, number, series, date, time,
                   fromDate, toDate, 
                   fromTime, toTime, nameGroupType
    PROPERTIES (r) READONLY countBon, countBonDetail, countSelectedBonDetail, 
                   sumBon, sumSelectedBonDetail
                   
    OBJECTS s = Store             
    PROPERTIES  name(s)READONLY, in(s,r)  
                 
    TREE groupTree g=Group PARENT parent(g)
    PROPERTIES READONLY order(g), id(g), name(g), in(g,r) CHANGEABLE 
    FILTERS groupType(g) == groupType(r)
    ORDER order(g), name(g)
    FILTERGROUP inactive1 FILTER 'Активные' active(g) 'F5' DEFAULT      
               
    TREE legalEntityTree lg = LegalEntityGroup PARENT parent(lg)
    PROPERTIES READONLY  name(lg)
    ORDER name(lg)
    PROPERTIES(lg,r) in

    OBJECTS l = LegalEntity
    PROPERTIES(l) READONLY id SHOWIF showIDs(), legalEntityName = name
    ORDER legalEntityName
    FILTERS isParent(lg, l) OR (l IS LegalEntity AND NOT lg)
    FILTERGROUP inactiveLegalEntity FILTER 'Активная' active(l) 'shift F10' DEFAULT
    PROPERTIES(l,r) in
    
    OBJECTS i = IntervalReport
    PROPERTIES(i) sum
    PROPERTIES NEW(i), deletei = DELETE(i) GRID
    FILTERS receiptReport(i) == r

    OBJECTS d = ReceiptReportDetail
    PROPERTIES (d) index, nameStore, countBon, countBonDetail, countSelectedBonDetail,
                   sumBon, sumSelectedBonDetail
                  
    PROPERTIES (d) NEW, deleteid=DELETE GRID
    
    PROPERTIES(r)  fillData TOOLBAR DRAW d  
    
    ORDER index(d)

    PROPERTIES(r) DRAW d deleteReceiptReportDetail
    
    FILTERS receiptReport(d) == r
    
    OBJECTS dd = IntervalReportDetail
    PROPERTIES (dd) sum, countBon, countBonDetail, countSelectedBonDetail,
                    sumBon, sumSelectedBonDetail    
    PROPERTIES (dd) NEW, deleteidd=DELETE GRID                    
    PROPERTIES deleteIntervalReportDetail(d) DRAW dd 
    
    ORDER sum(dd)  
                 
    FILTERS receiptReportDetail(dd) == d
            

    EDIT ReceiptReport OBJECT r
;

DESIGN receiptReport {
    BOX {
        size = (1024, 768);
        MOVE BOX(r) {
            type = CONTAINERH;
            NEW r1 {
                type = CONTAINERV;
                MOVE GROUP(numbered,r);
                MOVE GROUP(documentHeader,r);
                MOVE GROUP(documentPrm,r);
            }
            MOVE GROUP(documentSum,r) {type = CONTAINERV;}
            
        }        
        NEW headerBox {
            type = TABBED;
            fill = 1;
            NEW headerParams {
                fill = 1;
                caption = 'Параметры отчета';
                type = TABBED;
                NEW store {
                    fill = 1;
                    caption ='Выбор магазинов';
                    MOVE BOX(s);
                }
                NEW company {
                    fill = 1;
                    caption ='Выбор поставщиков';
                    type = SPLITH;
                    MOVE BOX(TREE legalEntityTree) {caption = 'Группы организаций';}
                    MOVE BOX(l);
                }
                NEW group {
                    fill = 1;
                    caption ='Выбор товарных групп';
                    type = CONTAINERV;
                    NEW group1 {
                        caption = 'Классификатор';
                        MOVE PROPERTY(nameGroupType(r));
                    }                    
                    MOVE BOX(TREE groupTree) {fill = 1; caption = 'Группы товаров';}
                } 
//                NEW row5 {
//                    fill = 1;
                    MOVE BOX(i){                    
                        caption = 'Задание диапазона сумм'; 
                    }
//                    NEW BOX(o);
//                }
            }
            NEW specContainer{
                fill = 1;
                caption = 'Спецификация';
                MOVE BOX(d);
                MOVE BOX(dd) {caption = 'Диапазоны сумм';}
            }           
        }        

        MOVE TOOLBARBOX;

    }
}

FORM receiptReports 'Отчеты по чекам'

    OBJECTS r = ReceiptReport 
    PROPERTIES (r) READONLY number, series, date, time, fromDate, toDate, 
                   fromTime, toTime, nameGroupType
    PROPERTIES (r) READONLY countBon, countBonDetail, countSelectedBonDetail, 
                   sumBon, sumSelectedBonDetail, countReceiptReportDetail

    PROPERTIES (r) NEWSESSION NEW, EDIT, deleter=DELETE   
    PROPERTIES (r) READONLY PANEL createdNameUser, createdTime, createdHostnameComputer 
        
    OBJECTS d = ReceiptReportDetail
    PROPERTIES (d) READONLY index, nameStore, countBon, 
                   countBonDetail, countSelectedBonDetail,
                   sumBon, sumSelectedBonDetail
    
    ORDER index(d)

    FILTERS receiptReport(d) == r
                
    LIST ReceiptReport OBJECT r
;
DESIGN receiptReports {
    BOX {
        size = (1024, 768);

        NEW documentContainer BEFORE TOOLBARBOX {
            fill = 1;
            type = SPLITV;
            MOVE BOX(r);   

            NEW documentDetail {
                fill = 1;
                type = TABBED;

                MOVE BOX(d) {
                    fill = 1;
                    caption = 'Спецификация';
                }
                NEW documentHistory {
                    caption = 'История';
                    type = CONTAINERV;

                    MOVE GROUP(created,r);                       
                }
                NEW printTab {
                    caption = 'Печатные формы';
                    NEW printContainer {
                        caption = 'Печать';
                        type = CONTAINERV;
                    }
                }
            }
        }
    }
}



NAVIGATOR {
    retailReports {
        NEW receiptReports;
    }
}
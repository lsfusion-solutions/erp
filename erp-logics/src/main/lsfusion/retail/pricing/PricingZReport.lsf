MODULE PricingZReport;

REQUIRE System,
        Utils,
        Stock,
        Store,
        ZReport,
        StockDocumentSkuLedger;

// документ скидок для товарного отчета
CLASS ZReportRepricing 'Скидка по продаже через кассы';
TABLE zReportRepricing (ZReportRepricing);

extraDiscountSumReceiptDetail = ABSTRACT NUMERIC[18,4] (ZReport);
totalDiscountSumReceiptDetail(ZReport zReport) = discountSum(zReport) (+) extraDiscountSumReceiptDetail(zReport);

zReportRepricing = AGGR ZReportRepricing WHERE totalDiscountSumReceiptDetail(ZReport zReport) MATERIALIZED INDEXED;
@defineDocumentAggregationHeaderNumberCustom(zReport, zReportRepricing, zReport);
@defineDocumentAggregationHeaderTime(zReport, zReportRepricing);
@defineDocumentAggregationHeaderStock(zReport, zReportRepricing, departmentStore, , 'Отдел магазина');
@defineDocumentAggregationHeaderPosted(zReport, zReportRepricing);
isClosed 'Закрыт' (ZReportRepricing o) = isClosed(zReport(o));
numberCashRegister (ZReportRepricing zReportRepricing) = overNumberCashRegister(zReport(zReportRepricing));

description (ZReportRepricing zReportRepricing) =
    STRING[200]('Скидка по кассе ' + numberCashRegister(zReportRepricing) + ' отдела ' + nameDepartmentStore(zReportRepricing) + ' от ' + date(zReportRepricing));

@implementStockDocumentLedgerOut(ZReportRepricing, departmentStore);
type(ZReportRepricing l) += STRING[50]('Скидка по кассе') IF l IS ZReportRepricing;
sumItem (ZReportRepricing ledger) += totalDiscountSumReceiptDetail(zReport(ledger));
sumContainer (ZReportRepricing ledger) += 0.0 IF ledger IS ZReportRepricing;
skip (ZReportRepricing ledger) += date(ledger) < startDateGroupCashRegister(departmentStore(ledger));
edit(ZReportRepricing l) + {  edit(zReport(l)); }

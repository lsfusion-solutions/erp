MODULE PricingWare;

REQUIRE PricingSum, PricingManufacturingPrice, PricingSumInvoice, Ware, PurchaseCreditNote, PricingSale;

nameStore 'Магазин' = name(store(departmentStore(Pricing p)));

overManufacturingMarkup = ABSTRACT NUMERIC[8,3](PricingDetail); 

manMarkup 'Оптовая надбавка' (PricingDetail d)= OVERRIDE overManufacturingMarkup(d), 
    round2([(X-Y)*100/Y](
    price(d),
    (manufacturingPrice(d) IF manufacturingPrice(d)!=0.0)));

EXTEND FORM pricings
    PROPERTIES (d) manMarkup
;    

retailVATPrice 'Цена розничного НДС' = (retailVATSum(PricingDetail d) (-) extraRetailVATSum(d)) / (quantity(d) IF quantity(d)!=0);
calcVATPrice 'Цена поставщика с надбавкой' = retailPrice(PricingDetail d) (-) retailVATPrice(d) (-) extraRetailPrice(d); 
calcMarkupPrice 'Цена надбавки'= calcVATPrice(PricingDetail d) (-) price(d);

notWareRetailPrice 'Цена розн. без посуды' = retailPrice(PricingDetail d) (-) extraRetailPrice(d) IF isNotContainer(sku(d));

retailVATSumNotWare 'Сумма НДС' (PricingDetail detail) = retailVATSum(detail) (-) extraRetailVATSum(detail) IF NOT (sku(detail) IS Ware);
retailSumNotWare 'Сумма розничная' (PricingDetail detail) = retailSum(detail) IF isNotContainer(sku(detail)) AND NOT (sku(detail) IS Ware);

retailMarkupSumNotWare =  (retailSum(PricingDetail d)  (-) retailVATSum(d) (+) extraRetailVATSum(d) (-) sum(d)) IF NOT (sku(d) IS Ware); 

//тара, в текущем варианте не нужна
//sumContainerPricingDetailWare 'Сумма поставщика (тара)' (pricing) = GROUP SUM round(pricingPriceWare(PricingDetail idetail)*quantity(idetail), currency(idetail)) IF isContainer(sku(idetail))  
//    BY pricing(idetail) IN documentSum;
//retailMarkupSumContainerPricingDetailWare 'Сумма надбавки (тара)' (pricing) = GROUP SUM retailMarkupSum(PricingDetail idetail) IF isContainer(sku(idetail))  
//    BY pricing(idetail) IN documentSum;
//retailVATSumContainerPricingDetailWare 'Сумма НДС (тара)' (pricing) = GROUP SUM retailVATSum(PricingDetail idetail) (-) extraRetailVATSum(idetail) IF isContainer(sku(idetail))  
//    BY pricing(idetail) IN documentSum;
//retailSumContainerPricingDetailWare 'Сумма розничная (тара)' (pricing) = GROUP SUM retailSum(PricingDetail idetail) IF isContainer(sku(idetail))  
//    BY pricing(idetail) IN documentSum;
       
//markupWare '%' (PricingDetail d)=  retailMarkupSum(d)*100 / (round(pricingPriceWare(d)*quantity(d), currency(d))  IF pricingPriceWare(d)*quantity(d) !=0);   
retailMarkupPrice 'Надбавка, ед.' (PricingDetail d)= retailMarkupSum(d) / (quantity(d) IF quantity(d)!=0);
    
defaultInvoiceDetail = GROUP MAX invoiceDetail(PricingDetail d) BY pricing(d);
nameOperationInvoice (Pricing p) = IF isReturn(p) THEN overName(operation(defaultInvoiceDetail(p))) ELSE name(operation(defaultInvoiceDetail(p)));    

//НДС поставщика и сумма с НДС  
sumSupplierCNote = ABSTRACT VALUE NUMERIC[18,4](PricingDetail);
sumCNote (PricingDetail d) = sum(invoiceDetail(d)) (-) sum(invoiceCreditNoteDetail(invoiceDetail(d)));     
sumSupplierCNote(PricingDetail d) += sumCNote(d);

invoiceSum (PricingDetail d) = invoiceSum(invoiceDetail(d)) (-) invoiceSum(invoiceCreditNoteDetail(invoiceDetail(d))) (+) extraRetailSum(d);//invoiceSum(invoiceDetail(d)) (+) extraRetailSum(d);

overValueVAT = ABSTRACT VALUE NUMERIC[10,5] (PricingDetail);
valueVAT (PricingDetail d) = OVERRIDE overValueVAT(d), (IF (invoice(invoiceDetail(d)) IS Sale.Invoice) THEN valueVAT(lastOrderBatchA(sku(d),stock(d),dateTime(d))) ELSE valueVAT(invoiceDetail(d)));
sumSupplier 'Сумма поставщика без НДС'(PricingDetail d) = IF invoice(invoiceDetail(d)) IS Sale.Invoice THEN supplierSum[Sale.InvoiceDetail](invoiceDetail(d)) ELSE sumSupplierCNote(d);
VATSum 'Сумма НДС'(PricingDetail d) = IF invoice(invoiceDetail(d)) IS Sale.Invoice THEN round2(sumSupplier(d)*valueVAT(d)/100) 
                                                        ELSE VATSum(invoiceDetail(d)) (-) VATSum(invoiceCreditNoteDetail(invoiceDetail(d)));
sumSupplierWithVAT 'Сумма поставщика с НДС' (PricingDetail d) =  IF invoice(invoiceDetail(d)) IS Sale.Invoice THEN sumSupplier(d) (+) extraRetailSum(d) (+) VATSum(d) ELSE invoiceSum(d);

extraRetailVAT 'НДС посуды'(PricingDetail d) = round0(extraRetailVATSum(d) / (extraRetailSum(d) (-) extraRetailVATSum(d)) * 100);          
//extraRetailPrice(UserInvoiceDetail d) += retailPrice[InvoiceDetail](wareUserInvoiceDetail(d));

extraSupplierSum 'Сумма посуды без НДС'  (PricingDetail d) = extraRetailSum(d) (-) extraRetailVATSum(d);
extraSupplierPrice 'Цена посуды без НДС'  (PricingDetail d) = round2(extraSupplierSum(d) / (quantity(d) IF quantity(d) != 0.0));          

FORM pricingWare 'Реестр цен'
    OBJECTS p=Pricing PANEL SUBREPORT
    PROPERTIES (p) SELECTOR isPosted

    PROPERTIES (p) nameStore, nameDepartmentStore, fullNameLegalEntityStock, seriesNumber, number, series, date, time,
                   nameSupplier, nameCurrency, note,
                   countPricingDetail, quantityPricingDetail, sumPricingDetail,
                   retailMarkupSumPricingDetail, retailVATSumPricingDetail,
                   retailSumPricingDetail, namePricingCommittee,
                   nameFormedMan, nameCheckedMan, nameLabeledMan, nameAccountantMan, nameHeadMan,
                   isInvoice, fullNameSupplier

    OBJECTS d=PricingDetail

    PROPERTIES (d) index, idBarcodeSku, nameSku, extraDescription, shortNameUOMSku, retailPrice,
                   quantity, overPricingPrice, pricingPrice, manMarkup, price, sum, valueVAT, VATSum, invoiceSum,
                   retailMarkup, retailMarkupSum, numberRetailVAT, valueRetailVAT, retailVATSum,
                   extraRetailPrice, extraRetailSum, extraRetailVAT, extraRetailVATSum, extraSupplierSum, extraSupplierPrice,
                   notWareRetailPrice, retailMarkupPrice, retailVATSumNotWare, retailSumNotWare,
                   calcMarkupPrice, calcVATPrice, retailVATPrice, retailMarkupSumNotWare, sumCNote, sumSupplier, sumSupplierWithVAT
                   
                   
    ORDERS index(d)
    FILTERS pricing(d) == p,
            d IS PricingDetail AND NOT (sku(d) IS Ware),
            TRUE IF quantity(d)
;

pricingWare 'Реестр цен' (Pricing pricing)  { PRINT pricingWare OBJECTS p = pricing; }  IMAGE 'print.png' IN print;

EXTEND FORM pricings 
    PROPERTIES (p) pricingWare    
;

FORM pricingWareAct 'Расходный акт'
    OBJECTS p=Pricing PANEL SUBREPORT
    PROPERTIES (p) SELECTOR isPosted

    PROPERTIES (p) nameStore, nameDepartmentStore, fullNameLegalEntityStock, seriesNumber, number, series, date, time,
                   nameSupplier, nameCurrency, note,
                   countPricingDetail, quantityPricingDetail, sumPricingDetail,
                   retailMarkupSumPricingDetail, retailVATSumPricingDetail,
                   retailSumPricingDetail, namePricingCommittee,
                   nameFormedMan, nameCheckedMan, nameLabeledMan, nameAccountantMan, nameHeadMan,
                   isInvoice, fullNameSupplier, nameOperationInvoice

    OBJECTS d=PricingDetail

    PROPERTIES (d) index, idBarcodeSku, nameSku, extraDescription, shortNameUOMSku,
                   quantity, overPricingPrice, pricingPrice, manMarkup, price, sum, valueVAT, VATSum, invoiceSum,
                   retailMarkup, retailMarkupSum, numberRetailVAT, valueRetailVAT, retailVATSum,
                   extraRetailPrice, extraRetailSum, extraRetailVAT, extraRetailVATSum,
                   notWareRetailPrice, retailMarkupPrice, retailVATSumNotWare, retailSumNotWare,
                   calcMarkupPrice, calcVATPrice, retailVATPrice, retailMarkupSumNotWare, sumCNote
    ORDERS index(d)
    FILTERS pricing(d) == p,
            d IS PricingDetail AND NOT (sku(d) IS Ware),
            TRUE IF quantity(d)
;

pricingWareAct 'Расходный акт' (Pricing pricing)  { 
    PRINT pricingWareAct OBJECTS p = pricing; 
}  IMAGE 'print.png' IN print;

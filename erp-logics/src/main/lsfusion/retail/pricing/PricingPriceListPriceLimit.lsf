MODULE PricingPriceListPriceLimit;

REQUIRE PricingPriceList, PricingPriceLimit; 

NAMESPACE Pricing;

skipPricingPriceLimit = ABSTRACT BOOLEAN (Sku);
skipPricingPriceMarkupLimit = ABSTRACT BOOLEAN (Sku);
useCustomCalcLimitPrice = DATA BOOLEAN ();
overLimitBasePrice = ABSTRACT VALUE NUMERIC[16,4] (Sku, DepartmentStore, DATETIME);
limitBasePrice(Sku s, DepartmentStore d, DATETIME  dt) = OVERRIDE overLimitBasePrice(s,d,dt), 
                                                                         prevPriceA(basePriceLimitPriceListType(d), s, d, dt);

calculateLimitBasePrice ABSTRACT (DepartmentStore, DATETIME);

useLimitMarkupValue = ABSTRACT VALUE BOOLEAN (Sku, DepartmentStore);

extraRetailPrice 'Цена посуды' = ABSTRACT NUMERIC[16,4] (Sku, DATE );

limitPackagePrice = ABSTRACT NUMERIC[16,4](Sku, DepartmentStore, DATETIME);

overSetManagementRetailPrice(DepartmentStore d, DATETIME dt) + {
    LOCAL limitPrice = NUMERIC[16,4] (Sku);        

    FOR CalcPriceListType ct = priceLimitPriceListType(d) AND NOT useCustomCalcLimitPrice() NOINLINE DO {
        calculateLimitBasePrice(d, dt);

        LOCAL lpPrice = NUMERIC[16,5](Sku);

        LOCAL markupCorrect, importerMarkup = NUMERIC[8,3](Sku);
        markupCorrect(Sku sku) <- NULL;
        markupCorrect(Sku sku) <- markup(ct, sku) WHERE managementRetailPrice(sku);

        lpPrice (Sku sku) <- limitPackagePrice(sku, d, dt) WHERE managementRetailPrice(sku);
        importerMarkup(Sku sku) <- prevImporterMarkup(prevRetailPricingPriceListLedgerB( sku, d, dt)) WHERE managementRetailPrice(sku);
        markupCorrect(Sku sku) <-  floor(((100.0 + markupCorrect(sku))/(100 + importerMarkup(sku))-1.0)*100.0, 0.01) WHERE markupCorrect(sku) AND importerMarkup(sku);

        limitPrice(Sku sku) <- round([ ((X * (Y + 100.0)/100.0)(+) W) * (Z + 100.0) / 100.0](
                limitBasePrice(sku, d, dt),
                markupCorrect(sku),
                lpPrice(sku),
                valueVAT(sku, d)), roundCondition(ct, sku)) WHERE managementRetailPrice(sku);

        limitPrice(Sku sku) <- limitPrice(sku) (+) extraRetailPrice(sku, DATE(dt)) WHERE limitPrice(sku);

        managementRetailPrice (Sku sku) <- limitPrice(sku) WHERE limitPrice(sku) < managementRetailPrice (sku) AND NOT skipPricingPriceLimit(sku) AND NOT skipPricingPriceMarkupLimit(sku);
    }
}

calculateDescriptionPricing (Sku sku, DepartmentStore d, DATETIME dt) + {
    FOR CalcPriceListType ct = priceLimitPriceListType(d) NOINLINE DO {

        LOCAL markupCorrect, importerMarkup = NUMERIC[8,4]();
        markupCorrect() <- markup(ct, sku);
        importerMarkup() <- prevImporterMarkup(prevRetailPricingPriceListLedgerB( sku, d, dt)) ;
        markupCorrect() <-  floor(((100.0 + markupCorrect())/(100 + importerMarkup())-1.0)*100.0, 0.01) WHERE markupCorrect() AND importerMarkup();

        descriptionPricing (sku, 'Предельные надбавки') <- STRING(
            round([ ((X * (Y + 100.0)/100.0)(+) W) * (Z + 100.0) / 100.0](
                    limitBasePrice(sku, d, dt),
                    markupCorrect(),
                    limitPackagePrice(sku, d, dt),
                    valueVAT(sku, d)), roundCondition(ct, sku)));
        descriptionPricing (sku, 'Цена посуды') <- STRING(extraRetailPrice(sku, DATE(dt)));
    }
}
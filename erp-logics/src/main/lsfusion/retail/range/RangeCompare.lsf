MODULE RangeCompare;

REQUIRE RangeLevel;

NAMESPACE Range;

inSession 'Вкл.' = DATA LOCAL BOOLEAN (RangeRev);

EXTEND FORM range
    PROPERTIES inSession(v) BEFORE dateTime(v)
;

levels(RangeRev v, Sku s) = OVERRIDE name(level(v,s)),'';
countInRanges 'Сколько раз товар встречается в версиях' = GROUP SUM 1 IF inSession(RangeRev v) AND level(v, Sku s) BY s;
countInLevels 'Сколько разных уровней (кроме пустых)' (Sku s) = (GROUP SUM 1 IF [GROUP SUM 1 IF inSession(RangeRev v) BY level(v, s)](RangeLevel l));
changed 'Больше 1 разновидности уровня или есть версии без этого товара' (Sku s) = countInLevels(s) > 1 OR countInRanges(s) != (GROUP SUM 1 IF inSession(RangeRev v));
nameSupplier 'Поставщик' (Sku s, Stock st)=  
    nameCompanyA(defaultPriceListType(), s, st, (GROUP MAX dateTime(RangeRev v) IF inSession(v)));

FORM rangeCompare 'Сравнение версий матрицы' 
    OBJECTS d = Stock PANEL
    PROPERTIES(d) SELECTOR name
    FILTERS isCompany(d)

    OBJECTS v = RangeRev
    FILTERS inSession(v)
    OBJECTS s = Sku
    PROPERTIES READONLY id(s), idBarcode(s), name(s), nameSupplier(s,d)
    PROPERTIES READONLY levels(v,s) COLUMNS(v) HEADER date(v)
    ORDERS name(s)
    FILTERS changed(s);
;

DESIGN rangeCompare {
    BOX {
        size = (1024, 768);
    }
}

comareRangeRev 'Сравнить версии' ()  { 
    IF (GROUP SUM 1 IF inSession(RangeRev v)) > 1 THEN {
        SHOW rangeCompare;
    } ELSE {
        MESSAGE 'Не выбраны версии для сравнения';
    }
    
} TOOLBAR;

EXTEND FORM range
    PROPERTIES comareRangeRev() DRAW v
;


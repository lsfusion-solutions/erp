MODULE ZReportPayment;

REQUIRE ZReport, Payment;

type = DATA INTEGER (Payment.Payment);
nameType 'Тип платежа' = CASE WHEN type(Payment.Payment payment) == 0 THEN 'Наличные'
                                                      WHEN type(payment) == 1 THEN 'Карточка';
zReport = DATA ZReport (Payment.Payment);
numberZReport 'Номер Z-отчета' = number(zReport(Payment.Payment payment));

extraPositiveSumCash 'Дополнительная сумма Z-отчета(наличные)' = 
    GROUP SUM sum(Payment.Payment payment) IF type(payment) == 0 AND NOT isReturn(payment) BY zReport(payment);         
extraNegativeSumCash 'Дополнительная сумма возврата Z-отчета(наличные)' = 
    GROUP SUM sum(Payment.Payment payment)*(-1) IF type(payment) == 0 AND isReturn(payment) BY zReport(payment);
extraSumCash 'Дополнительная сумма Z-отчета(наличные)' = 
    extraPositiveSumCash(ZReport zReport) (+) extraNegativeSumCash(zReport);            
     
extraPositiveSumCard 'Дополнительная сумма Z-отчета(карточка)' = 
    GROUP SUM sum(Payment.Payment payment) IF type(payment) == 1 AND NOT isReturn(payment) BY zReport(payment);          
extraNegativeSumCard 'Дополнительная сумма возврата Z-отчета(карточка)' = 
    GROUP SUM sum(Payment.Payment payment)*(-1) IF type(payment) == 1 AND isReturn(payment) BY zReport(payment);
extraSumCard 'Дополнительная сумма Z-отчета(карточка)' = 
    extraPositiveSumCard(ZReport zReport) (+) extraNegativeSumCard(zReport);       
        
extraSum 'Дополнительная сумма Z-отчета' = 
    [=GROUP SUM sum(Payment.Payment payment) IF NOT isReturn(payment) BY zReport(payment)](ZReport zReport) (-) 
    [=GROUP SUM sum(Payment.Payment payment) IF isReturn(payment) BY zReport(payment)](zReport);   

totalSumCash 'Общая сумма Z-отчета(наличные)' (ZReport ZReport) = sumCash(ZReport) (+) extraSumCash(ZReport);
totalSumCard 'Общая сумма Z-отчета(карточка)' (ZReport ZReport) = sumCard(ZReport) (+) extraSumCard(ZReport);
totalSum 'Общая сумма Z-отчета' (ZReport ZReport) = sumReceiptDetail(ZReport) (+) extraSum(ZReport);

EXTEND FORM zReports
    PROPERTIES(z) READONLY BEFORE sumReceiptDetail(z) totalSum, totalSumCash, totalSumCard
    PROPERTIES(z) READONLY AFTER sumReceiptDetail(z) extraSum, extraPositiveSumCash, extraPositiveSumCard, 
                                 extraNegativeSumCash, extraNegativeSumCard
;

EXTEND FORM payments
    PROPERTIES(p) READONLYIF isReadonly() numberZReport, nameType
    
    FILTERGROUP type 
        FILTER 'По безналу' NOT type(p) AND NOT zReport(p) 'F5'    
        FILTER 'По кассе' zReport(p) 'F6'    
        FILTER 'Прочие' type(p) AND NOT zReport(p) 'F7'    
;

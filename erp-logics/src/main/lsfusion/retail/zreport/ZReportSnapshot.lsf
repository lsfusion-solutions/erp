MODULE ZReportSnapshot;

REQUIRE ZReport, SaleSnapshot;

NAMESPACE Stock;

//discountSumSaleLedger 'Сумма скидки' (d) = MULTI discountSumReceiptSaleDetail(d), -discountSumReceiptReturnDetail(d);

//-- Sku
discountSum 'Сумма скидки' = DATA NUMERIC[16,2] (Sku, Stock, Snapshot);

//-- Batch
discountSum 'Сумма скидки' = DATA NUMERIC[16,2] (Batch, Stock, Snapshot);

//-- По поставщикам
discountSumBatch 'Сумма скидки' (supplier, Snapshot snapshot) =
    GROUP SUM discountSum (Batch batch, Stock stock, snapshot) BY supplier(batch);

//-- По складу поставщика
discountSumBatchSupplier 'Сумма скидки' (supplier, Snapshot snapshot) =
    GROUP SUM discountSum (Batch batch, Stock stock, snapshot) BY supplierStock(batch);

//-- группа/склад       
discountSum 'Сумма скидки' = DATA NUMERIC[16,2] (SkuGroup, Stock, Snapshot);  
        
//-- группа         
discountSum 'Сумма скидки' (SkuGroup group, Snapshot snapshot) =
    GROUP  SUM discountSum (group, Stock stock, snapshot);    
                
//--По складам
discountSum 'Сумма скидки' (Stock stock, Snapshot snapshot) =
    GROUP  SUM discountSum (SkuGroup group, stock, snapshot);  
                     
//-- Sku на дату
discountSum 'Сумма скидки' = DATA NUMERIC[16,2] (Sku, Stock, Snapshot, DATE);        
             
//--По складам на дату     

discountSum 'Сумма скидки' (Stock stock, Snapshot snapshot, DATE date) =
    GROUP  SUM discountSum (Sku sku, stock, snapshot, date);    
           
nameDiscountSum (Stock st) = CONCAT ' ', name(st), ' (сумма скидки)';    
       
isDiscount 'Скидка' = DATA BOOLEAN (Snapshot) IN evidence;
hintDiscountBackground  = RGB(0, 214, 161) IF TRUE;  
      
isDiscount 'Скидка' = DATA BOOLEAN (SnapshotType);

EXTEND FORM snapshotType
    PROPERTIES (t) isDiscount
;
DESIGN snapshotType {
    row3 {
        MOVE PROPERTY (isDiscount(t));
    }
}
EXTEND FORM snapshotTypes
    PROPERTIES (t) READONLY  isDiscount
;
@deriveDocumentSnapshotProperty(isDiscount);      
       
//-- Расширяем ACTION    
discountSum 'Скидка (сумма)' (sku, stock, date) =  GROUP SUM discountSum(SaleLedger ledger) BY sku[SkuLedger](ledger), stock[SkuLedger](ledger), date[SkuLedger](ledger);
  
discountSum 'Скидка за интервал (сумма)' (Sku sku, Stock stock, DATE dateFrom, DATE dateTo) = GROUP
        SUM
        discountSum(sku, stock, DATE date) IF date >= dateFrom AND date <= dateTo;  

                    
overTakeSkuFromTo(Snapshot snapshot, DATE dateFrom, DATE dateTo) + { 
    IF isDiscount(snapshot) THEN {
        IF isDate(snapshot) THEN {
            IF isSum(snapshot) THEN {
                discountSum(Sku sku, Stock stock, snapshot, DATE date) <- NULL;
                discountSum(Sku sku, Stock stock, snapshot, DATE date) <- NUMERIC[16,2](discountSum(sku, stock, date)) 
                    WHERE include(snapshot, stock) AND include(snapshot, sku) AND date AS DATE AND date >= (dateFrom AS DATE) 
                        AND date <= (dateTo AS DATE) AND discountSum(sku, stock, date);  
                     
            }                               
        }  
        IF isSum(snapshot) THEN {  
            discountSum(Sku sku, Stock stock, snapshot) <- NULL;
            discountSum(SkuGroup skuGroup, Stock stock, snapshot) <- NULL;
            discountSum(Sku sku, Stock stock, snapshot) <- NUMERIC[16,2](discountSum(sku, stock, dateFrom, dateTo)) 
                WHERE include(snapshot, stock) AND include(snapshot, sku) AND discountSum(sku, stock, dateFrom, dateTo);    
            discountSum(SkuGroup skuGroup, Stock stock, snapshot) <- NUMERIC[16,2](GROUP SUM discountSum (Sku sku, stock, snapshot) IF isParent(skuGroup, sku))
                WHERE (GROUP SUM discountSum (Sku sku, stock, snapshot) IF isParent(skuGroup, sku)); 
        }              
    }            
};                                                                                                
               
//-- Batch      
discountSum 'Скидка (сумма)' (batch, stock, date) =  GROUP SUM discountSum(SaleLedger ledger) BY batch[SkuLedger](ledger), stock[SkuLedger](ledger), date[SkuLedger](ledger);
  
discountSum 'Скидка за интервал (сумма)' (Batch batch, Stock stock, DATE dateFrom, DATE dateTo) = GROUP
        SUM
        discountSum(batch, stock, DATE date) IF date >= dateFrom AND date <= dateTo;  

overTakeBatchFromTo(Snapshot snapshot, DATE dateFrom, DATE dateTo) + { 
    IF isDiscount(snapshot) THEN {
        IF isSum(snapshot) THEN { 
            discountSum(Batch batch, Stock stock, snapshot) <- NULL;
            discountSum(Batch batch, Stock stock, snapshot) <- NUMERIC[16,2](discountSum(batch, stock, dateFrom, dateTo)) 
                WHERE include(snapshot, stock) AND include(snapshot, sku(batch)) AND discountSum(batch, stock, dateFrom, dateTo);   
       
        }          
    }      
};  
       
//-- SHOWIF

isSumDiscount= isSum(Snapshot snapshot) AND isDiscount(snapshot);

isSumDiscountDate= isSum(Snapshot snapshot) AND isDiscount(snapshot) AND isDate(snapshot);

isSumDiscountBatch= isSum(Snapshot snapshot) AND isDiscount(snapshot) AND isBatch(snapshot);
                                                    
                                                         
EXTEND FORM snapshot
    PROPERTIES(r)      BACKGROUND hintDiscountBackground() isDiscount
//--sku  
    PROPERTIES(s,st,r) READONLY BACKGROUND hintDiscountBackground() BEFORE inQuantity(s,st,r)                      
                       discountSum SHOWIF isSumDiscount(r)

//--sku на дату  
    PROPERTIES(s3,st3,r,d3) READONLY BACKGROUND hintDiscountBackground() BEFORE inQuantity(s3,st3,r,d3)
                       discountSum SHOWIF isSumDiscountDate(r)

//--По складам на дату 
    PROPERTIES(st4,r,d4) READONLY BACKGROUND hintDiscountBackground() BEFORE inQuantity(st4,r,d4)
                       discountSum COLUMNS 'g' (st4) HEADER nameDiscountSum(st4) SHOWIF isSumDiscountDate(r)
                                                                 
//-- batch
    PROPERTIES(bt,ss,r) READONLY BACKGROUND hintDiscountBackground() BEFORE inQuantity(bt,ss,r) 
                       discountSum SHOWIF isSumDiscountBatch(r)
        
//-- По группам                           
    PROPERTIES(sk1,r)  READONLY BACKGROUND hintDiscountBackground() BEFORE balanceA(sk1,r)         
                       discountSum SHOWIF isSumDiscount(r)

//-- По складам                           
    PROPERTIES(ts1,r)  READONLY BACKGROUND hintDiscountBackground() BEFORE inQuantity(ts1,r)                               
                       discountSum SHOWIF isSumDiscount(r)

//-- По поставщикам
    PROPERTIES(l,r)    READONLY BACKGROUND hintDiscountBackground() BEFORE inQuantityBatch(l,r)
                       discountSumBatch SHOWIF isSumDiscountBatch(r)
                       
//-- По складам поставщика
    PROPERTIES(lst,r)  READONLY BACKGROUND hintDiscountBackground() BEFORE inQuantityBatchSupplier(lst,r)
                       discountSumBatchSupplier SHOWIF isSumDiscountBatch(r) 
;  
DESIGN snapshot {
    row3 {
        MOVE PROPERTY (isDiscount(r));
    }
}


EXTEND FORM snapshots
    PROPERTIES(r)      READONLY  BACKGROUND hintDiscountBackground() isDiscount     
; 

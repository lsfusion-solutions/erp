MODULE ZReport;

REQUIRE System,
        Utils,
        Document,
        Retail,
        Currency,
        Store,
        StoreSkuLedger,
        Stock,
        StockTax,
        CashRegisterStore,
        SaleLedger,
        PriceRoundStore,
        PriceListType,
        StockSkuDocument,
        Item;

// ---- Добавляем отдельную вкладку на форму Настройки
DESIGN options {
    pane {
        NEW zReport {
            caption = 'Z-отчеты';
        }
    }
}
        
// ------------------------------------- Объявление Z-отчета ---------------------------------------- //

CLASS ZReport 'Z-отчет (открытый)';
TABLE zReport (ZReport);
TABLE zReportSession (ZReport,Session);

@defineExternalizable(zReport, STRING[100]);
@defineDocumentHeaderTime(ZReport);
@defineDocumentHeaderCreated(ZReport);

@defineDocumentHeaderPosted(ZReport);
@defineDocumentHeaderClosed(ZReport);
@defineDocumentClosedConstraint(ZReport);

WHEN LOCAL SET(ZReport o IS ZReport) DO {
    IF o IS ZReport AND NOT date(o) THEN date(o) <- currentDate();
    IF o IS ZReport AND NOT time(o) THEN time(o) <- currentTime();
};

@defineDocumentHeaderDepartmentStore(zReport);

INDEX departmentStore(ZReport z);

@defineDocumentHeaderNumber(ZReport, BPSTRING[2]);

idDepartmentStore 'Код' (ZReport zReport) = id(departmentStore(zReport));

CONSTRAINT isPosted(ZReport z) AND NOT departmentStore(z)
    MESSAGE 'Не задан отдел магазина для Z-отчета';

cashRegister (zReport) = DATA CashRegister(ZReport) INDEXED;
numberCashRegister 'Касса Z-отчета' (ZReport zReport) = npp(cashRegister(zReport)) IN documentPrm;
overNumberCashRegister 'Касса Z-отчета' (ZReport zReport) = overNpp(cashRegister(zReport)) IN documentPrm;

overBasis = ABSTRACT STRING[20](ZReport);

basis 'Основание' (ZReport zReport) = (OVERRIDE overBasis(zReport), 'Z-отчет №') + number(zReport) + ' с кассы ' + overNumberCashRegister(zReport) + ' от ' + date(zReport) IN documentPrm;

departmentStore(ZReport zReport) <- stock(cashRegister(zReport))
    WHEN CHANGED(cashRegister(zReport));

description 'Название документа' (ZReport zReport) =  STRING[200] (CONCAT '', 'Продажа по кассе ' +overNumberCashRegister(zReport),
                                            ' отдела ' + nameDepartmentStore(zReport),
                                            ' от ' + date(zReport)) IN id;
    
succeededExtraCheck 'Проверено' (zReport) = DATA BOOLEAN(ZReport);   
//----------------------------------------------- Чеки ---------------------------------------------------//

CLASS Receipt 'Чек';
TABLE receipt (Receipt);
TABLE receiptSession(Receipt,Session);

@defineCreated(Receipt);
@defineExternalizable(receipt, STRING[100]);


CLASS ABSTRACT ReceiptDetail 'Строка чека';
TABLE receiptDetail (ReceiptDetail);
TABLE receiptDetailSession(ReceiptDetail,Session);

CLASS ReceiptSaleDetail 'Строка продажи' : ReceiptDetail;
TABLE receiptSaleDetail (ReceiptSaleDetail);
is(ReceiptSaleDetail receiptSaleDetail) = receiptSaleDetail IS ReceiptSaleDetail;

CLASS ReceiptReturnDetail 'Строка возврата' : ReceiptDetail;
TABLE receiptReturnDetail (ReceiptReturnDetail);
is(ReceiptReturnDetail receiptReturnDetail) = receiptReturnDetail IS ReceiptReturnDetail;

@defineExternalizable(receiptDetail, STRING[100]);

number 'Номер позиции чека' = DATA INTEGER (ReceiptDetail); 

zReport = DATA ZReport (Receipt) NONULL DELETE AGGR;
in(ZReport o, Receipt d) = zReport(d) == o;

@defineDocumentHeaderCount(zReport, Receipt);
numberZReport 'Номер Z-отчета' (Receipt receipt) = number(zReport(receipt)) IN id;
cashRegister (Receipt receipt) = cashRegister(zReport(receipt));
numberCashRegister 'Номер кассы' (Receipt receipt) = npp(cashRegister(receipt)) IN id;
overNumberCashRegister 'Номер кассы' (Receipt receipt) = overNpp(cashRegister(receipt)) IN id;
numberGroupCashRegister 'Номер кассы' (Receipt receipt) = nppGroupMachinery(cashRegister(receipt)) IN id;

groupCashRegister (Receipt receipt) = groupCashRegister(cashRegister(receipt)) COMPLEX;

cashRegisterModel (Receipt receipt) = cashRegisterModel(groupCashRegister(cashRegister(receipt)));
sidCashRegisterModel 'Код модели' (Receipt receipt) = sid(cashRegisterModel(receipt));

employee = DATA Employee(Receipt);
idEmployee (Receipt receipt) = id(employee(receipt));
firstNameEmployee (Receipt receipt) = firstName(employee(receipt));
lastNameEmployee (Receipt receipt) = lastName(employee(receipt));
nameEmployee 'Кассир' (Receipt receipt) = name[Contact](employee(receipt));
employee(Receipt receipt) <- currentUser() WHEN SET(receipt IS Receipt);

@defineDocumentHeaderTime(Receipt);
WHEN LOCAL SET(Receipt o IS Receipt) DO {
    IF o IS Receipt AND NOT date(o) THEN date(o) <- currentDate();
    IF o IS Receipt AND NOT time(o) THEN time(o) <- currentTime();
};

hour 'Час' (Receipt r) = extractHour(dateTime(r));  

@defineDocumentDetailDepartmentStoreCustom(zReport, receipt);
@defineDocumentDetailPosted(zReport, Receipt);
@defineDocumentDetailClosed(zReport, Receipt);

idDepartmentStore 'Отдел магазина' (Receipt idetail) = id(departmentStore(idetail));    
    

dataSkip 'Аннулирован' = DATA BOOLEAN (Receipt);
skip 'Не проводить по регистру' (Receipt r) = dataSkip(r) AND NOT notSkipCancel(cashRegister(r));  

@defineDocumentRelation(receipt, ReceiptSaleDetail);
@defineDocumentRelation(receipt, ReceiptReturnDetail);

@defineDocumentHeaderCurrency (receipt);
@deriveDocumentCurrency (receipt, departmentStore);

@defineDocumentHeaderNote(Receipt);

@defineDocumentDetailCurrency (receipt, receiptSaleDetail);
@defineDocumentDetailCurrency (receipt, receiptReturnDetail);

receipt = ABSTRACT Receipt (ReceiptDetail) MATERIALIZED AGGR;
receipt(ReceiptReturnDetail detail) += receipt(detail);
receipt(ReceiptSaleDetail detail) += receipt(detail);

zReport(ReceiptSaleDetail d) = zReport(receipt(d)); 
zReport(ReceiptReturnDetail d) = zReport(receipt(d)); 
zReport (ReceiptDetail d) = zReport(receipt(d));
numberZReport 'Номер Z-отчета'(ReceiptDetail d) = number(zReport(d));

sidCashRegisterModel 'Код модели' (ReceiptDetail receiptDetail) = sidCashRegisterModel(receipt(receiptDetail));
numberCashRegister 'Номер кассы' (ReceiptDetail receiptDetail) = overNumberCashRegister(receipt(receiptDetail));
numberGroupCashRegister 'Номер группы касс' (ReceiptDetail receiptDetail) = numberGroupCashRegister(receipt(receiptDetail));

type 'Тип' = ABSTRACT BPSTRING[10] (ReceiptDetail) CHARWIDTH 10 MATERIALIZED;
type(ReceiptSaleDetail detail) += BPSTRING[10]('Продажа') IF detail IS ReceiptSaleDetail;
type(ReceiptReturnDetail detail) += BPSTRING[10]('Возврат') IF detail IS ReceiptReturnDetail;

index 'Номер строки' (ReceiptDetail d) =
    PARTITION SUM 1
    ORDER d BY receipt(d) CHARWIDTH 4;

@defineDocumentDetailTime(receipt, ReceiptSaleDetail);
dateZReport 'Дата Z-отчета'(ReceiptSaleDetail d) = date(zReport(receipt(d)));

@defineDocumentDetailTime(receipt, ReceiptReturnDetail);
dateZReport 'Дата Z-отчета'(ReceiptReturnDetail d) = date(zReport(receipt(d)));

date 'Дата' = ABSTRACT DATE (ReceiptDetail) MATERIALIZED INDEXED;
date(ReceiptReturnDetail detail) += date(detail);
date(ReceiptSaleDetail detail) += date(detail);

dateZReport 'Дата Z-отчета' (ReceiptDetail d) = date(zReport(receipt(d)));

dateTime 'Дата/время' = ABSTRACT DATETIME (ReceiptDetail) MATERIALIZED INDEXED;
dateTime(ReceiptReturnDetail detail) += dateTime(detail);
dateTime(ReceiptSaleDetail detail) += dateTime(detail);

time 'Время' (ReceiptDetail d) = TIME(dateTime(d));

// ------------ Проведен --------------- // 

isPosted 'Проведен' (receiptDetail) = ABSTRACT BOOLEAN (ReceiptDetail) MATERIALIZED;

@defineDocumentDetailPosted(receipt, ReceiptSaleDetail);
isPosted(ReceiptSaleDetail d) += isPosted(d);

@defineDocumentDetailPosted(receipt, ReceiptReturnDetail);
isPosted(ReceiptReturnDetail d) += isPosted(d); 

// ------------ Закрыт --------------- // 
isClosed 'Закрыт' (receiptDetail) = ABSTRACT BOOLEAN (ReceiptDetail);

@defineDocumentDetailClosed(receipt, ReceiptSaleDetail);
isClosed(ReceiptSaleDetail d) += isClosed(d);
 
@defineDocumentDetailClosed(receipt, ReceiptReturnDetail);
isClosed(ReceiptReturnDetail d) += isClosed(d);

// --------------- Магазин --------------- //
departmentStore (receiptDetail) = ABSTRACT DepartmentStore (ReceiptDetail) MATERIALIZED;
nameDepartmentStore 'Склад' (ReceiptDetail d) = name(departmentStore(d));
store (ReceiptDetail receiptDetail) = store(departmentStore(receiptDetail));

INDEX departmentStore(ReceiptDetail d), date(d);

@defineDocumentDetailDepartmentStoreCustom(receipt, receiptSaleDetail);
departmentStore (ReceiptSaleDetail d) += departmentStore(d);
 
@defineDocumentDetailDepartmentStoreCustom(receipt, receiptReturnDetail);
departmentStore (ReceiptReturnDetail d) += departmentStore(d); 

// Валюта
@defineDocumentDetailCurrency(receipt, receiptDetail);

@defineDocumentDetailSku(receiptSale, sku);
@defineDocumentDetailSku(receiptReturn, sku);
sku (ReceiptDetail receiptDetail) = MULTI sku[ReceiptReturnDetail](receiptDetail), sku[ReceiptSaleDetail](receiptDetail) MATERIALIZED;

INDEX receipt(ReceiptDetail d), sku(d);

idSku 'Код' (ReceiptDetail receiptDetail) = id(sku(receiptDetail));
idSkuGroup 'Код' (ReceiptDetail receiptDetail) = id(skuGroup(sku(receiptDetail)));
nameSkuGroup1Sku 'Категория' (ReceiptDetail receiptDetail) = nameSkuGroup1(sku(receiptDetail));
nameSkuGroup2Sku 'Направление' (ReceiptDetail receiptDetail) = nameSkuGroup2(sku(receiptDetail));
nameSkuGroup3Sku 'Группа' (ReceiptDetail receiptDetail) = nameSkuGroup3(sku(receiptDetail));
nameSkuGroup4Sku 'Подгруппа' (ReceiptDetail receiptDetail) = nameSkuGroup4(sku(receiptDetail));
nameSkuGroup5Sku 'Субгруппа' (ReceiptDetail receiptDetail) = nameSkuGroup5(sku(receiptDetail));
nameSkuGroup6Sku 'Вид' (ReceiptDetail receiptDetail) = nameSkuGroup6(sku(receiptDetail));
shortNameUOMSku 'Ед. изм.' (ReceiptDetail receiptDetail) = shortNameUOM(sku(receiptDetail));


skip 'Не проводить по регистру' (ReceiptSaleDetail receiptDetail) = skip(receipt(receiptDetail)) OR skuType(sku(receiptDetail)) == SkuType.skuTypeCharge; 
skip 'Не проводить по регистру' (ReceiptReturnDetail receiptDetail) = skip(receipt(receiptDetail)) OR skuType(sku(receiptDetail)) == SkuType.skuTypeCharge; 

active(ReceiptSaleDetail d) = isPosted(d) AND NOT skip(d); 
active(ReceiptReturnDetail d) = isPosted(d) AND NOT skip(d); 
active(ReceiptDetail d) = MULTI active[ReceiptSaleDetail](d), active[ReceiptReturnDetail](d); 

setAttributes  ABSTRACT ( ReceiptDetail);

changeDialogSku(ReceiptDetail detail)   { 
    setAttributes(detail); 
    DIALOG skus OBJECTS s = sku(detail) INPUT NULL DO
        sku(detail) <- s;
} TOOLBAR;

overBNameSku  = ABSTRACT  ISTRING[255](ReceiptDetail);

nameSku 'Товар' (ReceiptDetail d) = OVERRIDE overBNameSku(d), name(sku(d)) IN id CHARWIDTH 20;

nameCashRegisterSku (ReceiptDetail d) = OVERRIDE nameCashRegisterAttribute(sku(d)), nameSku(d);

boardNameSku 'Товар' = ABSTRACT ISTRING[255] (ReceiptDetail);
overNameSku 'Товар' (ReceiptDetail d) = OVERRIDE boardNameSku(d), nameSku(d);

explicitBatchLedger(Receipt receipt) = explicitBatchLedger(departmentStore(zReport(receipt)));
notExplicitBatchLedger(Receipt receipt) = departmentStore(zReport(receipt)) AND NOT explicitBatchLedger(receipt);

number 'Номер чека' (receipt) = DATA INTEGER (Receipt) IN documentHeader;
numberReceipt 'Номер чека' (ReceiptDetail receiptDetail) = number(receipt(receiptDetail));

nameEmployee 'Кассир' = nameEmployee(receipt(ReceiptDetail detail));

maxNumberReceipt 'Максимальный номер чека' (zReport) = GROUP MAX number(Receipt receipt) BY zReport(receipt) MATERIALIZED;

GROUP receiptDiscount 'Дисконтная карта' : public;

idBarcode 'Штрихкод' (receiptDetail) = DATA STRING[15] (ReceiptDetail) CHARWIDTH 14 NOFLEX;

// Возвраты

saleReceipt = DATA Receipt (ReceiptReturnDetail);
receiptDetail(receipt, sku) = GROUP LAST ReceiptDetail receiptDetail ORDER receiptDetail BY receipt(receiptDetail), sku(receiptDetail) COMPLEX;
saleReceiptDetail(ReceiptReturnDetail returnReceiptDetail) = receiptDetail(saleReceipt(returnReceiptDetail), sku[ReceiptDetail](returnReceiptDetail));                                                                                                                                                             
receiptSaleDetail = DATA ReceiptSaleDetail (ReceiptReturnDetail);
overReceiptSaleDetail(ReceiptReturnDetail detail) = OVERRIDE saleReceiptDetail(detail), receiptSaleDetail(detail);
receiptSale (ReceiptReturnDetail returnDetail) = receipt[ReceiptSaleDetail](overReceiptSaleDetail(returnDetail));

descriptionSale 'Чек продажи' (ReceiptReturnDetail returnDetail) = CONCAT ' ',
    'Чек № ' + number(receiptSale(returnDetail)),
    'от ' + dateTime(receiptSale(returnDetail)),
    'позиция ' + index(overReceiptSaleDetail(returnDetail)) CHARWIDTH 15;

changeDescriptionSale  ABSTRACT LIST ( ReceiptReturnDetail);

// Количества и суммы

quantity 'Количество' (receiptDetail) = DATA NUMERIC[16,5] (ReceiptSaleDetail);
price 'Цена' (receiptDetail) = DATA NUMERIC[16,4] (ReceiptSaleDetail);
sum 'Сумма' (receiptDetail) = DATA NUMERIC[18,4] (ReceiptSaleDetail);
discountPercent 'Процент скидки' (receiptDetail) = DATA NUMERIC[6,2] (ReceiptSaleDetail);
discountPercent (receipt)= GROUP SUM 1 IF discountPercent(ReceiptSaleDetail detail) BY receipt[ReceiptDetail](detail);
discountSum 'Сумма скидки' (receiptDetail) = DATA NUMERIC[18,4] (ReceiptSaleDetail);

priceSum 'Сумма без скидки' = round(quantity(ReceiptSaleDetail detail) * price(detail), roundCondition(departmentStore(detail)));

//discountSumReceiptSaleDetail (detail) <- round(priceSumReceiptSaleDetail(detail) * discountPercentReceiptSaleDetail (detail) / 100,
//                                               priceRoundCurrency(currencyReceiptSaleDetail (detail)))
//    WHEN CHANGED (quantityReceiptSaleDetail(detail)) OR
//         CHANGED (priceReceiptSaleDetail(detail)) OR
//         CHANGED (discountPercentReceiptSaleDetail(detail)) OR
//         CHANGED (currencyReceiptSaleDetail (detail));
calcDiscountSum (ReceiptSaleDetail detail) = round(priceSum(detail) * discountPercent (detail) / 100,
                                               roundDiscountCondition(departmentStore(detail)));

extraDiscountSum = ABSTRACT NUMERIC[18,4] (ReceiptSaleDetail);
calcSum (ReceiptSaleDetail detail) = priceSum(detail) (-) discountSum(detail) (-) extraDiscountSum(detail);
sum (ReceiptSaleDetail detail) <- calcSum(detail)
    WHEN CHANGED (quantity(detail)) OR
         CHANGED (price(detail)) OR
         CHANGED (discountPercent(detail)) OR
         CHANGED (discountSum(detail)) OR
         CHANGED (extraDiscountSum(detail)) OR
         CHANGED (departmentStore(detail));

quantity 'Количество' (receiptDetail) = DATA NUMERIC[16,5] (ReceiptReturnDetail);
price 'Цена' (receiptDetail) = DATA NUMERIC[16,4] (ReceiptReturnDetail);
sum 'Сумма' (receiptDetail) = DATA NUMERIC[18,4] (ReceiptReturnDetail);
discountSum 'Сумма скидки' (receiptDetail) = DATA NUMERIC[18,4] (ReceiptReturnDetail);

quantityReturned 'Возвращено' (saleDetail) = GROUP SUM quantity(ReceiptReturnDetail returnDetail) BY receiptSaleDetail(returnDetail) MATERIALIZED;
CONSTRAINT quantityReturned(ReceiptSaleDetail detail) > quantity (detail) MESSAGE 'Количество возвратов по строке чека превышает проданное количество';

overSum (ReceiptReturnDetail d) = round(
        (quantity(d) * sum(receiptSaleDetail(d)) / 
        (quantity(receiptSaleDetail(d)) IF quantity(receiptSaleDetail(d))!=0)), 
    roundCondition(departmentStore (d)));

priceSum 'Сумма без скидки' = round(
        quantity(ReceiptReturnDetail d) * price(d),
    roundCondition(departmentStore(d)));

sum (ReceiptReturnDetail detail) <- IF receiptSaleDetail(detail) 
                                        THEN overSum(detail)
                                        ELSE priceSum(detail)
                                            
                                   WHEN CHANGED(quantity(detail)) OR
                                        CHANGED(receiptSaleDetail(detail)) OR
                                        CHANGED(departmentStore(detail));

discountSum (ReceiptReturnDetail detail) <- IF receiptSaleDetail(detail) THEN
                                                round(quantity(detail) * discountSum(receiptSaleDetail(detail)) / quantity(receiptSaleDetail(detail)),
                                                    roundDiscountCondition(departmentStore(receiptSaleDetail(detail))))
                                           ELSE
                                                quantity(detail) * price(detail) - sum(detail)
                                           WHEN CHANGED(quantity(detail)) OR
                                                CHANGED(sum(detail)) OR
                                                CHANGED(receiptSaleDetail(detail)) OR
                                                CHANGED(departmentStore(detail));

quantity 'Количество' = ABSTRACT NUMERIC[16,5] (ReceiptDetail) MATERIALIZED;
quantity(ReceiptReturnDetail detail) += quantity(detail);
quantity(ReceiptSaleDetail detail) += quantity(detail);

price 'Цена' = ABSTRACT NUMERIC[16,4] (ReceiptDetail) MATERIALIZED CHARWIDTH 8;
price(ReceiptReturnDetail detail) += price(detail);
price(ReceiptSaleDetail detail) += price(detail);

INDEX sku(ReceiptDetail d), date(d);

sum 'Сумма' = ABSTRACT NUMERIC[18,4] (ReceiptDetail) MATERIALIZED CHARWIDTH 8;
sum(ReceiptReturnDetail detail) += sum(detail);
sum(ReceiptSaleDetail detail) += sum(detail);

discountSum 'Сумма скидки' = ABSTRACT NUMERIC[18,4] (ReceiptDetail) MATERIALIZED CHARWIDTH 8;
discountSum(ReceiptSaleDetail d) += discountSum(d);
discountSum(ReceiptReturnDetail d) += discountSum(d);

fullSum 'Сумма без скидки' (ReceiptSaleDetail d) = sum(d) (+) discountSum(d);
fullSum 'Сумма без скидки' (ReceiptReturnDetail d) = sum(d) (+) discountSum(d);
fullSum 'Сумма без скидки' (ReceiptDetail d) = sum(d) (+) discountSum(d);

sumReceiptSaleDetail 'Сумма продажи' (receipt) = GROUP SUM sum(ReceiptSaleDetail receiptDetail)
    BY receipt (receiptDetail) IN documentSum MATERIALIZED;

sumReceiptReturnDetail 'Сумма возврата' (receipt) = GROUP SUM sum(ReceiptReturnDetail receiptDetail)
    BY receipt (receiptDetail) IN documentSum MATERIALIZED;

signedSum 'Сумма' = ABSTRACT NUMERIC[18,4] (ReceiptDetail) MATERIALIZED;
signedSum(ReceiptReturnDetail detail) += -sum(detail);
signedSum(ReceiptSaleDetail detail) += sum(detail);

signedQuantity 'Количество' = ABSTRACT NUMERIC[16,5] (ReceiptDetail) MATERIALIZED;
signedQuantity(ReceiptReturnDetail detail) += -quantity(detail);
signedQuantity(ReceiptSaleDetail detail) += quantity(detail);

signedDiscountSum 'Сумма скидки' = ABSTRACT NUMERIC[18,4] (ReceiptDetail);
signedDiscountSum(ReceiptReturnDetail detail) += -discountSum(detail);
signedDiscountSum(ReceiptSaleDetail detail) += discountSum(detail);

sumReceiptDetail 'Сумма чека' (receipt) = GROUP SUM signedSum(ReceiptDetail detail)
    BY receipt(detail) MATERIALIZED;

sumSkuReceiptDetail 'Сумма чека по товарам' (receipt) = 
    GROUP SUM signedSum(ReceiptDetail detail) IF sku(detail) AND active(detail) BY receipt(detail) MATERIALIZED;
//sumReceiptDetailReceipt 'Сумма чека' (receipt) = sumReceiptSaleDetailReceipt(receipt) (-) sumReceiptReturnDetailReceipt(receipt) MATERIALIZED;
sumReceiptDetail 'Сумма чека' (ReceiptDetail receiptDetail) = sumReceiptDetail(receipt(receiptDetail));

discountSumSaleReceiptDetailSale 'Сумма скидки (продажи)' (receipt) = GROUP SUM discountSum(ReceiptSaleDetail receiptDetail)
    BY receipt (receiptDetail) IN documentSum;
discountSumSale 'Сумма скидки (продажи) по чеку' (receipt) = DATA NUMERIC[18,4] (Receipt);

discountSumReturnReceiptDetailReturn 'Сумма скидки (возврат)' (receipt) = GROUP SUM discountSum(ReceiptReturnDetail receiptDetail)
    BY receipt (receiptDetail) IN documentSum;
discountSumReturn 'Сумма скидки (возврат) по чеку' (receipt) = DATA NUMERIC[18,4] (Receipt);

discountSumReceiptDetail 'Сумма скидки' (Receipt receipt) = discountSumSaleReceiptDetailSale(receipt) (-) discountSumReturnReceiptDetailReturn(receipt) MATERIALIZED;
discountSum 'Сумма скидки по чеку' (Receipt receipt) = discountSumSale(receipt) (-) discountSumReturn(receipt);

@defineDocumentHeaderCount(receipt);

@defineDocumentHeaderQuantity(receipt);
@defineDocumentHeaderSkuQuantity(receipt, sku);
@defineDocumentHeaderSkuQuantity(receipt, receiptSaleDetail, sku);

// -------------------------------------------------- НДС ------------------------------------------------ //

VAT = DATA Range (ReceiptSaleDetail);
CONSTRAINT tax(VAT(ReceiptSaleDetail detail)) != Tax.taxVAT OR
           country(VAT(detail)) != country(departmentStore(detail))
           CHECKED BY VAT[ReceiptSaleDetail]
           MESSAGE 'ошибка: Шкала должна соответствовать шкале НДС и стране магазина: продажа';

VAT(ReceiptSaleDetail d) <- VAT(sku(d), departmentStore(d))
    WHEN CHANGED(sku(d)) OR CHANGED(departmentStore(d));

VAT = DATA Range (ReceiptReturnDetail);
CONSTRAINT tax(VAT(ReceiptReturnDetail detail)) != Tax.taxVAT OR
           country(VAT(detail)) != country(departmentStore(detail))
           CHECKED BY VAT[ReceiptReturnDetail]
           MESSAGE 'ошибка: Шкала должна соответствовать шкале НДС и стране магазина: возврат';

VAT(ReceiptReturnDetail d) <- VAT(sku(d), departmentStore(d))
    WHEN CHANGED(sku(d)) OR CHANGED(departmentStore(d));

VAT 'НДС' = ABSTRACT Range(ReceiptDetail) MATERIALIZED;
VAT(ReceiptSaleDetail d) += VAT(d);
VAT(ReceiptReturnDetail d) += VAT(d);

numberVAT 'НДС, номер' (ReceiptDetail receiptDetail) = number(VAT(receiptDetail));

valueVAT 'НДС, %' (receiptDetail) = DATA NUMERIC[10,5] (ReceiptDetail);
overValueVAT(d) = ABSTRACT VALUE NUMERIC[10,5] (ReceiptDetail);
overValueVAT(ReceiptDetail d) += valueRate(VAT(d), date(d));
WHEN CHANGED(VAT(ReceiptDetail d)) OR CHANGED(date(d)) DO {
    valueVAT(d) <- overValueVAT(d);
}

sumVAT 'Сумма НДС' (d) = DATA NUMERIC[18,4] (ReceiptDetail);
calcSumVAT 'Сумма НДС' (ReceiptDetail d) = round(sum(d) * valueVAT(d) / (100 + valueVAT(d)), currency(d)) CHARWIDTH 8;
// пока делаем не сессионным событием, чтобы быстрее принималось
WHEN CHANGED(valueVAT(ReceiptDetail d)) OR CHANGED(sum(d)) OR CHANGED(currency(d)) DO {
    sumVAT(d) <- calcSumVAT(d);
}

signedSumVAT 'Сумма НДС' (ReceiptDetail d) = IF d IS ReceiptReturnDetail THEN -sumVAT(d) ELSE sumVAT(d);

// используется только для приема реализации из внешних касс
description 'Название документа' (Receipt receipt) = 'Чек № ' + number(receipt) + ' от ' + dateTime(receipt);

overDescriptionNumber = ABSTRACT ISTRING[28] (Receipt);
description (ReceiptSaleDetail receiptDetail) = STRING[200]('Продажа № ' + (OVERRIDE overDescriptionNumber(receipt(receiptDetail)),ISTRING[28](number(receipt(receiptDetail)))) + ' от ' + dateTime(receipt(receiptDetail)) + ' на ' + (OVERRIDE description(cashRegister(receipt(receiptDetail))), ''));

description (ReceiptReturnDetail receiptDetail) = STRING[200]('Возврат № ' + (OVERRIDE overDescriptionNumber(receipt(receiptDetail)),ISTRING[28](number(receipt(receiptDetail)))) + ' от ' + dateTime(receipt(receiptDetail)) + ' на ' + (OVERRIDE description(cashRegister(receipt(receiptDetail))), ''));

description 'Название документа' (ReceiptDetail receiptDetail) = MULTI description[ReceiptReturnDetail](receiptDetail), description[ReceiptSaleDetail](receiptDetail);

@defineAddDetailDialogSkuStockCustom(receipt, receiptSaleDetail, ' (продажа)', sku, departmentStore, dialogSku);
@defineAddDetailDialogSkuStockCustom(receipt, receiptReturnDetail, ' (возврат)', sku, departmentStore, dialogSku);

// ----------------- Оплаты по чеку ------------------------------

CLASS Payment 'Оплата по чеку';
TABLE payment (Payment);
TABLE paymentSession(Payment,Session);

@defineExternalizable(payment, STRING[100]);

CLASS PaymentMeans 'Форма оплаты'{
    paymentMeansCash 'Наличные',
    paymentMeansCard 'Карточка'
}
order = ABSTRACT CASE INTEGER (PaymentMeans);
order(PaymentMeans p) += WHEN p == PaymentMeans.paymentMeansCash THEN 1;
order(PaymentMeans p) += WHEN p == PaymentMeans.paymentMeansCard THEN 2;

FORM paymentMeans
    OBJECTS m = PaymentMeans
    PROPERTIES(m) staticCaption
    LIST PaymentMeans OBJECT m
;

CLASS PaymentType 'Тип платежа';
TABLE paymentType (PaymentType);

name 'Наименование' = DATA ISTRING[110](PaymentType) IN id CHARWIDTH 10;

paymentMeans (paymentType) = DATA PaymentMeans (PaymentType);
namePaymentMeans 'Форма оплаты' (PaymentType paymentType) = staticCaption(paymentMeans(paymentType)) IN id CHARWIDTH 10;

sid 'Идентификатор' = DATA BPSTRING[20] (PaymentType) IN base;
typePaymentSID (tp) = GROUP AGGR PaymentType paymentType WHERE paymentType IS PaymentType BY sid(paymentType);

idTypeRegister 'Идентификатор для кассы' = DATA INTEGER (PaymentType);

FORM paymentType 'Тип платежа'
    OBJECTS pt = PaymentType PANEL
    PROPERTIES(pt) name, namePaymentMeans, sid, idTypeRegister

    EDIT PaymentType OBJECT pt
;

FORM paymentTypes 'Тип платежа'
    OBJECTS pt = PaymentType
    PROPERTIES(pt) READONLY name, namePaymentMeans, sid
    PROPERTIES(pt) NEWSESSION NEW, EDIT, DELETE 

    LIST PaymentType OBJECT pt
;

receipt (payment) = DATA Receipt (Payment) NONULL DELETE INDEXED;

countPayment 'Кол-во типов платежей' (receipt) = GROUP SUM 1 BY receipt(Payment payment);

paymentType (payment) = DATA PaymentType(Payment);
namePaymentType 'Тип платежа' (Payment payment) = name(paymentType(payment));
sidPaymentType 'ID Типа платежа' (Payment payment) = sid(paymentType(payment));
idTypeRegister(Payment payment) = idTypeRegister(paymentType(payment));

minCashPaymentType () = GROUP MIN PaymentType paymentType IF paymentMeans(paymentType) == PaymentMeans.paymentMeansCash;
minCardPaymentType () = GROUP MIN PaymentType paymentType IF paymentMeans(paymentType) == PaymentMeans.paymentMeansCard;

paymentMeans (Payment payment) = paymentMeans(paymentType(payment)) MATERIALIZED;
namePaymentMeans 'Форма оплаты' (Payment payment) = staticCaption(paymentMeans(payment)) CHARWIDTH 10;

number 'Номер платежа' (payment) = DATA INTEGER (Payment);

sum 'Сумма платежа' (payment) = DATA NUMERIC[18,4] (Payment);

sumPayment 'Сумма платежа' (receipt) = GROUP SUM sum(Payment payment) BY receipt(payment);

sumInCashPayment (receipt) = GROUP SUM sum(Payment payment) IF paymentMeans(payment) == PaymentMeans.paymentMeansCash  BY receipt(payment);

changePayment 'Сдача' (Receipt receipt) = sumPayment(receipt) - sumReceiptDetail(receipt);

sumCashPayment 'Сумма продажа (наличные)' (Receipt receipt) = sumInCashPayment(receipt) - changePayment(receipt) MATERIALIZED;
sumCardPayment 'Сумма продажа (карточка)' (receipt) = GROUP SUM sum(Payment payment) IF paymentMeans(payment) == PaymentMeans.paymentMeansCard BY receipt(payment);

sumCashPayment 'Наличными (чек)' (ReceiptDetail detail) = sumCashPayment(receipt(detail));
sumCardPayment 'Карточкой (чек)' (ReceiptDetail detail) = sumCardPayment(receipt(detail));

sumPositiveCashPayment 'Сумма продажа (наличные)' (Receipt receipt) =  sumCashPayment (receipt) IF sumCashPayment (receipt) >= 0.0 IN documentSum;
sumNegativeCashPayment 'Сумма возврата (наличные)' (Receipt receipt) =  sumCashPayment (receipt) IF sumCashPayment (receipt) < 0.0 IN documentSum;

sumPositiveCardPayment 'Сумма продажа (карточка)' (Receipt receipt) =  sumCardPayment (receipt) IF sumCardPayment (receipt) >= 0.0 IN documentSum;
sumNegativeCardPayment 'Сумма возврата (карточка)' (Receipt receipt) =  sumCardPayment (receipt) IF sumCardPayment (receipt) < 0.0 IN documentSum;

isSumPaymentReceipt 'Выключать ограничения по суммам платежей в чеке' = DATA BOOLEAN ();

EXTEND FORM options
    PROPERTIES() isSumPaymentReceipt
;

DESIGN options {
    zReport {
        MOVE PROPERTY(isSumPaymentReceipt());
    }
}

CONSTRAINT Receipt receipt IS Receipt AND NOT isSumPaymentReceipt() AND NOT sumPayment(receipt) AND sumReceiptDetail(receipt) MESSAGE 'По чеку не указаны платежи';

CONSTRAINT ((sumReceiptDetail(Receipt receipt) > 0 AND sumReceiptDetail(receipt) > sumPayment(receipt)) OR
           (sumReceiptDetail(receipt) < 0 AND sumReceiptDetail(receipt) != sumPayment(receipt))) AND NOT isSumPaymentReceipt()
    MESSAGE 'Сумма платежей по чеку меньше суммы чека';
    
CONSTRAINT ((sumReceiptDetail(Receipt receipt) > 0 AND sumReceiptDetail(receipt) < sumCardPayment(receipt)) 
           OR (sumReceiptDetail(receipt) <0 AND sumReceiptDetail(receipt) > sumCardPayment(receipt))) AND NOT isSumPaymentReceipt()
    MESSAGE 'Сумма платежей по безналичному расчету больше суммы чека';

// Итоги по Z-отчету

quantityReceiptDetail 'Кол-во (всего)' (zReport) = GROUP SUM signedQuantity(ReceiptDetail detail) BY zReport(detail) IN documentSum MATERIALIZED;

sumReceiptDetail 'Сумма Z-отчета' (zReport) = GROUP SUM sumReceiptDetail(Receipt receipt) IF NOT skip(receipt) BY zReport(receipt) MATERIALIZED IN documentSum;

sumCash 'Сумма наличных Z-отчета' (zReport) = GROUP SUM sumCashPayment(Receipt receipt) BY zReport(receipt) MATERIALIZED IN documentSum;
sumCard 'Сумма по карточкам Z-отчета' (zReport) = GROUP SUM sumCardPayment(Receipt receipt) BY zReport(receipt) MATERIALIZED IN documentSum;

sumPositiveCashPayment 'Сумма продажа (наличные)' (zReport) = GROUP SUM sumPositiveCashPayment(Receipt r) IF NOT skip(r) BY zReport(r) IN documentSum;
sumNegativeCashPayment 'Сумма возврата (наличные)' (zReport) = GROUP SUM sumNegativeCashPayment(Receipt r) IF NOT skip(r) BY zReport(r) MATERIALIZED IN documentSum;

sumPositiveCardPayment 'Сумма продажа (карточка)' (zReport) = GROUP SUM sumPositiveCardPayment(Receipt r) IF NOT skip(r) BY zReport(r) IN documentSum;
sumNegativeCardPayment 'Сумма возврата (карточка)' (zReport) = GROUP SUM sumNegativeCardPayment(Receipt r) IF NOT skip(r)  BY zReport(r) MATERIALIZED IN documentSum;

sumSkuReceiptDetail 'Сумма Z-отчета по sku' (zReport) = GROUP SUM sumSkuReceiptDetail(Receipt receipt) BY zReport(receipt) MATERIALIZED;

// Пока вот такой хак, чтобы не подключить ZReportReceiptDetailPayment
negMin = FORMULA 'CASE WHEN $1 < 0 THEN ($1+$2+ABS($1-$2))/2 ELSE ($1+$2-ABS($1-$2))/2 END'; //для отрицательных max, для положительных min

discountSumReceiptDetail 'Сумма скидок Z-отчета' (zReport) = GROUP SUM discountSumReceiptDetail(Receipt receipt) IF NOT skip(receipt) BY zReport(receipt) MATERIALIZED IN documentSum;
discountSumReceipt 'Сумма скидок по чекам Z-отчета' (zReport) = GROUP SUM discountSum(Receipt receipt) IF NOT skip(receipt) BY zReport(receipt) MATERIALIZED IN documentSum;
discountSum 'Сумма скидок Z-отчета' (ZReport zReport) = discountSumReceiptDetail(zReport) (+) discountSumReceipt(zReport) IN documentSum;

sumCashZReport 'Сумма наличных по кассе' = GROUP SUM sumCash(ZReport z) BY cashRegister(z); 
sumCardZReport 'Сумма безнал. по кассе' = GROUP SUM sumCard(ZReport z) BY cashRegister(z); 
// НДС

sumVATSkuReceiptDetail 'Сумма НДС по sku' (zReport) = GROUP SUM signedSumVAT(ReceiptDetail detail)
    IF sku(detail) BY zReport(detail);
sumVATReceiptDetail'Сумма НДС' (receipt) = GROUP SUM signedSumVAT(ReceiptDetail detail)
    BY receipt(detail) MATERIALIZED; 
sumVATReceiptDetail 'Сумма НДС' (zReport) = GROUP SUM sumVATReceiptDetail(Receipt receipt)
    BY zReport(receipt) IN documentSum;            

discountSumVAT 'Сумма НДС в скидке' (ReceiptDetail d) = round(discountSum(d) * valueVAT(d) / (100 + valueVAT(d)), currency(d));
 
discountSumVATSale 'Сумма НДС в скидке (продажа)' (zReport)= GROUP SUM discountSumVAT(ReceiptSaleDetail d) IF d IS ReceiptSaleDetail BY zReport[ReceiptDetail](d);
discountSumVATReturn 'Сумма НДС в скидке (возврат)' (zReport)= GROUP SUM discountSumVAT(ReceiptReturnDetail d) IF d IS ReceiptReturnDetail BY zReport[ReceiptDetail](d);

discountSumVAT 'Сумма НДС в скидке' (ZReport zReport)= discountSumVATSale(zReport) (-) discountSumVATReturn(zReport);

discountSumMarkup 'Сумма набавки в скидке' (ZReport z) = discountSum(z) (-) discountSumVAT(z);

//----------------------------------- Формы -------------------------------------------------//

background = ABSTRACT COLOR (ReceiptDetail);
background = ABSTRACT COLOR (Receipt);

FORM zReport 'Z-отчет'
    OBJECTS z=ZReport PANEL
    PROPERTIES (z) overNumberCashRegister, nameDepartmentStore, date, time, number, //sumCashZReport
                   basis, countReceipt, quantityReceiptDetail, 
                   sumPositiveCashPayment, sumPositiveCardPayment, discountSum,
                   sumNegativeCashPayment, sumNegativeCardPayment, sumReceiptDetail,
                   sumVATReceiptDetail

    OBJECTS b=Receipt, d=ReceiptDetail
    PROPERTIES(b) number BACKGROUND background(b), date, time, nameEmployee,
                  sumReceiptDetail, discountSumReceiptDetail,
                  discountSum, sumVATReceiptDetail, countReceiptDetail, quantityReceiptDetail,
                  sumCashPayment, sumCardPayment, NEW, DELETE GRID
    FILTERS zReport(b)==z

    PROPERTIES(b) DRAW d addDetailDialogSkuStockReceiptSaleDetail, addDetailDialogSkuStockReceiptReturnDetail
                           // todo : добавить операции возврата по штрихкоду

    PROPERTIES(d) BACKGROUND background(d) type READONLY, idBarcode, idSku,
                  nameSku ON CHANGE changeDialogSku(d) , 
                  descriptionSale ON CHANGE changeDescriptionSale(d),
                  quantity, quantityReturned, price,
                  sum, discountPercent, discountSum ,
                  numberVAT, valueVAT 
    PROPERTIES(d) NEW, DELETE GRID

    FILTERS receipt(d)==b

    OBJECTS p=Payment
    PROPERTIES(p) namePaymentType, namePaymentMeans, sum, NEW, del = DELETE GRID

    FILTERS receipt(p)==b

    EDIT ZReport OBJECT z
;

DESIGN zReport {

    NEW pane {
        type = CONTAINERH;      
           
        NEW first {
            fill = 1;
            type = CONTAINERV;
            MOVE GROUP(documentHeader,z) {
                type = CONTAINERH;
                MOVE PROPERTY (nameDepartmentStore(z));
                MOVE PROPERTY (number(z));
                MOVE PROPERTY (date(z));
                MOVE PROPERTY (time(z));
            }
            MOVE GROUP(documentPrm,z);
        }
        MOVE GROUP(documentSum,z) { columns = 2;}    

    }    
    MOVE BOX(b);
    NEW secondCase{
        fill = 1;
        type = SPLITH;

        MOVE BOX(d) {
            fill = 3;
            caption = 'Строка чека';
        }
        MOVE BOX(p);
    }           

    MOVE TOOLBARBOX;
}



overCopy  ABSTRACT LIST ( ReceiptDetail, ReceiptDetail);
overCopy  ABSTRACT LIST ( ReceiptDetail, Receipt);    

show 'Просмотреть' (ZReport z)  { 
	NEWSESSION {
	    SHOW zReport OBJECTS z = z DOCKED READONLY ;
	}
} TOOLBAR;

copy 'Копировать'(Receipt receipt)  { 
    NEW r = Receipt {
        zReport(r) <- zReport(receipt);
        dataSkip(r) <- dataSkip(receipt);
        currency(r) <- currency(receipt);
        discountSumSale(r) <- discountSumSale(receipt);
        discountSumReturn(r) <- discountSumReturn(receipt);        
        note(r) <- note(receipt);
        date(r) <- date(receipt);
        time(r) <- time(receipt);
        
        FOR receipt(ReceiptDetail detail) == receipt ORDER detail DO {
            IF detail IS ReceiptSaleDetail THEN {
                NEW d=ReceiptSaleDetail {                    
                    receipt(d) <- r;
                    sku(d) <- sku[ReceiptSaleDetail](detail);
                    idBarcode(d) <- idBarcode(detail);
                    quantity(d) <- quantity[ReceiptSaleDetail](detail);
                    price(d) <- price[ReceiptSaleDetail](detail);
                    discountPercent(d) <- discountPercent(detail);
                    discountSum(d) <- discountSum[ReceiptSaleDetail](detail);
                    VAT(d) <- VAT[ReceiptSaleDetail](detail);
                    sum(d) <- sum(detail);
                    
                    overCopy(d, detail);
                }            
            } 
            IF detail IS ReceiptReturnDetail THEN {
                NEW d=ReceiptReturnDetail {
                    receipt(d) <- r;
                    sku(d) <- sku[ReceiptReturnDetail](detail);
                    idBarcode(d) <- idBarcode(detail);
                    quantity(d) <- quantity[ReceiptReturnDetail](detail);
                    price(d) <- price[ReceiptReturnDetail](detail);
                    discountSum(d) <- discountSum[ReceiptReturnDetail](detail);
                    VAT(d) <- VAT[ReceiptReturnDetail](detail);                    
                    sum(d) <- sum[ReceiptReturnDetail](detail);

                    overCopy(d, detail);
                }            
            }           
            overCopy(detail,r);

        }
        FOR receipt(Payment payment) == receipt DO {
            NEW p=Payment {
                receipt(p) <- r;
                paymentType(p) <- paymentType(payment);
                number(p) <- number(payment);
                sum(p) <- sum(payment);
                
            }
        }
        SEEK zReport.b = r;
    }
} TOOLBAR;

EXTEND FORM zReport 
    PROPERTIES (b) copy
;

overPrice 'Цена' (Sku sku, Receipt receipt, DATETIME dateTime) =
        priceA(priceListType(groupCashRegister(receipt)), sku, departmentStore(receipt), dateTime);
        
WHEN LOCAL FORMS zReport (CHANGED (sku(ReceiptDetail d))) AND notExplicitBatchLedger(receipt(d)) AND NOT (CHANGED (price(d))) DO {
    price(d) <- overPrice(sku(d), receipt(d), dateTime(d));    
}
WHEN LOCAL FORMS zReport CHANGED (sku(ReceiptDetail d))  AND NOT (CHANGED (idBarcode(d))) DO {
    idBarcode(d) <- idBarcode(sku(d));
}
        
edit (ReceiptReturnDetail d) + {  DIALOG zReport OBJECTS z = zReport(d), b = receipt(d), d = d DOCKED; }
edit (ReceiptSaleDetail d) + {  DIALOG zReport OBJECTS z = zReport(d), b = receipt(d), d = d DOCKED; }

show 'Просмотреть'  ABSTRACT LIST  ( ReceiptDetail)  TOOLBAR;

show 'Просмотреть' (ReceiptReturnDetail d)  { 
	NEWSESSION {
	    SHOW zReport OBJECTS z = zReport(d), b = receipt(d), d = d DOCKED READONLY ;
	}
}
show(ReceiptReturnDetail d) + {  show(d); }

show 'Просмотреть' (ReceiptSaleDetail d)  { 
	NEWSESSION {
	    SHOW zReport OBJECTS z = zReport(d), b = receipt(d), d = d DOCKED READONLY;
	}
}
show(ReceiptSaleDetail d) + {  show(d); }


isPostedOpened (ZReport z)= isPosted(z) AND isOpened(z);
isDraftOpened (ZReport z)= isDraft(z) AND isOpened(z);

filterZReportDateFrom 'Дата с' = DATA LOCAL DATE ();
filterDateFromZreport (ZReport z) = date(z) >= filterZReportDateFrom() OR (z IS ZReport AND NOT filterZReportDateFrom());      

filterZReportDateTo 'Дата по' = DATA LOCAL DATE ();
filterDateToZreport (ZReport z) = date(z) <= filterZReportDateTo() OR (z IS ZReport AND NOT filterZReportDateTo());

filterZReportDepartmentStore  = DATA LOCAL DepartmentStore ();
nameFilterZReportDepartmentStore 'Отдел магазина' = name(filterZReportDepartmentStore()) CHARWIDTH 15;          
filterDepartmentStore (ZReport z) = departmentStore(z) == filterZReportDepartmentStore() OR (z IS ZReport AND NOT filterZReportDepartmentStore());
 
filterZReportCashRegister 'Касса Z-отчета'   = DATA LOCAL STRING[10] ();
filterCashRegister (ZReport z) = overNumberCashRegister(z) == filterZReportCashRegister() OR (z IS ZReport AND NOT filterZReportCashRegister()); 

INDEX zReport(Receipt b), dateTime(b), b;

lastDateTimeReceipt 'Время последнего чека'  = GROUP LAST dateTime(Receipt b)
        ORDER dateTime(b), b
        BY zReport(b);    
firstDateTimeReceipt 'Время первого чека' = GROUP LAST dateTime(Receipt b)
        ORDER DESC dateTime(b), b
        BY zReport(b);      

skipShowEdit = ABSTRACT BOOLEAN (ZReport);
skipShowDelete = ABSTRACT BOOLEAN (ZReport);

showEdit (ZReport i) =  isOpened(i) AND NOT skipShowEdit(i); 
showDelete (ZReport i) =  isDraftOpened(i) AND NOT skipShowDelete(i);   
     
FORM zReports 'Z-отчеты'
    PROPERTIES ()  filterZReportDateFrom, filterZReportDateTo, nameFilterZReportDepartmentStore, filterZReportCashRegister

    OBJECTS z=ZReport LAST
    PROPERTIES (z) READONLY succeededExtraCheck, isClosed, isPosted, number, date, time, nameDepartmentStore, 
                            overNumberCashRegister, basis, countReceipt, quantityReceiptDetail, 
                            sumReceiptDetail, sumPositiveCashPayment, sumPositiveCardPayment,                             
                            sumNegativeCashPayment, sumNegativeCardPayment, sumVATReceiptDetail,
                            discountSum, firstDateTimeReceipt, lastDateTimeReceipt                                                                                 
                                                        
    PROPERTIES (z) NEWSESSION NEW, editZ = EDIT SHOWIF showEdit(z) 
    PROPERTIES (z) close SHOWIF isOpened(z), open SHOWIF isClosed(z)

    PROPERTIES (z) NEWSESSION deletez=DELETE SHOWIF showDelete(z),
                            post SHOWIF isDraftOpened(z), unpost SHOWIF isPostedOpened(z)
    
    FILTERS filterDateFromZreport(z),
            filterDateToZreport(z),
            filterDepartmentStore(z),
            filterCashRegister(z)

    OBJECTS b=Receipt
    PROPERTIES(b) READONLY  number, date, time, nameEmployee,
                            sumReceiptDetail, discountSumReceiptDetail,
                            discountSum, sumVATReceiptDetail, countReceiptDetail, quantityReceiptDetail,
                            sumCashPayment, sumCardPayment

    FILTERS zReport(b)==z
    ORDERS                number(b)

    OBJECTS d=ReceiptDetail
    PROPERTIES(d) READONLY  type, idBarcode, idSku, nameSku,
                            descriptionSale ,
                            quantity, quantityReturned , price,
                            sum, discountPercent ,
                            discountSum 

    FILTERS receipt(d)==b

    OBJECTS p=Payment
    PROPERTIES(p) READONLY  namePaymentType, namePaymentMeans, sum

    FILTERS receipt(p)==b
;
@extendFormFilterAccessStock(ZReport, z, zReports, departmentStore, company);

DESIGN zReports {

    NEW topContainer{
        fill = 1;
        type = SPLITV;

        NEW firstCase {
            fill = 2;
            type = SPLITV;
            NEW ZFilters {
                fill = 1;
                NEW filters {
                    alignment = STRETCH;
                    caption = 'Фильтры';
                    type = CONTAINERH;
                    MOVE PROPERTY(filterZReportDateFrom());
                    MOVE PROPERTY(filterZReportDateTo());
                    MOVE PROPERTY(nameFilterZReportDepartmentStore());
                    MOVE PROPERTY(filterZReportCashRegister());
                }
                MOVE BOX(z);   
            }
            MOVE BOX(b);
        }

        NEW secondCase{
            fill = 1;
            type = SPLITH;

            MOVE BOX(d){
                caption = 'Строка чека';
                fill = 3;
            }
            MOVE BOX(p);
        }
    }
    MOVE TOOLBARBOX;
}
@defineFilterIsOpened (zReport, zReports, z);

NAVIGATOR {
    retailNavigator {
        NEW FOLDER ZReportNavigator 'Касса' BEFORE retailMasterData {
            NEW zReports;
        }
    }
}

EXTEND FORM zReport
    PROPERTIES(b) READONLY dataSkip BEFORE number(b) 
;

EXTEND FORM zReports
    PROPERTIES(b) READONLY dataSkip BEFORE number(b) 
;

countZReports 'Кол-во Z-отчетов' (dep, date) = GROUP SUM 1 IF isPosted(ZReport r) BY departmentStore(r), date(r);
quantityZReports 'Кол-во товаров Z-отчетов' (dep, date) = GROUP SUM quantityReceiptDetail(ZReport r) IF isPosted(r) BY departmentStore(r), date(r);
sumZReports 'Сумма Z-отчетов' (dep, date) = GROUP SUM sumReceiptDetail(ZReport r) IF isPosted(r) BY departmentStore(r), date(r); 

countZReports 'Кол-во Z-отчетов' (s, date) = GROUP SUM 1 IF isPosted(ZReport r) BY store(departmentStore(r)), date(r);
sumZReports 'Сумма Z-отчетов' (s, date) = GROUP SUM sumReceiptDetail(ZReport r) IF isPosted(r) BY store(departmentStore(r)), date(r); 

EXTEND FORM zReports
    OBJECTS dt = DATE PANEL 
    PROPERTIES (dt) VALUE        
    
    TREE treeStore1 a1=BPSTRING[3], t1=ChainStores, st1=StoreType
    PROPERTIES READONLY VALUE(a1), name(t1), name(st1)
    FILTERS chainStores(st1) == t1

    FILTERS stringEqualsAll(a1)
    FILTERS in (t1, st1)          
    
    OBJECTS s1=Store
    PROPERTIES(s1) READONLY name, id SHOWIF showIDs(), countCashRegister
    FILTERS in(t1, st1, s1)
    FILTERGROUP active1
            FILTER 'Активные' active(s1) 'F5' DEFAULT    
    FILTERGROUP withCashRegisters
            FILTER 'С кассами' countCashRegister(s1) 'F6' DEFAULT
        
    PROPERTIES (s1,dt) READONLY countZReports, sumZReports       
    
    TREE treeStore a=BPSTRING[3], t=ChainStores, st=StoreType, s = Store 
    PROPERTIES READONLY VALUE(a), name(t), name(st), name(s)

    FILTERS stringEqualsAll(a)
    FILTERS in(t, st, s) 
 
    OBJECTS dep=DepartmentStore
    PROPERTIES(dep) READONLY name, id SHOWIF showIDs(), countSalesCashRegister
    PROPERTIES READONLY countZReports(dep,dt), sumZReports(dep,dt)
     
    FILTERS in(t, st, s, dep),
            isCompany(dep)    
    FILTERGROUP active2
        FILTER 'Активные' active(dep) 'F5' DEFAULT
    FILTERGROUP withSalesCashRegister
        FILTER 'С реализацией' countSalesCashRegister(dep) 'F6' DEFAULT        
;

DESIGN zReports {
    NEW tabContainer BEFORE TOOLBARBOX {
        fill = 1;
        type = TABBED;
        MOVE topContainer {caption = 'Z-отчеты';}
        NEW department {
            caption = 'Магазины';
            fill = 1;
            type = CONTAINERV;
            MOVE BOX(dt);
            NEW treeContainer {
                fill = 1;  
                type = TABBED;
                NEW treeContainer1{
                    fill = 1;
                    type = SPLITH;
                    caption = 'По магазинам';
                    MOVE BOX(TREE treeStore1);
                    MOVE BOX(s1) { fill = 2;}                 
                }
                NEW treeContainer2{
                    fill = 1;
                    type = SPLITH;
                    caption = 'По отделам';
                    MOVE BOX(TREE treeStore);
                    MOVE BOX(dep) { fill = 2;}                    
                }                
                
            }
        }
    }
}

@extendFormFilterStockGroupAccess(s1, zReports, company);
@extendFormFilterStockAccess(dep, zReports, company);


// ----------------------------------------------- Стандартные значения ------------------------------------- //

loadDefaultType 'Добавить тип оплаты'(ISTRING[110] string, PaymentMeans paymentMeans, BPSTRING[10] sid)  { 
    NEW p = PaymentType {
        name(p) <- string;
        paymentMeans(p) <- paymentMeans;
        sid(p) <- sid;
    }
}

overLoadDefaultPaymentTypes 'Загрузить дополнительные типы оплаты'  ABSTRACT LIST ( );
loadDefaultPaymentTypes 'Загрузить стандартные типы оплаты'()  { 
    loadDefaultType('Наличные', PaymentMeans.paymentMeansCash, 'cash');
    loadDefaultType('Карточка', PaymentMeans.paymentMeansCard, 'card');
    overLoadDefaultPaymentTypes();
} IN loadDefault;

@implementLoadDefaultData(loadDefaultPaymentTypes);

//--
isDateZReport 'Дата чека должна равняться дате Z-отчета' = DATA BOOLEAN ();

EXTEND FORM options
    PROPERTIES() isDateZReport
;

DESIGN options {
    zReport {
        MOVE PROPERTY(isDateZReport());
    }
}

countDate (z) = GROUP SUM 1 IF date(zReport(Receipt r)) != date(r) IF sumReceiptDetail(r) AND isPosted(r) BY zReport(r);

CONSTRAINT isDateZReport() AND countDate(ZReport z) 
    MESSAGE 'В z-отчете должны быть чеки с одной датой';

// ------------------


CLASS ABSTRACT SaleZReportSkuLedger : SkuLedger;

META extendClassSaleZReportSkuLedger(class)
    EXTEND CLASS class : SaleZReportSkuLedger;
END

CLASS ABSTRACT ReturnZReportSkuLedger : SkuLedger;

META extendClassReturnZReportSkuLedger(class)
    EXTEND CLASS class : ReturnZReportSkuLedger;
END

//CLASS ZReportOperation : Operation.Operation;

CLASS ZReportOperation 'Операция (касса)'{
    zReportSale 'Продажа через кассу',
    zReportReturn 'Возврат через кассу'
} : Operation.Operation;

name(ZReportOperation o) += ISTRING[100](staticCaption(o)) IF o IS ZReportOperation;


showToShow (ZReport z) = z IS ZReport AND NOT showEdit(z);

EXTEND FORM zReports
    PROPERTIES show(z) SHOWIF showToShow(z) BEFORE editZ
;

//----------------------------- Накопительные суммы ------------------------------
sumCashEnd 'Сумма наличных на конец' = DATA NUMERIC[18,4] (ZReport);
sumProtectedEnd 'Накопительная выручка' = DATA NUMERIC[18,4] (ZReport);
sumBack 'Накопительный возврат' = DATA NUMERIC[18,4] (ZReport);
MODULE ZReportBonus;

REQUIRE ZReportDiscountCard;

NAMESPACE ZReport;

bonusSum 'Начислено бонусов' = DATA NUMERIC[18,4] (ReceiptSaleDetail);
bonusSum 'Начислено бонусов' = DATA NUMERIC[18,4] (ReceiptReturnDetail);

bonusSum 'Начислено бонусов' = ABSTRACT NUMERIC[18,4] (ReceiptDetail) CHARWIDTH 8;
bonusSum(ReceiptSaleDetail d) += bonusSum(d);
bonusSum(ReceiptReturnDetail d) += bonusSum(d);

bonusSum 'Начислено бонусов' (Receipt r) = GROUP SUM bonusSum(ReceiptDetail d) BY receipt(d);

signedBonusSum 'Начислено бонусов' = ABSTRACT NUMERIC[18,4] (ReceiptDetail) MATERIALIZED;
signedBonusSum(ReceiptSaleDetail d) += bonusSum(d);
signedBonusSum(ReceiptReturnDetail d) += -bonusSum(d);

signedBonusSum 'Начислено бонусов' (Receipt r) = GROUP SUM signedBonusSum(ReceiptDetail d) BY receipt(d);

bonusPaid 'Оплачено бонусами' = DATA NUMERIC[18,4] (ReceiptSaleDetail);
bonusPaid 'Оплачено бонусами' = DATA NUMERIC[18,4] (ReceiptReturnDetail);

bonusPaid 'Оплачено бонусами' = ABSTRACT NUMERIC[18,4] (ReceiptDetail) CHARWIDTH 8;
bonusPaid(ReceiptSaleDetail d) += bonusPaid(d);
bonusPaid(ReceiptReturnDetail d) += bonusPaid(d);
extraDiscountSum(ReceiptSaleDetail d) += bonusPaid(d);

bonusPaid 'Оплачено бонусами' (Receipt r) = GROUP SUM bonusPaid(ReceiptDetail d) BY receipt(d);

bonusPaidSale 'Оплачено бонусами' = GROUP SUM bonusPaid(ReceiptDetail d) IF d IS ReceiptSaleDetail
    BY zReport(receipt(d));

bonusPaidReturn 'Оплачено бонусами' = GROUP SUM bonusPaid(ReceiptDetail d) IF d IS ReceiptReturnDetail
    BY zReport(receipt(d));

bonusPaid 'Оплачено бонусами' = GROUP SUM bonusPaid(ReceiptDetail d)
    BY zReport(receipt(d));

signedBonusPaid 'Оплачено бонусами' = ABSTRACT NUMERIC[18,4] (ReceiptDetail) MATERIALIZED;
signedBonusPaid(ReceiptSaleDetail d) += bonusPaid(d);
signedBonusPaid(ReceiptReturnDetail d) += -bonusPaid(d);

signedBonusPaid 'Оплачено бонусами' (Receipt r) = GROUP SUM signedBonusPaid(ReceiptDetail d) BY receipt(d);

signedBonusSum 'Начислено бонусов' = GROUP SUM signedBonusSum(ReceiptDetail d)
    BY zReport(receipt(d));

signedBonusPaidSale 'Оплачено бонусами' = GROUP SUM signedBonusPaid(ReceiptDetail d) IF d IS ReceiptSaleDetail
    BY zReport(receipt(d));

signedBonusPaidReturn 'Оплачено бонусами' = GROUP SUM signedBonusPaid(ReceiptDetail d) IF d IS ReceiptReturnDetail
    BY zReport(receipt(d));

signedBonusPaid 'Оплачено бонусами' = GROUP SUM signedBonusPaid(ReceiptDetail d)
    BY zReport(receipt(d));


sipShowBonus = ABSTRACT BOOLEAN ();

EXTEND FORM zReport
    PROPERTIES(b) SHOWIF NOT sipShowBonus() signedBonusSum, signedBonusPaid  
    PROPERTIES(d) SHOWIF NOT sipShowBonus() bonusSum, bonusPaid  
;
    
EXTEND FORM zReports
    PROPERTIES(z) READONLY SHOWIF NOT sipShowBonus() signedBonusSum, signedBonusPaid
    PROPERTIES(b) READONLY SHOWIF NOT sipShowBonus() signedBonusSum, signedBonusPaid  
    PROPERTIES(d) READONLY SHOWIF NOT sipShowBonus() signedBonusSum, signedBonusPaid  
;

bonusPaidSaleReceiptDetailSale 'Сумма бонусов (продажи)' (receipt) = GROUP SUM bonusPaid(ReceiptSaleDetail receiptDetail)
    BY receipt (receiptDetail) IN documentSum;
bonusPaidReturnReceiptDetailReturn 'Сумма бонусов (возврат)' (receipt) = GROUP SUM bonusPaid(ReceiptReturnDetail receiptDetail)
    BY receipt (receiptDetail) IN documentSum;
bonusPaidReceiptDetail 'Сумма бонусов' (Receipt receipt) = bonusPaidSaleReceiptDetailSale(receipt) (-) bonusPaidReturnReceiptDetailReturn(receipt) MATERIALIZED;
bonusPaidReceiptDetail 'Сумма бонусов Z-отчета' (zReport) = GROUP SUM bonusPaidReceiptDetail(Receipt receipt) BY zReport(receipt) MATERIALIZED IN documentSum;

EXTEND FORM zReports
    PROPERTIES(z)READONLY bonusPaidReceiptDetail
;    

cumulativeBonuses 'Накоплено бонусов' (DiscountCard dc) = GROUP SUM signedBonusSum(ReceiptDetail rd) BY discountCard(rd) MATERIALIZED;

paidBonuses 'Оплачено бонусами' = GROUP SUM signedBonusPaid(ReceiptDetail rd) BY discountCard(rd) MATERIALIZED;

overAvailableBonuses = ABSTRACT NUMERIC[18,4](DiscountCard);
availableBonuses 'Доступно бонусов (основная)' (DiscountCard dc) = cumulativeBonuses(dc) (+) overAvailableBonuses(dc) (-) paidBonuses(dc) MATERIALIZED;
prevAvailableBonuses (DiscountCard dc) = PREV(availableBonuses(dc)); 

externalBonuses 'Дополнительные бонусы' = ABSTRACT NUMERIC[18,4] (DiscountCard) MATERIALIZED;
totalBonuses 'Доступно бонусов' (DiscountCard d) = availableBonuses(d) (+) externalBonuses(d);
prevTotalBonuses 'Доступно бонусов' (DiscountCard d) = PREV(totalBonuses(d));

totalBonuses 'Доступно бонусов' (Receipt r) = prevTotalBonuses(discountCard(r)) IN receiptDiscount;

//-- Скидки с учетом бонусов
discountBonusSumVAT 'Сумма НДС в скидке' (ReceiptDetail d) = round((discountSum(d) (+) bonusPaid(d))* valueVAT(d) / (100 + valueVAT(d)), currency(d));
 
discountBonusSumVATSale 'Сумма НДС в скидке (продажа)' (zReport)= GROUP SUM discountBonusSumVAT(ReceiptSaleDetail d) IF d IS ReceiptSaleDetail BY zReport[ReceiptDetail](d);
discountBonusSumVATReturn 'Сумма НДС в скидке (возврат)' (zReport)= GROUP SUM discountBonusSumVAT(ReceiptReturnDetail d) IF d IS ReceiptReturnDetail BY zReport[ReceiptDetail](d);

discountBonusSumVAT 'Сумма НДС в скидке' (ZReport zReport)= discountBonusSumVATSale(zReport) (-) discountBonusSumVATReturn(zReport);

discountBonusSumMarkup 'Сумма набавки в скидке' (ZReport z) = discountSum(z) (+) bonusPaidSale(z) (-) bonusPaidReturn(z) (-) discountBonusSumVAT(z);

EXTEND FORM discountCard
    PROPERTIES(d) READONLY SHOWIF NOT sipShowBonus() cumulativeBonuses, paidBonuses, availableBonuses, externalBonuses, totalBonuses
;

DESIGN discountCard {
    PANEL(d) {
        NEW row5 {
            caption = 'Бонусы';
            type = COLUMNS;
            columns = 4;
            MOVE PROPERTY(cumulativeBonuses(d));
            MOVE PROPERTY(paidBonuses(d));
            MOVE PROPERTY(availableBonuses(d));
            MOVE PROPERTY(externalBonuses(d));
            MOVE PROPERTY(totalBonuses(d));
        }
    }
}

EXTEND FORM discountCards
    PROPERTIES(d) READONLY SHOWIF NOT sipShowBonus() cumulativeBonuses, paidBonuses, availableBonuses, externalBonuses, totalBonuses
;
MODULE ZReportRetailBonus;

REQUIRE ZReportBonus, RetailBonus;

NAMESPACE ZReport;

sipShowBonus() += NOT useBonus();

returnBonusSum = GROUP SUM bonusSum(ReceiptReturnDetail rrd) BY receiptSaleDetail(rrd) MATERIALIZED;
blockedBonuses 'Заблокировано бонусов' (DiscountCard dc) = 
    GROUP SUM (bonusSum(ReceiptSaleDetail rsd) (-) returnBonusSum(rsd)) IF date(rsd) > subtract(currentDate(), blockDaysBonus()) BY discountCard(rsd);
externalBlockedBonuses 'Дополнительные заблокированные бонусы' = ABSTRACT NUMERIC[18,4] (DiscountCard);  
totalBlockedBonuses 'Всего заблокировано бонусов' (DiscountCard dc)= blockedBonuses(dc) (+) externalBlockedBonuses(dc);

overAvailableBonuses(DiscountCard dc) += sumBonusLedger(dc) (-) blockedBonuses(dc);

EXTEND FORM discountCard
    PROPERTIES(d) READONLY SHOWIF NOT sipShowBonus() blockedBonuses, externalBlockedBonuses, totalBlockedBonuses
;

DESIGN discountCard {
    row5 {
        MOVE PROPERTY(blockedBonuses(d)) BEFORE PROPERTY (paidBonuses(d));
        MOVE PROPERTY(sumBonusLedger(d)) BEFORE PROPERTY (availableBonuses(d));
    }
}

EXTEND FORM discountCards
    PROPERTIES(d) READONLY SHOWIF useBonus() blockedBonuses, externalBlockedBonuses, totalBlockedBonuses
;

// Перевод бонусов

WHEN LOCAL FORMS discountCard SETCHANGED(transferCard(BonusLedger l)) DO {
    sum(l) <- availableBonuses(transferCard(l));
}

WHEN SETCHANGED(discountCard(DiscountCard d)) AND NOT transferBonusLedger(d) DO {
    NEW l = BonusLedger {
        discountCard(l) <- discountCard(d);
        typeBonusLedger(l) <- TypeBonusLedger.transfer;
        transferCard(l) <- d;
    }
}

WHEN (CHANGED(totalBonuses(DiscountCard d)) OR CHANGED(discountCard(d))) AND discountCard(d) AND BonusLedger l == transferBonusLedger(d) DO {
    sum(l) <- prevTotalBonuses(d);
}

bonusPaid (ReceiptReturnDetail detail) <- round2(quantity(detail) * bonusPaid(receiptSaleDetail(detail)) / quantity(receiptSaleDetail(detail))) IF receiptSaleDetail(detail)
                                           WHEN CHANGED(quantity(detail)) OR
                                                CHANGED(receiptSaleDetail(detail));
bonusSum (ReceiptReturnDetail detail) <- round2(quantity(detail) * bonusSum(receiptSaleDetail(detail)) / quantity(receiptSaleDetail(detail))) IF receiptSaleDetail(detail)
                                           WHEN CHANGED(quantity(detail)) OR
                                                CHANGED(receiptSaleDetail(detail));

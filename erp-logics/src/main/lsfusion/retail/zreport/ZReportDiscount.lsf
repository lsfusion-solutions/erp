MODULE ZReportDiscount;

REQUIRE ZReport;

NAMESPACE ZReport;

CLASS Discount 'Скидка';
TABLE discount (Discount);
TABLE receiptDetailDiscount (ReceiptDetail, Discount);

mode 'Тип' = DATA ISTRING[100] (Discount) IN base;
name 'Наименование' = DATA ISTRING[100] (Discount) IN base CHARWIDTH 20;
discount = GROUP MAX (Discount type) BY name(type);
discountMode = GROUP MAX Discount type BY mode(type);

discountSum 'Сумма скидки' = DATA NUMERIC[18,4] (ReceiptDetail, Discount);

FORM discount 'Скидка'
    OBJECTS o = Discount PANEL
    PROPERTIES(o) name, mode

    EDIT Discount OBJECT o
;

FORM discounts 'Скидка'
    OBJECTS dates = (dFrom = DATE, dTo = DATE) PANEL
    PROPERTIES valFrom = VALUE(dFrom), valTo = VALUE(dTo)

    OBJECTS o = Discount
    PROPERTIES(o) READONLY name, mode SHOWIF showIDs()
    PROPERTIES(o) NEWSESSION NEW, EDIT, DELETE

    OBJECTS rds = ReceiptDetail
    PROPERTIES (rds) READONLY nameDepartmentStore, numberZReport, numberReceipt, numberCashRegister, nameEmployee,
        dateZReport, date, time, number, nameSku, idBarcode, signedQuantity, price, signedSum, signedDiscountSum
    PROPERTIES READONLY discountSum(rds, o)
    FILTERS date(rds) >= dFrom, date(rds) <= dTo, discountSum(rds, o)
;

FORM dialogDiscounts 'Скидка'
    OBJECTS o = Discount
    PROPERTIES(o) READONLY name

    LIST Discount OBJECT o
;

NAVIGATOR {
    ZReportNavigator {
        NEW discounts;
    }
}

EXTEND FORM zReports
    OBJECTS di = Discount
    PROPERTIES READONLY mode(di), name(di), discountSum(d, di)
    FILTERS discountSum(d, di)
;

DESIGN zReports {
    firstCase {
        NEW receipt {
            fill = 1;
            horizontal = TRUE;
            MOVE BOX(b) {fill = 3;};
            MOVE BOX(p);
        }
    }

    secondCase {
        detail {
            MOVE BOX (di);
        }
    }
}
MODULE ReceiptDetailReport;

REQUIRE ZReport;//, ZReportDiscountCard;

NAMESPACE ZReport;

filterDepartmentStoreReceiptDetail 'Просмотр только по одному магазину' = DATA BOOLEAN () COMPLEX;
filterSkuReceiptDetail 'Просмотр только по одному товару' = DATA BOOLEAN () COMPLEX;

EXTEND FORM options
    PROPERTIES() filterDepartmentStoreReceiptDetail, filterSkuReceiptDetail
;

DESIGN options {
    zReport {
        NEW receiptDetail {
            caption = 'Регистр строк чека';
            MOVE PROPERTY (filterDepartmentStoreReceiptDetail());
            MOVE PROPERTY (filterSkuReceiptDetail());
        }
    }
}

filterDepartmentStore  = DATA LOCAL DepartmentStore();
nameFilterDepartmentStore 'Магазин' = name(filterDepartmentStore());

filterSku = DATA LOCAL Sku();
nameFilterSku 'Товар' = name(filterSku());

skipShowFilterDates = ABSTRACT BOOLEAN ();
filterDateFrom 'Дата с' = DATA LOCAL NESTED DATE () PREREAD; 
filterDateTo 'Дата по' = DATA LOCAL NESTED DATE () PREREAD;

FORM receiptDetailReport 'Регистр строк чека'
    PROPERTIES () SHOWIF NOT skipShowFilterDates() filterDateFrom, filterDateTo
    PROPERTIES() nameFilterDepartmentStore, nameFilterSku
    
    OBJECTS rd = ReceiptDetail
    PROPERTIES(rd) READONLY nameDepartmentStore, numberZReport, numberReceipt, numberCashRegister, nameEmployee, dateZReport, date, time,  
                            id, number, nameSku, idBarcode, signedQuantity, price, signedSum, signedDiscountSum, show CHANGEABLE
    ORDERS date(rd)
    FILTERS NOT date(rd) < filterDateFrom() AND NOT date(rd) > filterDateTo(),
            departmentStore(rd) == filterDepartmentStore() OR (NOT filterDepartmentStore() AND NOT filterDepartmentStoreReceiptDetail()),            
            sku(rd) == filterSku() OR (NOT filterSku() AND NOT filterSkuReceiptDetail())
    EVENTS 
        ON INIT {
            filterDateFrom() <- currentDate();
            filterDateTo() <- currentDate();
        }
;

@extendFormFilterAccessStock(receipt, rd, receiptDetailReport, departmentStore, company);

DESIGN receiptDetailReport {
    NEW dates {
        type = CONTAINERH;
        caption = 'Период';
        MOVE PROPERTY(filterDateFrom());
        MOVE PROPERTY(filterDateTo());
    }
    NEW top{
        type = CONTAINERH;       
        NEW operationContainer{
            type = CONTAINERH;
            caption = 'Фильтры';
            MOVE PROPERTY(nameFilterDepartmentStore());
            MOVE PROPERTY(nameFilterSku());
        }
    }
    MOVE BOX(rd);
    MOVE TOOLBARBOX;
}

NAVIGATOR {
    retailNavigator {
        MOVE retailReports BEFORE retailMasterData {
            NEW receiptDetailReport;
        }
    }
}

showReceiptDetailReport 'Показать чеки товара по складу' (Sku sku, Stock stock)  { 
    filterDepartmentStore() <- stock;
    filterSku() <- sku;
    SHOW receiptDetailReport DOCKED;
}

EXTEND FORM currentBalanceSkuStock
    PROPERTIES showReceiptDetailReport(s, st)
;

DESIGN currentBalanceSkuStock {
    actionContainer {
        NEW receipt {
            caption = 'Чеки';
            MOVE PROPERTY (showReceiptDetailReport(s, st));
        }
    }
}
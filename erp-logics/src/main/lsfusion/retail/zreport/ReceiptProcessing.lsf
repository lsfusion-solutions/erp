MODULE ReceiptProcessing;

REQUIRE ZReport;
  
diffsumPayment 'Отклонение' = sumReceiptDetail(Receipt b) (-) sumPayment(b);
prevDiffsumPayment 'Отклонение' = PREV(diffsumPayment(Receipt b));                                                                        
   
filterProcessingStock  = DATA LOCAL DepartmentStore ();
nameFilterProcessingStock 'Склад' = name[Stock](filterProcessingStock()) CHARWIDTH 15;          
filterStockProcessing (Receipt b) = departmentStore(b) == filterProcessingStock() OR (b IS Receipt AND NOT filterProcessingStock());                                                                        
                                                                      
FORM receiptProcessing 'Обработка чеков'    
    OBJECTS dates = (dFrom = DATE, dTo = DATE) PANEL
    PROPERTIES objFrom = VALUE(dFrom), objTo = VALUE(dTo)
    OBJECTS b=Receipt
    PROPERTIES(b) READONLY nameDepartmentStore, number, dateTime, nameEmployee, overNumberCashRegister,
                  sumReceiptDetail, discountSumReceiptDetail,
                  discountSum, sumVATReceiptDetail, countReceiptDetail, quantityReceiptDetail,
                  sumPayment, prevDiffsumPayment
    ORDERS   dateTime(b)           
    PROPERTIES  nameFilterProcessingStock()            
    FILTERS filterStockProcessing (b),
            date(b) >= dFrom,
            date(b) <= dTo

    OBJECTS d = ReceiptDetail GRID
    PROPERTIES(d) READONLY  type, idBarcode
    PROPERTIES(d) READONLY  idSku, nameSku 
    PROPERTIES(d)  descriptionSale READONLY, quantity, quantityReturned,
                  price, sum, discountPercent, discountSum, numberVAT READONLY, 
                  valueVAT READONLY                  
    PROPERTIES(d) DELETE                
    
    OBJECTS p=Payment
    PROPERTIES(p) namePaymentType, namePaymentMeans, sum, NEW, DELETE GRID

    FILTERS receipt(p)==b
               
    FILTERS receipt(d) == b          
    FILTERGROUP receiptFilter
        FILTER  'Нет платежа' NOT sumPayment(b)
        FILTER  'Сумма платежей меньше суммы чека' 
            ((sumReceiptDetail(b) > 0 AND sumReceiptDetail(b) > sumPayment(b)) OR (sumReceiptDetail(b) < 0 AND sumReceiptDetail(b) != sumPayment(b)))
        FILTER  'Сумма по безналу больше суммы чека' 
            ((sumReceiptDetail(b) > 0 AND sumReceiptDetail(b) < sumCardPayment(b)) OR (sumReceiptDetail(b) <0 AND sumReceiptDetail(b) > sumCardPayment(b)))   
        FILTER  'С отклонениями'  prevDiffsumPayment(b)  DEFAULT              
                                                           
;
DESIGN receiptProcessing {

    NEW filter {
        caption = 'Фильтры';
        type = CONTAINERH;
        MOVE PROPERTY (objFrom) {caption = 'Дата с';};
        MOVE PROPERTY (objTo) {caption = 'Дата по';};
        MOVE PROPERTY (nameFilterProcessingStock());
    }
    MOVE BOX(b);
    NEW secondCase{
        fill = 1;
        type = SPLITH;

        MOVE BOX(d) {
            fill = 3;
            caption = 'Строка чека';            
        }
        MOVE BOX(p);
    }           
    
    MOVE TOOLBARBOX;
}

NAVIGATOR {
    retailDashboardNavigator {
        NEW receiptProcessing;
    }
}
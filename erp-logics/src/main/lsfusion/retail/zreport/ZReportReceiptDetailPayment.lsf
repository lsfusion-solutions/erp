MODULE ZReportReceiptDetailPayment;

REQUIRE ZReport;

NAMESPACE ZReport;

TABLE receiptDetailPayment(ReceiptDetail, Payment);
sum 'Расписано для строки чека' = DATA NUMERIC[18,4] (ReceiptDetail, Payment);
CONSTRAINT sum(ReceiptDetail d, Payment p) AND receipt(d) != receipt(p)
    MESSAGE 'Несоответствие чека строки и платежа';

EXTEND FORM zReport
    PROPERTIES(d, p) READONLY sum AFTER sum(p)
;

EXTEND FORM zReports
    PROPERTIES(d, p) READONLY sum
;

calculateSumReceiptDetailPayment 'Расписать платежи по строкам' (Receipt receipt)  { 
    sum(ReceiptDetail d, Payment p) <- NULL WHERE receipt(d) == receipt AND receipt(p) == receipt; 
    LOCAL left = NUMERIC[18,4] (ReceiptDetail);
    left(ReceiptDetail d) <- sum(d) IF receipt(d) == receipt;
    
    FOR receipt(Payment p) == receipt ORDER DESC order(paymentMeans(p)) DO {
        LOCAL sum = NUMERIC[18,4]();
        sum() <- sum(p);
        FOR left(ReceiptDetail d) DO {
            IF sum() > 0 THEN {
                sum(d, p) <- min(left(d), sum());
                sum() <- sum() (-) sum(d, p);
                left(d) <- left(d) (-) sum(d, p);
            } ELSE {
                sum(d, p) <- -min(left(d), -sum());                
                sum() <- sum() (-) sum(d, p);
                left(d) <- left(d) (+) sum(d, p);
            }
        }            
    }    
}

// не очень правильно, но пока так
WHEN CHANGED(sumReceiptDetail(Receipt receipt)) OR CHANGED(sumCashPayment(receipt)) OR CHANGED(sumCardPayment(receipt)) DO {
    calculateSumReceiptDetailPayment(receipt);        
}

// Продажи товаров
calcSumVAT (ReceiptDetail detail, Payment payment) = sum(detail, payment) * valueVAT(detail) / (100 + valueVAT(detail));

TABLE zReportPaymentMeansRange(ZReport, PaymentMeans, Range);
sumSkuReceiptDetail 'Сумма по товарам' (zReport, paymentMeans, range) = GROUP SUM sum(ReceiptDetail detail, Payment payment) 
    IF sku(detail) BY zReport(detail), paymentMeans(payment), VAT(detail);
    
sumVATSkuReceiptDetail 'Сумма НДС по товарам' (ZReport zReport, PaymentMeans paymentMeans, Range range) = (round2(
    [ GROUP SUM calcSumVAT(ReceiptDetail detail, Payment payment) IF sku(detail) 
        BY zReport(detail), paymentMeans(payment), VAT(detail)](zReport, paymentMeans, range)));
sumVATSkuReceiptDetail 'Сумма НДС по товарам' (ZReport zReport, Range range)= NUMERIC[18,4]((GROUP SUM sumVATSkuReceiptDetail(zReport, PaymentMeans paymentMeans, range)));

sumCashSkuReceiptDetail 'Сумма (нал.) по товарам' (ZReport zReport, Range range) = sumSkuReceiptDetail(zReport, PaymentMeans.paymentMeansCash, range);
sumCardSkuReceiptDetail 'Сумма (безнал.) по товарам' (ZReport zReport, Range range) = sumSkuReceiptDetail(zReport, PaymentMeans.paymentMeansCard, range);

sumCashSkuReceiptDetail 'Сумма (нал.) по товарам' (ZReport zReport) = GROUP SUM sumCashSkuReceiptDetail(zReport, Range range);
sumCardSkuReceiptDetail 'Сумма (безнал.) по товарам' (ZReport zReport) = GROUP SUM sumCardSkuReceiptDetail(zReport, Range range);

sumVATCashReceiptDetail 'Сумма НДС (нал.) по товарам' (ZReport zReport, Range range) = sumVATSkuReceiptDetail(zReport, PaymentMeans.paymentMeansCash, range);
sumVATCardReceiptDetail 'Сумма НДС (безнал.) по товарам' (ZReport zReport, Range range) = sumVATSkuReceiptDetail(zReport, PaymentMeans.paymentMeansCard, range);

// Продажи сертификатов
sumGiftCardReceiptDetail 'Сумма проданных сертификатов' (zReport, paymentMeans) = GROUP SUM sum(ReceiptDetail detail, Payment payment) 
    IF NOT sku(detail) BY zReport(detail), paymentMeans(payment);
sumGiftCardReceiptDetail 'Сумма проданных сертификатов' (ZReport zReport) = GROUP SUM sumGiftCardReceiptDetail(zReport, PaymentMeans paymentMeans);    

sumGiftCardCashReceiptDetail 'Сумма (нал.) проданных сертификатов' (ZReport zReport) = sumGiftCardReceiptDetail(zReport, PaymentMeans.paymentMeansCash);
sumGiftCardCardReceiptDetail 'Сумма (безнал.) проданных сертификатов' (ZReport zReport) = sumGiftCardReceiptDetail(zReport, PaymentMeans.paymentMeansCard);   

// продажи по товарам, без учета возвратов
sumSkuReceiptDetailPositive 'Сумма по товарам' (zReport, paymentMeans) = GROUP SUM sum(ReceiptDetail detail, Payment payment) 
    IF sku(detail) AND sum(detail, payment) > 0.0 BY zReport(detail), paymentMeans(payment);
    
sumCashSkuReceiptDetailPositive 'Сумма (нал.) по товарам' (ZReport zReport) = GROUP SUM sumSkuReceiptDetailPositive(zReport, PaymentMeans.paymentMeansCash);
sumCardSkuReceiptDetailPositive 'Сумма (безнал.) по товарам' (ZReport zReport) = GROUP SUM sumSkuReceiptDetailPositive(zReport, PaymentMeans.paymentMeansCard); 
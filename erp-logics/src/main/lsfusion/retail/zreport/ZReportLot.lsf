MODULE ZReportLot;

REQUIRE ZReport, Lot;

NAMESPACE ZReport;

skipSkuControl = ABSTRACT VALUE BOOLEAN (ReceiptDetail);

@defineOption(skipLotControl, 'Разрешать прием марок с неправильным sku', BOOLEAN, zReport){
    skipLotControl 'Разрешать прием марок с неправильным sku' = DATA BOOLEAN () COMPLEX;
    
    EXTEND FORM options PROPERTIES() skipLotControl;
    DESIGN options { disassembly { MOVE PROPERTY(skipLotControl()); } }
};
skipSkuControl (ReceiptDetail d) += skipLotControl() AND d IS ReceiptDetail; 

lot 'Марка' = DATA Lot (ReceiptSaleDetail) INDEXED;
idLot 'Марка' (ReceiptSaleDetail d) = id(lot(d)) IN id;


CONSTRAINT lot(ReceiptSaleDetail d) AND NOT sku(lot(d)) = sku(d) AND NOT skipSkuControl(d) CHECKED BY lot[ReceiptSaleDetail]
    MESSAGE 'Товар марки не соответствует товару строки чека продажи';

lot 'Марка' = DATA Lot (ReceiptReturnDetail) INDEXED;
idLot 'Марка' (ReceiptReturnDetail d) = id(lot(d)) IN id;

CONSTRAINT lot(ReceiptReturnDetail d) AND NOT sku(lot(d)) = sku(d) AND NOT skipSkuControl(d) CHECKED BY lot[ReceiptReturnDetail]
    MESSAGE 'Товар марки не соответствует товару строки чека продажи';

lot 'Марка' (ReceiptDetail d) = MULTI lot[ReceiptSaleDetail](d), lot[ReceiptReturnDetail](d) MATERIALIZED; 
idLot 'Код марки' (ReceiptDetail d) = id(lot(d));
tailLot 'Хвост марки' (ReceiptDetail d) = tail(lot(d));
    
EXTEND FORM zReport PROPERTIES(d) idLot;
EXTEND FORM zReports PROPERTIES(d) READONLY idLot;


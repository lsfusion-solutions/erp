MODULE DateStockDocumentAccountSum;

REQUIRE DateSkuLedgerAccountPrice;

NAMESPACE ZReport;

//--
CLASS RetailSales 'Розничные продажи';
TABLE retailSales (RetailSales);

sumSaleSkuLedger (DepartmentStore d, DATE date) = GROUP SUM 
    accountSum(DateSaleSkuLedger z) IF isPosted[DataSkuLedger](z) 
    BY departmentStore(z), date(z);

sumReturnSkuLedger (DepartmentStore d, DATE date) = GROUP SUM 
    accountSum(DateReturnSkuLedger z) IF isPosted[DataSkuLedger](z) 
    BY departmentStore(z), date(z);
    
sumSkuLedger (DepartmentStore d, date) = sumSaleSkuLedger(d, date) (-) sumReturnSkuLedger(d, date);

retailSales = AGGR RetailSales WHERE sumSkuLedger(DepartmentStore departmentStore, DATE date) MATERIALIZED INDEXED;
dateTime 'Дата/время' (RetailSales o) = dateTimeToDateTime(date(o), 23:59);
time 'Время' (RetailSales o) = 23:59 IF o IS RetailSales;

description (RetailSales zReportRepricing) =
    STRING[200]('Розничные продажи за ' + date(zReportRepricing));

EXTEND CLASS RetailSales : OutStockDocumentLedger, ZReportStockDocumentledger;

dateTime (RetailSales ledger) += dateTime(ledger);
isPosted (RetailSales ledger) += ledger IS RetailSales;
isClosed (RetailSales ledger) += ledger IS RetailSales;
stock (RetailSales ledger) += departmentStore(ledger);
description (RetailSales ledger) += description(ledger);

type(RetailSales l) += STRING[50]('Розничные продажи') IF l IS RetailSales;
sumItem (RetailSales ledger) += sumSkuLedger(departmentStore(ledger),date(ledger));
sumContainer (RetailSales ledger) += 0.0 IF ledger IS RetailSales;
skip (RetailSales ledger) += date(ledger) < startDateGroupCashRegister(departmentStore(ledger));

stockDocumentLedger(DateSaleSkuLedger ledger) += retailSales(departmentStore(ledger), date(ledger));
stockDocumentLedger(DateReturnSkuLedger ledger) += retailSales(departmentStore(ledger), date(ledger));
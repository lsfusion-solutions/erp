MODULE CashOperation;

REQUIRE ZReport, CashDocument;

NAMESPACE CashOperation;

//--------------------------------- Операции с наличными -------------------------------------------------//
// Интерфес для внесения/изъятия денег с кассы
CLASS ABSTRACT CashOperation 'Операция с наличными';
TABLE cashOperation(CashOperation);

date 'Дата документа' = ABSTRACT DATE (CashOperation) IN documentHeader;
time 'Время документа' = ABSTRACT TIME (CashOperation) IN documentHeader;
dateTime 'Дата/время документа'(CashOperation cashOperation) = dateTimeToDateTime(date(cashOperation), time(cashOperation)) IN documentHeader;

number 'Номер' (cashOperation) = ABSTRACT STRING[50] (CashOperation) IN documentPrm CHARWIDTH 8;

basis 'Основание' = ABSTRACT STRING[100] (CashOperation) IN documentPrm;

operation = ABSTRACT Operation (CashOperation);
nameOperation 'Операция' (CashOperation c) = name(operation(c));

sum 'Сумма' (cashOperation) = ABSTRACT NUMERIC[18,4] (CashOperation);

isComplete 'Фискализировано' = DATA BOOLEAN (CashOperation);

cashRegister = ABSTRACT CashRegister(CashOperation);
cashRegisterModel (CashOperation cashOperation) = cashRegisterModel(groupCashRegister(cashRegister(cashOperation)));
sidCashRegisterModel 'Код модели' (CashOperation cashOperation) = sid(cashRegisterModel (cashOperation));
numberCashRegister 'Касса' (CashOperation cashOperation) = npp(cashRegister(cashOperation)) IN documentPrm;

fiscalService 'Движение денег в фискальном регистраторе' (cashOperation)  ABSTRACT LIST ( CashOperation) CONFIRM;
fiscalServiceCashRegister 'Движение денег в фискальном регистраторе' (CashOperation cashOperation)  { IF cashRegister(cashOperation) == currentCashRegister() THEN fiscalService(cashOperation); }

zReport = ABSTRACT ZReport(CashOperation);
numberZReport 'Z-отчет' (CashOperation cashOperation) = number(zReport(cashOperation)) IN documentPrm;

//---------------------------- Внесение денег ----------------------------------------------------------------//

CLASS IncomeCashOperation 'Внесение денег' : CashOperation;
TABLE incomeCashOperation (IncomeCashOperation);

is (IncomeCashOperation cashOperation) = cashOperation IS IncomeCashOperation;

@defineDocumentHeaderCreated(IncomeCashOperation);
@defineDocumentHeaderNumber(IncomeCashOperation, BPSTRING[2]);

@defineNumeratedDefault(IncomeCashOperation, 'Внесения денег', 'ВД');

@defineDocumentHeaderTime(IncomeCashOperation);
@deriveDocumentHeaderTimePrefix(IncomeCashOperation, );

@defineDocumentOperationCash(IncomeCashOperation, 'Внесение денег');

cashRegister (incomeCashOperation) = DATA CashRegister (IncomeCashOperation);
numberCashRegister 'Касса ККМ' (IncomeCashOperation incomeCashOperation) = npp(cashRegister(incomeCashOperation)) IN documentPrm CHARWIDTH 20;

zReport(incomeCashOperation) = DATA ZReport (IncomeCashOperation);
numberZReport 'Z-отчет' (IncomeCashOperation incomeCashOperation) = number(zReport(incomeCashOperation)) IN documentPrm;

CONSTRAINT zReport(IncomeCashOperation c) AND cashRegister(zReport(c)) != cashRegister(c)
    CHECKED BY zReport[IncomeCashOperation]
    MESSAGE 'Z-отчет внесения денег должен соответствовать кассе';

departmentStore = stock(cashRegister(IncomeCashOperation incomeCashOperation));
nameDepartmentStore 'Отдел магазина' (IncomeCashOperation incomeCashOperation) = name(departmentStore(incomeCashOperation)) IN documentHeader
        CHARWIDTH 20;
fullNameDepartmentStore 'Отдел магазина' (IncomeCashOperation incomeCashOperation) = fullName(departmentStore(incomeCashOperation)) IN documentHeader
        CHARWIDTH 20;
        
legalEntityStock (IncomeCashOperation incomeCashOperation) = legalEntity(departmentStore(incomeCashOperation));
nameLegalEntityStock 'Компания (Отдел магазина)' (IncomeCashOperation incomeCashOperation) = name(legalEntityStock (incomeCashOperation));
fullNameLegalEntityStock 'Компания (Отдел магазина) полное наим-ие' (IncomeCashOperation incomeCashOperation) = fullName(legalEntityStock (incomeCashOperation));       
addressLegalEntityStock 'Адрес компании (Отдел магазина)' (IncomeCashOperation incomeCashOperation) = address(legalEntityStock (incomeCashOperation));

addressStock 'Адрес (Отдел магазина)' (IncomeCashOperation incomeCashOperation) = address(departmentStore(incomeCashOperation));

country (IncomeCashOperation incomeCashOperation) = country(departmentStore(incomeCashOperation));

sumCash 'Сумма внесения' = DATA NUMERIC[18,4] (IncomeCashOperation) IN documentSum;
basis 'Основание' (IncomeCashOperation incomeCashOperation) =
     CONCAT '','Внесение денег',' №' + seriesNumber(incomeCashOperation),' в кассу ' + numberCashRegister(incomeCashOperation),' от ' + date(incomeCashOperation) IN documentPrm;

curSumIncomeOperation 'Текущая сумма внесенных денег' (cashRegister) =
    GROUP SUM sumCash(IncomeCashOperation incomeCashOperation)
    BY cashRegister[CashOperation](incomeCashOperation);

sumAIncomeOperation 'Сумма внесенных денег после даты/время' (cashRegister, DATETIME date) =
    GROUP SUM sumCash(IncomeCashOperation incomeCashOperation) IF
              dateTime[CashOperation](incomeCashOperation) > date
    BY cashRegister[CashOperation](incomeCashOperation);

sumIncomeOperation 'Сумма внесенных денег на дату/время' (CashRegister cashRegister, DATETIME date) =
    curSumIncomeOperation(cashRegister) (-)
    sumAIncomeOperation(cashRegister, date);

employee = DATA Employee (IncomeCashOperation);
idEmployee 'Код кассира' (IncomeCashOperation d) = id(employee(d));
nameEmployee 'Кассир' (IncomeCashOperation d) = shortName(employee(d));

date[CashOperation](IncomeCashOperation incomeCashOperation) += date(incomeCashOperation);
time[CashOperation](IncomeCashOperation incomeCashOperation) += time(incomeCashOperation);
number[CashOperation](IncomeCashOperation incomeCashOperation) += seriesNumber(incomeCashOperation);
basis[CashOperation](IncomeCashOperation incomeCashOperation) += STRING[100](basis(incomeCashOperation));
sum[CashOperation](IncomeCashOperation incomeCashOperation) += sumCash(incomeCashOperation);
operation[CashOperation](IncomeCashOperation incomeCashOperation) += operation(incomeCashOperation);
cashRegister[CashOperation](IncomeCashOperation incomeCashOperation) += cashRegister(incomeCashOperation);
zReport[CashOperation](IncomeCashOperation incomeCashOperation) += zReport(incomeCashOperation);
//employee(IncomeCashOperation incomeCashOperation) += employee(incomeCashOperation);

//------------------------------ Изъятие денег -----------------------------------------------------------//

CLASS OutcomeCashOperation 'Изъятие денег' : CashOperation;
TABLE outcomeCashOperation (OutcomeCashOperation);

is (OutcomeCashOperation cashOperation) = cashOperation IS OutcomeCashOperation;

@defineDocumentHeaderCreated(OutcomeCashOperation);
@defineDocumentHeaderNumber(OutcomeCashOperation, BPSTRING[2]);

@defineNumeratedDefault(OutcomeCashOperation, 'Изъятия денег', 'ВД');

@defineDocumentHeaderTime(OutcomeCashOperation);
@deriveDocumentHeaderTimePrefix(OutcomeCashOperation, );

@defineDocumentOperationCash(OutcomeCashOperation, 'Изъятие денег');

cashRegister (outcomeCashOperation) = DATA CashRegister (OutcomeCashOperation);
numberCashRegister 'Касса ККМ' (OutcomeCashOperation outcomeCashOperation) = npp(cashRegister(outcomeCashOperation)) IN documentPrm CHARWIDTH 20;

zReport(outcomeCashOperation) = DATA ZReport (OutcomeCashOperation) INDEXED;
numberZReport 'Z-отчет' (OutcomeCashOperation outcomeCashOperation) = number(zReport(outcomeCashOperation)) IN documentPrm;

CONSTRAINT zReport(OutcomeCashOperation c) AND cashRegister(zReport(c)) != cashRegister(c)
    CHECKED BY zReport[OutcomeCashOperation]
    MESSAGE 'Z-отчет изъятия денег должен соответствовать кассе';

departmentStore = stock(cashRegister(OutcomeCashOperation outcomeCashOperation));
nameDepartmentStore 'Отдел магазина' (OutcomeCashOperation outcomeCashOperation) = name(departmentStore(outcomeCashOperation)) IN documentHeader
        CHARWIDTH 20;
fullNameDepartmentStore 'Отдел магазина' (OutcomeCashOperation outcomeCashOperation) = fullName(departmentStore(outcomeCashOperation)) IN documentHeader
        CHARWIDTH 20;
        
legalEntityStock (OutcomeCashOperation outcomeCashOperation) = legalEntity(departmentStore(outcomeCashOperation));
nameLegalEntityStock 'Компания (Отдел магазина)' (OutcomeCashOperation outcomeCashOperation) = name(legalEntityStock (outcomeCashOperation));
fullNameLegalEntityStock 'Компания (Отдел магазина) полное наим-ие' (OutcomeCashOperation outcomeCashOperation) = fullName(legalEntityStock (outcomeCashOperation));       
addressLegalEntityStock 'Адрес компании (Отдел магазина)' (OutcomeCashOperation outcomeCashOperation) = address(legalEntityStock (outcomeCashOperation));

addressStock 'Адрес (Отдел магазина)' (OutcomeCashOperation outcomeCashOperation) = address(departmentStore(outcomeCashOperation));

country (OutcomeCashOperation outcomeCashOperation) = country(departmentStore(outcomeCashOperation));
                
sumCash 'Сумма изъятия' = DATA NUMERIC[18,4] (OutcomeCashOperation) IN documentSum;
basis 'Основание' (OutcomeCashOperation outcomeCashOperation) =
    CONCAT '', 'Изъятие денег',' №' + seriesNumber(outcomeCashOperation) , ' из кассы ' + numberCashRegister(outcomeCashOperation), ' от ' + date(outcomeCashOperation) IN documentPrm;

curSumOutcomeOperation 'Текущая сумма изъятых денег' (cashRegister) =
    GROUP SUM sumCash(OutcomeCashOperation outcomeCashOperation)
    BY cashRegister[CashOperation](outcomeCashOperation);

sumAOutcomeOperation 'Сумма изъятых денег после даты/время' (cashRegister, DATETIME date) =
    GROUP SUM sumCash(OutcomeCashOperation outcomeCashOperation) IF
              dateTime[CashOperation](outcomeCashOperation) > date
    BY cashRegister[CashOperation](outcomeCashOperation);

sumOutcomeOperation 'Сумма изъятых денег на дату/время' (CashRegister cashRegister, DATETIME date) =
    curSumOutcomeOperation(cashRegister) (-)
    sumAOutcomeOperation(cashRegister, date);

employee = DATA Employee (OutcomeCashOperation);
idEmployee 'Код кассира' (OutcomeCashOperation d) = id(employee(d));
shortNameEmployee 'Кассир' (OutcomeCashOperation d) = shortName(employee(d));
nameEmployee 'Кассир' (OutcomeCashOperation d) = name(employee(d));

date[CashOperation](OutcomeCashOperation outcomeCashOperation) += date(outcomeCashOperation);
time[CashOperation](OutcomeCashOperation outcomeCashOperation) += time(outcomeCashOperation);
number[CashOperation](OutcomeCashOperation outcomeCashOperation) += seriesNumber(outcomeCashOperation);
basis[CashOperation](OutcomeCashOperation outcomeCashOperation) += STRING[100](basis(outcomeCashOperation));
sum[CashOperation](OutcomeCashOperation outcomeCashOperation) += -sumCash(outcomeCashOperation);
operation[CashOperation](OutcomeCashOperation outcomeCashOperation) += operation(outcomeCashOperation);
cashRegister[CashOperation](OutcomeCashOperation outcomeCashOperation) += cashRegister(outcomeCashOperation);
zReport[CashOperation](OutcomeCashOperation outcomeCashOperation) += zReport(outcomeCashOperation);

// Формы
FORM incomeCashOperation 'Внесение денег'

    OBJECTS i = IncomeCashOperation PANEL
    PROPERTIES(i) nameNumerator, number, series, date, time, 
                  nameOperation, numberCashRegister, nameDepartmentStore READONLY, numberZReport, nameEmployee,
                  sumCash

    EDIT IncomeCashOperation OBJECT i
;

DESIGN incomeCashOperation {
    NEW topContainer FIRST {
        NEW firstCase{
            horizontal = TRUE;
            caption = 'Шапка документа';
            MOVE PROPERTY(nameNumerator(i));
            MOVE PROPERTY(number(i));
            MOVE PROPERTY(series(i));
            MOVE PROPERTY(date(i));
            MOVE PROPERTY(time(i));
        }
        NEW secondCase{
            caption = 'Параметры документа';
            MOVE PROPERTY(nameOperation(i));
            MOVE PROPERTY(numberCashRegister(i));
            MOVE PROPERTY(nameDepartmentStore(i));
            MOVE PROPERTY(numberZReport(i));
            MOVE PROPERTY(nameEmployee(i));
        }
        NEW thirdCase{
            caption = 'Суммы';
            MOVE PROPERTY(sumCash(i));
        }
    }
}

FORM outcomeCashOperation 'Изъятие денег'

    OBJECTS o = OutcomeCashOperation PANEL
    PROPERTIES(o) nameNumerator, number, series, date, time, 
                  nameOperation, numberCashRegister, nameDepartmentStore READONLY, numberZReport, nameEmployee, 
                  sumCash

    EDIT OutcomeCashOperation OBJECT o;
;

DESIGN outcomeCashOperation {
    NEW topContainer FIRST {
        NEW firstCase{
            horizontal = TRUE;
            caption = 'Шапка документа';
            MOVE PROPERTY(nameNumerator(o));
            MOVE PROPERTY(number(o));
            MOVE PROPERTY(series(o));
            MOVE PROPERTY(date(o));
            MOVE PROPERTY(time(o));
        }
        NEW secondCase{
            caption = 'Параметры документа';
            MOVE PROPERTY(nameOperation(o));
            MOVE PROPERTY(numberCashRegister(o));
            MOVE PROPERTY(nameDepartmentStore(o));
            MOVE PROPERTY(numberZReport(o));
            MOVE PROPERTY(nameEmployee(o));
        }
        NEW thirdCase{
            caption = 'Суммы';
            MOVE PROPERTY(sumCash(o));
        }
    }
}
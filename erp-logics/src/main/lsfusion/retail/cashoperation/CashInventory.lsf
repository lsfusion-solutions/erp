MODULE CashInventory;

REQUIRE CashDocument;

NAMESPACE CashOperation;

CLASS CashInventory 'Инвентаризация наличных' : CashDocument;
TABLE cashInventory (CashInventory);

@defineDocumentHeaderCreated(CashInventory);
@defineDocumentHeaderNumber(CashInventory, BPSTRING[3]);

@defineNumeratedDefault(CashInventory, 'Инвентаризация наличных', 'ИН');

@defineDocumentHeaderTime(CashInventory);
@deriveDocumentHeaderTimePrefix(CashInventory, );

@defineDocumentOperationCash(CashInventory, 'Инвентаризация наличных');

@defineDocumentHeaderDepartmentStore(cashInventory);
@defineDocumentDialogStocks(cashInventory, departmentStore, company, , );

@defineDocumentHeaderDescription (CashInventory, 'Инвентаризация наличных');

@defineDocumentHeaderNote(CashInventory);

sumBalance 'Сумма по учету' = DATA NUMERIC[18,4] (CashInventory) IN documentSum;
WHEN LOCAL CHANGED(departmentStore(CashInventory c)) OR CHANGED(dateTime(c)) DO
    sumBalance(c) <- balanceB(departmentStore(c), dateTime(c)); // пока без PREV, чтобы нормально работало изменение даты/времени

sum 'Сумма по факту' = DATA NUMERIC[18,4] (CashInventory) IN documentSum;

FORM cashInventory 'Инвентаризация наличных'
    OBJECTS c = CashInventory PANEL
    PROPERTIES(c) nameNumerator, number, series, date, time,
                  nameOperation, nameDepartmentStore ON CHANGE changeDepartmentStoreCompany(c), 
                  sumBalance, sum,
                  note

    EDIT CashInventory OBJECT c
;

DESIGN cashInventory {
    NEW header FIRST {
        NEW headerInfo {
            horizontal = TRUE;
            caption = 'Шапка документа';
            MOVE PROPERTY(nameNumerator(c));
            MOVE PROPERTY(number(c));
            MOVE PROPERTY(series(c));
            MOVE PROPERTY(date(c));
            MOVE PROPERTY(time(c));
        }
        NEW headerParams {
            caption = 'Параметры документа';
            MOVE PROPERTY(nameOperation(c));
            MOVE PROPERTY(nameDepartmentStore(c));
        }
        NEW headerSum {
            horizontal = TRUE;
            MOVE PROPERTY(sumBalance(c));
            MOVE PROPERTY(sum(c));
        }
        MOVE PROPERTY(note(c)) { align = STRETCH; }
    }
}

FORM cashInventories 'Инвентаризация наличных'
    OBJECTS c = CashInventory
    PROPERTIES(c) READONLY number, series, date, time, nameOperation, nameDepartmentStore, sumBalance, sum, note
    PROPERTIES(c) NEWSESSION NEW, EDIT, DELETE

    LIST CashInventory OBJECT c
;

NAVIGATOR {
    retailDashboardNavigator {
        NEW cashInventories;
    }
}


// --- Implement cash document

date(CashInventory c) += date(c);
time(CashInventory c) += time(c);
dateTime(CashInventory c) += dateTime(c);
overNumber = ABSTRACT BPSTRING[200] (CashInventory);
number(CashInventory c) += OVERRIDE overNumber(c), seriesNumber(c);
operation(CashInventory c) += operation(c);
departmentStore(CashInventory c) += departmentStore(c);
sum(CashInventory c) += sum(c) (-) sumBalance(c);
basis(CashInventory c) += STRING[100](description(c));
isPosted(CashInventory c) += c IS CashInventory;

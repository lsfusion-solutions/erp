MODULE CashBookOutcome;

REQUIRE CashBook, CashOrder,
        CashOutcome, CashOperation, Collection;

NAMESPACE CashOperation;

cashBookList = DATA CashBookList (OutcomeCashOrder) INDEXED;
numberCashBookList 'Лист кассовой книги' (OutcomeCashOrder o) = listNumber(cashBookList(o));

countOutcome 'Расход' (CashBookList l) = GROUP SUM 1 BY cashBookList(OutcomeCashOrder o) MATERIALIZED;
sumOutcome 'Расход' (CashBookList l) = GROUP SUM sumCash(OutcomeCashOrder o) BY cashBookList(o) MATERIALIZED;

filterDateFromZreport (OutcomeCashOrder c) = date(c) >= filterZReportDateFrom() OR (c IS OutcomeCashOrder AND NOT filterZReportDateFrom());
filterDateToZreport (OutcomeCashOrder c) = date(c) <= filterZReportDateTo() OR (c IS OutcomeCashOrder AND NOT filterZReportDateTo());
filterDepartmentStore (OutcomeCashOrder c) = departmentStore(c) == filterZReportDepartmentStore() OR (c IS OutcomeCashOrder AND NOT filterZReportDepartmentStore());

skipShowEdit = ABSTRACT VALUE BOOLEAN (OutcomeCashOrder);
skipShowDelete = ABSTRACT VALUE BOOLEAN (OutcomeCashOrder);

showEdit (OutcomeCashOrder l) = l IS OutcomeCashOrder AND NOT skipShowEdit(l);
showDelete (OutcomeCashOrder l) = l IS OutcomeCashOrder AND NOT skipShowDelete(l);

EXTEND FORM cashBooks
    PROPERTIES(l) READONLY sumOutcome

    OBJECTS i = OutcomeCashOrder
    PROPERTIES(i) READONLY number, series, date, time, nameLegalEntity, nameDepartmentStore, basis, nameOperation, sumCash, numberCashBookList
    PROPERTIES(i) NEWSESSION 'Внесение наличных в ККМ' = NEW[IncomeCashOperation],
                             'Инкассация' = NEW[Collection],
                             'Расход наличных' = NEW[CashOutcome],
                             EDIT SHOWIF showEdit(i), DELETE SHOWIF showDelete(i)

    FILTERS filterDateFromZreport(i),
        filterDateToZreport(i),
        filterDepartmentStore(i)
;

DESIGN cashBooks {
    tabbedPane {
        MOVE BOX(i) { caption = 'Расходные кассовые ордера'; }
    }
}

EXTEND FORM cashBooks FILTERS (accessCompany(currentUser(),departmentStore(i)) OR NOT departmentStore(i));

fill (CashBookList l) + {
    cashBookList(OutcomeCashOrder o) <- l WHERE date(o) = date(l) AND departmentStore(o) = departmentStore(l) AND NOT cashBookList(o);
}
cashBookList(OutcomeCashOrder o) += cashBookList(o);

EXTEND FORM cashBookList
    PROPERTIES GRID READONLY sumOutcome 'Расход' = sumCash[OutcomeCashOrder](d)
;
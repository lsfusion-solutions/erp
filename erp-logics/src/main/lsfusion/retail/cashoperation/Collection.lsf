MODULE Collection;

REQUIRE CashOperationCashRegister, CashOperationCashOrder;

NAMESPACE CashOperation;

CLASS Collection 'Инкассация' : OutcomeCashOrder;
TABLE collection (Collection);

is (Collection collection) = collection IS Collection;

@defineDocumentHeaderCreated(Collection);
@defineDocumentHeaderNumber(Collection, BPSTRING[2]);

@defineNumeratedDefault(Collection, 'Инкассации', 'ИН');

@defineDocumentHeaderTime(Collection);
@deriveDocumentHeaderTimePrefix(Collection, );

@defineDocumentOperationCash(Collection, 'Инкассация');

@defineDocumentHeaderDepartmentStore(collection);
INDEX departmentStore(Collection c);
@defineDocumentDialogStocks(collection, departmentStore, company, , );

@defineDocumentHeaderDescription (Collection, 'Инкассация');

bankAccount = DATA Bank.Account (Collection);
bankAccount(Collection collection) <- bankAccount(store(departmentStore(collection)))
    WHEN SET(departmentStore(collection));

employee = DATA Employee (Collection);
nameEmployee 'Кассир' (Collection c) = shortName(employee(c));

issuedTo 'Выдать' = DATA ISTRING (Collection);

sumCash 'Сумма' = DATA NUMERIC[18,4] (Collection) IN documentSum;
basis 'Основание' (Collection collection) =
    'Инкассация №' + seriesNumber(collection) + ' от ' + date(collection) IN documentPrm;

date(Collection collection) += date(collection);
time(Collection collection) += time(collection);
dateTime(Collection collection) += dateTime(collection);
overNumber = ABSTRACT BPSTRING[200] (Collection);
number(Collection o) += OVERRIDE overNumber(o), seriesNumber(o);
operation(Collection collection) += operation(collection);
departmentStore(Collection collection) += departmentStore(collection);
employee(Collection collection) += employee(collection);
basis(Collection collection) += STRING[100](basis(collection));
isPosted(Collection collection) += collection IS Collection;

issuedTo(Collection o) += issuedTo(o);
sumCash(Collection collection) += sumCash(collection);

sumCashForCollection 'Сумма инкассации' = DATA LOCAL NUMERIC[18,4] (DepartmentStore);

FORM addCollection 'Инкассация'

    OBJECTS dt = DATE PANEL
    PROPERTIES(dt) date = VALUE

    OBJECTS t = TIME PANEL
    PROPERTIES(t) time = VALUE

    OBJECTS d = DepartmentStore PANEL
    PROPERTIES(d) name SELECTOR, sumCashForCollection
;

DESIGN addCollection {
    NEW topContainer {
        NEW firstCase {
            type = CONTAINERH;
            NEW dateTimeContainer{
                caption = 'Дата/время';
                MOVE PROPERTY(date);
                MOVE PROPERTY(time);
            }
            MOVE BOX(d);
        }
    }
    MOVE TOOLBARBOX;
}

FORM collection 'Инкассация'
    OBJECTS c = Collection PANEL
    PROPERTIES(c) nameNumerator, number, series, date, time, 
                  nameOperation, nameDepartmentStore ON CHANGE changeDepartmentStoreCompany(c), 
                  nameEmployee, issuedTo,
                  sumCash

    EDIT Collection OBJECT c
;

DESIGN collection {
    NEW topContainer{
        NEW firstCase{
            type = CONTAINERH;
            caption = 'Шапка документа';
            MOVE PROPERTY(nameNumerator(c));
            MOVE PROPERTY(number(c));
            MOVE PROPERTY(series(c));
            MOVE PROPERTY(date(c));
            MOVE PROPERTY(time(c));
        }
        NEW secondCase{
            caption = 'Параметры документа';
            MOVE PROPERTY(nameOperation(c));
            MOVE PROPERTY(nameDepartmentStore(c));
            MOVE PROPERTY(nameEmployee(c));
            MOVE PROPERTY(issuedTo(c));
        }
        NEW thirdCase {
            caption = 'Суммы';
            MOVE PROPERTY(sumCash(c));
        }
    }
    MOVE TOOLBARBOX;
}

addCollectionForm 'Инкассация' ()  { 
    DIALOG addCollection OBJECTS dt INPUT, t INPUT DO {
        FOR sumCashForCollection(DepartmentStore departmentStore) DO {
            NEW c = Collection {
                departmentStore(c) <- departmentStore;
                date(c) <- dt;
                time(c) <- t;
                sumCash(c) <- sumCashForCollection(departmentStore);
                generateSeriesNumber(c);
            }
        }
        apply();
    }
} TOOLBAR;

FORM centralCashRegister 'Центральная касса'

    OBJECTS interval = (dFrom = DATE, dTo = DATE) PANEL
    PROPERTIES objFrom = VALUE(dFrom), objTo = VALUE(dTo)

    OBJECTS d = DepartmentStore PANEL
    PROPERTIES(d) name SELECTOR
    PROPERTIES(d) READONLY curSumCash

    OBJECTS i = IncomeCashOrder
    PROPERTIES(i) READONLY objectClassName, number, date, time, nameDepartmentStore,
                           numberCashRegister[OutcomeCashOperation] GRID, sumCash, basis, nameOperation, isPrinted, idEmployee, nameEmployee
    PROPERTIES(i) READONLY isComplete 
    PROPERTIES addOutcomeCashOperationForm(d) DRAW i TOOLBAR
    PROPERTIES eoc = EDIT(i) SHOWIF is(i)
    PROPERTIES(i) NEWSESSION DELETE 
    ORDERS date(i), time(i)

    FILTERGROUP dep  
        FILTER 'По подразделению' departmentStore(i) == d DEFAULT 
    FILTERS date(i) >= dFrom,
            date(i) <= dTo,
            accessCompany(currentUser(), departmentStore(i))

    OBJECTS o = OutcomeCashOrder
    PROPERTIES(o) READONLY number, date, time, nameDepartmentStore,
                           numberCashRegister[IncomeCashOperation] GRID, sumCash, basis, nameOperation, isPrinted, idEmployee, nameEmployee
    PROPERTIES addIncomeCashOperationForm(d) DRAW o TOOLBAR
    PROPERTIES addCollectionForm() DRAW o TOOLBAR
    PROPERTIES(o) NEWSESSION EDIT, DELETE 
    ORDERS date(o), time(o)
    FILTERGROUP dep2  
        FILTER 'По подразделению' departmentStore(o) == d DEFAULT     
    FILTERS date(o) >= dFrom,
            date(o) <= dTo,
            isPosted(i),
            isPosted(o),
            accessCompany(currentUser(), departmentStore(o))
;
@extendFormFilterStockAccess(d, centralCashRegister);

DESIGN centralCashRegister {

    NEW top {
        type = CONTAINERH;
        MOVE BOX(interval) {
            caption = 'Период';
            type = CONTAINERH;
            PROPERTY(objFrom) {
                caption = 'Дата (с)';
            }
            PROPERTY(objTo) {
                caption = 'Дата (по)';
            }
        }
        MOVE BOX(d);
    }

    NEW center {
        fill = 1;
        type = SPLITV;
        MOVE BOX(i){
            caption = 'ПКО';
        }
        MOVE BOX(o){
            caption = 'РКО';
        }
    }
    MOVE TOOLBARBOX;
}

NAVIGATOR {
    retailDashboardNavigator {
        NEW centralCashRegister;
    }
}
MODULE CashIncome;

REQUIRE CashOrder;

NAMESPACE CashOperation;

CLASS CashIncome 'Приход наличных' : IncomeCashOrder;
TABLE cashIncome (CashIncome);

@defineDocumentHeaderCreated(CashIncome);
@defineDocumentHeaderNumber(CashIncome, BPSTRING[3]);

@defineNumeratedDefault(CashIncome, 'Приходы наличных', 'ПН');

@defineDocumentHeaderTime(CashIncome);
@deriveDocumentHeaderTimePrefix(CashIncome, );

@defineDocumentOperationCash(CashIncome, 'Приход наличных');

@defineDocumentHeaderDepartmentStore(cashIncome);
@defineDocumentDialogStocks(cashIncome, departmentStore, company, , );

@defineDocumentHeaderDescription (CashIncome, 'Приход наличных');

@defineDocumentHeaderNote(CashIncome);

sumCash 'Сумма' = DATA NUMERIC[18,4] (CashIncome) IN documentSum;
basis 'Основание' (CashIncome cashIncome) =
    'Приход наличных №' + seriesNumber(cashIncome) + ' от ' + date(cashIncome) IN documentPrm;

FORM cashIncome 'Приход наличных'
    OBJECTS c = CashIncome PANEL
    PROPERTIES(c) nameNumerator, number, series, date, time,
                  nameOperation, nameDepartmentStore ON CHANGE changeDepartmentStoreCompany(c), sumCash, 
                  note

    EDIT CashIncome OBJECT c
;

DESIGN cashIncome {
    NEW header FIRST {
        NEW headerInfo {
            horizontal = TRUE;
            caption = 'Шапка документа';
            MOVE PROPERTY(nameNumerator(c));
            MOVE PROPERTY(number(c));
            MOVE PROPERTY(series(c));
            MOVE PROPERTY(date(c));
            MOVE PROPERTY(time(c));
        }
        NEW headerParams {
            caption = 'Параметры документа';
            MOVE PROPERTY(nameOperation(c));
            MOVE PROPERTY(nameDepartmentStore(c));
            MOVE PROPERTY(sumCash(c));
        }
        MOVE PROPERTY(note(c)) { align = STRETCH; }
    }
}


// --- Implement cash document

date(CashIncome c) += date(c);
time(CashIncome c) += time(c);
dateTime(CashIncome c) += dateTime(c);
overNumber = ABSTRACT BPSTRING[200] (CashIncome);
number(CashIncome c) += OVERRIDE overNumber(c), seriesNumber(c);
operation(CashIncome c) += operation(c);
departmentStore(CashIncome c) += departmentStore(c);
sumCash(CashIncome c) += sumCash(c);
basis(CashIncome c) += STRING[100](description(c));
isPosted(CashIncome c) += c IS CashIncome;

MODULE CashBookIncome;

REQUIRE CashBook, CashOrder,
        CashIncome, CashOperation;

NAMESPACE CashOperation;

cashBookList = DATA CashBookList (IncomeCashOrder) INDEXED;
numberCashBookList 'Лист кассовой книги' (IncomeCashOrder o) = listNumber(cashBookList(o));

countIncome 'Приход' (CashBookList l) = GROUP SUM 1 BY cashBookList(IncomeCashOrder o) MATERIALIZED;
sumIncome 'Приход' (CashBookList l) = GROUP SUM sumCash(IncomeCashOrder o) BY cashBookList(o) MATERIALIZED;

filterDateFromZreport (IncomeCashOrder c) = date(c) >= filterZReportDateFrom() OR (c IS IncomeCashOrder AND NOT filterZReportDateFrom());
filterDateToZreport (IncomeCashOrder c) = date(c) <= filterZReportDateTo() OR (c IS IncomeCashOrder AND NOT filterZReportDateTo());
filterDepartmentStore (IncomeCashOrder c) = departmentStore(c) == filterZReportDepartmentStore() OR (c IS IncomeCashOrder AND NOT filterZReportDepartmentStore());

skipShowEdit = ABSTRACT VALUE BOOLEAN (IncomeCashOrder);
skipShowDelete = ABSTRACT VALUE BOOLEAN (IncomeCashOrder);

showEdit (IncomeCashOrder l) = l IS IncomeCashOrder AND NOT skipShowEdit(l);
showDelete (IncomeCashOrder l) = l IS IncomeCashOrder AND NOT skipShowDelete(l);

EXTEND FORM cashBooks
    PROPERTIES(l) READONLY sumIncome
    
    OBJECTS o = IncomeCashOrder
    PROPERTIES(o) READONLY number, series, date, time, nameLegalEntity, nameDepartmentStore, basis, nameOperation, sumCash, numberCashBookList
    PROPERTIES(o) NEWSESSION 'Изъятие наличных из ККМ' = NEW[OutcomeCashOperation], 'Приход наличных' = NEW[CashIncome], EDIT SHOWIF showEdit(o), DELETE SHOWIF showDelete(o)

    FILTERS filterDateFromZreport(o),
        filterDateToZreport(o),
        filterDepartmentStore(o)
;

DESIGN cashBooks {
    tabbedPane {
        MOVE BOX(o) { caption = 'Приходные кассовые ордера'; }
    }
}

EXTEND FORM cashBooks FILTERS (accessCompany(currentUser(),departmentStore(o)) OR NOT departmentStore(o));

fill (CashBookList l) + {
    cashBookList(IncomeCashOrder o) <- l WHERE date(o) = date(l) AND departmentStore(o) = departmentStore(l) AND NOT cashBookList(o);
}
cashBookList(IncomeCashOrder o) += cashBookList(o);

EXTEND FORM cashBookList
    PROPERTIES GRID READONLY sumIncome 'Приход' = sumCash[IncomeCashOrder](d) 
;
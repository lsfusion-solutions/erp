MODULE CashCommittee;

REQUIRE EmployeeStock, Store;
NAMESPACE CashOperation;

// ----------------------------------- Комиссия ------------------------------------------ //

CLASS CashCommittee 'Комиссия ПКО/РКО' : Committee;

name 'Наименование' = DATA ISTRING[150](CashCommittee);

name(CashCommittee committee) += name(committee) IF committee IS CashCommittee;


//@defineCommitteeEmployee(committee, chairman, 'Председатель');
@defineCommitteeEmployeePrefix(cashCommittee, cashierMan, 'Кассир');
@defineCommitteeEmployeePrefix(cashCommittee, fromMan, 'Принято от');
@defineCommitteeEmployeePrefix(cashCommittee, toMan, 'Выдать');
@defineCommitteeEmployeePrefix(cashCommittee, accountantMan, 'Бухгалтер');

// ----------------------------------- Комиссия по умолчанию для отдела ------------------------------------------ //

cashCommittee  = DATA CashCommittee (DepartmentStore);
nameCashCommittee 'Комиссия для ПКО/РКО' (DepartmentStore departmentStore) = name(cashCommittee(departmentStore)) IN committee CHARWIDTH 20;
isDefault 'По умолчанию' (CashCommittee cashCommittee, DepartmentStore departmentStore) = cashCommittee(departmentStore) == cashCommittee;
CONSTRAINT cashCommittee(DepartmentStore departmentStore) AND NOT in(cashCommittee(departmentStore), departmentStore)
    CHECKED BY cashCommittee[DepartmentStore] MESSAGE 'Для отдела выбрана комиссия, которая для него не действует';

EXTEND FORM departmentStore PROPERTIES nameCashCommittee(d);

DESIGN departmentStore {
    headerExtraParams{
        MOVE PROPERTY(nameCashCommittee(d));
    }
}

// ----------------------------------- Формы для комиссий для реестра цен ------------------------------------------ //

FORM cashCommittee 'Комиссия для ПКО/РКО'
    OBJECTS c=CashCommittee PANEL
    PROPERTIES(c)      name, nameFromMan, nameCashierMan, nameAccountantMan, nameToMan
                       

    TREE treeStore a =BPSTRING[3], t=ChainStores, st=StoreType, s=Store
    PROPERTIES         READONLY VALUE(a), name(t), name(st), name(s)
    FILTERS            stringEqualsAll(a), in (t, st), in(st, s)

    OBJECTS dep=DepartmentStore
    PROPERTIES(dep)    READONLY depName = name, id SHOWIF showIDs()
    PROPERTIES(c, dep) in, isDefault
    FILTERS            in(t, st, s, dep)
    ORDERS depName

    FILTERGROUP filters5
        FILTER 'Показывать отделы только для данной комиссии' in(c, dep) 'F9'

    EDIT CashCommittee OBJECT c
;
@extendFormFilterStockAccess(dep, cashCommittee);
@extendFormFilterStockGroupAccess(t, cashCommittee);
@extendFormFilterStockGroupAccess(st, cashCommittee);
@extendFormFilterStockGroupAccess(s, cashCommittee);

DESIGN cashCommittee {
    BOX {
        size = (1024, 768);

        OBJECTS {
            NEW stocks AFTER BOX(c) {
                fill = 1;
                type = SPLITH;
    
                MOVE BOX(TREE treeStore);
                MOVE BOX(dep) {
                    fill = 3;
                }
            }
    
            NEW managers AFTER stocks {
                caption = 'Состав комиссии';
                type = COLUMNS;
                columns = 3;
                MOVE PROPERTY (nameFromMan(c)); 
                MOVE PROPERTY (nameCashierMan(c));
                MOVE PROPERTY (nameAccountantMan(c));
                MOVE PROPERTY (nameToMan(c));
            }
        }
    }
}

FORM cashCommitteeDialog 'Комиссии для ПКО/РКО'
    OBJECTS r=CashCommittee
    PROPERTIES(r)      READONLY name, nameStocks, nameFromMan, nameCashierMan, nameAccountantMan, nameToMan
    PROPERTIES(r)      NEWSESSION NEW, EDIT

    LIST CashCommittee OBJECT r
;

META defineDocumentHeaderCashCommittee(object, stockClass, prefix)
    prefix###cashCommittee (object) = DATA CashCommittee(###object);

    name###prefix##CashCommittee 'Комиссия' (###object object) = name(prefix###cashCommittee(object)) IN documentPrm CHARWIDTH 10;
    prefix###cashCommittee (###object object) <- cashCommittee(stockClass(object))
        WHEN CHANGED(stockClass(object));

    CONSTRAINT prefix###cashCommittee (###object object) AND NOT in(prefix###cashCommittee(object), stockClass(object))
        CHECKED BY prefix###cashCommittee[###object] MESSAGE 'Для документа выбрана комиссия, которая не действует для склада документа';
        
    data###prefix##FromMan 'Принято от' (object) = DATA Employee (###object);
    data###prefix##CashierMan 'Кассир' (object) = DATA Employee (###object);
    data###prefix##ToMan 'Выдать' (object) = DATA Employee (###object);
    data###prefix##AccountantMan 'Бухгалтер' (object) = DATA Employee (###object);
  
          
    CONSTRAINT data###prefix##FromMan(###object object) AND NOT in(stockClass(object), data###prefix##FromMan(object)) AND NOT allowStockEmployee()
        CHECKED BY data###prefix##FromMan[###object] MESSAGE 'В качестве "Принято от" выбран сотрудник, который не является сотрудником склада документа.';                  
    CONSTRAINT data###prefix##CashierMan(###object object) AND NOT in(stockClass(object), data###prefix##CashierMan(object)) AND NOT allowStockEmployee()
        CHECKED BY data###prefix##CashierMan[###object] MESSAGE 'В качестве "Кассир" выбран сотрудник, который не является сотрудником склада документа.';                        
    CONSTRAINT data###prefix##ToMan(###object object) AND NOT in(stockClass(object), data###prefix##ToMan(object)) AND NOT allowStockEmployee()
        CHECKED BY data###prefix##ToMan[###object] MESSAGE 'В качестве "Выдать" выбран сотрудник, который не является сотрудником склада документа.';                         
    CONSTRAINT data###prefix##AccountantMan(###object object) AND NOT in(stockClass(object), data###prefix##AccountantMan(object)) AND NOT allowStockEmployee()
        CHECKED BY data###prefix##AccountantMan[###object] MESSAGE 'В качестве "Бухгалтер" выбран сотрудник, который не является сотрудником склада документа.';                         

    
    prefix###fromMan 'Принято от' (###object object) = OVERRIDE data###prefix##FromMan(object), fromMan(prefix###cashCommittee(object));
    prefix###cashierMan 'Кассир' (###object object) = OVERRIDE data###prefix##CashierMan(object), cashierMan(prefix###cashCommittee(object));
    prefix###toMan 'Выдать' (###object object) = OVERRIDE data###prefix##ToMan(object), toMan(prefix###cashCommittee(object));
    prefix###accountantMan 'Бухгалтер' (###object object) = OVERRIDE data###prefix##AccountantMan(object), accountantMan(prefix###cashCommittee(object));

    
    name###prefix##FromMan 'Принято от' (###object object) = name[Contact](prefix###fromMan(object)) IN documentPrm CHARWIDTH 10;
    name###prefix##CashierMan 'Кассир' (###object object) = name[Contact](prefix###cashierMan(object)) IN documentPrm CHARWIDTH 10;
    name###prefix##ToMan 'Выдать' (###object object) = name[Contact](prefix###toMan(object)) IN documentPrm CHARWIDTH 10;
    name###prefix##AccountantMan 'Бухгалтер' (###object object) = name[Contact](prefix###accountantMan(object)) IN documentPrm CHARWIDTH 10;
 
       
    positionName###prefix##FromMan 'Принято от' (###object object) = positionName(prefix###fromMan(object)) IN documentPrm;
    positionName###prefix##CashierMan 'Кассир' (###object object) = positionName(prefix###cashierMan(object)) IN documentPrm;
    positionName###prefix##ToMan 'Выдать' (###object object) = positionName(prefix###toMan(object)) IN documentPrm;
    positionName###prefix##AccountantMan 'Бухгалтер' (###object object) = positionName(prefix###accountantMan(object)) IN documentPrm;


    positionShortName###prefix##FromMan 'Принято от' (###object object) = positionShortName(prefix###fromMan(object)) IN documentPrm;
    positionShortName###prefix##CashierMan 'Кассир' (###object object) = positionShortName(prefix###cashierMan(object)) IN documentPrm;
    positionShortName###prefix##ToMan 'Выдать' (###object object) = positionShortName(prefix###toMan(object)) IN documentPrm;
    positionShortName###prefix##AccountantMan 'Бухгалтер' (###object object) = positionShortName(prefix###accountantMan(object)) IN documentPrm;

END
META defineDocumentHeaderAbstractCashCommittee(object, stockClass, prefix)
    prefix###cashCommittee (object) = ABSTRACT CashCommittee(###object) MATERIALIZED;

    name###prefix##CashCommittee 'Комиссия для акта расценки' (###object object) = name(prefix###cashCommittee(object)) IN documentPrm CHARWIDTH 10;

    data###prefix##FromMan 'Принято от' (object) = ABSTRACT Employee (###object);
    data###prefix##CashierMan 'Кассир' (object) = ABSTRACT Employee (###object);
    data###prefix##ToMan 'Выдать' (object) = ABSTRACT Employee (###object);
    data###prefix##AccountantMan 'Бухгалтер' (object) = ABSTRACT Employee (###object);


    prefix###fromMan 'Принято от' (###object object) = OVERRIDE data###prefix##FromMan(object), fromMan(prefix###cashCommittee(object));
    prefix###cashierMan 'Кассир' (###object object) = OVERRIDE data###prefix##CashierMan(object), cashierMan(prefix###cashCommittee(object));
    prefix###toMan 'Выдать' (###object object) = OVERRIDE data###prefix##ToMan(object), toMan(prefix###cashCommittee(object));
    prefix###accountantMan 'Бухгалтер' (###object object) = OVERRIDE data###prefix##AccountantMan(object), accountantMan(prefix###cashCommittee(object));

    
    name###prefix##FromMan 'Принято от' (###object object) = name[Contact](prefix###fromMan(object)) IN documentPrm;
    name###prefix##CashierMan 'Кассир' (###object object) = name[Contact](prefix###cashierMan(object)) IN documentPrm;
    name###prefix##ToMan 'Выдать' (###object object) = name[Contact](prefix###toMan(object)) IN documentPrm;
    name###prefix##AccountantMan 'Бухгалтер' (###object object) = name[Contact](prefix###accountantMan(object)) IN documentPrm;

       
    positionName###prefix##FromMan 'Принято от' (###object object) = positionName(prefix###fromMan(object)) IN documentPrm;
    positionName###prefix##CashierMan 'Кассир' (###object object) = positionName(prefix###cashierMan(object)) IN documentPrm;
    positionName###prefix##ToMan 'Выдать' (###object object) = positionName(prefix###toMan(object)) IN documentPrm;
    positionName###prefix##AccountantMan 'Бухгалтер' (###object object) = positionName(prefix###accountantMan(object)) IN documentPrm;
     

    positionShortName###prefix##FromMan 'Принято от' (###object object) = positionShortName(prefix###fromMan(object)) IN documentPrm;
    positionShortName###prefix##CashierMan 'Кассир' (###object object) = positionShortName(prefix###cashierMan(object)) IN documentPrm;
    positionShortName###prefix##ToMan 'Выдать' (###object object) = positionShortName(prefix###toMan(object)) IN documentPrm;
    positionShortName###prefix##AccountantMan 'Бухгалтер' (###object object) = positionShortName(prefix###accountantMan(object)) IN documentPrm;

END

META defineDocumentInterfaceHeaderCashCommittee (object, stockClass, prefix)
    @defineDocumentHeaderAbstractCashCommittee (object, stockClass, prefix);
    @defineDocumentHeaderCashCommittee (user###object, stockClass, prefix);
    prefix###cashCommittee (User###object object) += prefix###cashCommittee(object);
    
    data###prefix##FromMan (User###object object) += data###prefix##FromMan(object);
    data###prefix##CashierMan (User###object object) += data###prefix##CashierMan(object);
    data###prefix##ToMan(User###object object) += data###prefix##ToMan(object);
    data###prefix##AccountantMan (User###object object) += data###prefix##AccountantMan(object);

    
END
META defineDocumentInterfaceHeaderCashCommittee (object, stockClass)
    @defineDocumentInterfaceHeaderCashCommittee (object, stockClass, );
END
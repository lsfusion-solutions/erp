MODULE CashBook;

REQUIRE CashOrder;

NAMESPACE CashOperation;

// list

CLASS CashBookList 'Лист кассовой книги';
TABLE cashBookList (CashBookList);

@defineDocumentHeaderCreated(CashBookList);

listNumber 'Номер' = DATA INTEGER (CashBookList) IN numbered CHARWIDTH 8;

@defineDocumentHeaderTime(CashBookList);
@deriveDocumentHeaderTimePrefix(CashBookList, );
year (CashBookList l) = extractYear(date(l));

@defineDocumentHeaderDepartmentStore(cashBookList);
@defineDocumentDialogStocks(cashBookList, departmentStore, company, , );

maxNumber (DepartmentStore d, INTEGER y) = GROUP MAX listNumber(CashBookList l) BY departmentStore(l), year(l);
prevMaxNumber (DepartmentStore d, INTEGER y) = PREV(maxNumber(d, y));
WHEN LOCAL (SETCHANGED(departmentStore(CashBookList l)) OR SETCHANGED(year(l))) AND NOT CHANGED(listNumber(l)) DO
    listNumber(l) <- maxNumber(departmentStore(l), year(l)) (+) 1;    

@defineDocumentHeaderNote(CashBookList);

FORM cashBookList 'Лист кассовой книги'
    OBJECTS c = CashBookList PANEL
    PROPERTIES(c) listNumber, date, time,
                  nameDepartmentStore ON CHANGE changeDepartmentStoreCompany(c),
                  note

    EDIT CashBookList OBJECT c
;

DESIGN cashBookList {
    NEW header FIRST {
        NEW headerInfo {
            type = CONTAINERH;
            caption = 'Шапка документа';
            MOVE PROPERTY(listNumber(c));
            MOVE PROPERTY(date(c));
            MOVE PROPERTY(time(c));
            MOVE PROPERTY(nameDepartmentStore(c));
        }
        MOVE PROPERTY(note(c)) { align = STRETCH; }
    }
}

FORM cashBooks 'Кассовые книги'
    OBJECTS l = CashBookList
    PROPERTIES(l) READONLY listNumber, date, time, nameDepartmentStore, note
    PROPERTIES(l) NEWSESSION NEW, EDIT, DELETE
;

DESIGN cashBooks {
    NEW tabbedPane FIRST {
        fill = 1;
        type = TABBED;
        MOVE BOX(l) {
            caption = 'Листы кассовой книги';
        }
        
    }
}

NAVIGATOR {
    retailDashboardNavigator {
        NEW cashBooks;
    }
}

// cash document
fill 'Заполнить' ABSTRACT LIST (CashBookList);

cashBookList = ABSTRACT CashBookList (CashDocument);
EXTEND FORM cashBookList
    OBJECTS d = CashDocument
    PROPERTIES(d) READONLY number, series, date, time, nameLegalEntity, nameDepartmentStore, basis, nameOperation
    PROPERTIES(d) NEWSESSION EDIT
    PROPERTIES fill(c) DRAW d TOOLBAR
    FILTERS cashBookList(d) = c
;

endDateTime (CashBookList l) = GROUP MAX dateTime(CashDocument d) BY cashBookList(d) MATERIALIZED;
endBalance 'Конечный остаток' (CashBookList l) = balanceA(departmentStore(l), endDateTime(l));

EXTEND FORM cashBooks
    PROPERTIES(l) READONLY endBalance
;
EXTEND FORM cashBookList
    PROPERTIES(c) READONLY endBalance DRAW d TOOLBAR 
;
MODULE CashBook;

REQUIRE CashOrder;

NAMESPACE CashOperation;

// list

CLASS CashBookList 'Лист кассовой книги';
TABLE cashBookList (CashBookList);

@defineDocumentHeaderCreated(CashBookList);

listNumber 'Номер' = DATA INTEGER (CashBookList) IN numbered CHARWIDTH 8;

@defineDocumentHeaderTime(CashBookList);
@deriveDocumentHeaderTimePrefix(CashBookList, );
year (CashBookList l) = extractYear(date(l));

@defineDocumentHeaderDepartmentStore(cashBookList);
@defineDocumentDialogStocks(cashBookList, departmentStore, company, , );

maxNumber (DepartmentStore d, INTEGER y, CashBookList bl) = GROUP MAX listNumber(CashBookList l) IF l != bl BY departmentStore(l), year(l);
prevMaxNumber (DepartmentStore d, INTEGER y, CashBookList l) = PREV(maxNumber(d, y, l));
WHEN LOCAL (SETCHANGED(departmentStore(CashBookList l)) OR SETCHANGED(year(l))) AND departmentStore(l) AND NOT CHANGED(listNumber(l)) DO
    listNumber(l) <- prevMaxNumber(departmentStore(l), year(l), l) (+) 1;

employee 'Кассир' = DATA Employee (CashBookList);
nameEmployee 'Кассир' (CashBookList c) = shortName(employee(c)) CHARWIDTH 20;
employee(CashBookList l) <- currentUser() WHEN SET(l IS CashBookList);

@defineDocumentHeaderNote(CashBookList);

FORM cashBookList 'Лист кассовой книги'
    OBJECTS c = CashBookList PANEL
    PROPERTIES(c) listNumber, date, time,
                  nameDepartmentStore ON CHANGE changeDepartmentStoreCompany(c), nameEmployee,
                  note

    EDIT CashBookList OBJECT c
;

DESIGN cashBookList {
    NEW header FIRST {
        NEW headerInfo {
            horizontal = TRUE;
            caption = 'Шапка документа';
            MOVE PROPERTY(listNumber(c));
            MOVE PROPERTY(date(c));
            MOVE PROPERTY(time(c));
        }
        NEW params {
            horizontal = TRUE;
            caption = 'Параметры документа';
            MOVE PROPERTY(nameDepartmentStore(c));
            MOVE PROPERTY(nameEmployee(c));
        }
        MOVE PROPERTY(note(c)) { align = STRETCH; }
    }
}

filterZReportDateFrom 'Дата с' = DATA LOCAL DATE ();
filterDateFromZreport (CashBookList c) = date(c) >= filterZReportDateFrom() OR (c IS CashBookList AND NOT filterZReportDateFrom());

filterZReportDateTo 'Дата по' = DATA LOCAL DATE ();
filterDateToZreport (CashBookList c) = date(c) <= filterZReportDateTo() OR (c IS CashBookList AND NOT filterZReportDateTo());

filterZReportDepartmentStore  = DATA LOCAL DepartmentStore ();
nameFilterZReportDepartmentStore 'Отдел магазина' = name(filterZReportDepartmentStore()) CHARWIDTH 15;
filterDepartmentStore (CashBookList c) = departmentStore(c) == filterZReportDepartmentStore() OR (c IS CashBookList AND NOT filterZReportDepartmentStore());

FORM cashBooks 'Кассовые книги'
    
    PROPERTIES ()  filterZReportDateFrom, filterZReportDateTo, nameFilterZReportDepartmentStore
    
    OBJECTS l = CashBookList
    PROPERTIES(l) READONLY listNumber, date, time, nameDepartmentStore, nameEmployee, note
    PROPERTIES(l) NEWSESSION NEW, EDIT, DELETE

    FILTERS filterDateFromZreport(l),
        filterDateToZreport(l),
        filterDepartmentStore(l)
;

DESIGN cashBooks {
    NEW filters FIRST {
        alignment = STRETCH;
        caption = 'Фильтры';
        horizontal = TRUE;
        MOVE PROPERTY(filterZReportDateFrom());
        MOVE PROPERTY(filterZReportDateTo());
        MOVE PROPERTY(nameFilterZReportDepartmentStore());
    }
    NEW tabbedPane AFTER filters {
        fill = 1;
        tabbed = TRUE;
        MOVE BOX(l) {
            caption = 'Листы кассовой книги';
        }
        
    }
}

@extendFormFilterAccessStockNoNull(CashBookList, l, cashBooks, departmentStore, company);

NAVIGATOR {
    retailDashboardNavigator {
        NEW cashBooks;
    }
}

// cash document
fill 'Заполнить' ABSTRACT LIST (CashBookList);

cashBookList = ABSTRACT CashBookList (CashDocument);
EXTEND FORM cashBookList
    OBJECTS d = CashDocument
    PROPERTIES(d) READONLY number, series, date, time, nameLegalEntity, nameDepartmentStore, basis, nameOperation
    PROPERTIES(d) NEWSESSION EDIT
    PROPERTIES fill(c) DRAW d TOOLBAR
    FILTERS cashBookList(d) = c
;

startDateTime (CashBookList l) = GROUP MIN dateTime(CashDocument d) BY cashBookList(d) MATERIALIZED;
startBalance 'Начальный остаток' (CashBookList l) = balanceB(departmentStore(l), startDateTime(l));

endDateTime (CashBookList l) = GROUP MAX dateTime(CashDocument d) BY cashBookList(d) MATERIALIZED;
endBalance 'Конечный остаток' (CashBookList l) = balanceA(departmentStore(l), endDateTime(l));

EXTEND FORM cashBooks
    PROPERTIES(l) READONLY startBalance, endBalance
;
EXTEND FORM cashBookList
    PROPERTIES(c) READONLY DRAW d TOOLBAR startBalance, endBalance 
;
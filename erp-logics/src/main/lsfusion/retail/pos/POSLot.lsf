MODULE POSLot;

REQUIRE POS, ZReportLot, SkuLedgerLot;

NAMESPACE POS;

EXTEND FORM POS
    PROPERTIES(d) READONLY idLot SHOWIF lotType(sku(d)) BACKGROUND RGB(255,224,224) IF NOT lot(d) PANEL
;

DESIGN POS {
    PANEL(d) {
        NEW batchContainer AFTER PROPERTY(nameSkuPanel) {
            type = CONTAINERH;
            alignment = STRETCH;
            MOVE PROPERTY(nameBatchPanel);
            MOVE PROPERTY(idLot(d)) {
                panelCaptionVertical = TRUE; focusable = FALSE; font = 'bold 24';
            }        
        }
    }
}

overChangeBarcodeSale(STRING[200] b, Receipt r) + {
    IF length(initialBarcode()) > 20 THEN {
        FOR Lot l = lot(barcodeToId(initialBarcode())) AND Sku s = sku(l) DO {
            IF NOT currentBalance(l, departmentStore(r)) > 0 THEN {
                MESSAGE 'Контрольная марка отсутствует на остатках. Ее продажа невозможна.';
            } ELSE {
                NEW d = ReceiptSaleDetail {
                    receipt(d) <- r;
                    idBarcode(d) <- b;
                    sku(d) <- s;
                    quantity(d) <- 1.0;
                    price(d) <- currentPOSPrice(s, r);
                    lot(d) <- l;
                    IF explicitBatchLedger(r) THEN {
                        IF batch(l) THEN
                            batch(d) <- batch(l);
                        ELSE
                            setSaleDetailBatch(d, r);
                    }
                }
            }
            consumedChangeBarcodeSaleReceipt() <- TRUE;
        } 
    } ELSE {
        FOR Sku sk = skuBarcode(b, currentDate()) AND DepartmentStore dt = departmentStore(r) DO {
            IF explicitLot(sk) OR (prevCurrentBalance(sk, dt) = prevCurrentBalanceLot(sk, dt)) THEN {
                MESSAGE 'Товар подлежит обязательной маркировке. Просканируйте контрольную марку вместо штрих-кода'; 
                consumedChangeBarcodeSaleReceipt() <- TRUE;
            }
        }
    }
}

WHEN LOCAL FORMS POS CHANGED(receiptSaleDetail(ReceiptReturnDetail d)) AND NOT CHANGED (lot(d)) DO {
    lot(d) <- lot(receiptSaleDetail(d));
}

MODULE POSConsultant;

REQUIRE POS;


barcode 'Штрихкод' = DATA VARSTRING[30] (Employee) CHARWIDTH 15 NOFLEX;
isConsultant 'Продавец' = DATA BOOLEAN (Employee);

employeeBarcode (string) =
    GROUP AGGR Employee e WHERE e IS Employee
        BY barcode(e);

EXTEND FORM employee
    PROPERTIES (e) isConsultant, barcode
;
DESIGN employee{
    personal {
        type = COLUMNS;
        columns = 2;
        MOVE PROPERTY (barcode(e));
        MOVE PROPERTY (isConsultant(e));
    }
}

EXTEND FORM employees
    PROPERTIES (e) READONLYIF isReadonly() barcode
;

FORM consultants 'Продавцы'
    OBJECTS d = DepartmentStore

    OBJECTS e = Employee
    PROPERTIES(e) READONLY lastName, firstName, nameMainRole, nameStock, barcode
    FILTERS isConsultant(e), in(d, e), stock(e)==d
;

isConsultantReceipt 'Учет личных продаж' = DATA BOOLEAN (Store);
isConsultantReceipt 'Учет личных продаж' = isConsultantReceipt(store(DepartmentStore d));
isConsultantReceipt = isConsultantReceipt(departmentStore(Receipt r));

EXTEND FORM store
    PROPERTIES (s) isConsultantReceipt
;
DESIGN store {
    headerExtraParams {
        MOVE PROPERTY (isConsultantReceipt(s));
    }
}

consultant = DATA Employee (Receipt);
nameConsultant 'Продавец' (Receipt r) = name[Contact](consultant(r)) CHARWIDTH 20;

CONSTRAINT consultant(Receipt r) AND NOT in(departmentStore(r), consultant(r))
    CHECKED BY consultant[Receipt] MESSAGE 'В качестве продавца в чеке выбран сотрудник, которые не является работником данного отдела';
CONSTRAINT consultant(Receipt r) AND NOT isConsultant(consultant(r))
    CHECKED BY consultant[Receipt] MESSAGE 'В качестве продавца в чеке выбран сотрудник, у которого не задан признак "Продавец".';    
    
        
consultant = ABSTRACT Employee (ReceiptDetail);
nameConsultant 'Продавец' (ReceiptDetail d) = name[Contact](consultant(d)) CHARWIDTH 20;

consultant = DATA Employee (ReceiptSaleDetail);

WHEN LOCAL FORMS POS SET (ReceiptSaleDetail d IS ReceiptSaleDetail) AND NOT CHANGED (consultant(d)) DO {
    consultant(d) <- consultant(receipt(d));
};

CONSTRAINT consultant(ReceiptSaleDetail r) AND NOT in(departmentStore(r), consultant(r))
    CHECKED BY consultant[ReceiptSaleDetail] MESSAGE 'В качестве продавца в строке чеке выбран сотрудник, которые не является работником данного отдела';
CONSTRAINT consultant(ReceiptSaleDetail r) AND NOT isConsultant(consultant(r))
    CHECKED BY consultant[ReceiptSaleDetail] MESSAGE 'В качестве продавца в строке чека выбран сотрудник, у которого не задан признак "Продавец".';  
        
consultant = DATA Employee (ReceiptReturnDetail);

WHEN LOCAL FORMS POS CHANGED (receiptSaleDetail(ReceiptReturnDetail d)) AND NOT CHANGED (consultant(d))  DO {
    consultant(d) <- consultant(receiptSaleDetail(d));
};

CONSTRAINT consultant(ReceiptReturnDetail r) AND NOT in(departmentStore(r), consultant(r))
    CHECKED BY consultant[ReceiptReturnDetail] MESSAGE 'В качестве продавца в строке чека выбран сотрудник, которые не является работником данного отдела';
CONSTRAINT consultant(ReceiptReturnDetail r) AND NOT isConsultant(consultant(r))
    CHECKED BY consultant[ReceiptReturnDetail] MESSAGE 'В качестве продавца в строке чека выбран сотрудник, у которого не задан признак "Продавец".';  
    
consultant[ReceiptDetail](ReceiptSaleDetail d) += consultant(d);
consultant[ReceiptDetail](ReceiptReturnDetail d) += consultant(d);

changeConsultant (Receipt r)  { 
    DIALOG consultants OBJECTS d = departmentStore(r), e = consultant(r) CHANGE;
}

changeConsultant (ReceiptDetail d)  { 
    DIALOG consultants OBJECTS d = departmentStore(d), e = consultant(d) INPUT NULL DO {
        IF d IS ReceiptSaleDetail THEN
            consultant[ReceiptSaleDetail](d) <- e;
        IF d IS ReceiptReturnDetail THEN
            consultant[ReceiptReturnDetail](d) <- e;
    }
}

EXTEND FORM POS
    PROPERTIES (r) SHOWIF isConsultantReceipt(r) nameConsultant ON CHANGE changeConsultant(r)
    PROPERTIES (d) SHOWIF isConsultantReceipt(r) nameConsultant ON CHANGE changeConsultant(d)
;

CONSTRAINT CHANGED(consultant(Receipt r)) AND NOT stock(consultant(r)) == departmentStore(r) AND SET (r IS Receipt)
    CHECKED BY consultant[Receipt] MESSAGE 'Продавец должен быть сотрудником магазина';
    
CONSTRAINT CHANGED(consultant(ReceiptSaleDetail d)) AND NOT stock(consultant(d)) == departmentStore(d) AND SET (d IS ReceiptSaleDetail)
    CHECKED BY consultant[ReceiptSaleDetail] MESSAGE 'Продавец должен быть сотрудником магазина';    
    
//CONSTRAINT CHANGED(consultant(ReceiptReturnDetail d)) AND NOT stock(consultant(d)) == departmentStore(d) AND SET (d IS ReceiptReturnDetail)
//    CHECKED BY consultant[ReceiptReturnDetail] MESSAGE 'Продавец должен быть сотрудником магазина';      

DESIGN POS {
    receiptDiscountRow3 {
        MOVE PROPERTY (nameConsultant(r)){ alignment = STRETCH; focusable = FALSE; font = 'bold 22'; }
    } 
}

EXTEND FORM zReport
    PROPERTIES (d) SHOWIF isConsultantReceipt(b) nameConsultant ON CHANGE changeConsultant(d)
;
EXTEND FORM zReports
    PROPERTIES (d) READONLY SHOWIF isConsultantReceipt(b) nameConsultant
;

consultant = ABSTRACT Employee (SaleLedger);
nameConsultant 'Продавец' (SaleLedger l) = name[Contact](consultant(l)) CHARWIDTH 20;

consultant[SaleLedger](ReceiptSaleDetail l) +=  consultant(l);
consultant[SaleLedger](ReceiptReturnDetail l) +=  consultant(l); 

saleConsultant 'Продавец' = DATA LOCAL Employee ();
nameSaleConsultant 'Продавец' = name[Contact](saleConsultant());
filterSaleConsultant = consultant(SaleLedger ledger)==saleConsultant() OR (ledger IS SaleLedger AND NOT saleConsultant());

CONSTRAINT saleConsultant()  AND NOT isConsultant(saleConsultant())
    CHECKED BY saleConsultant[] MESSAGE 'В качестве продавца в фильтре выбран сотрудник, у которого не задан признак "Продавец".';  

EXTEND FORM saleLedger
    PROPERTIES()   nameSaleConsultant
    PROPERTIES (s) READONLY nameConsultant
    
    FILTERS filterSaleConsultant(s)
;
DESIGN saleLedger {
    operationContainer{
        MOVE PROPERTY(nameSaleConsultant());
    } 
}

//задание продавца по штрихкоду для чека

overChangeBarcodeConsultant(VARSTRING[30] string, Receipt receipt)  { 
    IF employeeBarcode(string) AND isConsultant(employeeBarcode(string)) AND receipt IS Receipt AND string IS VARSTRING[30]  THEN {
        consultant(receipt) <- employeeBarcode(string);        
        consumedChangeBarcodeSaleReceipt() <- TRUE;
    }
} TOOLBAR;

overChangeBarcodeSale(VARSTRING[30] string, Receipt receipt) + {  overChangeBarcodeConsultant(string, receipt); }




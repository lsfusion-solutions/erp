MODULE POSOrder;

REQUIRE POS;
NAMESPACE POS;

META defineOrderReceipt(sign, stockProp, supplierProp, customerProp, prop)

    orderDetail = ABSTRACT OrderDetail (ReceiptDetail) MATERIALIZED INDEXED;
    orderDetail = DATA OrderDetail (ReceiptSaleDetail);
    orderDetail = DATA OrderDetail (ReceiptReturnDetail);
    orderDetail(Receipt###prop##Detail receiptDetail) += orderDetail(receiptDetail);
    orderDetail(ReceiptReturnDetail receiptDetail) += orderDetail(receiptDetail);

    CONSTRAINT sku(ReceiptDetail detail) != sku(orderDetail(detail))
        CHECKED BY orderDetail[ReceiptDetail]
            MESSAGE 'Товар в заказе и чеке должны соответствовать друг другу';

    descriptionIndexOrderDetail 'Строка заказа' (ReceiptDetail detail) = descriptionIndex(orderDetail(detail));

    quantityReceiptDetail (order, receipt) = GROUP SUM quantity(ReceiptDetail receiptDetail) BY order(orderDetail(receiptDetail)), receipt(receiptDetail);
    sumReceiptDetail (order, receipt) = GROUP SUM sum(ReceiptDetail receiptDetail) BY order(orderDetail(receiptDetail)), receipt(receiptDetail);
    relationOrder 'Связь'(Receipt receipt)  = GROUP SUM 1 IF quantityReceiptDetail (Order order, receipt);

    orders 'Заказы'(Receipt receipt)  = GROUP
                                                    CONCAT STRING[255](description(Order order)) IF quantityReceiptDetail(order, receipt) , ', '
                                                    ORDER order IN order CHARWIDTH 30 MATERIALIZED;

    receipted 'Кол-во (по чеку)' (orderDetail) = GROUP SUM quantity(ReceiptDetail receiptDetail) BY orderDetail(receiptDetail) //IF isPostedReceiptDetail(receiptDetail)
                                                                        MATERIALIZED;
                                                                       
    receipted 'Кол-во (по чеку)' (order) = GROUP SUM receipted(OrderDetail detail) IF isPosted(detail)
                                                                       BY order(detail) MATERIALIZED;    
                                                                                                                                          
    receiptQuantityOrderDetail 'Кол-во товара в документе' (sku, order) = 
        GROUP SUM receipted(OrderDetail idetail)
              BY  sku(idetail),
                  order(idetail);
                                                                                                                                                              
    notReceipted = NOT receipted(Order order);

    toReceipt 'Не отпущено (по чеку)' (OrderDetail orderDetail) = quantity (orderDetail) (-) receipted(orderDetail);

    toReceiptOrderDetail 'Не отпущено (по чеку))' (order) = GROUP SUM toReceipt(OrderDetail detail) IF isPosted(detail)
                                                                       BY order(detail) MATERIALIZED; 
    backgroundReceipted 'Цвет' (Order order) = RGB(255, 224, 255) IF order IS Order;
    backgroundReceipted (OrderDetail detail) = backgroundReceipted(order(detail));
    
    sumReceipt 'Сумма оплаты' = GROUP SUM sum(ReceiptDetail receiptDetail) 
                                                                     BY order(orderDetail(receiptDetail)) MATERIALIZED;

    FORM receiptOrders 'Заказы'###sign
        OBJECTS s = LegalEntity PANEL
        PROPERTIES (s) READONLY name
        OBJECTS c = LegalEntity PANEL
        PROPERTIES (c) READONLY name

        OBJECTS i = Order
        PROPERTIES (i) READONLY isPosted, number, series, date, time,
                                nameSupplier, name###stockProp, nameCustomer, nameCustomerStock,
                                nameCurrency, seriesNumberContractSku, isCommission,
                                countOrderDetail, quantityOrderDetail, sumOrderDetail,
                                VATSumOrderDetail, invoiceSumOrderDetail, note
        FILTERS supplier(i) == s,
                customer(i) == c,
                isPosted(i),
                toReceiptOrderDetail(i)
        FILTERGROUP orderfilters6 FILTER 'Открыт' isOpened(i) 'F6' DEFAULT
        
        OBJECTS d = OrderDetail
        PROPERTIES (d) READONLY index, idBarcodeSku, nameSku, shortNameUOMSku
        PROPERTIES (d) READONLY quantity, receipted, price, sum,
                       numberVAT, valueVAT, VATSum, invoiceSum,
                       name###stockProp
        FILTERS order(d) == i
    ;

    DESIGN receiptOrders {
        BOX {
            size = (1024, 768);
            NEW header {
                type = CONTAINERH;
                MOVE BOX(s) { caption = 'Поставщик';};
                MOVE BOX(c) { caption = 'Покупатель';};
            }
            MOVE BOX(i);
            MOVE BOX(d);
            MOVE TOOLBARBOX;
        }
    }
    
    FORM receiptSupplierOrders 'Заказы'###sign
        OBJECTS s = LegalEntity PANEL
        PROPERTIES (s) READONLY name

        OBJECTS i = Order
        PROPERTIES (i) READONLY isPosted, number, series, date, time,
                                nameSupplier, name###stockProp, nameCustomer, nameCustomerStock,
                                nameCurrency, seriesNumberContractSku, isCommission,
                                countOrderDetail, quantityOrderDetail, sumOrderDetail,
                                VATSumOrderDetail, invoiceSumOrderDetail,
                                note
        FILTERS supplier(i) == s,                
                isPosted(i),
                toReceiptOrderDetail(i)
        FILTERGROUP orderfilters6 FILTER 'Открыт' isOpened(i) 'F6' DEFAULT
        
        OBJECTS d = OrderDetail
        PROPERTIES (d) READONLY index, idBarcodeSku, nameSku, shortNameUOMSku
        PROPERTIES (d) READONLY quantity, receipted, price, sum,
                       numberVAT, valueVAT, VATSum, invoiceSum,
                       name###stockProp
        FILTERS order(d) == i
    ;

    DESIGN receiptSupplierOrders {
        BOX {
            size = (1024, 768);
            NEW header {
                type = CONTAINERH;
                MOVE BOX(s) { caption = 'Поставщик';};               
            }
            MOVE BOX(i);
            MOVE BOX(d);
            MOVE TOOLBARBOX;
        }
    }    
    
    overFillOrder  ABSTRACT LIST ( Receipt, Order);                          // документ
    overFillOrder  ABSTRACT LIST ( ReceiptDetail, OrderDetail);  // детайл
    
    fillOrder(Receipt receipt, Order order)   { 
        overFillOrder(receipt, order);
        FOR order(OrderDetail orderDetail) == order AND
            toReceipt(orderDetail) > 0
            NEW d = Receipt###prop###detail DO {
                receipt(d) <- receipt;
                orderDetail(d) <- orderDetail;

                sku(d) <- sku(orderDetail);
                idBarcode(d) <- idBarcode(sku(orderDetail));
                
                quantity (d) <- toReceipt(orderDetail);                
                price (d) <- invoicePrice(orderDetail);
                VAT (d) <- VAT(orderDetail);  
                              
                overFillOrder(d, orderDetail);
        }    
    }

    fillOrder 'По заказу'(Receipt receipt)   { 
        IF customerProp(receipt) THEN {
            DIALOG receiptOrders OBJECTS s = supplierProp(receipt), c = customerProp(receipt), i INPUT DO
                fillOrder(receipt, i);
        } ELSE
            DIALOG receiptSupplierOrders OBJECTS s = supplierProp(receipt), i INPUT DO
                fillOrder(receipt, i);
    } IN order;

    EXTEND FORM POS
        PROPERTIES(r) fillOrder
//        PROPERTIES(d) READONLY descriptionIndexOrderDetailReceiptDetail
    ;
    DESIGN POS {
        misc {
            NEW order {
                alignment = STRETCH;
                caption = 'Заказ';
                MOVE PROPERTY(fillOrder(r)) { fill = 1; focusable = FALSE; font = 'bold 24';  }
            }
        }
    }    
    
    EXTEND FORM zReports
        PROPERTIES(b) READONLY orders
        PROPERTIES(d) READONLY descriptionIndexOrderDetail
    ;
    EXTEND FORM zReport
        PROPERTIES(b) READONLY orders
        PROPERTIES(d) descriptionIndexOrderDetail
    ;    

END



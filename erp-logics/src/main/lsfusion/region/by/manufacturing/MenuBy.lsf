MODULE  MenuBy;

REQUIRE Menu;

//-----------------Печатные формы (выводятся на печать только два уровня групп)-----------------//

FORM menuPrint 'Меню'
    OBJECTS m = Menu PANEL SUBREPORT
    PROPERTIES (m) fullNameStock, number, series, date, time,
                   nameCurrency, namePriceListType, note, countMenuDetail,
                   nameGroupType

    OBJECTS g1=Group
    PROPERTIES READONLY nameG = name(g1)
    PROPERTIES(m,g1) positionNumberMenuGroup1=positionNumber
    FILTERS groupType(g1) == groupType(m),
            g1 IS Group AND NOT parent(g1)
    ORDERS positionNumberMenuGroup1
    FILTERGROUP inactive FILTER 'Активные' active(g1) 'F5' DEFAULT

    OBJECTS g2=Group
    PROPERTIES READONLY nameGr = name(g2)
    PROPERTIES(m,g2) positionNumberMenuGroup2=positionNumber
    FILTERS groupType(g2) == groupType(m),
            parent(g2) == g1
    ORDERS positionNumberMenuGroup2
    FILTERGROUP inactive1 FILTER 'Активные' active(g2) 'F5' DEFAULT

    OBJECTS d = MenuDetail
    PROPERTIES (d) READONLY idBarcodeSku, nameSku,
                   price, composition, productYield
    PROPERTIES (d,g2) positionNumber
    FILTERS hasParent(d,g2),
            menu(d) == m

    ORDERS positionNumber(d,g2)
;
print 'Меню' (Menu menu)  { PRINT menuPrint OBJECTS m = menu; }   IMAGE 'print.png' IN print;

EXTEND FORM menus
    PROPERTIES(m)  print
;
DESIGN menus {
    printTab {
        MOVE GROUP(print,m);
    }
}









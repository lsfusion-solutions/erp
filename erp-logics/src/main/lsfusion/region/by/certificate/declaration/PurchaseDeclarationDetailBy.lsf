MODULE PurchaseDeclarationDetailBy;

REQUIRE PurchaseDeclarationDetail, LegalEntityBy, Item;

NAMESPACE Purchase;

UNPLegalEntity 'Полное имя импортера' (Declaration declaration) = UNP(legalEntity(declaration));

customsCode 'Код в декларации' = DATA STRING[3] (UOM);
EXTEND FORM UOM
    PROPERTIES(u) customsCode
;
EXTEND FORM UOMs
    PROPERTIES(u) READONLY customsCode
;
customsCodeUOM 'Код ед. изм. в декларации' (DeclarationDetail d) = customsCode(UOM(d));

typePayment 'Вид платежа' = DATA STRING[4] (CustomsDocument);
refDoc 'Ссылка на документ' = DATA STRING[6] (CustomsDocument);
description 'Описание' = DATA STRING[60] (CustomsDocument);

markin = ABSTRACT STRING[1000] (DeclarationDetail);
extraComponentsQuantity 'Кол-во с учетом комплектов' = ABSTRACT NUMERIC[14,3] (DeclarationDetail);

isVATCustomsException 'По льготам' = DATA BOOLEAN (CustomsDocument); 

in = GROUP SUM 1 IF in(DeclarationDetail d, UserInvoiceDetail i) BY d, brand(sku(i));
nameBrand 'Бренды' (DeclarationDetail d) =
    GROUP
              CONCAT name(Brand b) IF in(d, b), ',' ORDER b CHARWIDTH 20;

in = GROUP SUM 1 IF in(DeclarationDetail d, UserInvoiceDetail i) BY d, manufacturer(sku(i));
nameManufacturer 'Производители' (DeclarationDetail d) =
    GROUP
              CONCAT name(Manufacturer m) IF in(d, m), ',' ORDER m CHARWIDTH 20;

EXTEND FORM declaration
    PROPERTIES(cd) BEFORE deleted description, refDoc, typePayment, isVATCustomsException
;

isVATCustomsException 'Льготный НДС' = DATA BOOLEAN (CustomsGroup);
EXTEND FORM customsGroup
    PROPERTIES(cg) isVATCustomsException  
;
DESIGN customsGroup {
    dateContainer {
        MOVE PROPERTY(isVATCustomsException(cg));
    }
}

EXTEND FORM customsGroups
    PROPERTIES(cg) READONLYIF isReadonly() BEFORE deleteg isVATCustomsException 
;

isVATCustomsException(DeclarationDetail d) = VATCustomsException(d) OR isVATCustomsException(customsGroup(d)); 

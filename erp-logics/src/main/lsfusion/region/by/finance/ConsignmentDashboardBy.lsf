MODULE ConsignmentDashboardBy;

REQUIRE ConsignmentDashboard, ConsignmentBy;

NAMESPACE Order;

//умная печать накладных
//sessionIssuanceAllowed = DATA LOCAL Employee ();
//nameSessionIssuanceAllowed 'Отпуск разрешил' = positionShortNameEmployee(sessionIssuanceAllowed());
//
//sessionIssuanceExecuted = DATA LOCAL Employee();
//nameSessionIssuanceExecuted 'Отпуск произвел' () = positionShortNameEmployee(sessionIssuanceExecuted());
//
//sessionLoadingExecuter = DATA LOCAL LegalEntity();
//nameSessionLoadingExecuter 'Исполнитель погрузки' () = nameLegalEntity(sessionLoadingExecuter());
//
//sessionWayOfLoading = DATA LOCAL WayOfLoading();
//nameSessionWayOfLoading 'Способ погрузки' () = nameWayOfLoading(sessionWayOfLoading());
//
//sessionUnloadingExecuter = DATA LOCAL LegalEntity();
//nameSessionUnloadingExecuter 'Исполнитель разгрузки' () = nameLegalEntity(sessionUnloadingExecuter());
//
//sessionWayOfUnloading = DATA LOCAL WayOfLoading();
//nameSessionWayOfUnloading 'Способ разгрузки' () = nameWayOfLoading(sessionWayOfUnloading());
//
//sessionCodeLoading 'Код ПРР' = DATA LOCAL BPSTRING[3] ();

printed 'Распечатана' = DATA BOOLEAN (Consignment);
notPrinted = TRUE AND NOT printed(Consignment consignment);

numberCopiesConsignment 'Кол-во экз. накладной (ТТН-1)' = DATA INTEGER ();
numberCopiesAttach 'Кол-во экз. приложения (ТТН-1)' = DATA INTEGER ();

numberCopiesSimpleConsignment 'Кол-во экз. накладной (ТН-2)' = DATA INTEGER ();
numberCopiesSimpleAttach 'Кол-во экз. приложения (ТН-2)' = DATA INTEGER ();

countRowsVerticalAB = ABSTRACT INTEGER (Consignment);
overCountRowsVerticalAB (Consignment consignment) = OVERRIDE countRowsVerticalAB(consignment), countRowsVerticalAB();

FORM issuanceEmpoyee 'Сотрудники' 
    OBJECTS c = Consignment PANEL
    
    OBJECTS e = Employee
    PROPERTIES(e) READONLY positionShortName
    FILTERS in(supplierStock(c), e)
    FILTERGROUP active FILTER 'Активные' active(e) 'F5'  DEFAULT     
;

changeIssuanceAllowed(Consignment consignment)  { 
    DIALOG issuanceEmpoyee OBJECTS c = consignment, e = issuanceAllowed(consignment) INPUT NULL DO
        issuanceAllowed(consignment) <- e;   
}

changeIssuanceExecuted(Consignment consignment)  { 
     DIALOG issuanceEmpoyee OBJECTS c = consignment, e = issuanceExecuted(consignment) INPUT NULL DO
         issuanceExecuted(consignment) <- e;   
}

EXTEND FORM options
    PROPERTIES() numberCopiesConsignment, numberCopiesAttach, numberCopiesSimpleConsignment, numberCopiesSimpleAttach
;

DESIGN options {
    consignment  {
        NEW consignmentOptions {
            type = CONTAINERH;
            NEW row1{
                type = CONTAINERV;
                MOVE PROPERTY(dataDowntime());
                MOVE PROPERTY(notArrivalTimeConsignment());    
                MOVE PROPERTY(notNullInvoiceVAT());          
            }
            NEW row2{
                type = CONTAINERV;
                MOVE PROPERTY(countRowsSimpleVertical());
                MOVE PROPERTY(countRowsSimpleHorizontal());
                MOVE PROPERTY(countRowsVerticalA());
                MOVE PROPERTY(countRowsVerticalAB());
                MOVE PROPERTY(countRowsHorizontal());
            }
            NEW row3{
                type = CONTAINERV;
                MOVE PROPERTY(numberCopiesConsignment());
                MOVE PROPERTY(numberCopiesAttach());
                MOVE PROPERTY(numberCopiesSimpleConsignment());
                MOVE PROPERTY(numberCopiesSimpleAttach());                
                
            }
        }    
    }
}

//TABLE consignment (Consignment);
dataNumberCopiesConsignment 'Кол-во экз. накладной' = DATA INTEGER (Consignment);
dataNumberCopiesAttach 'Кол-во экз. приложения' = DATA INTEGER (Consignment);

numberCopiesConsignment 'Кол-во экз. накладной' (Consignment consignment)= OVERRIDE dataNumberCopiesConsignment(consignment), 
                                                                                    (numberCopiesConsignment() IF consignment IS Consignment);
numberCopiesAttach 'Кол-во экз. приложения' (Consignment consignment)= OVERRIDE dataNumberCopiesAttach(consignment), 
                                                                                (numberCopiesAttach() IF consignment IS Consignment);
                                                                                    
FORM numberCopies 'Настройки печати ТТН-1'
    OBJECTS c = Consignment PANEL
    PROPERTIES(c) numberCopiesConsignment, numberCopiesAttach 
        
    PROPERTIES(c) nameIssuanceAllowed ON CHANGE changeIssuanceAllowed(c), 
                  nameIssuanceExecuted ON CHANGE changeIssuanceExecuted(c), 
                  nameLoadingExecuter, 
                  nameWayOfLoading, 
                  nameUnloadingExecuter, 
                  nameWayOfUnloading, codeLoading         
;
 
DESIGN numberCopies {
    BOX {
        NEW top{
            type = CONTAINERH;
            NEW topLeft {
                caption = 'Погрузка';
                type = CONTAINERV;
                MOVE PROPERTY (nameIssuanceAllowed(c));
                MOVE PROPERTY (nameIssuanceExecuted(c));
                MOVE PROPERTY (nameLoadingExecuter(c));
                MOVE PROPERTY (nameWayOfLoading(c));
                MOVE PROPERTY (nameUnloadingExecuter(c));
                MOVE PROPERTY (nameWayOfUnloading(c));
                MOVE PROPERTY (codeLoading(c));
            }
            NEW topReight {
                caption = 'Кол-во копий';
                type = CONTAINERV;
                MOVE PROPERTY (numberCopiesConsignment(c));
                MOVE PROPERTY (numberCopiesAttach(c));
            }
        }           
        MOVE TOOLBARBOX;
    }
}
           
overPrintDoubleSidedConsignmentsVertical  ABSTRACT LIST ( Consignment);
overSetDoubleSidedConsignmentsVertical  ABSTRACT LIST ( Consignment);

prePrintActionConsignmentsVertical(Consignment consignment)  { 
    issuanceAllowed(consignment) <- issuanceAllowed(supplierStock(consignment)) WHERE NOT issuanceAllowed(consignment);
    issuanceExecuted(consignment) <- issuanceExecuted(supplierStock(consignment)) WHERE NOT issuanceExecuted(consignment);
    loadingExecuter(consignment) <- loadingExecuter(supplierStock(consignment)) WHERE NOT loadingExecuter(consignment);
    wayOfLoading(consignment) <- wayOfLoading(supplierStock(consignment))WHERE NOT wayOfLoading(consignment);
    unloadingExecuter(consignment) <- unloadingExecuter(supplierStock(consignment)) WHERE NOT unloadingExecuter(consignment);
    wayOfUnloading(consignment) <- wayOfUnloading(supplierStock(consignment)) WHERE NOT wayOfUnloading(consignment);    
    codeLoading(consignment) <- codeLoading(supplierStock(consignment)) WHERE NOT codeLoading(consignment);
}

printDoubleSidedConsignmentsVertical 'Печать ТТН-1' (Consignment consignment)  { 
    prePrintActionConsignmentsVertical(consignment);                                                  
    
    DIALOG numberCopies OBJECTS c = consignment DO {

        LOCAL countConsignment = INTEGER();        
        LOCAL countAttach = INTEGER();  
        countConsignment() <- 0;
        countAttach() <- 0;
           
        overSetDoubleSidedConsignmentsVertical(consignment);
        IF countConsignmentDetail(consignment) <= overCountRowsVerticalAB (consignment) THEN {
            WHILE countConsignment() < numberCopiesConsignment(consignment) DO {
                printVerticalABAuto(consignment);
                countConsignment() <- countConsignment() + 1;
            }               
        } ELSE {
            IF  countConsignmentDetail(consignment) <= countRowsVerticalA () THEN {
                WHILE countConsignment() < numberCopiesConsignment(consignment) DO {
                    printVerticalAuto(consignment);
                    countConsignment() <- countConsignment() + 1;
                }                 
            
            } ELSE {
                WHILE countConsignment() < numberCopiesConsignment(consignment) DO {
                    printVerticalABAuto(consignment);
                    countConsignment() <- countConsignment() + 1;
                }      
                WHILE countAttach() < numberCopiesAttach(consignment) DO {
                    printAttachAuto(consignment);
                    countAttach() <- countAttach() + 1;
                }                          
            }                                    
        }
        
        printed(consignment) <- TRUE IF consignment IS Consignment;
        overPrintDoubleSidedConsignmentsVertical(consignment);
        apply();                 
    }
    cancel();
} IMAGE 'print.png';

printStepsConsignmentsVertical 'Печать ТТН-1 (пошаговая)' (Consignment consignment)  { 
    prePrintActionConsignmentsVertical(consignment);                                                  

    DIALOG numberCopies OBJECTS c = consignment DO {
        LOCAL countConsignment = INTEGER();        
        LOCAL countAttach = INTEGER();  
        countConsignment() <- 0;
        countAttach() <- 0;   
                                    
        IF countConsignmentDetail(consignment) <= overCountRowsVerticalAB (consignment) THEN {
            ASK 'Печать будет выполняться на ОДНОЙ стороне ТТН БЕЗ ПРИЛОЖЕНИЯ' DO {
                WHILE countConsignment() < numberCopiesConsignment(consignment) DO {
                    printVerticalAB(consignment);
                    countConsignment() <- countConsignment() + 1;
                }
                printed(consignment) <- TRUE;
            }  
        } ELSE {
            IF  countConsignmentDetail(consignment) <= countRowsVerticalA () THEN {
                ASK 'Печать будет выполняться на ДВУХ сторонах ТТН' DO {
                    WHILE countConsignment() < numberCopiesConsignment(consignment) DO {
                        printVerticalAAuto(consignment);
                        countConsignment() <- countConsignment() + 1;
                    }
                    MESSAGE 'Для продолжения печати положите бланк ТТН обратной стороной'; 
                    WHILE countConsignment() < numberCopiesConsignment(consignment) DO {
                        printVerticalBAuto(consignment);
                        countConsignment() <- countConsignment() + 1;
                    }
                    printed(consignment) <- TRUE;
                }  
            } ELSE {
                ASK 'Печать будет выполняться на ОДНОЙ стороне ТТН С ПРИЛОЖЕНИЕМ' DO {
                    WHILE countConsignment() < numberCopiesConsignment(consignment) DO {
                        printVerticalABAuto(consignment);
                        countConsignment() <- countConsignment() + 1;
                    }      
                    WHILE countAttach() < numberCopiesAttach(consignment) DO {
                        printAttachAuto(consignment);
                        countAttach() <- countAttach() + 1;
                    } 
                    printed(consignment) <- TRUE;
                } 
            }                                    
        }
        IF printed(consignment) THEN
            apply();
    }
    cancel();
} IMAGE 'print.png';

EXTEND FORM consignmentDashboard
    PROPERTIES (i) printDoubleSidedConsignmentsVertical , printStepsConsignmentsVertical 

    PROPERTIES (i)  READONLYIF banPrint(i) printVerticalA, printVerticalAB, printVerticalABAttach, printHorizontalA, printHorizontalAB,
                                printVerticalB, printHorizontalB,
                                printAttach, printAttachXLS, printSimpleAttachXLS, printSimpleHorizontal, editAttributes SHOWIF isOpened(i),
                                printSimpleVertical, printSimpleAttach                        

    FILTERGROUP filters2 
        FILTER 'Нераспечатанные' notPrinted(i) 'F10' DEFAULT 
;

DESIGN consignmentDashboard {
    printContainer {
        NEW consignmentRow1 {
            align = START;
            type = CONTAINERH;

            NEW contOne {
                caption = 'Накладная';
                type = CONTAINERH;
                MOVE PROPERTY(editAttributes(i));
            }
            NEW tn{
                type = CONTAINERH;
                caption = 'ТН-2';
                MOVE PROPERTY(printSimpleVertical(i));
                MOVE PROPERTY(printSimpleHorizontal(i));
                MOVE PROPERTY(printSimpleAttach(i));
            }
        }
        NEW consignmentRow2 {
            type = COLUMNS;
            columns = 5;
            caption = 'ТТН-1';
            MOVE PROPERTY(printVerticalA(i));
            MOVE PROPERTY(printVerticalAB(i));
            MOVE PROPERTY(printVerticalABAttach(i));
            MOVE PROPERTY(printHorizontalA(i));
            MOVE PROPERTY(printHorizontalAB(i));
            MOVE PROPERTY(printVerticalB(i));
            MOVE PROPERTY(printHorizontalB(i));
            MOVE PROPERTY(printAttach(i));
            MOVE PROPERTY(printAttachXLS(i));
            MOVE PROPERTY(printDoubleSidedConsignmentsVertical(i));
            MOVE PROPERTY(printStepsConsignmentsVertical(i));
        }
        NEW consignmentRow3 {
            type = CONTAINERH;
            NEW export {
                type = CONTAINERH;
                caption = 'Экспорт';  
                MOVE PROPERTY(printAttachXLS(i));
                MOVE PROPERTY(printSimpleAttachXLS(i));                                
            }                        
        }
    
    }
}

numberCopiesSimpleConsignment 'Кол-во экз. накладной' (Consignment consignment)= OVERRIDE dataNumberCopiesConsignment(consignment), 
                                                                                    (numberCopiesSimpleConsignment() IF consignment IS Consignment);
numberCopiesSimpleAttach 'Кол-во экз. приложения' (Consignment consignment)= OVERRIDE dataNumberCopiesAttach(consignment), 
                                                                                (numberCopiesSimpleAttach() IF consignment IS Consignment);

FORM numberCopiesSimple 'Настройки печати ТН-2'
    OBJECTS c = Consignment PANEL
    PROPERTIES(c) numberCopiesSimpleConsignment, numberCopiesSimpleAttach 
    
    PROPERTIES(c) nameIssuanceAllowed ON CHANGE changeIssuanceAllowed(c), 
                  nameIssuanceExecuted ON CHANGE changeIssuanceExecuted(c), 
                  forwarder ON CHANGE changeForwarder(c),
                  warrant, warrantHolder, overGoodsAccepted ON CHANGE changeGoodsAccepted(c)    
;
 
DESIGN numberCopiesSimple {
    BOX {
        NEW top{
            type = CONTAINERH;
            NEW topLeft {
                caption = 'Погрузка';
                type = CONTAINERV;
                MOVE PROPERTY(nameIssuanceAllowed(c));
                MOVE PROPERTY(nameIssuanceExecuted(c));
                MOVE PROPERTY(forwarder(c));
                MOVE PROPERTY(warrant(c));
                MOVE PROPERTY(warrantHolder(c));
                MOVE PROPERTY(overGoodsAccepted(c));
            }
            NEW topReight {
                caption = 'Кол-во копий';
                type = CONTAINERV;
                MOVE PROPERTY (numberCopiesSimpleConsignment(c));
                MOVE PROPERTY (numberCopiesSimpleAttach(c));
            }
        }           
        MOVE TOOLBARBOX;
    }
}

countRowsSimpleVertical = ABSTRACT INTEGER (Consignment);
overCountRowsSimpleVertical = OVERRIDE countRowsSimpleVertical(Consignment consignment), countRowsSimpleVertical();

overPrintDoubleSidedConsignmentsSimpleVertical  ABSTRACT LIST ( Consignment);
overSetDoubleSidedConsignmentsSimpleVertical  ABSTRACT LIST ( Consignment);
printDoubleSidedConsignmentsSimpleVertical 'Печать ТН-2' (Consignment consignment)  { 
    issuanceAllowed(consignment) <- issuanceAllowed(supplierStock(consignment)) WHERE NOT issuanceAllowed(consignment);
    issuanceExecuted(consignment) <- issuanceExecuted(supplierStock(consignment)) WHERE NOT issuanceExecuted(consignment);
    loadingExecuter(consignment) <- loadingExecuter(supplierStock(consignment)) WHERE NOT loadingExecuter(consignment);
    wayOfLoading(consignment) <- wayOfLoading(supplierStock(consignment))WHERE NOT wayOfLoading(consignment);
    unloadingExecuter(consignment) <- unloadingExecuter(supplierStock(consignment)) WHERE NOT unloadingExecuter(consignment);
    wayOfUnloading(consignment) <- wayOfUnloading(supplierStock(consignment)) WHERE NOT wayOfUnloading(consignment);    
    codeLoading(consignment) <- codeLoading(supplierStock(consignment)) WHERE NOT codeLoading(consignment);
    
    DIALOG numberCopiesSimple OBJECTS c = consignment DO {
        LOCAL countConsignment = INTEGER();        
        LOCAL countAttach = INTEGER(); 
        countConsignment() <- 1;
        countAttach() <- 1;                                                          
                            
        overSetDoubleSidedConsignmentsSimpleVertical(consignment);
        
        countConsignment() <- 0;            
        countAttach() <- 0;            
        
        IF countConsignmentDetail(consignment) <= overCountRowsSimpleVertical (consignment) THEN {
            WHILE countConsignment() < numberCopiesSimpleConsignment(consignment) DO {
                printSimpleVertical(consignment); 
                countConsignment() <- countConsignment() + 1;
            }               
        } ELSE {      
            WHILE countConsignment() < numberCopiesSimpleConsignment(consignment) DO {
                printSimpleVertical(consignment); 
                countConsignment() <- countConsignment() + 1;
            }      
            WHILE countAttach() < numberCopiesSimpleAttach(consignment) DO {
                printSimpleAttach(consignment); 
                countAttach() <- countAttach() + 1;
            }                          
                                                
        }
        
        printed(consignment) <- TRUE IF consignment IS Consignment;
        overPrintDoubleSidedConsignmentsSimpleVertical(consignment);
        apply();                 
    }
    cancel();
} IMAGE 'print.png';

EXTEND FORM consignmentDashboard
    PROPERTIES (i) printDoubleSidedConsignmentsSimpleVertical 
;

DESIGN consignmentDashboard {
    tn {
        MOVE PROPERTY(printDoubleSidedConsignmentsSimpleVertical(i));
    }
}

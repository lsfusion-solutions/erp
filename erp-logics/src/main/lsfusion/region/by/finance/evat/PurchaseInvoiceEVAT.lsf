MODULE PurchaseInvoiceEVAT;

REQUIRE PurchaseInvoice, EVAT, CustomsGroup, PurchaseDeclaration, InvoiceConsignmentBy, PurchaseDeclarationDetail;

NAMESPACE Purchase;

EXTEND CLASS Invoice : EVATDocument;
name(Invoice document) += description(document);
isPosted[EVATDocument](Invoice document) += isPosted(document);
number[EVATDocument](Invoice document) += STRING[100](number(document));
series[EVATDocument](Invoice document) += STRING[2](series(document));
dateTime[EVATDocument](Invoice document) += dateTime(document);

@defineOperationProperty(eVAT, 'Создавать ЭСЧФ', createContainer);

overCreateEVATPurchaseInvoice  ABSTRACT LIST ( EVATDetail, InvoiceDetail);
overCreateEVATPurchaseInvoice  ABSTRACT LIST ( EVAT, Invoice);
overCreateEVATPurchaseInvoice 'Накладные (закупка-возврат)'  ABSTRACT LIST ( DATE, DATE);//-- для возвратов
skipEVAT = ABSTRACT CASE BOOLEAN (Invoice);

EXTEND CLASS EVATType { purchase 'Закупка' }
type[EVATDocument](Invoice i) += EVATType.purchase IF i IS Invoice;

createEVATPurchaseInvoice (EVAT e, Invoice i)  { 
    EVATDocument(e) <- i;
    type(e) <- EVATType.purchase;
    status(e) <- EVATStatus.original;
    legalEntityStatusSupplier(e) <- LegalEntityStatus.foreignOrganization;
    legalEntityStatusCustomer(e) <- LegalEntityStatus.customer;
    declarationCustomer(e) <- STRING[100](number(declaration(i)));
           
    date(e) <- date(i);
    
    numberContract (e) <- number(contractSku(i));
    dateContract (e) <- dateFrom(contractSku(i));
    codeDocType (e) <- 609;
    valueDocType(e) <- STRING[100] ('invoice');
//        blankCodeDoc (e) <- OVERRIDE '402861', blankCodeDoc();
    seriesDoc(e) <- series(i);
    numberDoc(e) <- number(i);
    unpSender(e) <- UNP(customer(i));
    
    //Поставщик
    supplier(e) <- supplier(i);
    branchCodeSupplier(e) <- branchCode(supplierStock(i));
    
    //Получатель
    customer(e) <- customer(i);
    branchCodeCustomer(e) <- branchCode(customerStock(i));
    
    //Грузоотправитель
    consignor(e) <- ISTRING[200](OVERRIDE fullName(supplier(i)), name(supplier(i)));
    countryCodeConsignor (e) <- sid(country(supplier(i)));
    unpConsignor (e) <- OVERRIDE UNPForeign(supplier(i)), UNP(supplier(i)); 
    shippingAddressConsignor(e) <- OVERRIDE dataAddressSupplierStock (i), address(supplierStock(i));
    
    //Грузополучатель
    consignee(e) <- ISTRING[200](OVERRIDE fullName(customer(i)), name(customer(i)));
    countryCodeConsignee (e) <- sid(country(customer(i)));
    unpConsignee (e) <- OVERRIDE UNPForeign(customer(i)), UNP(customer(i)); 
    shippingAddressConsignee(e) <- OVERRIDE dataAddressCustomerStock (i), address(customerStock(i));
            
    overCreateEVATPurchaseInvoice(e,i);
} 

createEVATPurchaseInvoice (EVAT e, InvoiceDetail id, EVATDetail ed)  { 
    evat(ed) <- e;
    
    name(ed) <- STRING[255](OVERRIDE documentNameSku(id), nameSku(id));
    sku(ed) <- sku(id);
    code(ed) <- OVERRIDE codeCustomsGroup(id), codeCustomsGroup(defaultCountry(), sku(id));
    UOM(ed) <- UOM(sku(id));
    quantity(ed) <- quantity(id); 
    //            price(ed) <- price(id);
    sum(ed) <- homeSum(id);
    exciseSum(ed) <- 0;
    vatRate(ed) <- valueVAT(sku(id), date(id));
    vatSum(ed) <- 0;
    sumWithVAT(ed) <- 0;
    overCreateEVATPurchaseInvoice(ed,id);
    
    descriptionType(ed) <- DescriptionType.importVAT;

}

needCreateEVAT[EVATDocument](Invoice i) += WHEN eVAT(operation(i)) AND quantityInvoiceDetail(i) AND NOT skipEVAT(i) THEN TRUE;

VATSum= ABSTRACT CASE NUMERIC[18,4](Purchase.Invoice);
VATSum (Purchase.Invoice i) += WHEN i IS Purchase.Invoice THEN NUMERIC[18,4](customsVATSumInvoiceDetail(i));
VATSum[EVATDocument] (Purchase.Invoice i) += NUMERIC[18,4](VATSum(i));

VATSumRange[EVATDocument, Range](Purchase.Invoice i, Range r) += [ GROUP SUM VATSum(InvoiceDetail d) BY invoice(d), VAT(d)](i,r);
sumWithVATRange[EVATDocument, Range](Purchase.Invoice i, Range r) += [ GROUP SUM invoiceSum(InvoiceDetail d) BY invoice(d), VAT(d)](i,r);
customer[EVATDocument](Purchase.Invoice i) += customer(i);

//priceRound (InvoiceDetail d) += WHEN currency(d) THEN defaultRound(currency(d));
inEVAT = DATA LOCAL BOOLEAN (Invoice);
inEVAT = DATA LOCAL BOOLEAN ();
createEVATPurchaseInvoiceOver   ABSTRACT LIST ( DATE, DATE);

createEVATPurchaseInvoice 'Накладные (закупка)' (DATE dFrom, DATE dTo)  { 
    inEVAT() <- NULL;
    createEVATPurchaseInvoiceOver(dFrom,dTo); // разная фильтрация накладных
    
    IF NOT inEVAT() THEN {
        inEVAT(Invoice i) <- NULL;
        inEVAT(Invoice i) <- TRUE WHERE eVAT(operation(i)) AND isPosted(i) AND NOT EVAT(i) AND date(i) >= dFrom AND date(i) <= dTo AND quantityInvoiceDetail(i) AND NOT skipEVAT(i);      
    }

    FOR inEVAT(Invoice i) NEW e = EVAT DO {
        createEVATPurchaseInvoice(e,i);
        
        FOR invoice(InvoiceDetail id) == i AND quantity(id) > 0 NEW ed = EVATDetail DO {
            createEVATPurchaseInvoice(e,id,ed);
        }
        FOR NUMERIC[16,5] vatSum == [ GROUP SUM customsVATSum(InvoiceDetail id) BY invoice(id),valueVAT(sku(id), date(id))](i, NUMERIC[10,5] vat) NEW ed = EVATDetail DO {
            evat(ed) <- e;
            name(ed) <- 'Сумма НДС, уплаченная при ввозе';
            code(ed) <- [ GROUP MAX codeCustomsGroup(InvoiceDetail id) BY invoice(id), valueVAT(sku(id), date(id))](i, vat);
            sum(ed) <- 0;
            exciseSum(ed) <- 0;
            vatRate(ed) <- vat;
            vatSum(ed) <- vatSum;
            sumWithVAT(ed) <- 0;
            
            descriptionType(ed) <- DescriptionType.importVAT;
        }
    } 
    APPLY;
//    overCreateEVATPurchaseInvoice(dFrom,dTo);
} CONFIRM;

EXTEND FORM evats
    PROPERTIES createEVATPurchaseInvoice(dFrom, dTo) TOOLBAR DRAW e
;

DESIGN evats {
    generate {
        MOVE PROPERTY(createEVATPurchaseInvoice(dFrom, dTo));
    }
}
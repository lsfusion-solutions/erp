MODULE ZReportEVAT;

REQUIRE ZReport, EVAT, CustomsGroup;

NAMESPACE ZReport;

createEVATZReport 'Z-отчеты' (LegalEntity l, DATE dFrom, DATE dTo)  { 
    FOR [ GROUP SUM 1 IF active(ReceiptDetail d) AND date(d) >= dFrom AND date(d) <= dTo BY valueVAT(sku(d), defaultCountry(), dTo)](NUMERIC[10,5] vat) NEW e = EVAT DO {
        status(e) <- EVATStatus.original;
        
        date(e) <- dTo;
        
        codeDocType (e) <- 603;
        blankCodeDoc (e) <- blankCodeDefault();
        seriesDoc(e) <- 'РК';
        numberDoc(e) <- STRING[100](dTo);
        unpSender(e) <- UNP(l);
        
        skipDeliveryCondition(e) <- TRUE;
        
        //Поставщик
        supplier(e) <- l;
//        branchCodeSupplier(e) <- branchCode(supplierStock(i));
        
        //Получатель
//        customer(e) <- l;
//        branchCodeCustomer(e) <- branchCode(customerStock(i));
        
        FOR [ GROUP SUM sum(ReceiptSaleDetail d) IF active(d) AND date(d) >= dFrom AND date(d) <= dTo BY sku(d)](Sku sku) AND valueVAT(sku, defaultCountry(), dTo) == vat NEW ed = EVATDetail DO {
            evat(ed) <- e;

            name(ed) <- name(sku);
            sku(ed) <- sku;
            code(ed) <- codeCustomsGroup(defaultCountry(), sku);
            UOM(ed) <- UOM(sku);
//            quantity(ed) <- [ GROUP SUM quantity(ReceiptSaleDetail d) IF active(d) AND date(d) >= DATE dFrom AND date(d) <= DATE dTo BY sku(d), dFrom, dTo](sku, dFrom, dTo); 
            vatRate(ed) <- vat;
            vatSum(ed) <- [ GROUP SUM sumVAT(ReceiptSaleDetail d) IF active(d) AND date(d) >= dFrom AND date(d) <= dTo BY sku(d)](sku);
            sumWithVAT(ed) <- [ GROUP SUM sum(ReceiptSaleDetail d) IF active(d) AND date(d) >= dFrom AND date(d) <= dTo BY sku(d)](sku);
            descriptionType(ed) <- IF valueVAT(sku, defaultCountry(), dTo) == 0 THEN DescriptionType.vatExcemption ELSE 
                                    IF NOT notDeductionInFull() THEN DescriptionType.deductionInFull;  
        } 

        FOR [ GROUP SUM signedSum(ReceiptReturnDetail d) IF active(d) AND date(d) >= dFrom AND date(d) <= dTo BY sku(d)](Sku sku) AND valueVAT(sku, defaultCountry(), dTo) == vat NEW ed = EVATDetail DO {
            evat(ed) <- e;

            name(ed) <- name(sku);
            sku(ed) <- sku;
            code(ed) <- codeCustomsGroup(defaultCountry(), sku);
            UOM(ed) <- UOM(sku);
//            quantity(ed) <- [ GROUP SUM quantity(ReceiptReturnDetail d) IF active(d) AND date(d) >= DATE dFrom AND date(d) <= DATE dTo BY sku(d), dFrom, dTo](sku, dFrom, dTo); 
            vatRate(ed) <- vat;
            vatSum(ed) <- [ GROUP SUM -sumVAT(ReceiptReturnDetail d) IF active(d) AND date(d) >= dFrom AND date(d) <= dTo BY sku(d)](sku);
            sumWithVAT(ed) <- [ GROUP SUM -sum(ReceiptReturnDetail d) IF active(d) AND date(d) >= dFrom AND date(d) <= dTo BY sku(d)](sku);
            descriptionType(ed) <- IF valueVAT(sku, defaultCountry(), dTo) == 0 THEN DescriptionType.vatExcemption ELSE 
                                     IF NOT notDeductionInFull() THEN DescriptionType.deductionInFull;  
        } 
        exciseSum(EVATDetail d) <- 0 WHERE evat(d) == e;
        sum(EVATDetail d) <- sumWithVAT(d) (-) vatSum(d) WHERE evat(d) == e;
    } 
    APPLY;
} CONFIRM;

EXTEND FORM evats
    PROPERTIES createEVATZReport(l, dFrom, dTo) TOOLBAR DRAW e
;

DESIGN evats {
    generate {
        MOVE PROPERTY(name(l));
        MOVE PROPERTY(createEVATZReport(l, dFrom, dTo));
    }
}
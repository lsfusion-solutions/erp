MODULE ItemAlcohol;

REQUIRE ItemSize, SaleStatisticsBy;

NAMESPACE Item;


loadDefaultAlcoholGroupsBy 'Загрузить стандартные статистические группы'()  { 

    EXEC loadDefaultStatisticGroupType('Оптовая торговля (алкоголь)', 'BYA', '112');

    EXEC loadDefaultStatistic('BYA', ' ', 'ВСЕ', '1000', GroupStatic.statisticLiter, 10000);
    EXEC loadDefaultStatistic('BYA','1000','Водка','1010',GroupStatic.statisticLiter,10000);
    EXEC loadDefaultStatistic('BYA','1000','Ликеро-водочные изделия','1020',GroupStatic.statisticLiter,10000);
    EXEC loadDefaultStatistic('BYA','1000','Виски','1030',GroupStatic.statisticLiter,10000);
    EXEC loadDefaultStatistic('BYA','1000','Ром','1040',GroupStatic.statisticLiter,10000);        
    EXEC loadDefaultStatistic('BYA','1000','Текила','1160',GroupStatic.statisticLiter,10000);
    EXEC loadDefaultStatistic('BYA','1000','Вина полодовые','1060',GroupStatic.statisticLiter,10000);
    EXEC loadDefaultStatistic('BYA','1000','Вина игристые (в том числе шампанские)','1090',GroupStatic.statisticLiter,10000);
    EXEC loadDefaultStatistic('BYA','1000','Вина виноградные','1100',GroupStatic.statisticLiter,10000);   
    EXEC loadDefaultStatistic('BYA','1000','Коньяки','1130',GroupStatic.statisticLiter,10000);    
    EXEC loadDefaultStatistic('BYA','1000','Бренди','1140',GroupStatic.statisticLiter,10000);    
    EXEC loadDefaultStatistic('BYA','1000','Другие алкогольные напитки','1150',GroupStatic.statisticLiter,10000);
}
loadDefaultStatisticGroups () + {  loadDefaultAlcoholGroupsBy(); }

//--
CLASS AlcoholDirectionType 'Тип движения алкоголя' { 
    income 'Поступление',
    outcome 'Расход'    
}
TABLE alcoholDirectionType (AlcoholDirectionType);

number 'Номер типа движения алкоголя' = DATA INTEGER (AlcoholDirectionType);

onStarted() + { 
    number(AlcoholDirectionType d) <- 3 WHERE d == AlcoholDirectionType.income; 
    number(AlcoholDirectionType d) <- 4 WHERE d == AlcoholDirectionType.outcome; 

}
FORM alcoholDirectionTypies 'Типы приобретения алкоголя'
    OBJECTS d = AlcoholDirectionType
    PROPERTIES(d) READONLY staticCaption, number
    ORDERS number(d)
    LIST AlcoholDirectionType OBJECT d
;
//--
CLASS AlcoholIncomeType 'Тип приобретения алкоголя' { 
    internal 'Приобретено в РБ',
    external 'Импортировано',
    other 'Прочее поступление'
}
TABLE alcoholIncomeType (AlcoholIncomeType);

number 'Номер типа приобретения алкоголя' = DATA INTEGER (AlcoholIncomeType);

onStarted() + { 
    number(AlcoholIncomeType d) <- 1 WHERE d == AlcoholIncomeType.internal; 
    number(AlcoholIncomeType d) <- 2 WHERE d == AlcoholIncomeType.external; 
    number(AlcoholIncomeType d) <- 3 WHERE d == AlcoholIncomeType.other; 

}
FORM alcoholIncomeTypies 'Типы приобретения алкоголя'
    OBJECTS d = AlcoholIncomeType
    PROPERTIES(d) READONLY staticCaption, number
    ORDERS number(d)
    LIST AlcoholIncomeType OBJECT d
;
//--
CLASS AlcoholSupplierType 'Тип поставщика алкоголя' { 
    importer 'От импортера',
    manufacturer 'От изготовителя',
    wholesale 'Приход от оптовой торговли',
    none 'Не заполнено'
}
TABLE alcoholSupplierType (AlcoholSupplierType);
alcoholSupplierTypeCaption = GROUP MAX AlcoholSupplierType l AS AlcoholSupplierType BY staticCaption(l);  

alcoholSupplierType 'Тип поставщика алкоголя' = DATA AlcoholSupplierType (LegalEntity, Item);
captionAlcoholSupplierType 'Тип поставщика алкоголя' (LegalEntity legalEntity, Item item) = staticCaption(alcoholSupplierType(legalEntity, item));

number 'Номер типа поставщика алкоголя' = DATA INTEGER (AlcoholSupplierType);
alcoholSupplierType (number) = GROUP AGGR AlcoholSupplierType alcoholSupplierType BY number(alcoholSupplierType);

onStarted() + { 
    number(AlcoholSupplierType d) <- 1 WHERE d == AlcoholSupplierType.manufacturer; 
    number(AlcoholSupplierType d) <- 2 WHERE d == AlcoholSupplierType.importer; 
    number(AlcoholSupplierType d) <- 3 WHERE d == AlcoholSupplierType.wholesale; 
    number(AlcoholSupplierType d) <- 4 WHERE d == AlcoholSupplierType.none; 
}
FORM alcoholSupplierTypies 'Типы поставщиков алкоголя'
    OBJECTS d = AlcoholSupplierType
    PROPERTIES(d) READONLY staticCaption, number
    ORDERS number(d)
    FILTERGROUP active 
        FILTER 'Активные' d != AlcoholSupplierType.none 'F10' DEFAULT 
    LIST AlcoholSupplierType OBJECT d
;








MODULE EZLotInit;

REQUIRE LotInitBy;

NAMESPACE EZ;

CLASS LotInitMode 'Режим маркировки' {
    PRODUCTION 'Производство',
    IMPORT_ 'Импорт',
    COMMISSION 'Комиссионная торговля',
    REMARK 'Перемаркировка (возврат товара)',
    IMPORTEAES 'Импорт из государств-членов ЕАЭС'
}

code 'Код' (LotInitMode m) = CASE 
    WHEN m == LotInitMode.PRODUCTION THEN 1
    WHEN m == LotInitMode.IMPORT_ THEN 2
    WHEN m == LotInitMode.COMMISSION THEN 3
    WHEN m == LotInitMode.REMARK THEN 4 
    WHEN m == LotInitMode.IMPORTEAES THEN 5 CHARWIDTH 2;

lotInitMode (INTEGER code) = CASE
    WHEN code == 1 THEN LotInitMode.PRODUCTION
    WHEN code == 2 THEN LotInitMode.IMPORT_ 
    WHEN code == 2 THEN LotInitMode.COMMISSION 
    WHEN code == 4 THEN LotInitMode.REMARK
    WHEN code == 5 THEN LotInitMode.IMPORTEAES;
    
name '{master.data.name}' (LotInitMode m) = staticCaption(m) IF m IS LotInitMode CHARWIDTH 15;

FORM dialogLotInitModes 'Режим маркировки'
    OBJECTS m = LotInitMode
    PROPERTIES(m) READONLY name
    
    LIST LotInitMode OBJECT m
;

mode = DATA LotInitMode (LotInit) NONULL;
codeMode (LotInit i) = code(mode(i));
nameMode 'Режим' (LotInit i) = name(mode(i));

CLASS LotInitProductionType 'Тип производственного заказа' {
    OWN 'Собственное производство',
    CONTRACT 'Контрактное производство'
}

code 'Код' (LotInitProductionType m) = CASE 
    WHEN m == LotInitProductionType.OWN THEN 1
    WHEN m == LotInitProductionType.CONTRACT THEN 2 CHARWIDTH 1;
    
name '{master.data.name}' (LotInitProductionType t) = staticCaption(t) IF t IS LotInitProductionType CHARWIDTH 15;

FORM dialogLotInitProductionTypes 'Тип производственного заказа'
    OBJECTS m = LotInitProductionType
    PROPERTIES(m) READONLY name
    
    LIST LotInitProductionType OBJECT m
;

productionType = DATA LotInitProductionType (LotInit);
codeProductionType (LotInit i) = code(productionType(i));
nameProductionType 'Тип заказа' (LotInit i) = name(productionType(i));

contractDate 'Основание: дата документа' = DATA DATE (LotInit);
contractNumber 'Основание: номер документа ' = DATA STRING (LotInit);

CLASS ReasonMark 'Причина нанесения СИ' {
    INTEGRITY 'Нарушение целостности СИ',
    ABSENCE 'Отсутствие СИ'
}

code 'Код' (ReasonMark rm) = CASE
    WHEN rm == ReasonMark.INTEGRITY THEN 1
    WHEN rm == ReasonMark.ABSENCE THEN 2;

name '{master.data.name}' (ReasonMark rm) = staticCaption(rm) IF rm IS ReasonMark CHARWIDTH 15;

reasonMark = DATA ReasonMark (LotInit);
codeReasonMark(LotInit i) = code(reasonMark(i));
nameReasonMark 'Причина нанесения СИ' (LotInit i) = name(reasonMark(i));

CLASS ReasonRemark 'Причина перемаркировки' {
    BUYER_RETURN 'Возврат от покупателя',
    SUPPLIERS_REFUSAL 'Отказ поставщика в приемке товара',
    SUPPLIER_LIQUIDATION 'Поставщик в стадии ликвидации/ликвидирован',
    DAMAGE_GOODS 'Порча при обороте товаров'
}

code 'Код' (ReasonRemark rr) = CASE
    WHEN rr == ReasonRemark.BUYER_RETURN THEN 1
    WHEN rr == ReasonRemark.SUPPLIERS_REFUSAL THEN 2
    WHEN rr == ReasonRemark.SUPPLIER_LIQUIDATION THEN 3
    WHEN rr == ReasonRemark.DAMAGE_GOODS THEN 4;

name '{master.data.name}' (ReasonRemark rr) = staticCaption(rr) IF rr IS ReasonRemark CHARWIDTH 15;

reasonRemark = DATA ReasonRemark (LotInit);
codeReasonRemark(LotInit i) = code(reasonRemark(i));
nameReasonRemark 'Причина перемаркировки' (LotInit i) = name(reasonRemark(i));

CLASS LotInitMarkingMethod 'Способ маркировки (полиграфическая защита)' {
    PROTECTED 'Использование защищенного материального носителя',
    PROTECTION_MARK 'Использование знака защиты',
    NO_PROTECT 'Без защиты'
}

code 'Код' (LotInitMarkingMethod m) = CASE 
    WHEN m == LotInitMarkingMethod.PROTECTED THEN 1
    WHEN m == LotInitMarkingMethod.PROTECTION_MARK THEN 2
    WHEN m == LotInitMarkingMethod.NO_PROTECT THEN 3;

lotInitMethod (INTEGER code) = CASE
    WHEN code == 1 THEN LotInitMarkingMethod.PROTECTED
    WHEN code == 2 THEN LotInitMarkingMethod.PROTECTION_MARK
    WHEN code == 3 THEN LotInitMarkingMethod.NO_PROTECT;
    
name '{master.data.name}' (LotInitMarkingMethod m) = staticCaption(m) IF m IS LotInitMarkingMethod CHARWIDTH 15;

FORM dialogLotInitMarkingMethods 'Способ маркировки'
    OBJECTS m = LotInitMarkingMethod
    PROPERTIES(m) READONLY name
    
    LIST LotInitMarkingMethod OBJECT m
;

method = DATA LotInitMarkingMethod (LotInit) NONULL;
codeMethod (LotInit i) = code(method(i));
nameMethod 'Способ маркировки' (LotInit i) = name(method(i));

EXTEND FORM lotInit
    PROPERTIES(i) nameMode, nameProductionType SHOWIF LotInitMode.PRODUCTION == mode(i)
    PROPERTIES(i) SHOWIF LotInitMode.REMARK == mode(i) OR LotInitMode.COMMISSION == mode(i) contractDate, contractNumber, nameReasonMark
    PROPERTIES(i) SHOWIF LotInitMode.REMARK == mode(i) nameReasonRemark
    PROPERTIES(i) nameMethod
;
DESIGN lotInit {
    header {
        NEW headerRow3 {
            horizontal = TRUE;
            alignment = STRETCH;
            MOVE PROPERTY(nameMode(i)) { notNull = TRUE; }
            MOVE PROPERTY(nameProductionType(i)) { notNull = TRUE; }
            MOVE PROPERTY(contractDate(i)) { notNull = TRUE; }
            MOVE PROPERTY(contractNumber(i)) { notNull = TRUE; }
            MOVE PROPERTY(nameReasonMark(i)) { notNull = TRUE; }            
            MOVE PROPERTY(nameReasonRemark(i)) { notNull = TRUE; }            
            MOVE PROPERTY(nameMethod(i)) { notNull = TRUE; }
        }
    } 
}

EXTEND FORM lotInits
    PROPERTIES(i) READONLY nameMode, nameProductionType SHOWIF LotInitMode.PRODUCTION == mode(i)
    PROPERTIES(i) READONLY SHOWIF LotInitMode.REMARK == mode(i) OR LotInitMode.COMMISSION == mode(i) contractDate, contractNumber, nameReasonMark
    PROPERTIES(i) READONLY nameMethod
;

CLASS ReportEZ 'Отчет ЭЗ';
lotInit = DATA LotInit(ReportEZ) NONULL;
id '{integration.id}' = DATA STRING[100](ReportEZ);
reportEZ (id) = GROUP AGGR ReportEZ reportEZ BY id (reportEZ);

CLASS LotInitReportStatus 'Статус Отчета' {
    CREATED    'Создан',
    PROCESSING 'В обработке',
    CONFIRMED  'Подтвержден',
    COMPLETED  'Выполнен',
    ERROR      'Ошибка'
}

code 'Код' (LotInitReportStatus s) = CASE 
    WHEN s == LotInitReportStatus.CREATED THEN 0
    WHEN s == LotInitReportStatus.PROCESSING THEN 10
    WHEN s == LotInitReportStatus.CONFIRMED THEN 20
    WHEN s == LotInitReportStatus.COMPLETED THEN 30
    WHEN s == LotInitReportStatus.ERROR THEN 40
    CHARWIDTH 20;
  
statusReport (INTEGER code) = CASE 
    WHEN code == 0  THEN LotInitReportStatus.CREATED
    WHEN code == 10 THEN LotInitReportStatus.PROCESSING 
    WHEN code == 20 THEN LotInitReportStatus.CONFIRMED
    WHEN code == 30 THEN LotInitReportStatus.COMPLETED
    WHEN code == 40 THEN LotInitReportStatus.ERROR;
    
name 'Наименование' (LotInitReportStatus s) = staticCaption(s) IF s IS LotInitReportStatus CHARWIDTH 20;    
  
status 'Статус' = DATA LotInitReportStatus (ReportEZ);
codeStatus 'Код статуса' (ReportEZ r) = code(status(r));
nameStatus 'Статус' (ReportEZ r) = name(status(r));

EXTEND FORM lotInit
    OBJECTS r = ReportEZ
    PROPERTIES(r) READONLY id, nameStatus
    FILTERS lotInit(r) == i;
;

DESIGN lotInit {
    tabbedPane {
        NEW reports {
            caption = 'Отчеты'; 
            MOVE BOX(r);
        }
    }
}

DESIGN defaultNumerators {
    PROPERTY (nameDefaultNumeratorLotInit) { caption = 'Маркировка'; }
}
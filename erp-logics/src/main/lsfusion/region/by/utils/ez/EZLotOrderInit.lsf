MODULE EZLotOrderInit;

REQUIRE LotOrderInitBy, EZLotOrder, EZLotInit, LotBy;

NAMESPACE EZ;

//'Отчёт о маркировке'
lotInit  = DATA LOCAL LotInit ();
gtin  = DATA LOCAL STRING[14] ();
isSend 'Отправлен' (Lot l) = order(status(l)) >= order(LotStatus.MARKED);

select 'Отм.' = DATA LOCAL NESTED BOOLEAN (LotInitDetail);
select 'Отм.' (Lot l) = TRUE IF (GROUP SUM 1 IF quantity(LotInitDetail d, l) AND select(d));

EZActiveLegalEntity (LotInit i) = EZActive(legalEntityStock(i));

filterLot (Lot l) = (GROUP SUM quantity(LotInitDetail d, l) IF lotInit() == lotInit(d)) > 0 AND gtin() == gtin(l) 
    AND NOT isSend(l) AND
    (NOT (GROUP SUM 1 IF select(LotInitDetail d)) OR select(l));
    
exception 'Сообщение' = DATA TEXT (LotInit);
handleException (LotInit i, FILE exception) {
    logToFile('ez', messageCaughtException());
    
    logToFile('ez', exception);
    fileToString(exception);
    MESSAGE CONCAT ' ', 'Ошибка при обращении к ЭЗ', messageCaughtException();

    NEWSESSION NESTED (messageCaughtException, resultString) {
        exception(i) <- CONCAT ', ', messageCaughtException(), resultString();
        APPLY;
    }
}

EXTEND FORM lotInits
    PROPERTIES(i) READONLY exception PANEL SHOWIF exception(i)
;
DESIGN lotInits {
    tabbedPane {
        NEW ez {
            caption = 'Электронный знак';
            MOVE PROPERTY(exception(i)) { panelCaptionVertical = TRUE; fill = 1; };
        }
    }
}

getStatusReport 'Получить статус' (ReportEZ r) {
    EXPORT JSON FROM report_id  = id(r);         
              
    LOCAL result = FILE();   
    
    exception(lotInit(r)) <- NULL;
    getToken(legalEntityStock(lotInit(r)));
    ezToken() <- token(legalEntityStock(lotInit(r)));
    
    TRY {
        insecureSSL() <- TRUE;
        EXTERNAL HTTP POST urlEZ() + '/v2/reports' HEADERS ezHeadersLotOrder PARAMS exportFile() TO result;
    } CATCH {
        
        handleException(lotInit(r), result());
        RETURN;
    }       
    fileToString(result());
    //MESSAGE resultString();
    
    LOCAL statusCode = INTEGER ();
    
    FOR iterate(INTEGER i, 1, jsonAttributeCount(resultString())) AND 'status' == jsonAttributeKey(resultString(),i) DO {
        LOCAL subJson1 = STRING (); 
        subJson1() <- STRING(jsonAttributeValue(resultString(),i));        
                
        FOR iterate(INTEGER k, 1, jsonAttributeCount1(subJson1())) AND 'code' == jsonAttributeKey1(subJson1(),k) DO {
            statusCode() <- INTEGER(STRING(jsonAttributeValue1(subJson1(),k))); 
        }
    };   
    status(r) <- statusReport(statusCode());
    
    APPLY;
}    

getStatusReport 'Получить статус по всем' (LotInit i) {
    FOR ReportEZ r IF lotInit(r) == i DO {
        getStatusReport(r);
    }
}
    
FORM addMark 'Отчет о маркировке'

    PROPERTIES group = codeGroup(type(lotInit()))

    OBJECTS labels = Lot
    PROPERTIES (labels) code 
    FILTERS filterLot(labels)
;    

nameSku(LotInit i) = nameSku(GROUP MAX LotInitDetail d IF lotInit(d) == i);

getLabesTypes 'Типы кодов' (LotInit i) {          
    LOCAL result = FILE();       
    exception(i) <- NULL;
    getToken(legalEntityStock(i));
    ezToken() <- token(legalEntityStock(i));

    TRY {
        insecureSSL() <- TRUE;
        EXTERNAL HTTP GET urlEZ() + '/labels/types' HEADERS ezHeadersLotOrder TO result;
    } CATCH {
        handleException(i, result());
        RETURN;
    }       
    fileToString(result());
    MESSAGE resultString();
}


getParameters 'Получить параметры' (LotInit i) {          
    LOCAL result = FILE();   
    
    exception(i) <- NULL;
    getToken(legalEntityStock(i));
    ezToken() <- token(legalEntityStock(i));
    
    TRY {
        insecureSSL() <- TRUE;
        EXTERNAL HTTP POST urlEZ() + '/catalogs/10002' HEADERS ezHeadersLotOrder TO result; // Обувь
        fileToString(result());
//         EXTERNAL HTTP POST urlEZ() + '/catalogs/' + code()  HEADERS ezHeadersLotOrder TO result; // 
    } CATCH {
        fileToString(result());
        handleException(i, result());
        RETURN;
    }  
}    

getCatalogs 'Разделы каталога товаров' (LotInit i) {
          
    LOCAL result = FILE();
    
    exception(i) <- NULL;
    getToken(legalEntityStock(i));
    ezToken() <- token(legalEntityStock(i));
    
    TRY {
        insecureSSL() <- TRUE;
  //      EXTERNAL HTTP POST urlEZ() + '/catalogs/10002' HEADERS ezHeadersLotOrder TO result; // Обувь
         EXTERNAL HTTP POST urlEZ() + '/catalogs/'  HEADERS ezHeadersLotOrder TO result; // 
    } CATCH {
        handleException(i, result());
        RETURN;
    }       
    fileToString(result());
    MESSAGE resultString();        
} 

overGtin = ABSTRACT STRING[14] (LotInitDetail);

gtin 'GTIN' (LotInitDetail d) = OVERRIDE overGtin(d), gtin14(sku(d));

lastLotInitDetail(LotInit i, STRING[14] gtin) = GROUP LAST LotInitDetail d IF i == lotInit(d) AND gtin == gtin(d) ORDER d;

quantitySendLot 'Кол-во отправленных марок' (LotInitDetail d) = GROUP SUM quantity(d, Lot l) * count(l) IF isSend(l);
isSend 'Отправлен' (LotInitDetail d) = quantitySendLot(d) == quantityLot(d) AND quantitySendLot(d);
quantitySendLot 'Кол-во марок' (LotInit i) = GROUP SUM quantitySendLot(LotInitDetail d) IF lotInit(d) = i;
isSend 'Отправлен' (LotInit i) = quantitySendLot(i) == quantityLot(i) AND quantityLot(i);

// !!!смотреть список параметров и коды к ним по действию getParameters
// !!! getParameters брать только на проде api
// 101 Цель маркировки
// 100 Страна экспорта
// 97 Дата выпуска товаров по декларации
// 96 Регистрационный номер декларации на товары
// 95 Способ маркировки (полиграфическая защита)   
// 93 Тип производственного заказа 

markTargetProd () = '101';
markTargetTest () = '137';
markTarget 'Цель маркировки' () = IF testWebService() THEN markTargetTest() ELSE markTargetProd();

markMethodProd () = '95';
markMethodTest () = '124';
markMethod 'Способ маркировки (полиграфическая защита)' = IF testWebService() THEN markMethodTest() ELSE markMethodProd();

orderTypeProd () = '93';
orderTypeTest () = '123';
orderType 'Тип производственного заказа' = IF testWebService() THEN orderTypeTest() ELSE orderTypeProd();

// Режим «Комиссионная торговля» или «Перемаркировка (возврат товара)»
contractDateProd () = '102';
contractDateTest () = '136';
contractDate 'Основание: дата документа' = IF testWebService() THEN contractDateTest() ELSE contractDateProd();

contractNumberProd () = '103';
contractNumberTest () = '135';
contractNumber 'Основание: номер документа ' = IF testWebService() THEN contractNumberTest() ELSE contractNumberProd();

reasonMarkProd () = '104';
reasonMarkTest () = '134';
reasonMark 'Причина нанесения СИ' = IF testWebService() THEN reasonMarkTest() ELSE reasonMarkProd();


reasonRemarkProd () = '124';
reasonRemarkTest () = '153';
reasonRemark 'Причина перемаркировки' = IF testWebService() THEN reasonRemarkTest() ELSE reasonRemarkProd();

// разрешено формировать отчтет только по одному GTIN
addMarkImport ABSTRACT (LotInit, STRING[14]);
listParams = DATA LOCAL STRING();

addMark 'Отправить отчет'(LotInit i, STRING[14] gtin) {
    lotInit() <- i;
    gtin() <- gtin;
    
    // обновление статусов перед отправкой
    in(Lot l) <- filterLot(l);
    
    IF EZActive(legalEntityStock(i)) THEN
        getStatus(legalEntityStock(i));
    ELSE
        getStatus();
    
    IF (NOT GROUP SUM 1 IF filterLot (Lot l)) THEN {
        RETURN;
    }
    
    EXPORT addMark JSON;
    // Дополнительная обработка JSON к виду { "labels": ["01048113090167242128v8LZVn936rXO", "001048113090167242128v8LZVn936rXO"]}   
    LOCAL strResult = STRING ();
    fileToString(exportFile());   
//    strResult() <- replace(STRING(resultString()),'"code":[{"code":','"labels":[');
    strResult() <- replace(STRING(resultString()),'"code":','');    
    strResult() <- replace(strResult(),'\{','');
    strResult() <- replace(strResult(),'\}','');
    
    // add "params"
    listParams() <- '';
    
    // !!!смотреть список параметров и коды к ним по действию getParameters
    // !!! getParameters брать только на проде api
    // 101 Цель маркировки
    // 100 Страна экспорта
    // 97 Дата выпуска товаров по декларации
    // 96 Регистрационный номер декларации на товары
    // 95 Способ маркировки (полиграфическая защита)   
    // 93 Тип производственного заказа 
    CASE
        // тестовые
//        listParams() <- listParams() + ' \{ "code": 137, "value": "' + codeMode(i) + '"\}';              
//        IF codeProductionType(i) THEN listParams() <- listParams() + ', \{ "code": 123, "value": "' + codeProductionType(i) + '"\}'; 
//        IF codeMethod(i) THEN listParams() <- listParams() + ', \{ "code": 124, "value": "' + codeMethod(i) + '"\}';           
                    
        WHEN mode(i) == LotInitMode.PRODUCTION THEN {            
            listParams() <- listParams() + ' \{ "code": ' + markTarget() + ', "value": "' + codeMode(i) + '"\}';              
            IF codeProductionType(i) THEN listParams() <- listParams() + ', \{ "code": ' + orderType() + ', "value": "' + codeProductionType(i) + '"\}'; 
            IF codeMethod(i) THEN listParams() <- listParams() + ', \{ "code": ' + markMethod() + ', "value": "' + codeMethod(i) + '"\}';            
        }
        WHEN mode(i) == LotInitMode.IMPORT_ THEN {
            addMarkImport(i, gtin);
        }
        WHEN mode(i) == LotInitMode.COMMISSION THEN {
            listParams() <- listParams() + ' \{ "code": ' + markTarget() + ', "value": "' + codeMode(i) + '"\}';

            IF codeMethod(i) THEN listParams() <- listParams() + ', \{ "code": ' + markMethod() + ', "value": "' + codeMethod(i) + '"\}';

            IF contractDate(i) THEN {
                listParams() <- listParams() + ', \{ "code": ' + contractDate() + ', "value": "' + toChar(contractDate(i),'YYYY-MM-DD') + '"\}';
            } ELSE {
                MESSAGE 'Не задана дата документа основания' NOWAIT;
            }

            IF contractNumber(i) THEN {
                listParams() <- listParams() + ', \{ "code": ' + contractNumber() + ', "value": "' + contractNumber(i) + '"\}';
            } ELSE {
                MESSAGE 'Не задан номер документа основания' NOWAIT;
            }

            IF reasonMark(i) THEN {
                listParams() <- listParams() + ', \{ "code": ' + reasonMark() + ', "value": "' + codeReasonMark(i) + '"\}';
            } ELSE {
                MESSAGE 'Не задана причина нанесения СИ' NOWAIT;
            }

            IF NOT codeReasonMark(i) OR NOT contractNumber(i) OR NOT toChar(contractDate(i),'YYYY-MM-DD') THEN
                RETURN;
        }
        WHEN mode(i) == LotInitMode.REMARK THEN {
            listParams() <- listParams() + ' \{ "code": ' + markTarget() + ', "value": "' + codeMode(i) + '"\}';

            IF codeMethod(i) THEN listParams() <- listParams() + ', \{ "code": ' + markMethod() + ', "value": "' + codeMethod(i) + '"\}';

            IF contractDate(i) THEN {
                listParams() <- listParams() + ', \{ "code": ' + contractDate() + ', "value": "' + toChar(contractDate(i),'YYYY-MM-DD') + '"\}';
            } ELSE {
                MESSAGE 'Не задана дата документа основания' NOWAIT;
            }

            IF contractNumber(i) THEN {
                listParams() <- listParams() + ', \{ "code": ' + contractNumber() + ', "value": "' + contractNumber(i) + '"\}';
            } ELSE {
                MESSAGE 'Не задан номер документа основания' NOWAIT;
            }

            IF reasonMark(i) THEN {
                listParams() <- listParams() + ', \{ "code": ' + reasonMark() + ', "value": "' + codeReasonMark(i) + '"\}';
            } ELSE {
                MESSAGE 'Не задана причина нанесения СИ' NOWAIT;
            }

            IF reasonRemark(i) THEN {
                listParams() <- listParams() + ', \{ "code": ' + reasonRemark() + ', "value": "' + codeReasonRemark(i) + '"\}';
            }

            IF NOT codeReasonMark(i) OR NOT contractNumber(i) OR NOT toChar(contractDate(i),'YYYY-MM-DD') THEN
                RETURN;
        };
        
    strResult() <- '\{' + strResult() + ', "params": [' + listParams() + ']' + '\}';
     
    stringToFile(strResult());
      
    LOCAL result = FILE();      

    exception(i) <- NULL;
    getToken(legalEntityStock(i));
    ezToken() <- token(legalEntityStock(i));
    
    TRY {
        insecureSSL() <- TRUE;
        EXTERNAL HTTP POST urlEZ() + '/v2/reports/addMark' HEADERS ezHeadersLotOrder PARAMS resultFile() TO result;
        //isSend(i) <- TRUE;
    } CATCH {
        handleException(i, result());
        RETURN;
    }       
    fileToString(result());       
//    MESSAGE resultString();
    
    // Получение id отчета    
    FOR iterate(INTEGER k, 1, jsonAttributeCount(resultString())) AND 'report_id' == jsonAttributeKey(resultString(),k) DO {
         NEW r = ReportEZ {
            lotInit(r) <- i;
            id(r) <- replace(STRING(jsonAttributeValue(resultString(),k)),'"','');
            getStatusReport(r);
         }  
    }
        
    APPLY;    
}

addMark 'Отправить отчет'(LotInit i) {
    FOR (STRING[14] gtin) AND ((GROUP SUM 1 IF gtin == gtin(LotInitDetail d) AND i == lotInit(d)) > 0) DO {
        addMark(i, gtin);
    }

    in(Lot l) <- lotInit(l) == i AND NOT isSend(l) WHERE lotInit(l) == i;
    
    // получение статусов для марок
    IF EZActive(legalEntityStock(i)) THEN {
        getStatus(legalEntityStock(i));
    } 
    ELSE {
        getStatus();
    }
    APPLY;
}

EXTEND FORM lotInits 
    PROPERTIES (i) addMark SHOWIF isPosted(i) AND NOT isSend(i)//, getParameters//, getLabesTypes, getCatalogs
  //  PROPERTIES() code
;

DESIGN lotInits {
    tabbedPane {        
        ez {
            MOVE PROPERTY(addMark(i));
//            MOVE PROPERTY(code());
//            MOVE PROPERTY(getParameters(i));
//            MOVE PROPERTY(getLabesTypes(i));
//            MOVE PROPERTY(getCatalogs(i));
        }
    }
}

EXTEND FORM lotInit 
    PROPERTIES(i) isSend
;

DESIGN lotInit {
    headerRow1 {
        MOVE PROPERTY(isSend(i));
    }
}

getStatusApply 'Получить статус' (LotInit i, Lot l) {
    getStatusApply(l, legalEntityStock(i));
}

EXTEND FORM lotInit   
    PROPERTIES(r) getStatusReport TOOLBAR
    PROPERTIES(i) getStatusReport DRAW r TOOLBAR
    PROPERTIES(d) READONLY isSend, quantitySendLot
    PROPERTIES(l) READONLY nameStatus, isSend
    PROPERTIES(l) getStatusApply TOOLBAR SHOWIF NOT EZActiveLegalEntity(i)
    PROPERTIES(i, l) getStatusApply TOOLBAR DRAW l SHOWIF EZActiveLegalEntity(i)
;

EXTEND FORM lotInits   
    PROPERTIES(i) READONLY isSend
    PROPERTIES(d) READONLY isSend
;

//EXTEND FORM lots
//    PROPERTIES(l) READONLY isSend
//;

addMark 'Отправить отчет'(LotInitDetail d) {
    IF (NOT GROUP SUM 1 IF select(LotInitDetail dd) AND lotInit(d) == lotInit(dd)) THEN select(d) <- TRUE;
    
    addMark(lotInit(d));
    
    select(LotInitDetail dd) <- NULL;    
}

EXTEND FORM lotInit
    PROPERTIES (d) select BEFORE index(d), addMark SHOWIF isPosted(i) AND NOT isSend(d)
;

DESIGN lotInit {
    spec {
        NEW tabbedSpec {
            tabbed = TRUE;
            fill = 1;
            MOVE BOX(l) { caption = 'Марки'; }
            
            NEW ez {        
                caption = 'Электронный знак';
                MOVE PROPERTY(addMark(d));
            }
        }
    }   
}

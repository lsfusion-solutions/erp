MODULE LabelBy;

REQUIRE LabelItem;

NAMESPACE Label;

//цена за литр\кг

@defineItemGroupValue(printFullPrice, 'Печатать цену за килограмм(литр)', BOOLEAN);

@defineItemGroupValue(volumeMinPrice, 'Минимальный объем/вес для печати цены за килограмм(литр)', NUMERIC[16,5]);

labelBaseCoeff = ABSTRACT CASE NUMERIC[9,3](Sku);
dataLabelBaseCoeff 'Коэффициент за базовую единицу' = DATA NUMERIC[9,3](Item);
labelBaseCoeff(Item i) += WHEN dataLabelBaseCoeff(i) THEN dataLabelBaseCoeff(i);

labelBaseName = ABSTRACT CASE STRING[20](Sku);
dataLabelBaseName 'Базовая единица' = DATA STRING[20](Item);
labelBaseName(Item i) += WHEN dataLabelBaseName(i) THEN dataLabelBaseName(i);

EXTEND FORM item 
    PROPERTIES (i) dataLabelBaseCoeff, dataLabelBaseName;
    
DESIGN item {
    itemLabel {
        MOVE PROPERTY (dataLabelBaseCoeff(i));
        MOVE PROPERTY (dataLabelBaseName(i));
    }
}

volumeMinPrice (Sku d) = OVERRIDE volume(d) < volumeMinPrice(skuGroup(d)), netWeight(d) < volumeMinPrice(skuGroup(d));

skipFullPrice 'Не печатать цену за килограмм(литр)' = ABSTRACT BOOLEAN (Sku);
skipFullPrice 'Не печатать цену за килограмм(литр)'  = DATA BOOLEAN (Item) IN itemBase;
skipFullPrice(Item i) += skipFullPrice(i);

EXTEND FORM items
    PROPERTIES (i) READONLYIF isReadonly() skipFullPrice
;
EXTEND FORM item
    PROPERTIES (i) skipFullPrice
;


basePrice 'Цена за базовую единицу' (Sku d, NUMERIC[16,4] price) = IF printFullPrice(skuGroup(d)) AND NOT (volumeMinPrice(d) OR skipFullPrice(d))  THEN CASE 
                                                                                                          WHEN labelBaseCoeff(d) THEN round2(price / labelBaseCoeff(d))
                                                                                                          WHEN volume(d) != 1.0 THEN round2(price / volume(d))
                                                                                                          WHEN netWeight(d) != 1.0 THEN round2(price / netWeight(d)) ;
basePrice 'Цена за базовую единицу' (LabelTransactionDetail d) = basePrice(sku(d), price(d));
                                                                                                                                                                                                                  
basePriceName 'Базовая единица' (Sku d) = IF printFullPrice(skuGroup(d)) AND NOT (volumeMinPrice(d) OR skipFullPrice(d)) THEN CASE 
                                                                                                          WHEN labelBaseName(d) THEN labelBaseName(d)
                                                                                                          WHEN volume(d) != 1.0 THEN '1л'
                                                                                                          WHEN netWeight(d) != 1.0 THEN '1кг' ;
basePriceName 'Базовая единица' (LabelTransactionDetail d) = basePriceName(sku(d));
                                                                                                          
basePriceRub 'Рубли (за б.е.)' (Sku d, NUMERIC[16,4] price) = trunc(basePrice(d, price));
basePriceRub 'Рубли (за б.е.)' (LabelTransactionDetail d) = basePriceRub(sku(d), price(d));

basePriceKop 'Копейки (за б.е.)' (Sku d, NUMERIC[16,4] price) = OVERRIDE round0((basePrice(d, price) (-) basePriceRub(d, price)) * 100.0), 0.00 IF basePrice(d, price);
basePriceKop 'Копейки (за б.е.)' (LabelTransactionDetail d) = basePriceKop(sku(d), price(d));
basePriceKopText 'Копейки (за б.е.)' (LabelTransactionDetail d) = lpad(STRING[2](INTEGER(basePriceKop(sku(d), price(d)))) , 2, '0') ;

baseRetailPrice 'Цена за базовую единицу до' (Sku d, NUMERIC[16,4] retailPrice) = IF printFullPrice(skuGroup(d)) AND NOT (volumeMinPrice(d) OR skipFullPrice(d)) THEN CASE 
                                                                                                          WHEN labelBaseCoeff(d) THEN round2(retailPrice / labelBaseCoeff(d))
                                                                                                          WHEN volume(d) != 1.0 THEN round2(retailPrice / volume(d))
                                                                                                          WHEN netWeight(d) != 1.0 THEN round2(retailPrice / netWeight(d)) ;
baseRetailPrice 'Цена за базовую единицу до' (LabelTransactionDetail d) = baseRetailPrice(sku(d), retailPrice(d));

baseRetailPriceRub 'Рубли (за б.е. до)' (Sku d, NUMERIC[16,4] retailPrice) = trunc(baseRetailPrice(d, retailPrice));
baseRetailPriceRub 'Рубли (за б.е. до)' (LabelTransactionDetail d) = baseRetailPriceRub(sku(d), retailPrice(d));

baseRetailPriceKop 'Копейки (за б.е. до)' (Sku d, NUMERIC[16,4] retailPrice) = OVERRIDE round0((baseRetailPrice(d, retailPrice) (-) baseRetailPriceRub(d, retailPrice)) * 100.0), 0.00 IF baseRetailPrice(d, retailPrice);
baseRetailPriceKop 'Копейки (за б.е. до)' (LabelTransactionDetail d) = baseRetailPriceKop(sku(d), retailPrice(d));
baseRetailPriceKopText 'Копейки (за б.е. до)' (LabelTransactionDetail d) = lpad(STRING[2](INTEGER(baseRetailPriceKop(sku(d), retailPrice(d)))) , 2, '0') ;
                                                                                                          
discountSumRub 'Рубли (сумма скидки)' (LabelTransactionDetail d) = trunc(discountSum(d));
discountSumKop 'Копейки (сумма скидки)' (LabelTransactionDetail d) =  OVERRIDE round0((discountSum(d) (-) discountSumRub(d)) * 100.0), 0.00 IF discountSum(d);
discountSumKopText 'Копейки (сумма скидки)' (LabelTransactionDetail d) = lpad(STRING[2](INTEGER(discountSumKop(d))), 2, '0') ;

EXTEND FORM printLabelTransaction PROPERTIES(d) READONLY basePrice, basePriceName, basePriceRub, basePriceKop, basePriceKopText, baseRetailPrice, baseRetailPriceRub, baseRetailPriceKop, baseRetailPriceKopText, discountSumRub, discountSumKopText;
EXTEND FORM customLabelTransaction PROPERTIES(d) basePrice, basePriceName, basePriceRub, basePriceKop, basePriceKopText, baseRetailPrice, baseRetailPriceRub, baseRetailPriceKop, baseRetailPriceKopText, discountSumRub, discountSumKopText;
EXTEND FORM labelTransactions 
    PROPERTIES(dt) READONLY basePrice, basePriceName, basePriceRub, basePriceKop, basePriceKopText, baseRetailPrice, baseRetailPriceRub, baseRetailPriceKop, baseRetailPriceKopText, discountSumRub, discountSumKopText
    PROPERTIES(dts) READONLY basePrice, basePriceName, basePriceRub, basePriceKop, basePriceKopText, baseRetailPrice, baseRetailPriceRub, baseRetailPriceKop, baseRetailPriceKopText, discountSumRub, discountSumKopText
;

//----------------------- Переход на новый основной классификатор ---------------------------//
overReplaceToItemGroup(CustomGroup g, ItemGroup ng) + {
    dataPrintFullPrice(ng) <- prevPrintFullPrice(itemGroup(g));
    dataVolumeMinPrice(ng) <- prevVolumeMinPrice(itemGroup(g));
}
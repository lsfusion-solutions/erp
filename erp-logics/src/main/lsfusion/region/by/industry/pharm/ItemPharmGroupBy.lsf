MODULE ItemPharmGroupBy;

REQUIRE Item, Pharm;

NAMESPACE Item;

CLASS PharmacyPriceGroup 'Ценовая группа': Group;
TABLE pharmacyPriceGroup(PharmacyPriceGroup);

name 'Наименование' = DATA ISTRING[50](PharmacyPriceGroup);
@defineExternalizable(pharmacyPriceGroup, STRING[100]);
name(PharmacyPriceGroup group) += name(group);

inactive 'Неактивный' = DATA BOOLEAN (PharmacyPriceGroup) IN itemBase;
active 'Активный' (PharmacyPriceGroup group) = group IS PharmacyPriceGroup AND NOT inactive(group);

is366 '366-й указ' = DATA BOOLEAN (PharmacyPriceGroup);
is713 '713-е постановление' = DATA BOOLEAN (PharmacyPriceGroup);

sumMarkups 'Суммировать надбавки опта и розницы по 366 указу для расценки' = DATA BOOLEAN (PharmacyPriceGroup) IN itemBase;

CONSTRAINT SET(sumMarkups(PharmacyPriceGroup pg)) AND NOT is366(pg) 
    MESSAGE 'Нельзя выставлять данные признак для ценовых групп не относящихся к 366 указу';

maxTotalLimitMarkup 'Максимальная общая надбавка опта и розницы' =  DATA NUMERIC[8,3] (PharmacyPriceGroup) IN itemBase;

setIs713 = DATA BOOLEAN ();

onStarted() + {
    NEWSESSION {
        IF NOT setIs713() THEN {
            applyOnlyData();
            setIs713() <- TRUE;
            IF NOT (GROUP SUM 1 IF is713(PharmacyPriceGroup pg)) THEN {
                is713(PharmacyPriceGroup g) <- TRUE WHERE maxTotalLimitMarkup(g);
            }
            APPLY;
        }
    }
}

//отключил классификатор за его ненадобностью
//CLASS PharmacyGroupType 'Классификатор ценовых групп': GroupType;
//TABLE pharmacyGroupType (PharmacyGroupType);

//name 'Наименование' = DATA ISTRING[100](PharmacyGroupType) IN id CHARWIDTH 25;
//name(PharmacyGroupType group)+=name(group);

//pharmacyTypeGroup = DATA PharmacyGroupType(PharmacyPriceGroup);
//namePharmacyTypeGroup 'Наименование классификатора' = name(pharmacyTypeGroup(PharmacyPriceGroup group));

//groupType(PharmacyPriceGroup group) += pharmacyTypeGroup(group);

pharmacyPriceGroup = ABSTRACT PharmacyPriceGroup (Sku) MATERIALIZED;
namePharmacyPriceGroup 'Ценовая группа' (Sku sku) = name(pharmacyPriceGroup(sku));

is366PharmacyPriceGroup (Sku s) = is366(pharmacyPriceGroup(s));
isNot366PharmacyPriceGroup (Sku s) = id(pharmacyPriceGroup(s)) AND NOT is366(pharmacyPriceGroup(s));

is713PharmacyPriceGroup (Sku s) = is713(pharmacyPriceGroup(s));
isNot713PharmacyPriceGroup (Sku s) = id(pharmacyPriceGroup(s)) AND NOT is713(pharmacyPriceGroup(s));

calcMaxTotalLimitMarkup(Sku s, NUMERIC[8,4] baseMarkup) = trunc(((100.0 + maxTotalLimitMarkup(pharmacyPriceGroup(s)))
    /(100.0 + baseMarkup)-1)*100, 2);

//group(PharmacyGroupType type, Sku sku) += WHEN type IS PharmacyGroupType AND sku IS Sku THEN pharmacyPriceGroup(sku);

@defineObjectItemAttribute(pharmacyPriceGroup, PharmacyPriceGroup, name, 'Ценовая группа', itemPharmacy);

EXTEND FORM item 
    PROPERTIES (i) namePharmacyPriceGroup SHOWIF showPharmacyPriceGroup(i)
;

EXTEND FORM items 
    PROPERTIES (i) namePharmacyPriceGroup SHOWIF showPharmacyPriceGroup(i) READONLYIF isReadonly()
;

// Продублировал дизайн из ItemPharm, т.к. у некоторых ERP клиентов не подключен ItemPharm
// При изменении необходимо менять в обоих модулях
@defineItemPharmDesign();

@defineObjectItemAttributeBatch (pharmacyPriceGroup, namePharmacyPriceGroup, 'Ценовая группа', sku);
pharmacyPriceGroup(Item sku) += pharmacyPriceGroup(sku);

CLASS PharmacyPriceInterval 'Интервал цен';
TABLE pharmacyPriceInterval(PharmacyPriceInterval);

pharmacyGroupPharmacy= DATA PharmacyPriceGroup (PharmacyPriceInterval) NONULL DELETE;
namePharmacyGroupPharmacy 'Ценовая группа' (PharmacyPriceInterval interval) = name(pharmacyGroupPharmacy(interval));

pharmacyTypeExchange = DATA TypeExchange ();
namePharmacyTypeExchange 'Тип обмена для ценовых групп' ()= name(pharmacyTypeExchange());

EXTEND FORM options
    PROPERTIES() namePharmacyTypeExchange
;
DESIGN options {
    pharmacy {
        MOVE PROPERTY(namePharmacyTypeExchange());
    }
}

pricePharmacy 'Цена от (Базовая величина)' = DATA NUMERIC[16,4] (PharmacyPriceInterval) NONULL DELETE;

fromDatePharmacy 'Дата действия с' = DATA DATE (PharmacyPriceInterval) NONULL DELETE;
toDatePharmacy 'Дата действия по' = DATA DATE (PharmacyPriceInterval);

TABLE pharmacyPriceGroupPharmacyPriceInterval(PharmacyPriceGroup, PharmacyPriceInterval);

wholesaleMarkup '% оптовой надбавки' = DATA NUMERIC[8,3] (PharmacyPriceInterval);
retailMarkup '% розничной надбавки' = DATA NUMERIC[8,3] (PharmacyPriceInterval);
pharmacyExchange (price, DATE date) = price / rateOn(pharmacyTypeExchange(), currencyShortName('БВ'), date);

wholesaleLimitMarkupPharmacy 'Предельный % оптовой надбавки' (priceGroup, NUMERIC[16,4] price, DATE date) =
    GROUP LAST wholesaleMarkup(PharmacyPriceInterval interval)
          ORDER pricePharmacy(interval), interval
          WHERE pharmacyExchange(price AS NUMERIC[16,4], date) > pricePharmacy(interval) AND
                date >= fromDatePharmacy(interval) AND NOT date > toDatePharmacy(interval)
          BY pharmacyGroupPharmacy(interval) COMPLEX;
prevWholesaleLimitMarkupPharmacy 'Предельный % оптовой надбавки (пред.)' (PharmacyPriceGroup priceGroup, NUMERIC[16,4] price, DATE date) = PREV(wholesaleLimitMarkupPharmacy(priceGroup, price, date)) COMPLEX;

retailLimitMarkupPharmacy 'Предельный % розничной надбавки' (priceGroup, NUMERIC[16,4] price, DATE date) =
    GROUP LAST retailMarkup(PharmacyPriceInterval interval)
          ORDER pricePharmacy(interval), interval
          WHERE pharmacyExchange(price AS NUMERIC[16,4], date) > pricePharmacy(interval) AND
                date >= fromDatePharmacy(interval) AND NOT date > toDatePharmacy(interval)
          BY pharmacyGroupPharmacy(interval) COMPLEX;
prevRetailLimitMarkupPharmacy 'Предельный % розничной надбавки (пред.)' (PharmacyPriceGroup priceGroup, NUMERIC[16,4] price, DATE date) = PREV(retailLimitMarkupPharmacy(priceGroup, price, date)) COMPLEX;

wholesaleLimitMarkupPharmacy 'Предельный % оптовой надбавки' (Sku sku, NUMERIC[16,4] price, DATE date) =  wholesaleLimitMarkupPharmacy(pharmacyPriceGroup(sku), price, date) COMPLEX;
prevWholesaleLimitMarkupPharmacy 'Предельный % оптовой надбавки (пред.)' (Sku sku, NUMERIC[16,4] price, DATE date) =  prevWholesaleLimitMarkupPharmacy(pharmacyPriceGroup(sku), price, date);
wholesaleLimitMarkupPharmacy 'Предельный % оптовой надбавки' (Batch batch, NUMERIC[16,4] price, DATE date) =  wholesaleLimitMarkupPharmacy(sku(batch), price, date);

retailLimitMarkupPharmacy 'Предельный % розничной надбавки' (Sku sku, NUMERIC[16,4] price, DATE date) =  retailLimitMarkupPharmacy(pharmacyPriceGroup(sku), price, date);
prevRetailLimitMarkupPharmacy 'ППредельный % розничной надбавки (пред.)' (Sku sku, NUMERIC[16,4] price, DATE date) =  prevRetailLimitMarkupPharmacy(pharmacyPriceGroup(sku), price, date);
retailLimitMarkupPharmacy 'Предельный % розничной надбавки' (Batch batch, NUMERIC[16,4] price, DATE date) =  retailLimitMarkupPharmacy(sku(batch), price, date);

isActive 'Действующий' (PharmacyPriceInterval interval) = 
    (toDatePharmacy(interval) >= currentDate() OR NOT toDatePharmacy(interval)) AND fromDatePharmacy(interval) <= currentDate();

FORM pharmacyMarkups 'Ценовые группы'

    OBJECTS g=PharmacyPriceGroup
    PROPERTIES(g) inactive, name, id SHOWIF showIDs(), maxTotalLimitMarkup, is366, sumMarkups, is713
    PROPERTIES(g) NEW, DELETE GRID
    ORDERS id(g)
    FILTERGROUP active
        FILTER 'Активные' active(g) DEFAULT

    OBJECTS i=PharmacyPriceInterval
    PROPERTIES fromDatePharmacy(i), toDatePharmacy(i), pricePharmacy(i),
               wholesaleMarkup(i), retailMarkup(i)
    PROPERTIES(i) NEW, DELETE GRID
    FILTERS    pharmacyGroupPharmacy(i)==g
    FILTERGROUP isActive
        FILTER 'Действующие' isActive(i) DEFAULT
    ORDERS pricePharmacy(i)

    OBJECTS nu=NUMERIC[14,3] PANEL
    PROPERTIES val = VALUE(nu)

    OBJECTS t=DATE PANEL
    PROPERTIES value = VALUE(t)
    PROPERTIES(g, nu, t) READONLY wholesaleLimitMarkupPharmacy, retailLimitMarkupPharmacy

;

DESIGN pharmacyMarkups {
    BOX {
        MOVE BOX(g);
        MOVE BOX(i);
        NEW test {
            caption = 'Тестовая форма';
            horizontal = TRUE;
            MOVE PROPERTY (value) {
                caption = 'Выберите дату';
                panelCaptionVertical = TRUE;
                font = 'bold 24';
            }
            MOVE PROPERTY (val) {
                caption = 'Введите цену в рублях';
                panelCaptionVertical = TRUE;
                font = 'bold 24';
            }

            MOVE PROPERTY (wholesaleLimitMarkupPharmacy(g, nu, t)) {
                caption = 'Результат (оптовая надбавка)';
                panelCaptionVertical = TRUE;
                font = 'bold 24';
            }
            MOVE PROPERTY (retailLimitMarkupPharmacy(g, nu, t)) {
                caption = 'Результат (розничная надбавка)';
                panelCaptionVertical = TRUE;
                font = 'bold 24';
            }
        }
        MOVE TOOLBARBOX;
    }
}

NAVIGATOR {
    priceListMasterData {
        NEW pharmacyMarkups;
    }
}

FORM pharmacyPriceGroups 'Ценовые группы'
    OBJECTS g=PharmacyPriceGroup
    PROPERTIES(g) READONLY inactive, name, id SHOWIF showIDs(), maxTotalLimitMarkup, is366, sumMarkups, is713
    ORDERS id(g)
    FILTERGROUP active
        FILTER 'Активные' active(g) DEFAULT

    OBJECTS i=PharmacyPriceInterval
    PROPERTIES READONLY fromDatePharmacy(i), toDatePharmacy(i), pricePharmacy(i),
               wholesaleMarkup(i), retailMarkup(i)
    FILTERS    pharmacyGroupPharmacy(i)==g
    ORDERS pricePharmacy(i)
    FILTERGROUP isActive
        FILTER 'Действующие' isActive(i) DEFAULT

    LIST PharmacyPriceGroup OBJECT g
;

DESIGN pharmacyPriceGroups {
    BOX {
        size = (1024, 768);
    }
}

@defineUniteAttributeItemWithoutExtendForm(pharmacyPriceGroup, namePharmacyPriceGroup, 'ценовая группа', 'ценовые группы', item);

EXTEND FORM attributesItem
    OBJECTS pharmacyPriceGroup=PharmacyPriceGroup
    PROPERTIES in(pharmacyPriceGroup)       
    PROPERTIES(pharmacyPriceGroup) inactive, name, id SHOWIF showIDs(), maxTotalLimitMarkup, is366, sumMarkups, is713
    PROPERTIES(pharmacyPriceGroup) NEW, DELETE         
    PROPERTIES replace(pharmacyPriceGroup) TOOLBAR        
    ORDERS id(pharmacyPriceGroup)
    FILTERGROUP active
        FILTER 'Активные' active(pharmacyPriceGroup) DEFAULT    

    OBJECTS pharmacyPriceInterval=PharmacyPriceInterval
    PROPERTIES (pharmacyPriceInterval) fromDatePharmacy, toDatePharmacy, pricePharmacy,
                                       wholesaleMarkup, retailMarkup
    PROPERTIES (pharmacyPriceInterval) NEW, DELETE GRID
    FILTERS    pharmacyGroupPharmacy(pharmacyPriceInterval)==pharmacyPriceGroup
    ORDERS pricePharmacy(pharmacyPriceInterval)
    FILTERGROUP isActive
        FILTER 'Действующие' isActive(pharmacyPriceInterval) DEFAULT    

    OBJECTS numeric=NUMERIC[14,3] PANEL
    PROPERTIES val = VALUE(numeric)

    OBJECTS date=DATE PANEL
    PROPERTIES value = VALUE(date)
    PROPERTIES(pharmacyPriceGroup, numeric, date) READONLY wholesaleLimitMarkupPharmacy,retailLimitMarkupPharmacy
;

DESIGN attributesItem{
    tabContainer{
        NEW pharmacyPriceGroupContainer{
            caption = 'ценовые группы';
            MOVE BOX(pharmacyPriceGroup);
            MOVE BOX(pharmacyPriceInterval);
            NEW test {
                caption = 'Тестовая форма';
                horizontal = TRUE;
                MOVE PROPERTY (value) {
                    caption = 'Выберите дату';
                    panelCaptionVertical = TRUE;
                    font = 'bold 24';
                }
                MOVE PROPERTY (val) {
                    caption = 'Введите цену в рублях';
                    panelCaptionVertical = TRUE;
                    font = 'bold 24';
                }
    
                MOVE PROPERTY (wholesaleLimitMarkupPharmacy(pharmacyPriceGroup, numeric, date)) {
                    caption = 'Результат (оптовая надбавка)';
                    panelCaptionVertical = TRUE;
                    font = 'bold 24';
                }
                MOVE PROPERTY (retailLimitMarkupPharmacy(pharmacyPriceGroup, numeric, date)) {
                    caption = 'Результат (розничная надбавка)';
                    panelCaptionVertical = TRUE;
                    font = 'bold 24';
                }
            }
        }
    }
}

// ----------------------------------- Стандартные данные ----------------------------------- //

loadDefaultPharmacyPriceGroup 'Добавить ценовые группы'(ISTRING[50] iname, STRING[100] isid)  { 
    NEW g = PharmacyPriceGroup {
         name(g) <- iname;
         id(g) <- isid;
    }
}

loadDefaultPharmacyPriceInterval 'Добавить интервал цен ценовых групп (by)'(STRING[100] sidGroup, NUMERIC[16,4] priceFrom, NUMERIC[8,3] wMarkup, NUMERIC[8,3] rMarkup)  { 
    NEW i = PharmacyPriceInterval {
         pharmacyGroupPharmacy(i) <- pharmacyPriceGroup(sidGroup);
         pricePharmacy(i) <- priceFrom;
         fromDatePharmacy(i) <- 2016_07_01;
         wholesaleMarkup(i) <- wMarkup;
         retailMarkup(i) <- rMarkup;
    }
}


loadDefaultPharmacyPriceGroups 'Загрузить стандартные ценовые группы(указ 366) и надбавки для них' ()  { 
    loadDefaultPharmacyPriceGroup('Лекарственные средства', 'Лекарственные средства');
        is366(pharmacyPriceGroup('Лекарственные средства')) <- TRUE;
        loadDefaultPharmacyPriceInterval('Лекарственные средства', 0.00, 9.00, 30.00);
        loadDefaultPharmacyPriceInterval('Лекарственные средства', 0.50, 8.00, 25.00);
        loadDefaultPharmacyPriceInterval('Лекарственные средства', 1.00, 7.00, 14.00);
        loadDefaultPharmacyPriceInterval('Лекарственные средства', 1.50, 7.00, 12.00);
        loadDefaultPharmacyPriceInterval('Лекарственные средства', 3.00, 6.00, 10.00);
        loadDefaultPharmacyPriceInterval('Лекарственные средства', 5.00, 4.00, 5.00);
        loadDefaultPharmacyPriceInterval('Лекарственные средства', 10.00, 2.00, 1.00);

    loadDefaultPharmacyPriceGroup('Изделия медицинского назначения', 'Изделия медицинского назначения');
        is366(pharmacyPriceGroup('Изделия медицинского назначения')) <- TRUE;
        loadDefaultPharmacyPriceInterval('Изделия медицинского назначения', 0.00, 11.00, 30.00);
        loadDefaultPharmacyPriceInterval('Изделия медицинского назначения', 0.50, 8.00, 25.00);
        loadDefaultPharmacyPriceInterval('Изделия медицинского назначения', 1.00, 8.00, 21.00);
        loadDefaultPharmacyPriceInterval('Изделия медицинского назначения', 1.50, 8.00, 17.00);
        loadDefaultPharmacyPriceInterval('Изделия медицинского назначения', 5.00, 4.00, 6.00);

    loadDefaultPharmacyPriceGroup('Медицинская техника', 'Медицинская техника');
        is366(pharmacyPriceGroup('Медицинская техника')) <- TRUE;
        loadDefaultPharmacyPriceInterval('Медицинская техника', 0.00, 20.00, 20.00);
        loadDefaultPharmacyPriceInterval('Медицинская техника', 1000.00, 10.00, 0.00);
        loadDefaultPharmacyPriceInterval('Медицинская техника', 5000.00, 8.00, 0.00);
        loadDefaultPharmacyPriceInterval('Медицинская техника', 50000.00, 5.00, 0.00);
        loadDefaultPharmacyPriceInterval('Медицинская техника', 100000.00, 3.00, 0.00);

    loadDefaultPharmacyPriceGroup('Прочее', 'Прочее');        

}IN loadDefault;

@implementLoadDefaultData(loadDefaultPharmacyPriceGroups);

migratePharmRateExchange = DATA BOOLEAN ();
onStarted() +{
    IF NOT migratePharmRateExchange() THEN {
        IF NOT currencyShortName('БВ') THEN NEW c = Currency {
            shortName(c) <- 'БВ';
            name(c) <- 'Базовая величина';
        }
        FOR TypeExchange t==pharmacyTypeExchange() DO {
            name(t) <- 'Базовая величина';
            currency(t) <- currencyShortName('BYN');
        }
        FOR rate(pharmacyTypeExchange(), currencyShortName('USD'), DATE date) DO {
            rate(pharmacyTypeExchange(), currencyShortName('БВ'), date) <- rate(pharmacyTypeExchange(), currencyShortName('USD'), date);
            rate(pharmacyTypeExchange(), currencyShortName('USD'), date) <- NULL WHERE rate(pharmacyTypeExchange(), currencyShortName('USD'), date);
        }
        migratePharmRateExchange() <- TRUE; 
    }
}

//Делаем через общий метакод, что бы методики рассчёта надбавок была схожей в опте и рознице
//Менять сразу во всех метакодах
//Полагаем, что в фарме НЕ будут включать глобальный skipCalcPricingManufacturingPrice(), т.к. в этом нет смысла

META defineNotUseManufacturingPrice (class, basePrice, batchProp)
    //оставляем возможность задать свои условия не использования цены изготовителя как базы для рассчёта надбавки
    extraNotUseManufacturingPrice = ABSTRACT BOOLEAN (###class);
    //вынес в отдельное свойство, что бы все свойтсва не фиксирующиеся в документе собрать в одном месте и зафиксировать в отдельной переменной
    //иначе при изменении свойтсв задним числом параметров из calc свойства будут запущены события для "старых" документов
    //поставщик предоставил скидку на товар с предельной надбавкой по 713, поэтому берем цену поставки, а не изготовителя    
    calcNotUseManufacturingPrice(###class o) = basePrice(o)<manufacturingPrice(o) AND maxTotalLimitMarkup(pharmacyPriceGroup(sku(o))) AND is713PharmacyPriceGroup(sku(o));
    notUseManufacturingPrice(###class o) = calcNotUseManufacturingPrice(o) OR extraNotUseManufacturingPrice(o);
END

META defineBasePriceMarkupPharmacy(class, batchProp)
    basePrice(###class o) = prevCost(batchProp(o));
    @defineNotUseManufacturingPrice(class, basePrice, batchProp);
    //алгоритм рассчёта должен быть максимально приближен к pricingPrice из расценки
    calcBasePriceMarkupPharmacy 'База наценки' (###class o) = OVERRIDE manufacturingPrice(o) IF NOT notUseManufacturingPrice(o), basePrice(o);
END

META defineBaseVATPriceMarkupPharmacy(class, batchProp)
    @defineBasePriceMarkupPharmacy(user###class, batchProp);
    basePriceMarkupPharmacy 'База наценки' = DATA NUMERIC[16,4] (###user###class) CHARWIDTH 7 PATTERN '#,##0.00##';    
    baseMVATPrice(###user###class o) = basePriceMarkupPharmacy(o);
    basePVATPrice(###user###class o) = NUMERIC[16,4](basePriceMarkupPharmacy(o) * (100.0 + calcValueVAT(o)) / 100.0);

    WHEN LOCAL GOAFTER manufacturingPrice[###user###class] (CHANGED(supplierStock(###user###class o)) OR
        (CHANGED(dateTime(o)) AND NOT isPosted(o)) OR
        CHANGED(batch(o)) OR CHANGED(manufacturingPrice(o))) AND batch(o) DO {
        basePriceMarkupPharmacy(o) <- calcBasePriceMarkupPharmacy(o);
    }
        
    setBasePriceMarkupPharmacy###class = DATA BOOLEAN ();
        
    onStarted() + {
        NEWSESSION {
            IF NOT setBasePriceMarkupPharmacy###class() THEN {
                applyOnlyData();
                setNoEventsInTransaction();
                setBasePriceMarkupPharmacy###class() <- TRUE;
                basePriceMarkupPharmacy(###user###class o) <- calcBasePriceMarkupPharmacy(o) WHERE NOT basePriceMarkupPharmacy(o);
                APPLY;
            }
        }
    }   
        
    @defineBasePriceMarkupPharmacy(class, batchProp);
    basePriceMarkupPharmacy 'База наценки' = ABSTRACT NUMERIC[16,4] (###class) CHARWIDTH 7 PATTERN '#,##0.00##';
    basePriceMarkupPharmacy(###user###class o) += basePriceMarkupPharmacy(o);
    baseMVATPrice(###class o) = basePriceMarkupPharmacy(o);
    basePVATPrice(###class o) = NUMERIC[16,4](basePriceMarkupPharmacy(o) * (100.0 + calcValueVAT(o)) / 100.0);
END

META defineBasePriceMarkupPharmacyBatch(prop)
    basePrice(Batch o) = cost(o);
    notUseManufacturingPrice = ABSTRACT BOOLEAN (Batch);
    prevNotUseManufacturingPrice (Batch batch) = PREV(notUseManufacturingPrice(batch));
    //алгоритм рассчёта должен быть максимально приближен к pricingPrice из расценки    
    basePriceMarkupPharmacy 'База наценки' (Batch o) = 
        OVERRIDE manufacturingPrice(o) IF NOT notUseManufacturingPrice(o), basePrice(o);
END

dataWholesaleMarkupPrecision = DATA INTEGER ();
wholesaleMarkupPrecision 'Точность округления оптовых надбавок(макс. 3)' = OVERRIDE dataWholesaleMarkupPrecision(), 2 MATERIALIZED;

changeWholesaleMarkupPrecision() {
    NEWSESSION {
        INPUT n = INTEGER DO {
            applyOnlyData(); //исключаем пересчёт старых данных
            setNoEventsInTransaction(); 
            dataWholesaleMarkupPrecision() <- n;
            APPLY;
            recalculateMaterializations(tableColumnLong('_auto.Item_wholesaleMarkupPrecision'));
        }
    }
}

EXTEND FORM options
    PROPERTIES wholesaleMarkupPrecision() ON CHANGE changeWholesaleMarkupPrecision()
;

DESIGN options {
    pharmacy {
        MOVE PROPERTY(wholesaleMarkupPrecision()); 
    }
}

META defineWholesaleMarkupPharm(class)
    wholesaleMarkupPharm 'Оптовая надбавка(общая), %' (###class o) =
        NUMERIC[17,5](round(([((X*100.0)/Y-100.0)](price(o), baseMVATPrice(o) IF baseMVATPrice(o) != 0.0)), wholesaleMarkupPrecision()));
    wholesaleMarkupPharm 'Оптовая надбавка(общая), %' (User###class o) =
        NUMERIC[17,5](round(([((X*100.0)/Y-100.0)](price(o), baseMVATPrice(o) IF baseMVATPrice(o) != 0.0)), wholesaleMarkupPrecision()));

    //если Цена опт. надб. опускается ниже себестоимости или мы сами импортёры, то надбавка поставщика(стороннего) равна 0%
    supplierMarkupPharmacy 'Надбавка поставщика(стороннего), %' (###class o) =
        IF (round((min(((prevCost(batch(o))/baseMVATPrice(o))-1)*100,99999) < 0.0), wholesaleMarkupPrecision()) OR import(batch(o)))
        THEN 0.0 ELSE round((min(((prevCost(batch(o))/baseMVATPrice(o))-1)*100,99999)), wholesaleMarkupPrecision());
    wholesaleMarkupPharmacy 'Оптовая надбавка(собственная), %' (###class o) =
        NUMERIC[8,3](round((min(round((((price(o)/baseMVATPrice(o))-1)*100), wholesaleMarkupPrecision())(-)supplierMarkupPharmacy(o), 99999)), wholesaleMarkupPrecision()));    
    supplierMarkupPharmacy 'Надбавка поставщика(стороннего), %' (User###class o) =
        IF (round((min(((prevCost(batch(o))/baseMVATPrice(o))-1)*100,99999) < 0.0), wholesaleMarkupPrecision()) OR import(batch(o)))
        THEN 0.0 ELSE round((min(((prevCost(batch(o))/baseMVATPrice(o))-1)*100,99999)), wholesaleMarkupPrecision());
    wholesaleMarkupPharmacy 'Оптовая надбавка(собственная), %' (User###class o) =
        NUMERIC[8,3](round((min(round((((price(o)/baseMVATPrice(o))-1)*100), wholesaleMarkupPrecision())(-)supplierMarkupPharmacy(o), 99999)), wholesaleMarkupPrecision()));

    changeWholesaleMarkupPharmacy ABSTRACT LIST (User###class);
    changeWholesaleMarkupPharmacy(User###class o) + {
        INPUT n = NUMERIC[16,4] DO {
            //действие запускается для собственной надбавки, поэтому полагаем, что при включении данного мехнизма ВСЕГДА надо добавлять надбавку поставщика
            price(o) <- trunc(baseMVATPrice(o) * (100 + (n (+) supplierMarkupPharmacy(o))) / 100, 2);
            invoicePrice(o) <- trunc(basePVATPrice(o) * (100 + n) / 100, 2);
        }
    }
END

META implementWholesaleMarkupPharm(class)
    wholesaleMarkupPharm(###class o) += wholesaleMarkupPharm(o);
END    

META defineLimitMarkupPharm(class)
    wholesaleLimitMarkupPharmacy 'Предельный % оптовой надбавки' (###class o) = wholesaleLimitMarkupPharmacy(sku(o), manufacturingPrice(o), date(o));
    prevWholesaleLimitMarkupPharmacy 'Предельный % оптовой надбавки' (###class o) = prevWholesaleLimitMarkupPharmacy(sku(o), manufacturingPrice(o), date(o));

    calcMaxTotalLimitMarkup 'Максимальная общая надбавка, %' (###class o) = NUMERIC[8,4](calcMaxTotalLimitMarkup(sku(o), OVERRIDE importerMarkup(o), 0.0));
    wholesaleLimitPharmacyMarkup (###class o) = MIN prevWholesaleLimitMarkupPharmacy(o), calcMaxTotalLimitMarkup(o);//минимальная между предельной оптовой и общей

    wholesaleLimitMarkupPharmacy 'Предельный % оптовой надбавки' (User###class o) = wholesaleLimitMarkupPharmacy(sku(o), manufacturingPrice(o), date(o));
    prevWholesaleLimitMarkupPharmacy 'Предельный % оптовой надбавки' (User###class o) = prevWholesaleLimitMarkupPharmacy(sku(o), manufacturingPrice(o), date(o));

    calcMaxTotalLimitMarkup 'Максимальная общая надбавка, %' (User###class o) = NUMERIC[8,4](calcMaxTotalLimitMarkup(sku(o), OVERRIDE importerMarkup(o), 0.0));
    wholesaleLimitPharmacyMarkup (User###class o) = MIN prevWholesaleLimitMarkupPharmacy(o), calcMaxTotalLimitMarkup(o);//минимальная между предельной оптовой и общей    
END

//--------------------------------------------------------------------------------------------------------
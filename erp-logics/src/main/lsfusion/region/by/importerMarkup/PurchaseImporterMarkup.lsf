MODULE PurchaseImporterMarkup;

REQUIRE PurchaseInvoice;

NAMESPACE Purchase;

showImporterMarkup 'Надбавка импортера' (invoice) = ABSTRACT BOOLEAN(Invoice) IN documentPrm;
showImporterMarkup 'Надбавка импортера' (userInvoice) = DATA BOOLEAN(UserInvoice) IN documentPrm;
showImporterMarkup (UserInvoice invoice) += showImporterMarkup(invoice);

showImporterMarkup 'Надбавка импортера' (InvoiceDetail invoiceDetail) = showImporterMarkup(invoice(invoiceDetail));
showImporterMarkup 'Надбавка импортера' (UserInvoiceDetail userInvoiceDetail) = showImporterMarkup(userInvoice(userInvoiceDetail));

EXTEND FORM userInvoice
    PROPERTIES(i)  showImporterMarkup

;
DESIGN userInvoice {
    headerExtraParams {
        NEW headerImporterMarkup {
            caption = '713';
            MOVE PROPERTY(showImporterMarkup(i));
        }
    }
}

// -- Операция
@defineOperationProperty(showImporterMarkup, 'Надбавка импортера', priceContainer);
@deriveDocumentOperationProperty(UserInvoice, showImporterMarkup);

TABLE importerUserInvoiceDetail(UserInvoiceDetail);
importerMarkup '{document.markup} импортера' = ABSTRACT NUMERIC[8,4] (InvoiceDetail);
importerMarkup '{document.markup} импортера' = DATA NUMERIC[8,4] (UserInvoiceDetail) TABLE importerUserInvoiceDetail;
importerMarkup (UserInvoiceDetail invoiceDetail) += importerMarkup(invoiceDetail);

prevImporterMarkup (UserInvoiceDetail d) = PREV(importerMarkup(d));
prevImporterMarkup (InvoiceDetail d) = PREV(importerMarkup(d));

readonlyImporterMarkup = ABSTRACT VALUE BOOLEAN (UserInvoiceDetail);
backgroundImporterMarkup 'Цвет' = ABSTRACT CASE COLOR (InvoiceDetail);

EXTEND FORM userInvoice
    PROPERTIES(d) SHOWIF showImporterMarkup(i) READONLYIF readonlyImporterMarkup(d) importerMarkup AFTER price(d) BACKGROUND backgroundImporterMarkup(d)
;
EXTEND FORM invoices
    PROPERTIES(d) READONLY SHOWIF showImporterMarkup(i) importerMarkup AFTER price(d) BACKGROUND backgroundImporterMarkup(d)
;
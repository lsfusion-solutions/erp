MODULE RepricingImporterMarkup;

REQUIRE PricingPriceLimit, Repricing;

NAMESPACE Repricing;

TABLE importerUserUserRepricingDetail(UserRepricingDetail);
importerMarkup '{document.markup} импортера' = ABSTRACT NUMERIC[8,4] (RepricingDetail);
importerMarkup '{document.markup} импортера' = DATA NUMERIC[8,4] (UserRepricingDetail) TABLE importerUserUserRepricingDetail;
importerMarkup (UserRepricingDetail repricingDetail) += importerMarkup(repricingDetail);

showImporterMarkup = DATA  BOOLEAN ();

showImporterMarkup = ABSTRACT VALUE BOOLEAN (Repricing);
showImporterMarkup(Repricing r) += r IS Repricing AND showImporterMarkup();

EXTEND FORM userRepricing
    PROPERTIES(d) SHOWIF showImporterMarkup(p) BEFORE markup(d) importerMarkup
;

EXTEND FORM repricings
    PROPERTIES(d) READONLY SHOWIF showImporterMarkup(p) BEFORE markup(d) importerMarkup
;

Pricing.importerMarkup(RepricingDetail ledger) += Repricing.importerMarkup(ledger);

WHEN LOCAL  (CHANGED(sku(UserRepricingDetail detail)) OR
            CHANGED(departmentStore(detail)) OR
            (CHANGED(dateTime(detail)) AND NOT isPosted(detail))) AND NOT CHANGED(importerMarkup(detail)) DO {
    importerMarkup (detail) <- prevImporterMarkup(prevRetailPricingPriceListLedgerB( sku(detail), departmentStore(detail), dateTime(detail)));    
}

overRecalculatePrice(UserRepricingDetail detail) +  {
    importerMarkup (detail)  <- prevImporterMarkup(prevRetailPricingPriceListLedgerB( sku(detail), departmentStore(detail), dateTime(detail)));
}
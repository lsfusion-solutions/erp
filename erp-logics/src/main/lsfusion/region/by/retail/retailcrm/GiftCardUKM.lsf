MODULE GiftCardUKM;

REQUIRE GiftCardItem;

NAMESPACE GiftCard;


connectionStringExportGiftCards 'Строка подключения' = DATA STRING[255] ();
userExportGiftCards 'Пользователь' = DATA STRING[255] ();
passwordExportGiftCards 'Пароль' = DATA STRING[255] ();
useGiftCardDates 'Использовать дату активации из сертификата (вместо текущей)' = DATA BOOLEAN ();
checkUnderpayExportGiftCards 'check_underpay' = DATA INTEGER ();
monoAccountExportGiftCards 'mono_account' = DATA INTEGER ();

inExportGiftCards 'Экспорт' = DATA LOCAL BOOLEAN (GiftCard);
exportedExportGiftCards 'Экспортирован' = DATA BOOLEAN (GiftCard);
exportedActive 'Экспортирован (активация)' = DATA BOOLEAN (GiftCard);
exportGiftCardsUKMCustom 'Экспорт в УКМ'  INTERNAL  'lsfusion.erp.region.by.ukm.ExportGiftCardsActionProperty'();

idDepartmentStore = ABSTRACT ISTRING[10](GiftCard);
isSoldInvoice 'Активировать при выгрузке' = ABSTRACT VALUE BOOLEAN (GiftCard);
isDefect = ABSTRACT BOOLEAN (GiftCard);
useGiftCardDates(GiftCard g) = useGiftCardDates() AND isSoldInvoice(g);

allowReturn = ABSTRACT BOOLEAN (GiftCard);
allowReturnPayment = ABSTRACT BOOLEAN (GiftCard);

exportGiftCards 'Экспорт в УКМ' ()  { 

    inExportGiftCards(GiftCard g) <- NULL WHERE inExportGiftCards(g) AND (exportedActive(g) OR sold(g) IS ReceiptGiftCardSaleDetail);

    exportGiftCardsUKMCustom();
};

EXTEND FORM giftCards
    PROPERTIES(g) BEFORE number(g) inExportGiftCards, exportedExportGiftCards, exportedActive READONLY 
    PROPERTIES() exportGiftCards BEFORE generateGiftCards() DRAW g TOOLBAR
;


EXTEND FORM integrationData 
    PROPERTIES() connectionStringExportGiftCards, userExportGiftCards, passwordExportGiftCards, useGiftCardDates, checkUnderpayExportGiftCards, monoAccountExportGiftCards
;

DESIGN integrationData {
    pane {
        NEW exportGiftCards {
            caption = 'Экспорт сертификатов';
            MOVE PROPERTY(connectionStringExportGiftCards());
            MOVE PROPERTY(userExportGiftCards());
            MOVE PROPERTY(passwordExportGiftCards());
            MOVE PROPERTY(useGiftCardDates());
            MOVE PROPERTY(checkUnderpayExportGiftCards()) {toolTip = '0 - не разрешено, 1 - разрешено, 2 - решение принимает кассир';};
            MOVE PROPERTY(monoAccountExportGiftCards());
        }    
    }
}


MODULE GiftCardSaleUKM;

REQUIRE GiftCardUKM, GiftCardSaleInvoice;

NAMESPACE GiftCard;

soldInvoice(GiftCard g) = sold(g) IF sold(g) IS InvoiceGiftCardSaleDetail;
isSoldInvoice(GiftCard g) += sold(g) IS InvoiceGiftCardSaleDetail;

countInvoiceGiftCardSaleDetail = GROUP SUM 1 BY invoice(InvoiceGiftCardSaleDetail d);

exportGiftCardsUKM 'Экспорт в УКМ' (Sale.Invoice i)  { 

    inExportGiftCards(GiftCard c) <- TRUE WHERE invoice(soldInvoice(c)) == i;
    IF NOT GROUP SUM 1 IF inExportGiftCards(GiftCard c) THEN 
        {
            MESSAGE('Загрузка не требуентся');
            RETURN;
        }
    inExportGiftCards(GiftCard c) <- NULL WHERE inExportGiftCards(c) AND (exportedActive(c) OR sold(c) IS ReceiptGiftCardSaleDetail);
    
    MESSAGE  (OVERRIDE (countInvoiceGiftCardSaleDetail(i) (-) (GROUP  SUM 1 IF inExportGiftCards(GiftCard c))), 0) + '/' + countInvoiceGiftCardSaleDetail(i) + ' Выгружено ранее' NOWAIT;
    MESSAGE  (OVERRIDE (GROUP SUM 1 IF inExportGiftCards(GiftCard c)), 0) + '/' + countInvoiceGiftCardSaleDetail(i) + ' Принято к загрузке' NOWAIT;
    exportGiftCardsUKMCustom();
}

overExportGiftCardsForm ABSTRACT LIST (Sale.Invoice);
overExportGiftCardsForm(Sale.Invoice i) + {
    IF NOT inactiveUKMCustom() THEN {
        ASK 'Выгружать подарочные сертификаты в УКМ' DO exportGiftCardsUKM(i);
    }
}

exportGiftCardsForm(Sale.Invoice i)  {
    NEWSESSION{
        IF isPosted(i) AND createShipment(i) AND isGiftCard(i) THEN
            overExportGiftCardsForm(i);
    }
}

EXTEND FORM Sale.userInvoice 
    EVENTS 
        ON APPLY AFTER exportGiftCardsForm(i);

EXTEND FORM saleGiftCardDashboard
    PROPERTIES(i)  exportGiftCardsUKM SHOWIF NOT inactiveUKMCustom()
;

DESIGN saleGiftCardDashboard{
    TOOLBAR(i){
        MOVE PROPERTY (exportGiftCardsUKM(i));
    }
}
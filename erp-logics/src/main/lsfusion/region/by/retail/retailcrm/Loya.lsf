MODULE Loya;

REQUIRE System, Item, PriceListOperation, Store, MachineryPriceTransactionPromotion;

PRIORITY PriceList;

sessionKeyLoya 'Сессионный ключ' = DATA STRING[200] ();

ipLoya 'IP адрес' = DATA STRING[100] ();
portLoya 'Порт' = DATA STRING[100] ();
emailLoya 'Email' = DATA STRING[100] ();
passwordLoya 'Пароль' = DATA STRING[100] () ECHO;
apiKeyLoya 'Api Key' = DATA STRING[100] ();
idPartnerLoya 'Собственный ID' = DATA INTEGER ();

sessionKey 'Session Key' = DATA TEXT ();

maxDiscountLoya 'Максимальный процент скидки' = DATA INTEGER ();
maxAllowBonusLoya 'Максимальный процент списания бонусов' = DATA INTEGER ();
maxAwardBonusLoya 'Максимальный процент начисления бонусов' = DATA INTEGER ();

useRestrictionsForAwardBonus 'Учитывать запрет скидок в начислении бонусов'= DATA BOOLEAN ();

CLASS LoyaItemGroup 'Группа товаров Loya';
TABLE loyaItemGroup (LoyaItemGroup);

@defineExternalizable(loyaItemGroup, LONG);

active 'Активная' = DATA BOOLEAN (LoyaItemGroup);
name 'Наименование' = DATA STRING[100] (LoyaItemGroup);
description 'Описание' = DATA STRING[100] (LoyaItemGroup);
createdDate 'Дата создания' = DATA DATE (LoyaItemGroup);
createdDate(LoyaItemGroup g) <- currentDate() WHEN SET (g IS LoyaItemGroup);

fromDateTime 'Дата/время начала акции' = DATA DATETIME (LoyaItemGroup) NONULL;
toDateTime 'Дата/время окончания акции' = DATA DATETIME (LoyaItemGroup) NONULL;

loyaItemGroup 'Группа товаров Loya' = DATA LoyaItemGroup (UserPriceList);
nameLoyaItemGroup 'Группа товаров Loya' (UserPriceList p) = name(loyaItemGroup(p));

loyaItemGroup 'Группа товаров Loya' = ABSTRACT LoyaItemGroup (PriceList);
nameLoyaItemGroup 'Группа товаров Loya' (PriceList p) = name(loyaItemGroup(p));

loyaItemGroup(UserPriceList p) += loyaItemGroup(p);

isData 'Ручной' = DATA BOOLEAN (LoyaItemGroup);

checkDatesLoyaItemGroupPriceList 'Проверять сроки действия прайсов и групп товаров LOYA' = DATA BOOLEAN ();
EXTEND FORM options
    PROPERTIES checkDatesLoyaItemGroupPriceList()
;
DESIGN options {
    price {
        MOVE PROPERTY(checkDatesLoyaItemGroupPriceList());
    }
}
CONSTRAINT (CHANGED(isPosted(PriceList l)) OR CHANGED(loyaItemGroup(l)) OR CHANGED(fromDate(l)) OR CHANGED(toDate(l))) AND isPosted(l) AND loyaItemGroup(l) AND NOT isData(loyaItemGroup(l))
    AND checkDatesLoyaItemGroupPriceList() AND (NOT DATE(fromDateTime(loyaItemGroup(l))) == fromDate(l) OR NOT DATE(toDateTime(loyaItemGroup(l))) == toDate(l))
    MESSAGE 'Проверьте сроки действия групп товаров LOYA. Они должны совпадать с действием прайса!';

TABLE itemLoyaItemGroup (Item, LoyaItemGroup);

dataIn 'Вкл.' = DATA BOOLEAN (Item, LoyaItemGroup);
quantity 'Количество' = DATA NUMERIC[14,3] (Item, LoyaItemGroup);

in 'Вкл.' (Item s, LoyaItemGroup g) = OVERRIDE dataIn(s, g), [GROUP SUM 1 IF in(s , UserPriceList userPriceList) 
                                        BY loyaItemGroup(userPriceList)](g) > 0; 
                                       
CONSTRAINT isData(LoyaItemGroup g) AND in(Item i, g) AND quantity(i, g) <= 0.0 
    MESSAGE 'Количество товара должно быть положительным.';

maxDiscountLoya 'Максимальный процент скидки для группы товаров' = DATA INTEGER (LoyaItemGroup);
maxAllowBonusLoya 'Максимальный процент списания бонусов для группы товаров' = DATA INTEGER (LoyaItemGroup);
maxAwardBonusLoya 'Максимальный процент начисления бонусов для группы товаров' = DATA INTEGER (LoyaItemGroup);

overMaxDiscountLoyaItemGroup (LoyaItemGroup ig) = OVERRIDE maxDiscountLoya(ig), maxDiscountLoya();
overMaxAllowBonusLoyaItemGroup (LoyaItemGroup ig) = OVERRIDE maxAllowBonusLoya(ig), maxAllowBonusLoya();
overMaxAwardBonusLoyaItemGroup (LoyaItemGroup ig) = OVERRIDE maxAwardBonusLoya(ig), maxAwardBonusLoya();

enabledLoya 'Вкл. Loya' = DATA LOCAL BOOLEAN (DepartmentStore);
enabledLoya 'Вкл. Loya' = GROUP MAX enabledLoya(DepartmentStore d);
inLoya(DepartmentStore d) = OVERRIDE enabledLoya(d), NOT enabledLoya();
idLoya 'Id Loya'= DATA INTEGER (DepartmentStore);
departmentStoreIdLoya (idLoya) = GROUP AGGR DepartmentStore departmentStore BY idLoya(departmentStore);

EXTEND FORM departmentStore 
    PROPERTIES (d) idLoya;
DESIGN departmentStore {
    headerMainParams{
        MOVE PROPERTY (idLoya(d));
    }
}

useMinPrice 'Выгружать минимальные цены' = DATA BOOLEAN () COMPLEX;

loyaMinPrice 'Минимальная цена' = DATA LOCAL NUMERIC[16,4](Item, DepartmentStore);
    
in (SkuGroup skuGroup, LoyaItemGroup loyaItemGroup) = GROUP SUM 1 IF isParent(skuGroup, Sku sku) AND in(sku, loyaItemGroup);
in (SkuGroup skuGroup) = GROUP SUM in(skuGroup, LoyaItemGroup loyaItemGroup) IF active(loyaItemGroup);

empty 'Без товаров' (LoyaItemGroup g) = g IS LoyaItemGroup AND NOT (GROUP SUM 1 IF in(Sku sku, g));

deleted 'Удалена в Loya' = DATA BOOLEAN (LoyaItemGroup);
WHEN SETCHANGED (active(LoyaItemGroup g)) DO {
    deleted(g) <- NULL;
}

synchronizeLoya 'Синхронизировать'  INTERNAL  'lsfusion.erp.region.by.ukm.SynchronizeLoyaAction' ();
disableSynchronizeItemsLoya 'Отключить синхронизацию товаров при выгрузке групп' = DATA BOOLEAN ();
deleteInactiveItemGroupsLoya 'Удалять неактивные группы товаров' = DATA BOOLEAN ();
logRequestsLoya 'Логировать запросы' = DATA BOOLEAN ();
useBarcodeAsIdLoya 'Использовать ШК как ID' = DATA BOOLEAN ();

synchronizeBrand 'Синхронизировать бренд'  INTERNAL  'lsfusion.erp.region.by.ukm.SynchronizeBrandLoyaAction' (Brand);
synchronizeItemGroup 'Синхронизировать группу товаров'  INTERNAL  'lsfusion.erp.region.by.ukm.SynchronizeItemGroupLoyaAction' (ItemGroup);
synchronizeLoyaItemGroup 'Синхронизировать группу товаров Loya'  INTERNAL  'lsfusion.erp.region.by.ukm.SynchronizeLoyaItemGroupLoyaAction' (LoyaItemGroup);
synchronizeItem 'Синхронизировать товар (Loya)'  INTERNAL  'lsfusion.erp.region.by.ukm.SynchronizeItemLoyaAction' (Item);
synchronizeItemResult = DATA LOCAL STRING[255] ();

deleteUnexistingCategories 'Удалить несуществующие категории'  INTERNAL  'lsfusion.erp.region.by.ukm.DeleteUnexistingCategoriesLoyaAction' ();

idLoya 'Идентификатор бренда (Loya)' = DATA INTEGER (Brand);
in (Brand brand, LoyaItemGroup loyaItemGroup) = GROUP SUM 1 IF brand(Sku sku)==brand AND in(sku, loyaItemGroup);
in (Brand brand) = GROUP SUM in(brand, LoyaItemGroup loyaItemGroup) IF active(loyaItemGroup);

idLoya 'Код группы в Loya' = DATA STRING[100](ItemGroup);
overIdLoya 'Код группы в Loya' (ItemGroup ig) = OVERRIDE idLoya(ig), overId(ig);
idParentLoya (ItemGroup itemGroup) = overIdLoya(parent(itemGroup));

idUOM 'Единица измерения' (Item item) = id(UOM(item)) IN itemBase;
idLoyaBrand 'Бренд (Loya)' (Item item) = idLoya(brand(item)) IN itemBase;
overIdSkuGroup 'Группа' (Sku sku)= overIdLoya(skuGroup(sku));

//операция
isLoya 'Использовать группу товаров LOYA' = DATA BOOLEAN (PriceList.Operation);

EXTEND FORM operation
    PROPERTIES(o) isLoya
;
DESIGN operation {
    paramsContainer {
        MOVE PROPERTY(isLoya(o));
    }
}
    
isLoya(UserPriceList p) = isLoya(operation(p));
isLoya(PriceList p) = isLoya(operation(p));
isLoya(PriceListDetail d) = isLoya(operation(d));

EXTEND FORM userPriceList
    PROPERTIES (p) nameLoyaItemGroup SHOWIF isLoya(p);

DESIGN userPriceList{
    first {
        MOVE PROPERTY(nameLoyaItemGroup(p))
        {charWidth = 30;} 
    }
}

CONSTRAINT isLoya(UserPriceList p) AND (SET (isPosted( p))  AND  NOT loyaItemGroup(p) OR DROPPED (loyaItemGroup(p)) AND isPosted(p)) 
    MESSAGE 'Для прайса должна быть задана группа товаров LOYA';

include 'Вкл' = DATA LOCAL BOOLEAN (Sku);

FORM dialogSkuLoya 'Подбор товаров'
    TREE skuTree sk = ItemGroup PARENT parent(sk)
    PROPERTIES READONLY order(sk), skuTreeName = name(sk)
    ORDERS order(sk), skuTreeName
    FILTERGROUP inactive FILTER 'Активные' active(sk) 'F6' DEFAULT

    OBJECTS s=Item
    PROPERTIES READONLY    include(s) CHANGEABLE, idBarcode(s), id(s) SHOWIF showIDs(), inputName = name(s)
    PROPERTIES(s)          dialogQuantity
    PROPERTIES             EDIT(s) NEWSESSION

    FILTERS                isParent(sk, s)
    FILTERGROUP activeSku FILTER 'Активные' active(s) DEFAULT
    ORDERS inputName

    OBJECTS si=Item
    PROPERTIES READONLY     include(si) CHANGEABLE, idBarcode(si), selectedName = name(si)
    PROPERTIES(si)          dialogQuantity
    FILTERS                 dialogQuantity(si) OR include(si)
    ORDERS selectedName
;

DESIGN dialogSkuLoya {
    BOX {
        size = (1024, 768);
        NEW skuContainer BEFORE TOOLBARBOX {
            fill = 1;
            horizontal = TRUE;

            MOVE BOX(TREE skuTree);
            NEW inputContainer {
                fill = 2;
                horizontal = FALSE;

                NEW filterContainer {
                    align = STRETCH;
                    horizontal = TRUE;
                }

                NEW inputSkuContainer {
                    fill = 1;
                    horizontal = FALSE;

                    MOVE BOX(s) {
                        fill = 3;
                    }
                    MOVE BOX(si);
                }
            }
        }
    }
}

addItems 'Подбор товаров'(LoyaItemGroup g)  { 
    DIALOG dialogSkuLoya DO {
        dataIn(Item i, g) <- TRUE WHERE dialogQuantity(i) > 0.0 OR include(i);
        quantity(Item i, g) <- NUMERIC[14,3](dialogQuantity(i)) WHERE dialogQuantity(i);
    }
    dialogQuantity(Sku sku) <- NULL;
} TOOLBAR;

importXls 'Импорт из XLS'(LoyaItemGroup g)  { 
    INPUT f = EXCELFILE DO {
        LOCAL a = STRING[100](INTEGER );
        
        IMPORT XLS NOHEADER FROM f TO a;
        
        FOR Item i == skuBarcode(a(INTEGER r)) DO
            dataIn(i, g) <- TRUE;
    }
} TOOLBAR;

//формы
FORM loyaItemGroup 'Группа товаров Loya'
    OBJECTS g=LoyaItemGroup PANEL
    PROPERTIES(g) active, isData, id SHOWIF showIDs(), fromDateTime, toDateTime, name, description, maxDiscountLoya, maxAllowBonusLoya, maxAwardBonusLoya
   
    OBJECTS i=Item
    PROPERTIES(i) READONLY id, idBarcode, nameAttribute
    PROPERTIES (i, g) READONLYIF NOT dataIn(i, g) in SHOWIF isData(g) BEFORE id(i), quantity
    PROPERTIES (g) DRAW i SHOWIF isData(g) addItems, importXls                                                                
    FILTERS in(i, g)
     
    EDIT LoyaItemGroup OBJECT g
;

DESIGN loyaItemGroup {
    OBJECTS {
        NEW mainData {
            MOVE PROPERTY (active(g));
            MOVE PROPERTY (id(g));
            MOVE PROPERTY (name(g));
            MOVE PROPERTY (description(g));
            MOVE PROPERTY (maxDiscountLoya(g));
            MOVE PROPERTY (maxAllowBonusLoya(g));
            MOVE PROPERTY (maxAwardBonusLoya(g));
        }
        NEW dates {
            horizontal = TRUE;
            MOVE PROPERTY (fromDateTime(g));
            MOVE PROPERTY (toDateTime(g));
        }
        MOVE BOX(i);
    }
}

FORM loyaItemGroups 'Группы товаров Loya'
    OBJECTS g=LoyaItemGroup
    PROPERTIES(g) READONLY active, id SHOWIF showIDs(), fromDateTime, toDateTime, name, description, maxDiscountLoya, maxAllowBonusLoya, maxAwardBonusLoya
    FILTERGROUP active
        FILTER 'Активные' active(g) 'F6' DEFAULT
    FILTERS NOT isData(g)
    
    LIST LoyaItemGroup OBJECT g
;

FORM loya 'Группы товаров Loya'
    OBJECTS p = PriceList
    PROPERTIES(p) READONLY BACKGROUND background(p) isPosted , number, series, date, time,
           fromDate, fromTime, toDate, toTime, countPriceListDetail, stocks, nameLoyaItemGroup CHANGEABLE
    
    FILTERS isLoya(p)            
    FILTERGROUP inactivePriceList
        FILTER 'Текущие' isActive(p) 'F11'            
        FILTER 'Активные' isActive(p) OR (fromDateTime(p) > currentDateTime()) 'F10' DEFAULT
                    
    OBJECTS d = PriceListDetail
    PROPERTIES (d) READONLY exclude
    PROPERTIES(d) READONLY BACKGROUND background(d) index, idBarcodeSku, idSku SHOWIF showIDs(),nameSku,
                           shortNameUOMSku, nameBatch, nameCompany
    PROPERTIES(d) READONLY AFTER nameSku(d) originalIdBarcodeSku SHOWIF showOriginalIdBarcodeSku(p), originalNameSku SHOWIF showOriginalNameSku(p)
    
    ORDERS index(d)
    FILTERS priceList(d) == p
                    
    OBJECTS g=LoyaItemGroup
    PROPERTIES(g) READONLY active CHANGEABLE, id SHOWIF showIDs(), createdDate, fromDateTime, toDateTime,  name, description, 
                    maxDiscountLoya, maxAllowBonusLoya, maxAwardBonusLoya, deleted
    PROPERTIES(g) NEWSESSION NEW, EDIT, DELETE
    FILTERGROUP active
        FILTER 'Активные' active(g) 'F6' 
    
    OBJECTS i=Item
    PROPERTIES(i) READONLY id, idBarcode, nameAttribute
    PROPERTIES (i, g) READONLY in BEFORE id(i), quantity
    PROPERTIES() synchronizeLoya TOOLBAR
                                                                    
    FILTERS in(i, g)        
;

DESIGN loya {
    NEW pane BEFORE TOOLBARBOX {
        tabbed = TRUE;
        flex = 1;
        align = STRETCH;
        NEW priceLists {
            caption = 'Прайс-листы';
            MOVE BOX(p);
            MOVE BOX(d);
        }
        NEW itemGroups {
            caption = 'Группы товаров Loya';
            MOVE BOX(g);
            NEW items{
                horizontal = TRUE;
                fill = 1;
                MOVE BOX(i);
            }
            MOVE PROPERTY(synchronizeLoya());
        }
    }
}


EXTEND FORM integrationData
    PROPERTIES() ipLoya, portLoya, emailLoya, passwordLoya, apiKeyLoya, idPartnerLoya, 
                 maxDiscountLoya, maxAllowBonusLoya, maxAwardBonusLoya, useRestrictionsForAwardBonus, disableSynchronizeItemsLoya, deleteInactiveItemGroupsLoya, logRequestsLoya, useBarcodeAsIdLoya, useMinPrice
;

DESIGN integrationData {
    pane {
        NEW loyaContainer {
            caption = 'Loya';
            MOVE PROPERTY(ipLoya());
            MOVE PROPERTY(portLoya());
            MOVE PROPERTY(emailLoya());
            MOVE PROPERTY(passwordLoya());
            MOVE PROPERTY(apiKeyLoya());
            MOVE PROPERTY(idPartnerLoya());	
            MOVE PROPERTY(maxDiscountLoya());
            MOVE PROPERTY(maxAllowBonusLoya());
            MOVE PROPERTY(maxAwardBonusLoya());
            MOVE PROPERTY(useRestrictionsForAwardBonus());
            MOVE PROPERTY(disableSynchronizeItemsLoya());
            MOVE PROPERTY(deleteInactiveItemGroupsLoya());
            MOVE PROPERTY(logRequestsLoya());
            MOVE PROPERTY(useBarcodeAsIdLoya());
            MOVE PROPERTY(useMinPrice());
        }
    }
}

NAVIGATOR {
    priceListMasterData {
        NEW loya;
    }
}

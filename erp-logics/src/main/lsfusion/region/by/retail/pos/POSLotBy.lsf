MODULE POSLotBy;

REQUIRE POSLot, LotTypeBy;

NAMESPACE POS;

ukz 'УКЗ' = DATA STRING[30](ReceiptDetail);

ukz 'Товар подлежит маркировке УКЗ. Просканируйте марку.' = DATA LOCAL STRING[30] () CHANGEKEY 'F4;editing=all' EVENTID 'SCANNER';
changeUkz () {
    INPUT s = STRING[30] DO {
        IF regexPatternMatch(s, '[0-9]\{27\}[A-Z]\{3\}') THEN {
            ukz() <- s;
            formOk();
        } ELSE {
            MESSAGE s + '\nНе является корректным УКЗ' NOWAIT;
        }
    }
}

FORM gialogUKZ 'Запрос марки'
    PROPERTIES ukz() ON CHANGE changeUkz()

    EVENTS ON QUERYOK {
    }
;

DESIGN gialogUKZ {
    REMOVE PROPERTY (formOk());
}

//sale
overCreateSaleDetail(ReceiptDetail d) + {
    IF UKZ(sku(d)) == TTRUE AND NOT ukz(d) AND NOT skipCheckPosLot() THEN {
        ukz() <- NULL;
        DIALOG gialogUKZ NOMANAGESESSION DO {
            ukz(d) <- ukz();
        } ELSE {
            DELETE d;
        }
    }
}

//return
overCreateReturn(ReceiptReturnDetail d) + {
    ukz(d) <- ukz(receiptSaleDetail(d));
    ukz() <- NULL;
    IF UKZ(sku(d)) == TTRUE AND NOT ukz(d) AND NOT skipCheckPosLot() THEN {
        DIALOG gialogUKZ NOMANAGESESSION DO {
            ukz(d) <- ukz();
        } ELSE {
            DELETE d;
        }
    }
}
overCreateReturn(ReceiptReturnDetail d, ReceiptSaleDetail sd) + {
    ukz(d) <- ukz(sd);
    ukz() <- NULL;
    IF UKZ(sku(d)) == TTRUE AND NOT ukz(d) AND NOT skipCheckPosLot() THEN {
        DIALOG gialogUKZ NOMANAGESESSION DO {
            ukz(d) <- ukz();
        } ELSE {
            DELETE d;
        }
    }
}
MODULE LotTypeBy;

REQUIRE LotType, CustomsGroup;

NAMESPACE Stock;

EXTEND CLASS LotType {
    shoes 'Обувь',
    clothes 'Легкая промышленность', 
    milk 'Молочная продукция',
    tires 'Шины и покрышки',
    perfumery 'Духи и туалетная вода',
    photo 'Фотоаппараты и лампы-вспышки',
    animalFeed 'Корма для животных',
    antiseptics 'Антисептики и дезинфицирующие средства',
    bike 'Велосипеды и велосипедные рамы',
    caviar 'Икра осетровых и икра лососевых',
    dietarySupplements 'Биологически активные добавки к пище',
    juice 'Безалкогольные напитки и соки',
    oilFat 'Растительные масла и масложировая продукция',
    preserves 'Консервированная продукция',
    veterinaryMedicines 'Ветеринарные лекарственные препараты',
    water 'Вода упакованная',
    cosmetics 'Косметика и бытовая химия',
    grocery 'Пищевая продукция (Бакалея)',
    none 'Не подлежит маркировке СИ'
}

controlLot 'Контроль марки на кассе' = DATA BOOLEAN (LotType);

EXTEND FORM dialogLotTypes
    PROPERTIES(t) READONLY id BEFORE name(t), controlLot
    FILTERS t != LotType.unknown
    FILTERGROUP control FILTER 'Обяз.' controlLot(t) 'F6' DEFAULT
;

onStarted() + {
    NEWSESSION {
        controlLot(LotType t) <- TRUE WHERE t=LotType.clothes OR t=LotType.shoes OR t=LotType.milk  OR t=LotType.tires;
        APPLY;
    }
}

ukzPattern() = '^[0-9]\{27\}[A-Z]\{3\}$';

dataUKZ = DATA TBOOLEAN (SkuGroup);
nearestUKZ (SkuGroup child) = GROUP LAST dataUKZ(SkuGroup parent) ORDER DESC level(child, parent) WHERE dataUKZ(parent);

UKZ 'Маркировка УКЗ' (SkuGroup skuGroup) = OVERRIDE dataUKZ(skuGroup), nearestUKZ(skuGroup) MATERIALIZED IN paramsItemGroup;

EXTEND FORM itemGroup PROPERTIES(g) AFTER nameParent(g) UKZ;
EXTEND FORM itemGroups PROPERTIES(g) AFTER canonicalName(g) READONLYIF isReadonly() UKZ BACKGROUND RGB(190,190,190) IF dataUKZ(g);

dataUKZ  'Маркировка УКЗ' = DATA TBOOLEAN (Sku);

overUKZ = ABSTRACT VALUE TBOOLEAN (Sku);
overUKZ(Sku sku) += UKZ(skuGroup(sku));

UKZ 'Маркировка УКЗ' (Sku s) = OVERRIDE dataUKZ(s), overUKZ(s) MATERIALIZED IN itemBase;

EXTEND FORM item
    PROPERTIES (i) UKZ
;

EXTEND FORM items
    PROPERTIES (i) UKZ READONLYIF isReadonly()
;

//
dataLotType = DATA LotType (CustomsGroup);
nearestLotType (CustomsGroup child) = GROUP LAST dataLotType(CustomsGroup parent) ORDER DESC level(child, parent) WHERE dataLotType(parent);

lotType (CustomsGroup customsGroup) = OVERRIDE dataLotType(customsGroup), nearestLotType(customsGroup) MATERIALIZED;
nameLotType 'Маркировка СИ (Тип маркировки)' (CustomsGroup customsGroup) = name(lotType(customsGroup));// IN paramsItemGroup;

overLotType(Sku sku) += lotType(customsGroup(defaultCountry(),sku));

quantityChildLotType  = GROUP SUM 1 IF dataLotType(CustomsGroup childICustomsGroup) AND isParent(childICustomsGroup, CustomsGroup customsGroup) BY customsGroup MATERIALIZED;
quantityParentWithLotType = GROUP SUM 1 IF dataLotType(CustomsGroup parentCustomsGroup) AND isParent(CustomsGroup customsGroup, parentCustomsGroup) BY customsGroup MATERIALIZED;

backgroundShowLotType (CustomsGroup customsGroup) =
    IF dataLotType(customsGroup) THEN
        RGB(255,255,255) IF customsGroup IS CustomsGroup
    ELSE
        RGB(203,203,206) IF quantityChildLotType (customsGroup) != descendantNumber(customsGroup)
            AND NOT quantityParentWithLotType (customsGroup);

dataUKZ = DATA BOOLEAN (CustomsGroup);
nearestUKZ (CustomsGroup child) = GROUP LAST dataUKZ(CustomsGroup parent) ORDER DESC level(child, parent) WHERE dataUKZ(parent);

UKZ 'Маркировка УКЗ'  (CustomsGroup customsGroup) = OVERRIDE dataUKZ(customsGroup), nearestUKZ(customsGroup) MATERIALIZED;

overUKZ(Sku sku) += TTRUE IF UKZ(customsGroup(defaultCountry(),sku));

quantityChildUKZ  = GROUP SUM 1 IF dataUKZ(CustomsGroup childICustomsGroup) AND isParent(childICustomsGroup, CustomsGroup customsGroup) BY customsGroup MATERIALIZED;
quantityParentWithUKZ = GROUP SUM 1 IF dataUKZ(CustomsGroup parentCustomsGroup) AND isParent(CustomsGroup customsGroup, parentCustomsGroup) BY customsGroup MATERIALIZED;

backgroundShowUKZ (CustomsGroup customsGroup) =
    IF dataUKZ(customsGroup) THEN
        RGB(255,255,255) IF customsGroup IS CustomsGroup
    ELSE
        RGB(203,203,206) IF quantityChildUKZ (customsGroup) != descendantNumber(customsGroup)
            AND NOT quantityParentWithUKZ (customsGroup);

EXTEND FORM customsGroup
    PROPERTIES (cg) nameLotType, UKZ
;

EXTEND FORM customsGroups
    PROPERTIES(gcg) READONLYIF isReadonly() nameLotType BACKGROUND backgroundShowLotType(gcg), UKZ BACKGROUND backgroundShowUKZ(gcg)
    PROPERTIES(cg) READONLYIF isReadonly() nameLotType BACKGROUND backgroundShowLotType(gcg), UKZ BACKGROUND backgroundShowUKZ(gcg)
;

DESIGN customsGroup {
    topContainer {
        NEW lotContainer{
            horizontal = TRUE;
            caption = 'Маркировка';
            MOVE PROPERTY(nameLotType(cg));
            MOVE PROPERTY(UKZ(cg));
        }
    }
}
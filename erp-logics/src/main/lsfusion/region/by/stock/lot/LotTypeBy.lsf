MODULE LotTypeBy;

REQUIRE LotType;

NAMESPACE Stock;

EXTEND CLASS LotType {
    perfumery 'Парфюмерия',
    shoes 'Обувь',
    clothes 'Легкая промышленность', 
    milk 'Молочная продукция',
    tires 'Шины и покрышки',
    photo 'Фотоаппараты и лампы-вспышки',
    ukz 'Товары, подлежащие маркировке УКЗ'
}

codeGroup 'Код группы' (LotType g) = CASE
    WHEN g == LotType.clothes THEN 'clothes'
    WHEN g == LotType.perfumery THEN 'perfum'
    WHEN g == LotType.shoes THEN 'shoes'
    WHEN g == LotType.milk THEN 'milk' 
    WHEN g == LotType.tires THEN 'tires' 
    WHEN g == LotType.photo THEN 'photo' 
    WHEN g == LotType.ukz THEN 'ukz'
;

dataUKZ = DATA BOOLEAN (SkuGroup);
nearestUKZ (SkuGroup child) = GROUP LAST dataUKZ(SkuGroup parent) ORDER DESC level(child, parent) WHERE dataUKZ(parent);

UKZ 'Маркировка УКЗ' (SkuGroup skuGroup) = OVERRIDE dataUKZ(skuGroup), nearestUKZ(skuGroup) MATERIALIZED IN paramsItemGroup;

EXTEND FORM itemGroup PROPERTIES(g) AFTER nameParent(g) UKZ;
EXTEND FORM itemGroups PROPERTIES(g) AFTER canonicalName(g) READONLYIF isReadonly() UKZ BACKGROUND RGB(190,190,190) IF dataLotType(g);

dataUKZ  'Маркировка УКЗ' = DATA BOOLEAN (Sku);

overUKZ = ABSTRACT VALUE BOOLEAN (Sku);
overUKZ(Sku sku) += UKZ(skuGroup(sku));

UKZ 'Маркировка УКЗ' (Sku s) = OVERRIDE dataUKZ(s), overUKZ(s) MATERIALIZED IN itemBase;

EXTEND FORM item
    PROPERTIES (i) UKZ
;

EXTEND FORM items
    PROPERTIES (i) UKZ READONLYIF isReadonly()
;
                            
DESIGN dialogLotTypes {
    caption = 'Категория';
}

filterEZLotType (LotType t) = t != LotType.unknown ;

CLASS IdentificationMeansType 'Вид средства идентификации' {
    eaeuGeneral 'Код стран ЕАЭС идентификации товара',
    shoesTiresClothes 'Код РБ идентификации товара (обувь, шины, легкая промышленность)'
}   

name 'Наименование' (IdentificationMeansType t) = staticCaption(t) IF t IS IdentificationMeansType CHARWIDTH 30;

FORM dialogIdentificationMeansType 'Выбор вида средства идентификации'
    OBJECTS t = IdentificationMeansType
    PROPERTIES(t) READONLY name
    
    LIST IdentificationMeansType OBJECT t
;                                        
                                         
id (IdentificationMeansType t) = CASE
    WHEN t == IdentificationMeansType.eaeuGeneral THEN '7'
    WHEN t == IdentificationMeansType.shoesTiresClothes THEN '20';

EXTEND FORM dialogLotTypes
    PROPERTIES(t) READONLY id BEFORE name(t)
    FILTERS filterEZLotType(t)
;
MODULE LotTypeBy;

REQUIRE LotType;

NAMESPACE Stock;

EXTEND CLASS LotType {
    perfumery 'Парфюмерия',
    shoes 'Обувь',
    clothes 'Легкая промышленность', 
    milk 'Молочная продукция',
    tires 'Шины и покрышки',
    photo 'Фотоаппараты и лампы-вспышки',
    animalFeed 'Корма для животных',
    antiseptics 'Антисептики и дезинфицирующие средства',
    bike 'Велосипеды и велосипедные рамы',
    caviar 'Икра осетровых и икра лососевых',
    dietarySupplements 'Биологически активные добавки к пище',
    juice 'Безалкогольные напитки и соки',
    oilFat 'Растительные масла и масложировая продукция',
    preserves 'Консервированная продукция',
    veterinaryMedicines 'Ветеринарные лекарственные препараты',
    water 'Вода упакованная',
    cosmetics 'Косметика и бытовая химия',
    grocery 'Пищевая продукция (Бакалея)'
    //  ukz 'Товары, подлежащие маркировке УКЗ'
}

codeGroup 'Код группы' (LotType g) = CASE
    WHEN g == LotType.clothes THEN 'clothes'
    WHEN g == LotType.perfumery THEN 'perfum'
    WHEN g == LotType.shoes THEN 'shoes'
    WHEN g == LotType.milk THEN 'milk' 
    WHEN g == LotType.tires THEN 'tires' 
    WHEN g == LotType.photo THEN 'photo'
    WHEN g == LotType.animalFeed THEN 'animal_feed'
    WHEN g == LotType.antiseptics THEN 'antiseptics'
    WHEN g == LotType.bike THEN 'bike'
    WHEN g == LotType.caviar THEN 'caviar'
    WHEN g == LotType.dietarySupplements THEN 'dietary_supplements'
    WHEN g == LotType.juice THEN 'juice'
    WHEN g == LotType.oilFat THEN 'oil_fat'
    WHEN g == LotType.preserves THEN 'preserves'
    WHEN g == LotType.veterinaryMedicines THEN 'veterinary_medicines'
    WHEN g == LotType.water THEN 'water'
    WHEN g == LotType.cosmetics THEN 'cosmetics'
    WHEN g == LotType.grocery THEN 'grocery'
//    WHEN g == LotType.ukz THEN 'ukz'
;

dataUKZ = DATA TBOOLEAN (SkuGroup);
nearestUKZ (SkuGroup child) = GROUP LAST dataUKZ(SkuGroup parent) ORDER DESC level(child, parent) WHERE dataUKZ(parent);

UKZ 'Маркировка УКЗ' (SkuGroup skuGroup) = OVERRIDE dataUKZ(skuGroup), nearestUKZ(skuGroup) MATERIALIZED IN paramsItemGroup;

EXTEND FORM itemGroup PROPERTIES(g) AFTER nameParent(g) UKZ;
EXTEND FORM itemGroups PROPERTIES(g) AFTER canonicalName(g) READONLYIF isReadonly() UKZ BACKGROUND RGB(190,190,190) IF dataLotType(g);

dataUKZ  'Маркировка УКЗ' = DATA TBOOLEAN (Sku);

overUKZ = ABSTRACT VALUE TBOOLEAN (Sku);
overUKZ(Sku sku) += UKZ(skuGroup(sku));

UKZ 'Маркировка УКЗ' (Sku s) = OVERRIDE dataUKZ(s), overUKZ(s) MATERIALIZED IN itemBase;

EXTEND FORM item
    PROPERTIES (i) UKZ
;

EXTEND FORM items
    PROPERTIES (i) UKZ READONLYIF isReadonly()
;
                            
DESIGN dialogLotTypes {
    caption = 'Категория';
}

filterEZLotType (LotType t) = t != LotType.unknown ;

CLASS IdentificationMeansType 'Вид средства идентификации' {
    eaeuGeneral 'Код стран ЕАЭС идентификации товара',
    shoesTiresClothes 'Код РБ идентификации товара (обувь, шины, легкая промышленность)'
}   

name 'Наименование' (IdentificationMeansType t) = staticCaption(t) IF t IS IdentificationMeansType CHARWIDTH 30;

FORM dialogIdentificationMeansType 'Выбор вида средства идентификации'
    OBJECTS t = IdentificationMeansType
    PROPERTIES(t) READONLY name
    
    LIST IdentificationMeansType OBJECT t
;                                        
                                         
id (IdentificationMeansType t) = CASE
    WHEN t == IdentificationMeansType.eaeuGeneral THEN '7'
    WHEN t == IdentificationMeansType.shoesTiresClothes THEN '20';

EXTEND FORM dialogLotTypes
    PROPERTIES(t) READONLY id BEFORE name(t)
    FILTERS filterEZLotType(t)
;
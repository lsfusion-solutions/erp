MODULE ContainerMovementBy;

REQUIRE ContainerMovement, StockDocumentBy;

NAMESPACE ContainerMovement;

FORM printContainerMovementBy 'Отчет по таре'

    OBJECTS params = (dFrom = DATE, dTo = DATE) PANEL SUBREPORT
    PROPERTIES objFrom = VALUE(dFrom), objTo = VALUE(dTo)
    PROPERTIES() READONLY namesDepartmentStores, namesStores, namesLegalEntities
    PROPERTIES() READONLY accountSumContainerStockDateFrom, accountSumContainerStockDateTo
    PROPERTIES() READONLY balanceInBatches, balanceOutBatches, sumMoveOutBatches, balanceMoveInBatches, sumMoveInBatches, balanceMoveOutBatches
    
    OBJECTS sb = (dep = DepartmentStore, bt = Batch)
    PROPERTIES(dep) READONLY name
    PROPERTIES(dep) READONLY balanceInBatches, balanceOutBatches, sumMoveOutBatches, balanceMoveInBatches, sumMoveInBatches, balanceMoveOutBatches
    PROPERTIES(dep) READONLY accountSumContainerDateFrom, accountSumContainerDateTo
    PROPERTIES(bt) READONLY idBarcodeSku, nameSku, nameSupplier
    PROPERTIES(dep,bt) READONLY lastPrice, balanceIn, sumIn, balanceOut, sumOut, balanceMoveIn, sumMoveIn, balanceMoveOut, sumMoveOut
    
    FILTERS inContainerMovement(dep,bt)
    
    ORDERS name(dep), nameSku(bt)    
;

printContainerMovementBy 'Распечатать' (DATE dateFrom, DATE dateTo, DepartmentStore departmentStore) 
    { PRINT printContainerMovementBy OBJECTS dFrom = dateFrom, dTo = dateTo; }   IMAGE 'print.png' IN print;

EXTEND FORM containerMovement

    PROPERTIES printContainerMovementBy(dFrom, dTo, dep) DRAW params 
;

DESIGN containerMovement{

    print {
        MOVE PROPERTY(printContainerMovementBy(dFrom,dTo,dep));
    }
}
//-- 
FORM printStockDocumentContainerBy 'Товарный отчет с тарой'
    OBJECTS params = (dtFrom = DATE, dtTo = DATE) PANEL SUBREPORT,
            ds = Stock PANEL
    PROPERTIES objFrom = VALUE(dtFrom), objTo = VALUE(dtTo)
    PROPERTIES(ds) SELECTOR fullName, address, nameLegalEntity, fullNameLegalEntity, nameCustomUserResponsiblePerson, namePositionResponsiblePerson, nameCustomUserBooker,
                            namePositionBooker, nameCustomUserController, namePositionController, UNP, 
                            shortNamePositionResponsiblies, positionShortNameResponsiblePerson, positionShortNameController,
                            positionShortNameBooker, namePositionResponsiblies

    PROPERTIES sumAccountBDocumentLedger(ds, dtFrom), sumItemAccountBDocumentLedger(ds, dtFrom), sumContainerAccountBDocumentLedger(ds, dtFrom),
               sumItemAccountADocumentLedger(ds, dtTo), sumContainerAccountADocumentLedger(ds, dtTo), sumAccountADocumentLedger(ds, dtTo)
    PROPERTIES(ds, dtFrom, dtTo) countIncStockDocumentInterval, countOutStockDocumentInterval,
               sumItemIncStockDocumentInterval, sumContainerIncStockDocumentInterval,
               subtotalItemIncStockDocumentInterval, subtotalContainerIncStockDocumentInterval,
               sumItemOutStockDocumentInterval, sumContainerOutStockDocumentInterval, countStockDocumentInterval

    OBJECTS il = IncStockDocumentLedger
    PROPERTIES(il) SELECTOR order, iDateTime = dateTime, iObjName = objectClassName, iDescription = description,
                   sumItem, sumContainer, sum, nameLegalEntity,
                   date, seriesNumber, nameLegalEntityStock,
                   overNameStockDocumentLedger
    FILTERS active(il),
            stock(il) == ds,
            date(il) >= dtFrom,
            date(il) <= dtTo
    ORDERS date(il), order(il), seriesNumber(il) 

    OBJECTS ol = OutStockDocumentLedger
    PROPERTIES(ol) SELECTOR order, oDateTime = dateTime, oObjName = objectClassName, oDescription = description,
                   sumItem, sumContainer, sum, nameLegalEntity,
                   date, seriesNumber, nameLegalEntityStock,
                   overNameStockDocumentLedger
    FILTERS active(ol),
            stock(ol) == ds,
            date(ol) >= dtFrom,
            date(ol) <= dtTo
    ORDERS date(ol),order(ol), seriesNumber(ol) 
    
    OBJECTS e=Employee 
    PROPERTIES (e) positionShortName, positionName
    FILTERS isResponsiblePerson(ds,e)
    PROPERTIES orderResponsiblePerson(ds,e)
    ORDERS orderResponsiblePerson(ds,e)
    
    OBJECTS bt=(sk=Sku, p = NUMERIC[16,4])
    PROPERTIES(ds) balanceInBatches, balanceOutBatches, sumMoveOutBatches, balanceMoveInBatches,sumMoveInBatches, balanceMoveOutBatches 

    PROPERTIES(ds,sk,p) balanceInPrice, sumInPrice, balanceOutPrice, sumOutPrice, sumMoveOutPrice, balanceMoveInPrice,sumMoveInPrice, balanceMoveOutPrice 
    PROPERTIES READONLY accountSumContainerStockDateFrom(), accountSumContainerStockDateTo()
    PROPERTIES(sk) READONLY idBarcode, name
    PROPERTIES price = VALUE (p)
    
    FILTERS isContainer(sk),
            balanceInPrice (ds,sk,p) OR balanceOutPrice(ds,sk,p) OR balanceMoveInPrice(ds,sk,p) OR balanceMoveOutPrice(ds,sk,p),
            sk IS Sku AND ds IS Stock
    
    ORDERS name(sk)    
    
    OBJECTS e1=Employee 
    PROPERTIES (e1) positionShortName, positionName
    FILTERS isResponsiblePerson(ds,e1)    
    PROPERTIES orderResponsiblePerson(ds,e1)
    ORDERS orderResponsiblePerson(ds,e1)
;

printDocumentContainerBy 'С тарой' (DATE dateFrom, DATE dateTo, Stock stock)  {    
    in(Stock s) <- s == stock;
    includeContainerMovementBatchFromTo (dateFrom, dateTo);
    PRINT printStockDocumentContainerBy OBJECTS dtFrom = dateFrom, dtTo = dateTo, ds = stock ;
    
}  IMAGE 'print.png' IN print;
    
EXTEND FORM sumStockDocumentLedger

    PROPERTIES printDocumentContainerBy(dtFrom, dtTo, ds) DRAW params 
;
DESIGN sumStockDocumentLedger{
    print {
        MOVE PROPERTY(printDocumentContainerBy(dtFrom,dtTo,ds));
    }
}
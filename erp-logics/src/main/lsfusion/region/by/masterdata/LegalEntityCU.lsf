MODULE LegalEntityCU;

REQUIRE LegalEntity;

NAMESPACE LegalEntity;

UNPForeign 'УНП (иностр.)' = DATA STRING[50] (LegalEntity) IN doc CHARWIDTH 15;

requiredUNPForeign 'УНП (иностр.) обязательный к заполнению' = DATA BOOLEAN ();

EXTEND FORM options
    PROPERTIES requiredUNPForeign()
;

DESIGN options {
    legalEntity {
        MOVE PROPERTY(requiredUNPForeign());
    }
}

//todo: временно для обратной совместимости, удалить через пол года
setRequiredUNPForeign = DATA BOOLEAN ();

onStarted() + {
    NEWSESSION {
        IF NOT setRequiredUNPForeign() THEN {
            requiredUNPForeign() <- TRUE;
            setRequiredUNPForeign() <- TRUE;
            APPLY;
        }    
    }
}

CONSTRAINT country(LegalEntity l) != defaultCountry() AND NOT toShowIndividual(l) AND NOT UNPForeign(l) AND requiredUNPForeign() 
    MESSAGE 'Для организации должен быть задан УНП (иностр.)';

toShowForeign(LegalEntity l) += l IS LegalEntity AND defaultCountry() != country(l) AND NOT toShowIndividual(l);

EXTEND FORM legalEntity
    PROPERTIES (l) SHOWIF toShowForeign(l) UNPForeign
;
EXTEND FORM legalEntities
    PROPERTIES (l) READONLY UNPForeign
;
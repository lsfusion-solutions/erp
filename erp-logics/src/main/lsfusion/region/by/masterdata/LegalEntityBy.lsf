MODULE LegalEntityBy;

REQUIRE LegalEntity;

NAMESPACE LegalEntity;

UNP 'УНП' = DATA VARSTRING[9] (LegalEntity) IN doc CHARWIDTH 9 NOFLEX;
OKPO 'ОКПО' = DATA VARSTRING[20] (LegalEntity) IN doc CHARWIDTH 15;
OKYLP 'ОКЮЛП' = DATA VARSTRING[20] (LegalEntity) IN doc CHARWIDTH 15;

legalEntityUNP (unp) = GROUP MAX LegalEntity legalEntity BY UNP(legalEntity);
supplierUNP = GROUP MAX LegalEntity legalEntity IF isSupplier(legalEntity) BY UNP(legalEntity);

META defineDocumentUNPLegalEntity(object, contact, caption)
    UNP###contact 'УНП'###caption (###object object) = UNP(contact(object)) IN documentPrm;
    OKPO###contact 'ОКПО'###caption (###object object) = OKPO(contact(object)) IN documentPrm;
    OKYLP###contact 'ОКЮЛП'###caption (###object object) = OKYLP(contact(object)) IN documentPrm;
END



EXTEND FORM legalEntity

    PROPERTIES(l) SHOWIF toShowUNP(l) UNP  
    PROPERTIES(l) SHOWIF toShow(l) OKPO, OKYLP
;

DESIGN legalEntity{

    column2 {
        MOVE GROUP(doc,l) {
            columns = 3;
        }
    }
}

EXTEND FORM legalEntities
    PROPERTIES(l) READONLY UNP BEFORE shortNameOwnership(l)
;

EXTEND FORM supplierLegalEntities
    PROPERTIES(l) READONLY UNP BEFORE shortNameOwnership(l)
;

uniqueUNPSupplier 'Запретить повтор УНП для поставщиков' = DATA BOOLEAN ();
CONSTRAINT uniqueUNPSupplier() AND CHANGED (UNP(LegalEntity l)) AND [ GROUP SUM 1 IF isSupplier(LegalEntity ll) BY UNP(ll)](UNP(l)) > 1 MESSAGE 'Запрещен повтор УНП для поставщиков';

uniqueUNPCustomer 'Запретить повтор УНП для покупателей' = DATA BOOLEAN ();
CONSTRAINT uniqueUNPCustomer() AND CHANGED (UNP(LegalEntity l)) AND [ GROUP SUM 1 IF isCustomer(LegalEntity ll) BY UNP(ll)](UNP(l)) > 1 MESSAGE 'Запрещен повтор УНП для покупателей';

uniqueUNP 'Запретить повтор УНП' = DATA BOOLEAN ();
CONSTRAINT uniqueUNP() AND CHANGED (UNP(LegalEntity l)) AND [ GROUP SUM 1 BY UNP(LegalEntity ll)](UNP(l)) > 1 MESSAGE 'Запрещен повтор УНП';

EXTEND FORM options
    PROPERTIES() uniqueUNPSupplier, uniqueUNPCustomer, uniqueUNP
;
DESIGN options {
    legalEntity {
        MOVE PROPERTY(uniqueUNPSupplier());
        MOVE PROPERTY(uniqueUNPCustomer());
        MOVE PROPERTY(uniqueUNP());
    }
}
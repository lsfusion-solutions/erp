MODULE LegalEntityByGRPImport;

REQUIRE LegalEntityBy;

urlGRP 'URL Государственного реестра плательщиков Республики Беларусь по УНП' = DATA STRING[255] ();
upperNameGRP 'Импортировать наименование в верхнем регистре' = DATA BOOLEAN ();

onStarted() + { 
    urlGRP() <- 'http://www.portal.nalog.gov.by/grp/getData?unp=' WHERE NOT urlGRP();
}

EXTEND FORM options
    PROPERTIES() urlGRP, upperNameGRP
;

DESIGN options {
    legalEntity {
        MOVE PROPERTY(urlGRP());
        MOVE PROPERTY(upperNameGRP());
    }
}

fillGRP 'Заполнить из ГРП' (LegalEntity l)  { 
    IF urlGRP() THEN {
        LOCAL xml = FILE ();
        
        EXTERNAL HTTP GET urlGRP() + UNP(l) TO xml;     
        
        LOCAL NESTED VUNP = STRING[9] ();
        LOCAL NESTED VNAIMP = ISTRING[200] ();
        LOCAL NESTED VNAIMK = ISTRING[150] ();
        LOCAL NESTED VPADRES = STRING[150] ();
        
        IMPORT XML ROOT 'ROW' FROM xml() TO() VUNP = VUNP, VNAIMP = VNAIMP, VNAIMK = VNAIMK, VPADRES = VPADRES;
            
        IF VUNP() == UNP(l) THEN {
            IF upperNameGRP() THEN {
                fullName(l) <- upper(VNAIMP()) WHERE VNAIMP();
                name(l) <- upper(VNAIMK()) WHERE VNAIMK() AND NOT name(l);
            } ELSE {
                fullName(l) <- VNAIMP() WHERE VNAIMP();
                name(l) <- VNAIMK() WHERE VNAIMK() AND NOT name(l);
            }
            dataAddress(l, DATE d) <- VPADRES() WHERE VPADRES() AND d == currentDate();
        }
    } ELSE 
        MESSAGE 'Не задан URL Государственного реестра плательщиков Республики Беларусь по УНП';
}

EXTEND FORM legalEntity
    PROPERTIES(l) SHOWIF UNP(l) fillGRP
;

DESIGN legalEntity {
    GROUP(law,l) {
        MOVE PROPERTY(fillGRP(l)) BEFORE PROPERTY(fullName(l));
    }
}
MODULE SupplierEInvoice;

REQUIRE EInvoice, EDS;

NAMESPACE EDI;

exportedSupplier 'Экспортирован (поставщик)' = DATA BOOLEAN (EInvoice);
importedSupplier 'Импортирован (поставщик)' = DATA BOOLEAN (EInvoice);
importedNoticeSupplier 'Импортировано уведомление об изменении (поставщик)' = DATA BOOLEAN (EInvoice);
numberNoticeSupplier 'Номер уведомления об изменении' = DATA STRING[35](EInvoice);
dateNoticeSupplier 'Дата уведомления об изменении' = DATA DATETIME (EInvoice);
importedSupplierCancel 'Импортирован (поставщик) отмена' = DATA BOOLEAN (EInvoice);
exportedBlrapnSupplier 'Экспортировано извещение о прочтении (поставщик)' = DATA BOOLEAN (EInvoice);

isImportedSupplier 'Можно отправить уведомление о получении'(EInvoice i) = importedSupplier(i) OR importedNoticeSupplier(i) OR importedSupplierCancel(i);

@defineOption(changeNumberInvoice, 'Записывать номер ЭТТН в номер накладной-продажа', EDI);

skipSendSupplier = ABSTRACT BOOLEAN (EInvoice);
signAndSendSupplier 'Подписать и отправить (поставщик)'  ABSTRACT LIST ( EInvoice);
signAndSendSupplierEDI 'Подписать и отправить (поставщик)'() {
    IF askPasswordEDSService() THEN {
        DIALOG passwordEDSService DO {} ELSE { BREAK; };
    }
    FOR in(EInvoice i) DO { 
        IF isEDI(i) THEN {
            IF EDIProviderInvoice(customer(i), supplier(i)) THEN
                signAndSendSupplier(i);
            ELSE
                MESSAGE 'Не выбран EDI провайдер для накладной: ' + deliveryNoteNumber(i);
        } ELSE
            MESSAGE 'Запрещен обмен через провайдера для не EDI накладной: ' + deliveryNoteNumber(i);
        in(i) <- NULL;
    }
    ELSE
        MESSAGE 'Не выбрано ни одной накладной';
    
}

skipCheckImportedSupplier = DATA BOOLEAN ();

signAndSendSupplierCancelEDI 'Подписать и отправить отмену (поставщик)'() {
    IF askPasswordEDSService() THEN {
        DIALOG passwordEDSService DO {} ELSE { BREAK; };
    }
    FOR in(EInvoice i) DO { 
        IF isEDI(i) THEN {
            IF skipCheckImportedSupplier() OR NOT importedSupplier(i) THEN {
                IF EDIProviderInvoice(customer(i), supplier(i)) THEN {
                    exportedSupplier(i) <- NULL WHERE exportedSupplier(i) AND NOT isCancel(i); //если оригинал отправлялся а отмена нет то нужно разрешить отправить отмену
                    isCancel(i) <- TRUE;
                    signAndSendSupplier(i);
                } ELSE
                    MESSAGE 'Не выбран EDI провайдер для накладной: ' + deliveryNoteNumber(i);
            } ELSE
                MESSAGE 'Запрещена отмена подписанной покупателем накладной: ' + deliveryNoteNumber(i);
        } ELSE
            MESSAGE 'Запрещен обмен через провайдера для не EDI накладной: ' + deliveryNoteNumber(i);
        in(i) <- NULL;
    }
    ELSE
        MESSAGE 'Не выбрано ни одной накладной';
    
}

signAndSendBlrapnSupplier 'Подписать и отправить извещение о прочтении (поставщик)'  ABSTRACT LIST ( EInvoice);
signAndSendBlrapnSupplierEDI 'Подписать и отправить извещение о прочтении (поставщик)'() {
    IF askPasswordEDSService() THEN {
        DIALOG passwordEDSService DO {} ELSE { BREAK; };
    }
    FOR in(EInvoice i) DO { 
        IF isEDI(i) THEN {
            IF EDIProviderInvoice(customer(i), supplier(i)) THEN {
                IF isImportedSupplier(i) THEN {
                    signAndSendBlrapnSupplier(i);
                } ELSE
                    MESSAGE 'Не получен ответ от покупателя по накладной: ' + deliveryNoteNumber(i);
            } ELSE
                MESSAGE 'Не выбран EDI провайдер для накладной: ' + deliveryNoteNumber(i);
        } ELSE
            MESSAGE 'Запрещен обмен через провайдера для не EDI накладной: ' + deliveryNoteNumber(i);
        in(i) <- NULL;
    } ELSE
        MESSAGE 'Не выбрано ни одной накладной';
    
}

EXTEND FORM eInvoices
    PROPERTIES(e) BEFORE isCancel(e) READONLYIF isReadonly() exportedSupplier, importedSupplier, importedNoticeSupplier, importedSupplierCancel, exportedBlrapnSupplier
    EXTEND FILTERGROUP notExported
        FILTER 'Не экспортированы (поставщик)' NOT exportedSupplier(e) 'F8'
        FILTER 'Не импортированы (поставщик)' NOT isImportedSupplier(e) 'F11'
    PROPERTIES() DRAW e TOOLBAR signAndSendSupplierEDI, signAndSendSupplierCancelEDI, signAndSendBlrapnSupplierEDI
;

DESIGN eInvoices{
    actionContainer {
        caption = 'Действия';
        horizontal = TRUE;
        NEW supplier {
            caption = 'Поставщик';
            MOVE PROPERTY (signAndSendSupplierEDI()) { alignment = STRETCH; };
            MOVE PROPERTY (signAndSendSupplierCancelEDI()) { alignment = STRETCH; };
            MOVE PROPERTY (signAndSendBlrapnSupplierEDI()) { alignment = STRETCH; };
        }
    }
}

blrwbrUserID = DATA LOCAL NESTED STRING[35]();
blrwbrDocumentNumber = DATA LOCAL NESTED STRING[48]();
blrwbrDocumentId = DATA LOCAL NESTED STRING[35]();
blrwbrDateTime = DATA LOCAL NESTED DATETIME();
blrwbrDeliveryNoteNumber = DATA LOCAL NESTED STRING[70]();
blrwbrFunctionCode = DATA LOCAL NESTED STRING[3]();

FORM blrwbr
    PROPERTIES IN messageHeader blrwbrDocumentNumber() EXTID 'MessageID',
        blrwbrUserID() EXTID 'UserID'
    PROPERTIES IN deliveryNote blrwbrDocumentId() EXTID 'DocumentID',
        blrwbrDeliveryNoteNumber() EXTID 'DeliveryNoteID',
        blrwbrDateTime() EXTID 'CreationDateTime',
        blrwbrFunctionCode() EXTID 'FunctionCode'
;

blrdnrUserID = DATA LOCAL NESTED STRING[35]();
blrdnrDocumentNumber = DATA LOCAL NESTED STRING[48]();
blrdnrDocumentId = DATA LOCAL NESTED STRING[40]();
blrdnrDateTime = DATA LOCAL NESTED DATETIME();
blrdnrDeliveryNoteNumber = DATA LOCAL NESTED STRING[70]();
blrdnrFunctionCode = DATA LOCAL NESTED STRING[3]();

FORM blrdnr
    PROPERTIES IN messageHeader blrdnrDocumentNumber() EXTID 'MessageID',
        blrdnrUserID() EXTID 'UserID'
    PROPERTIES IN deliveryNote blrdnrDocumentId() EXTID 'DocumentID',
        blrdnrDeliveryNoteNumber() EXTID 'DeliveryNoteID',
        blrdnrDateTime() EXTID 'CreationDateTime',
        blrdnrFunctionCode() EXTID 'FunctionCode'
;

exportBlrwblDateTime = DATA LOCAL NESTED DATETIME();

GROUP freightPayer EXTID 'FreightPayer';
GROUP shipFrom EXTID 'ShipFrom';
GROUP transporter EXTID 'Transporter';
GROUP carrier EXTID 'Carrier';
GROUP despatchAdviceLogisticUnitLineItem EXTID 'DespatchAdviceLogisticUnitLineItem';

hideMessageHeaderBlrwbl = DATA LOCAL BOOLEAN ();


encodeTextNoNewLines(STRING s, STRING format) = replace(replace(encodeText(s,'base64'), '\n', ''), '\r', '');

FORM exportBlrwblNew FORMEXTID 'BLRWBL'
    PROPERTIES ATTR ='2.0' IF hideMessageHeaderBlrwbl() EXTID 'version'
    OBJECTS ei = EInvoice PANEL

    OBJECTS eih = EInvoice EXTID 'MessageHeader'
    FILTERS (eih == ei AND NOT hideMessageHeaderBlrwbl()) OR NOT eih IS EInvoice
    PROPERTIES DRAW eih 
        messageId = resultMilliseconds() EXTID 'MessageID',
        msgDateTime = toChar(exportBlrwblDateTime(),'YYYYMMDDHH24MISS') EXTID 'MsgDateTime',
        messageType = 'BLRWBL' EXTID 'MessageType',
        glnSupplier(eih) EXTID 'MsgSenderID',
        glnCustomer(eih) EXTID 'MsgReceiverID',
        userIDCustomerStock(eih) EXTID 'UserID',
        testIndicator = IF isTestDocument(eih) THEN '1' EXTID 'TestIndicator',
        formType = formType(eih) EXTID 'FormType'

    OBJECTS ein = EInvoice EXTID 'DeliveryNote'
    FILTERS ein == ei
    PROPERTIES DRAW ein 
        deliveryNoteType = '700' EXTID 'DeliveryNoteType',
        documentID = resultMilliseconds() EXTID 'DocumentID',
        creationDateTime = toChar(exportBlrwblDateTime(),'YYYYMMDDHH24MISS') EXTID 'CreationDateTime',
        functionCode = (IF isCancel(ein) THEN '1' ELSE '9') EXTID 'FunctionCode',
        deliveryNoteNumber(ein) EXTID 'DeliveryNoteID',
        deliveryNoteDate = toChar(deliveryNoteDate(ein),'YYYYMMDD') EXTID 'DeliveryNoteDate',
        contractName(ein) EXTID 'ContractName',
        contractId(ein) EXTID 'ContractID',
        contractDate = toChar(contractDate(ein),'YYYYMMDD') EXTID 'ContractDate',
        waybillId(ein) EXTID 'WaybillID'
    PROPERTIES DRAW ein IN shipper 
        glnSupplier(ein) EXTID 'GLN',
        nameSupplier(ein) EXTID 'Name',
        addressSupplier(ein) EXTID 'Address',
        unpSupplier(ein) EXTID 'VATRegistrationNumber',
        contactSupplier(ein) EXTID 'Contact'
    PROPERTIES DRAW ein IN receiver 
        glnCustomer(ein) EXTID 'GLN',
        nameCustomer(ein) EXTID 'Name',
        addressCustomer(ein) EXTID 'Address',
        unpCustomer(ein) EXTID 'VATRegistrationNumber'
    PROPERTIES DRAW ein IN freightPayer 
        glnFreightPayer(ein) EXTID 'GLN',
        nameFreightPayer(ein) EXTID 'Name',
        addressFreightPayer(ein) EXTID 'Address',
        unpFreightPayer(ein) EXTID 'VATRegistrationNumber'
    PROPERTIES DRAW ein IN shipFrom 
        glnSupplierStock(ein) EXTID 'GLN',
        addressSupplierStock(ein) EXTID 'Address',
        contactSupplierStock(ein) EXTID 'Contact'
    PROPERTIES DRAW ein IN shipTo 
        glnCustomerStock(ein) EXTID 'GLN',
        addressCustomerStock(ein) EXTID 'Address'
    PROPERTIES DRAW ein IN transporter 
        glnTransporter(ein) EXTID 'GLN',
        nameTransporter(ein) EXTID 'Name',
        unpTransporter(ein) EXTID 'VATRegistrationNumber'
    PROPERTIES DRAW ein IN carrier 
        transportContact(ein) EXTID 'TransportContact',
        deliveryContact(ein) EXTID 'DeliveryContact',
        proxyID(ein) EXTID 'ProxyID',
        proxyDate = toChar(proxyDate(ein),'YYYYMMDD') EXTID 'ProxyDate',
        partyIssuingProxyName(ein) EXTID 'PartyIssuingProxyName',
        quantityTrip(ein) EXTID 'QuantityTrip',
        transportOwnerName(ein) EXTID 'TransportOwnerName',
        transportID(ein) EXTID 'TransportID',
        trailerID(ein) EXTID 'TrailerID'
    PROPERTIES DRAW ein 
        sealIDReceiver(ein) EXTID 'SealID',
        orderID(ein) EXTID 'OrderID',
        cancelDeliveryNoteId(ein) EXTID 'CancelDeliveryNoteID',
        currency(ein) EXTID 'Currency'

    OBJECTS f = ExtraField EXTID 'ExtraField'
    FILTERS eInvoice(f) == ein
    PROPERTIES DRAW f 
        name(f) EXTID 'FieldName',
        code(f) EXTID 'FieldCode',
        value(f) EXTID 'FieldValue'

    OBJECTS d = EInvoiceDetail EXTID 'LineItem' IN despatchAdviceLogisticUnitLineItem
    FILTERS eInvoice(d) == ein
    PROPERTIES DRAW d 
        lineItemNumber(d) EXTID 'LineItemNumber',
        lineItemSign(d) EXTID 'LineItemSign',
        lineItemID(d) EXTID 'LineItemID',
        lineItemBuyerID(d) EXTID 'LineItemBuyerID',
        itemCustomCode(d) EXTID 'ItemCustomCode',
        itemCustomCodeOther(d) EXTID 'ItemCustomCodeOther',
        deliveryTypePrev(d) EXTID 'DeliveryTypePrev',
        deliveryNotePrev(d) EXTID 'DeliveryNotePrev',
        deliveryNoteDatePrev = toChar(deliveryNoteDatePrev(d),'YYYYMMDD') EXTID 'DeliveryNoteDatePrev',
        deliveryNotePrevLineID(d) EXTID 'DeliveryNotePrevLineID',
        lineItemSupplierID(d) EXTID 'LineItemSupplierID',
        lineItemName(d) EXTID 'LineItemName',
        grossWeightValue(d) EXTID 'GrossWeightValue',
        quantityDespatched(d) EXTID 'QuantityDespatched',
        lineItemQuantityUOM = mapToNewUOM(lineItemQuantityUOM(d)) EXTID 'LineItemQuantityUOM',
        despatchUnitQuantityDespatched(d) EXTID 'DespatchUnitQuantityDespatched',
        countryOfOrigin(d) EXTID 'CountryOfOrigin',
        expiryDate = toChar(expiryDate(d),'YYYYMMDD') EXTID 'BestBeforeDate',
        quantityDespatchedSPT(d) EXTID 'QuantityDespatchedSPT',
        lineItemQuantitySPT(d) EXTID 'LineItemQuantitySPT',
        valueVAT(d) EXTID 'TaxRate',
        additionalInformation(d) EXTID 'AdditionalInformation',
        lineItemPartyNumber(d) EXTID 'LineItemPartyNumber',
        manufactureDate = toChar(manufactureDate(d),'YYYYMMDD') EXTID 'LineItemProductDate',
        lineItemAmountWithoutCharges(d) EXTID 'LineItemAmountWithoutCharges',
        lineItemAmountCharges(d) EXTID 'LineItemAmountCharges' ,
        lineItemAmount(d) EXTID 'LineItemAmount',
        lineItemPrice(d) EXTID 'LineItemPrice',
        lineItemPriceSPT(d) EXTID 'LineItemPriceSPT',
        lineItemAmountExcise(d) EXTID 'LineItemAmountExcise'
    PROPERTIES DRAW d IN fieldCostControl
        lineItemManufacturerOutputPrice(d) EXTID 'LineItemManufacturerOutputPrice',
        typeDiscount(d) EXTID 'TypeDiscount',
        bulkDiscountRate(d) EXTID 'BulkDiscountRate',
        outputPriceIncludingShippingCoast(d) EXTID 'OutputPriceIncludingShippingCoast',
        outputPriceShippingCoast(d) EXTID 'OutputPriceShippingCoast',
        outputPricePackingCoast(d) EXTID 'OutputPricePackingCoast',
        bulkMarginRate(d) EXTID 'BulkMarginRate',
        cutRate(d) EXTID 'CutRate',
        additionalInformationPrice(d) EXTID 'AdditionalInformationPrice',
        additionalInformationNote(d) EXTID 'AdditionalInformation'

    OBJECTS lineItemIdentification = EInvoiceLotDetail EXTID 'LineItemIdentification'
    FILTERS eInvoiceDetail(lineItemIdentification) == d, isUpperLevelCodeMark(lineItemIdentification)

    PROPERTIES IN groupPackingHeader
        groupPackingCodeMark = encodeTextNoNewLines(idParent(lineItemIdentification),'base64') EXTID 'GroupPackingCodeMark',
        upperLevelCodeMark = encodeTextNoNewLines(id(lineItemIdentification),'base64') EXTID 'UpperLevelCodeMark'

    OBJECTS pdl = EInvoiceLotDetail EXTID 'FieldCodeMark' IN fieldCodeMarkHeader
    PROPERTIES  pid = encodeTextNoNewLines(id(pdl),'base64') EXTID 'value'
    FILTERS eInvoiceDetail(pdl) == d, lineItemIdentification = parent(pdl), isFieldCodeMark(pdl)

    OBJECTS dl = EInvoiceLotDetail EXTID 'FieldCodeMark' IN exportfieldCodeMarkHeader
    PROPERTIES  id = encodeTextNoNewLines(id(dl),'base64') EXTID 'value'
    FILTERS eInvoiceDetail(dl) == d, isFieldCodeMark(dl), NOT parent(dl)

    OBJECTS if = ItemExtraField EXTID 'LineItemExtraField'
    FILTERS eInvoiceDetail(if) == d
    PROPERTIES DRAW if 
        name(if) EXTID 'FieldName',
        code(if) EXTID 'FieldCode',
        value(if) EXTID 'FieldValue'

    OBJECTS eint = EInvoice EXTID 'Total'
    FILTERS eint == ei
    PROPERTIES DRAW ein
        totalAmountWithoutCharges(eint) EXTID 'TotalAmountWithoutCharges',
        totalAmountCharges(eint) EXTID 'TotalAmountCharges',
        totalAmount(eint) EXTID 'TotalAmount',
        totalLineItem(eint) EXTID 'TotalLineItem',
        totalLineItemQuantity(eint) EXTID 'TotalLineItemQuantity',
        totalGrossWeight(eint) EXTID 'TotalGrossWeight',
        totalDespatchUnitQuantity(eint) EXTID 'TotalDespatchUnitQuantity',
        totalAmountExcise(eint) EXTID 'TotalAmountExcise'
;

exportBlrdlnDateTime = DATA LOCAL NESTED DATETIME();

hideMessageHeaderBlrdln = DATA LOCAL BOOLEAN ();


FORM exportBlrdlnNew FORMEXTID 'BLRDLN'
    PROPERTIES ATTR ='2.0' IF hideMessageHeaderBlrdln() EXTID 'version'
    OBJECTS ei = EInvoice PANEL

    OBJECTS eih = EInvoice EXTID 'MessageHeader'
    FILTERS (eih == ei AND NOT hideMessageHeaderBlrdln()) OR NOT eih IS EInvoice
    PROPERTIES DRAW eih 
        messageId = resultMilliseconds() EXTID 'MessageID',
        msgDateTime = toChar(exportBlrdlnDateTime(),'YYYYMMDDHH24MISS') EXTID 'MsgDateTime',
        messageType = 'BLRDLN' EXTID 'MessageType',
        glnSupplier(eih) EXTID 'MsgSenderID',
        glnCustomer(eih) EXTID 'MsgReceiverID',
        userIDCustomerStock(eih) EXTID 'UserID',
        testIndicator = IF isTestDocument(eih) THEN '1' EXTID 'TestIndicator',
        formType = formType(eih) EXTID 'FormType'

    OBJECTS ein = EInvoice EXTID 'DeliveryNote'
    FILTERS ein == ei
    PROPERTIES DRAW ein 
        deliveryNoteType = '270' EXTID 'DeliveryNoteType',
        documentID = resultMilliseconds() EXTID 'DocumentID',
        creationDateTime = toChar(exportBlrdlnDateTime(),'YYYYMMDDHH24MISS') EXTID 'CreationDateTime',
        functionCode = (IF isCancel(ein) THEN '1' ELSE '9') EXTID 'FunctionCode',
        deliveryNoteNumber(ein) EXTID 'DeliveryNoteID',
        deliveryNoteDate = toChar(deliveryNoteDate(ein),'YYYYMMDD') EXTID 'DeliveryNoteDate',
        contractName(ein) EXTID 'ContractName',
        contractId(ein) EXTID 'ContractID',
        contractDate = toChar(contractDate(ein),'YYYYMMDD') EXTID 'ContractDate'
    PROPERTIES DRAW ein IN shipper 
        glnSupplier(ein) EXTID 'GLN',
        nameSupplier(ein) EXTID 'Name',
        addressSupplier(ein) EXTID 'Address',
        unpSupplier(ein) EXTID 'VATRegistrationNumber',
        contactSupplier(ein) EXTID 'Contact'
    PROPERTIES DRAW ein IN receiver 
        glnCustomer(ein) EXTID 'GLN',
        nameCustomer(ein) EXTID 'Name',
        addressCustomer(ein) EXTID 'Address',
        unpCustomer(ein) EXTID 'VATRegistrationNumber',
        country = sidOrigin2(country(customer(ein))) IF sidOrigin2(country(customer(ein))) != 'BY' EXTID 'Country'
    PROPERTIES DRAW ein IN shipFrom 
        glnSupplierStock(ein) EXTID 'GLN',
        addressSupplierStock(ein) EXTID 'Address',
        contactSupplierStock(ein) EXTID 'Contact'
    PROPERTIES DRAW ein IN shipTo 
        glnCustomerStock(ein) EXTID 'GLN',
        addressCustomerStock(ein) EXTID 'Address'
    PROPERTIES DRAW ein IN carrier 
        deliveryContact(ein) EXTID 'DeliveryContact',
        proxyID(ein) EXTID 'ProxyID',
        proxyDate = toChar(proxyDate(ein),'YYYYMMDD') EXTID 'ProxyDate',
        partyIssuingProxyName(ein) EXTID 'PartyIssuingProxyName'
    PROPERTIES DRAW ein 
        orderID(ein) EXTID 'OrderID',
        cancelDeliveryNoteId(ein) EXTID 'CancelDeliveryNoteID',
        currency(ein) EXTID 'Currency'

    OBJECTS f = ExtraField EXTID 'ExtraField'
    FILTERS eInvoice(f) == ein
    PROPERTIES DRAW f 
        name(f) EXTID 'FieldName',
        code(f) EXTID 'FieldCode',
        value(f) EXTID 'FieldValue'

    OBJECTS d = EInvoiceDetail EXTID 'LineItem' IN despatchAdviceLogisticUnitLineItem
    FILTERS eInvoice(d) == ein
    PROPERTIES DRAW d 
        lineItemNumber(d) EXTID 'LineItemNumber',
        lineItemSign(d) EXTID 'LineItemSign',
        lineItemID(d) EXTID 'LineItemID',
        itemCustomCode(d) EXTID 'ItemCustomCode',
        itemCustomCodeOther(d) EXTID 'ItemCustomCodeOther',
        deliveryTypePrev(d) EXTID 'DeliveryTypePrev',
        deliveryNotePrev(d) EXTID 'DeliveryNotePrev',
        deliveryNoteDatePrev = toChar(deliveryNoteDatePrev(d),'YYYYMMDD') EXTID 'DeliveryNoteDatePrev',
        deliveryNotePrevLineID(d) EXTID 'DeliveryNotePrevLineID',
        lineItemBuyerID(d) EXTID 'LineItemBuyerID',
        lineItemSupplierID(d) EXTID 'LineItemSupplierID',
        lineItemName(d) EXTID 'LineItemName',
        quantityDespatched(d) EXTID 'QuantityDespatched',
        lineItemQuantityUOM = mapToNewUOM(lineItemQuantityUOM(d)) EXTID 'LineItemQuantityUOM',
        countryOfOrigin(d) EXTID 'CountryOfOrigin',
        expiryDate = toChar(expiryDate(d),'YYYYMMDD') EXTID 'BestBeforeDate',
        quantityDespatchedSPT(d) EXTID 'QuantityDespatchedSPT',
        lineItemQuantitySPT(d) EXTID 'LineItemQuantitySPT',
        valueVAT(d) EXTID 'TaxRate',
        additionalInformation(d) EXTID 'AdditionalInformation',
        lineItemPartyNumber(d) EXTID 'LineItemPartyNumber',
        manufactureDate = toChar(manufactureDate(d),'YYYYMMDD') EXTID 'LineItemProductDate',
        lineItemAmountWithoutCharges(d) EXTID 'LineItemAmountWithoutCharges',
        lineItemAmountCharges(d) EXTID 'LineItemAmountCharges' ,
        lineItemAmount(d) EXTID 'LineItemAmount',
        lineItemPrice(d) EXTID 'LineItemPrice',
        lineItemPriceSPT(d) EXTID 'LineItemPriceSPT',
        lineItemAmountExcise(d) EXTID 'LineItemAmountExcise'
    PROPERTIES DRAW d IN fieldCostControl
        lineItemManufacturerOutputPrice(d) EXTID 'LineItemManufacturerOutputPrice',
        typeDiscount(d) EXTID 'TypeDiscount',
        bulkDiscountRate(d) EXTID 'BulkDiscountRate',
        outputPriceIncludingShippingCoast(d) EXTID 'OutputPriceIncludingShippingCoast',
        outputPriceShippingCoast(d) EXTID 'OutputPriceShippingCoast',
        outputPricePackingCoast(d) EXTID 'OutputPricePackingCoast',
        bulkMarginRate(d) EXTID 'BulkMarginRate',
        cutRate(d) EXTID 'CutRate',
        additionalInformationPrice(d) EXTID 'AdditionalInformationPrice',
        additionalInformationNote(d) EXTID 'AdditionalInformation'

    OBJECTS lineItemIdentification = EInvoiceLotDetail EXTID 'LineItemIdentification'
    FILTERS eInvoiceDetail(lineItemIdentification) == d, isUpperLevelCodeMark(lineItemIdentification)

    PROPERTIES IN groupPackingHeader
    groupPackingCodeMark = encodeTextNoNewLines(idParent(lineItemIdentification),'base64') EXTID 'GroupPackingCodeMark',
        upperLevelCodeMark = encodeTextNoNewLines(id(lineItemIdentification),'base64') EXTID 'UpperLevelCodeMark'

    OBJECTS pdl = EInvoiceLotDetail EXTID 'FieldCodeMark' IN fieldCodeMarkHeader
    PROPERTIES  pid = encodeTextNoNewLines(id(pdl),'base64') EXTID 'value'
    FILTERS eInvoiceDetail(pdl) == d, lineItemIdentification = parent(pdl), isFieldCodeMark(pdl)

    OBJECTS dl = EInvoiceLotDetail EXTID 'FieldCodeMark' IN exportfieldCodeMarkHeader
    PROPERTIES  id = encodeTextNoNewLines(id(dl),'base64') EXTID 'value'
    FILTERS eInvoiceDetail(dl) == d, isFieldCodeMark(dl), NOT parent(dl)

    OBJECTS if = ItemExtraField EXTID 'LineItemExtraField'
    FILTERS eInvoiceDetail(if) == d
    PROPERTIES DRAW if name(if) EXTID 'FieldName',
        code(if) EXTID 'FieldCode',
        value(if) EXTID 'FieldValue'

    OBJECTS eint = EInvoice EXTID 'Total'
    FILTERS eint == ei
    PROPERTIES DRAW ein
        totalAmountWithoutCharges(eint) EXTID 'TotalAmountWithoutCharges',
        totalAmountCharges(eint) EXTID 'TotalAmountCharges',
        totalAmount(eint) EXTID 'TotalAmount',
        totalLineItem(eint) EXTID 'TotalLineItem',
        totalLineItemQuantity(eint) EXTID 'TotalLineItemQuantity',
        totalAmountExcise(eint) EXTID 'TotalAmountExcise'
;

//акты

importedSupplier 'Импортирован (поставщик)' = DATA BOOLEAN (ECreditNote);
exportedSupplier 'Экспортирован (поставщик)' = DATA BOOLEAN (ECreditNote);

customerADF 'blradf от покупателя' = DATA XMLFILE (ECreditNote);
//Подпись покупателя
securityID = DATA STRING[14](ECreditNote);
certificateID = DATA STRING[256](ECreditNote);
securityPartyID = DATA STRING[512](ECreditNote);
creationSignatureDate = DATA STRING[8](ECreditNote);
creationSignatureTime = DATA STRING[15](ECreditNote);
signatureValue = DATA TEXT(ECreditNote);

signAndSendSupplier 'Подписать и отправить (поставщик)'  ABSTRACT LIST (ECreditNote);
signAndSendSupplierCreditNoteEDI 'Подписать и отправить (поставщик)'()  {
    IF askPasswordEDSService() THEN {
        DIALOG passwordEDSService DO {} ELSE { BREAK; };
    }
    FOR in(ECreditNote c) DO {
        IF EDIProvider(supplier(c)) THEN
            signAndSendSupplier(c);
        ELSE
            MESSAGE 'Не выбран EDI провайдер для акта приемки : ' + actDifNumber(c);
        in(c) <- NULL;
    }
        ELSE
    MESSAGE 'Не выбрано ни одного акта приемки';

}

EXTEND FORM eCreditNotes
    PROPERTIES(c) BEFORE isCancel(c) READONLYIF isReadonly() importedSupplier, exportedSupplier
    EXTEND FILTERGROUP notExported
        FILTER 'Не экспортированы (поставщик)' NOT exportedSupplier(c) 'F8'
        FILTER 'Не импортированы (поставщик)' NOT importedSupplier(c) 'F11'
    PROPERTIES() DRAW c TOOLBAR signAndSendSupplierCreditNoteEDI
;

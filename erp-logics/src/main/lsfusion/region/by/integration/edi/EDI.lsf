MODULE EDI;

REQUIRE EDIGLN, EDIGTIN, Item, Warehouse, LogTools, EDIGLNWarehouse;

@defineLogRotate(edi, 5, weekly);
downloadEdi 'edi' () { downloadFiles('logs/edi.log'); }

EXTEND FORM tools
    PROPERTIES() downloadEdi
;

DESIGN tools{
    download {
        MOVE PROPERTY(downloadEdi());
    }
}

DESIGN options {
    pane {
        NEW EDI {
            caption = 'EDI';
        }
    }
}

logStackForException 'Логировать Stack Trace' = DATA BOOLEAN ();
EXTEND FORM integrationData 
    PROPERTIES logStackForException();

DESIGN integrationData {
    pane {
        NEW edi {
            tabbed = TRUE;
            caption = 'EDI-провайдеры';
            NEW commonEdi {
                caption = 'Общие настройки';
                MOVE PROPERTY(logStackForException()); 
            }
        }
    }
}

extraCodeUOM 'Ед. изм. (EDI)' = DATA STRING[3] (UOM);
UOMExtraCode = GROUP MAX UOM u BY extraCodeUOM(u);
EXTEND FORM UOMs PROPERTIES(u) READONLY extraCodeUOM;
EXTEND FORM UOM PROPERTIES(u) extraCodeUOM;

TABLE stringEDI(STRING) NODEFAULT;

CLASS EDIProvider 'Провайдер EDI' : StaticObject;
prefix = ABSTRACT CASE STRING[4](EDIProvider);
name = ABSTRACT CASE STRING[100](EDIProvider);
gln =ABSTRACT CASE STRING[13](EDIProvider);

FORM EDIProviders 'Провайдеры EDI'
    OBJECTS t = EDIProvider
    PROPERTIES (t) READONLY staticCaption
    LIST EDIProvider OBJECT t
;

DESIGN EDIProviders {
    size = (600, 400);
    PROPERTY (staticCaption(t)){caption = 'Наименование';};
}

EDIProvider 'EDI-провайдер' = DATA EDIProvider(LegalEntity);
nameEDIProvider 'EDI-провайдер'(LegalEntity l) = staticCaption(EDIProvider(l));
useV2 'Использовать формат V2' = DATA BOOLEAN (LegalEntity);
id = DATA STRING (LegalEntity, LegalEntity);

EXTEND FORM legalEntity
    PROPERTIES(l) GLN, nameEDIProvider, notUseInStockGLN, useV2

    OBJECTS eCom = LegalEntity
    PROPERTIES (eCom) READONLY name, id
    FILTERS isCompany(eCom)
;

DESIGN legalEntity {
    extendContainer {
        NEW EDITab {
            caption = 'EDI';
            fill = 1;
            NEW EDI {
                horizontal = TRUE;
                NEW EDIComm {
                    caption = 'Общие';
                    MOVE PROPERTY (GLN(l));
                    MOVE PROPERTY (notUseInStockGLN(l));
                    MOVE PROPERTY (nameEDIProvider(l));
                    MOVE PROPERTY (useV2(l));
                }
            }
            MOVE BOX (eCom) {caption = 'Компании';};
        }
    }
}
EXTEND FORM legalEntities PROPERTIES(l) READONLYIF isReadonly() GLN, nameEDIProvider;

NAVIGATOR {
    financeNavigator {
        NEW FOLDER edi 'EDI';
    }
}

//message type/kind //

CLASS EInvoiceMessageKind 'Вид сообщения по накладной EDI' {
    in 'входящее',
    out 'исходящее'
}

CLASS EInvoiceMessageType 'Тип сообщения по накладной EDI' : StaticObject;

EXTEND CLASS EInvoiceMessageType {
    blrapn_blrapn 'BLRAPN-BLRAPN'
}

//-------------------------- Send Request ------------------------------//
sendRequestEDIResponse = DATA LOCAL FILE();
sendRequestEDIStatus = DATA LOCAL INTEGER();

isXml = DATA LOCAL BOOLEAN ();
basic = DATA LOCAL TEXT ();
token = DATA LOCAL TEXT ();
encodeText = FORMULA TEXT 'encode(convert_to(($1),\'UTF8\'),($2))';
decodeText = FORMULA TEXT 'convert_from(decode(($1),($2)), \'UTF8\')';
httpHeadersEDI(TEXT name) = CASE 
    WHEN isXml() AND name = 'Content-Type' THEN 'text/xml'
    WHEN basic() AND name = 'Authorization' THEN 'Basic ' + basic()
    WHEN token() AND name = 'Authorization' THEN token();

sendRequestEDI 'Отправить' (TEXT login, TEXT password, TEXT url, FILE request, BOOLEAN basicAuthorization, BOOLEAN client) {
    logToFile('edi', CONCAT ' ', 'SendRequestEDI ' + url, '(client)' IF client);
    basic() <- encodeText(login + ':' + password, 'base64') IF basicAuthorization;
    timeoutHttp() <- 300000;
    isXml() <- extension(request) == 'xml';
    IF client THEN {
        EXTERNAL HTTP CLIENT url HEADERS httpHeadersEDI PARAMS request TO sendRequestEDIResponse;
    } ELSE {
        EXTERNAL HTTP url HEADERS httpHeadersEDI PARAMS request TO sendRequestEDIResponse;
    }
    sendRequestEDIStatus() <- statusHttp();
}
sendRequestEDI 'Отправить' (TEXT url, XMLFILE request) {
    sendRequestEDI(NULL, NULL, url, FILE(request), NULL, NULL);
}
sendRequestEDI 'Отправить' (TEXT url, FILE request) {
    sendRequestEDI(NULL, NULL, url, request, NULL, NULL);
}

//----------------------- importRequestResult ---------------------------//

succesful = DATA LOCAL TEXT(INTEGER);
message = DATA LOCAL TEXT(INTEGER);
errorCode = DATA LOCAL INTEGER(INTEGER);

requestError = DATA LOCAL NESTED TEXT();
// todo : нужно переделать без входа xml и status
newNS = DATA LOCAL BOOLEAN ();
importRequestResult(FILE xml, INTEGER status, TEXT root) {
    IF status == 200 THEN {
        IF newNS() THEN {
            IMPORT XML ROOT root FROM xml TO succesful = '=ResponseSis:Succesful', message = '=ResponseSis:Message', errorCode = '=ResponseSis:ErrorCode';

        } ELSE {
            IMPORT XML ROOT root FROM xml TO succesful = 'Succesful', message = 'Message', errorCode = 'ErrorCode';
        }
        IF succesful(0) == 'true' THEN {
            requestError() <- NULL;
        } ELSE {
            IF errorCode(0) == 1300 THEN {
                requestError() <- 'Ошибка авторизации';
            } ELSE {
                requestError() <- CONCAT ' ', 'Ошибка ', errorCode(0) + ':', message(0);
            }
        }
    } ELSE {
        requestError() <- CONCAT ' ', 'Неизвестная ошибка: статус ', STRING[3](status);
    }
}

//добавление файлов
META openXML (class, object, table)
    XMLPath = DATA STRING[200] (class);
    openXML 'Открыть XML' (class object) {
        IF XMLPath(object) THEN {
            LOCAL file = FILE();
            TRY {READ 'file://' + XMLPath(object) + '.xml' TO file;}
            IF NOT file() THEN READ 'file://' + XMLPath(object) TO file;
            open(file());
        } ELSE {
            MESSAGE 'Отсутствует ссылка на файл xml';
        }
    }

    EXTEND FORM table
        PROPERTIES(object) openXML GRID
    ;
END

//
id = DATA LOCAL NESTED STRING (INTEGER);
documentType = DATA LOCAL NESTED STRING (INTEGER);
documentDate = DATA LOCAL NESTED DATETIME (INTEGER);
data = DATA LOCAL NESTED XMLFILE(INTEGER);

textMessage = DATA LOCAL TEXT();
sendRecipientErrorFailed = DATA LOCAL NESTED INTEGER();
supplierOrdersSucceeded = DATA LOCAL NESTED INTEGER();
supplierOrdersFailed = DATA LOCAL NESTED INTEGER();
orderMessagesSucceeded = DATA LOCAL NESTED INTEGER();
orderMessagesFailed = DATA LOCAL NESTED INTEGER();
responsesSucceeded = DATA LOCAL NESTED INTEGER();
responsesFailed = DATA LOCAL NESTED INTEGER();
despatchAdvicesSucceeded = DATA LOCAL NESTED INTEGER();
despatchAdvicesFailed = DATA LOCAL NESTED INTEGER();
invoiceMessagesSucceeded = DATA LOCAL NESTED INTEGER();
invoiceMessagesFailed = DATA LOCAL NESTED INTEGER();
eInvoicesSucceeded = DATA LOCAL NESTED INTEGER();
eInvoicesFailed = DATA LOCAL NESTED INTEGER();
creditNoteMessagesSucceeded = DATA LOCAL NESTED INTEGER();
creditNoteMessagesFailed = DATA LOCAL NESTED INTEGER();
eCreditNotesSucceeded = DATA LOCAL NESTED INTEGER();
eCreditNotesFailed = DATA LOCAL NESTED INTEGER();
documentMessagesSucceeded = DATA LOCAL NESTED INTEGER();
documentMessagesFailed = DATA LOCAL NESTED INTEGER();
eDocumentsSucceeded = DATA LOCAL NESTED INTEGER();
eDocumentsFailed = DATA LOCAL NESTED INTEGER();
blrwbrCount = DATA LOCAL NESTED INTEGER();
blrwbrSendCount = DATA LOCAL NESTED INTEGER();
blradfCount = DATA LOCAL NESTED INTEGER();
messagesConfirmed = DATA LOCAL NESTED INTEGER();

fillStartParams() {
    textMessage() <- NULL;
    sendRecipientErrorFailed() <- 0;
    supplierOrdersSucceeded() <- 0;
    supplierOrdersFailed() <- 0;
    orderMessagesSucceeded() <- 0;
    orderMessagesFailed() <- 0;
    responsesSucceeded() <- 0;
    responsesFailed() <- 0;
    despatchAdvicesSucceeded() <- 0;
    despatchAdvicesFailed() <- 0;
    invoiceMessagesSucceeded() <- 0;
    invoiceMessagesFailed() <- 0;
    eInvoicesSucceeded() <- 0;
    eInvoicesFailed() <- 0;
    creditNoteMessagesSucceeded() <- 0;
    creditNoteMessagesFailed() <- 0;
    eCreditNotesSucceeded() <- 0;
    eCreditNotesFailed() <- 0;
    documentMessagesSucceeded() <- 0;
    documentMessagesFailed() <- 0;
    eDocumentsSucceeded() <- 0;
    eDocumentsFailed() <- 0;
    blrwbrCount() <- 0;
    blrwbrSendCount() <- 0;
    blradfCount() <- 0;
    messagesConfirmed() <- 0;
}

fillEndParams() {
    IF supplierOrdersSucceeded() > 0 THEN textMessage() <- CONCAT '\n', textMessage(), 'Загружено заказов поставщика: ' + supplierOrdersSucceeded();
    IF supplierOrdersFailed() > 0 THEN textMessage() <- CONCAT '\n', textMessage(), 'Не загружено заказов поставщика: ' + supplierOrdersFailed();
    IF orderMessagesSucceeded() > 0 THEN textMessage() <- CONCAT '\n', textMessage(), 'Загружено сообщений по заказам: ' + orderMessagesSucceeded();
    IF orderMessagesFailed() > 0 THEN textMessage() <- CONCAT '\n', textMessage(), 'Не загружено сообщений по заказам: ' + orderMessagesFailed();
    IF responsesSucceeded() > 0 THEN textMessage() <- CONCAT '\n', textMessage(), 'Загружено ответов по заказам: ' + responsesSucceeded();
    IF responsesFailed() > 0 THEN textMessage() <- CONCAT '\n', textMessage(), 'Не загружено ответов по заказам: ' + responsesFailed();
    IF despatchAdvicesSucceeded() > 0 THEN textMessage() <- CONCAT '\n', textMessage(), 'Загружено уведомлений об отгрузке: ' + despatchAdvicesSucceeded();
    IF despatchAdvicesFailed() > 0 THEN textMessage() <- CONCAT '\n', textMessage(), 'Не загружено уведомлений об отгрузке: ' + despatchAdvicesFailed();
    IF invoiceMessagesSucceeded() > 0 THEN textMessage() <- CONCAT '\n', textMessage(), 'Загружено сообщений по накладным: ' + invoiceMessagesSucceeded();
    IF invoiceMessagesFailed() > 0 THEN textMessage() <- CONCAT '\n', textMessage(), 'Не загружено сообщений по накладным: ' + invoiceMessagesFailed();
    IF eInvoicesSucceeded() > 0 THEN textMessage() <- CONCAT '\n', textMessage(), 'Загружено электронных накладных: ' + eInvoicesSucceeded();
    IF eInvoicesFailed() > 0 THEN textMessage() <- CONCAT '\n', textMessage(), 'Не загружено электронных накладных: ' + eInvoicesFailed();
    IF creditNoteMessagesSucceeded() > 0 THEN textMessage() <- CONCAT '\n', textMessage(), 'Загружено сообщений по акту приемки: ' + creditNoteMessagesSucceeded();
    IF creditNoteMessagesFailed() > 0 THEN textMessage() <- CONCAT '\n', textMessage(), 'Не загружено сообщений по акту приемки: ' + creditNoteMessagesFailed();
    IF eCreditNotesSucceeded() > 0 THEN textMessage() <- CONCAT '\n', textMessage(), 'Загружено электронных актов приемки: ' + eCreditNotesSucceeded();
    IF eCreditNotesFailed() > 0 THEN textMessage() <- CONCAT '\n', textMessage(), 'Не загружено электронных актов приемки: ' + eCreditNotesFailed();
    IF documentMessagesSucceeded() > 0 THEN textMessage() <- CONCAT '\n', textMessage(), 'Загружено сообщений по документам: ' + documentMessagesSucceeded();
    IF documentMessagesFailed() > 0 THEN textMessage() <- CONCAT '\n', textMessage(), 'Не загружено сообщений по документам: ' + documentMessagesFailed();
    IF eDocumentsSucceeded() > 0 THEN textMessage() <- CONCAT '\n', textMessage(), 'Загружено электронных документов: ' + eDocumentsSucceeded();
    IF eDocumentsFailed() > 0 THEN textMessage() <- CONCAT '\n', textMessage(), 'Не загружено электронных документов: ' + eDocumentsFailed();
    IF blrwbrCount() > 0 THEN textMessage() <- CONCAT '\n', textMessage(), 'Получено ' + blrwbrCount() + ' BLRWBR/BLRDNR';
    IF blrwbrSendCount() > 0 THEN textMessage() <- CONCAT '\n', textMessage(), 'Отвечено на ' + blrwbrSendCount() + ' BLRDNR/BLRDNR';
    IF blradfCount() > 0 THEN textMessage() <- CONCAT '\n', textMessage(), 'Загружено ' + blradfCount() + ' BLRADF';
    IF messagesConfirmed() > 0 THEN textMessage() <- CONCAT '\n', textMessage(), 'Пропущено ' + messagesConfirmed() + ' сообщений';
}
//

GROUP reference;
messageDocumentNumber = DATA LOCAL NESTED STRING[35]();
messageDocumentDate = DATA LOCAL NESTED DATETIME();
messageBuyerGLN = DATA LOCAL NESTED STRING[48]();
messageSupplierGLN = DATA LOCAL NESTED STRING[48]();
name = DATA LOCAL NESTED STRING ();
documentType = DATA LOCAL NESTED TEXT();
documentNumber = DATA LOCAL NESTED STRING[48]();
documentDate = DATA LOCAL NESTED DATETIME();
code = DATA LOCAL NESTED STRING[10]();
description = DATA LOCAL NESTED STRING[1000]();

description (STRING[10] code) = CASE
    WHEN code == '1251' THEN 'Сообщение прочитано получателем'
    WHEN code == '1252' THEN 'Сообщение принято учётной системой получателя'
    WHEN code == '2650' THEN 'Извещение о прочтении'
    WHEN code == '2651' THEN 'Извещение о прочтении уведомления об изменении'
    WHEN code == '2750' THEN 'Уведомление об изменении';

FORM systemMessage
    PROPERTIES() messageDocumentNumber EXTID 'documentNumber',
                 messageDocumentDate EXTID 'documentDate',
                 messageBuyerGLN EXTID 'buyerGLN',
                 messageSupplierGLN EXTID 'supplierGLN'
    PROPERTIES() IN reference documentType, documentNumber, documentDate, code, description
;

GROUP messageHeader EXTID 'MessageHeader';
GROUP acknowledgement EXTID 'Acknowledgement';
GROUP referenceDocument EXTID 'ReferenceDocument';
GROUP blrapnReferenceDocument EXTID 'ReferenceDocument': acknowledgement;
GROUP blrapnShipper EXTID 'Shipper': acknowledgement;
GROUP blrapnReceiver EXTID 'Receiver': acknowledgement;
GROUP blrapnErrorOrAcknowledgement EXTID 'ErrorOrAcknowledgement': acknowledgement;
blrapnTestIndicator = DATA LOCAL NESTED STRING[1]();
blrapnUserID = DATA LOCAL NESTED STRING[35]();
blrapnDocumentNumber = DATA LOCAL NESTED STRING[35]();
blrapnDateTime = DATA LOCAL NESTED DATETIME();
blrapnMsgSender = DATA LOCAL NESTED STRING[13]();
blrapnMsgReceiver = DATA LOCAL NESTED STRING[13]();
blrapnDeliveryNoteId = DATA LOCAL NESTED STRING[46]();
blrapnDeliveryNoteDate = DATA LOCAL NESTED DATE();
blrapnReferenceDocumentType = DATA LOCAL NESTED STRING[6]();
blrapnReferenceDocumentId = DATA LOCAL NESTED STRING[46]();
blrapnReferenceDocumentDate = DATA LOCAL NESTED DATETIME();
blrapnShipper = DATA LOCAL NESTED STRING[48]();
blrapnReceiver = DATA LOCAL NESTED STRING[48]();
blrapnCode = DATA LOCAL NESTED STRING[8]();
blrapnDescription = DATA LOCAL NESTED STRING[1000]();

FORM importBlrapn
    PROPERTIES() IN messageHeader blrapnTestIndicator EXTID 'TestIndicator', blrapnUserID EXTID 'UserID'
    PROPERTIES() IN acknowledgement blrapnDocumentNumber EXTID 'DocumentID', blrapnDateTime EXTID 'CreationDateTime',
        blrapnDeliveryNoteId EXTID 'DeliveryNoteID', blrapnDeliveryNoteDate EXTID 'DeliveryNoteDate'
    PROPERTIES() IN blrapnReferenceDocument blrapnReferenceDocumentType EXTID 'Type',
        blrapnReferenceDocumentId EXTID 'ID', blrapnReferenceDocumentDate EXTID 'Date'
    PROPERTIES() IN blrapnShipper blrapnShipper EXTID 'GLN'
    PROPERTIES() IN blrapnReceiver blrapnReceiver EXTID 'GLN'
    PROPERTIES() IN blrapnErrorOrAcknowledgement blrapnCode EXTID 'Code', blrapnDescription EXTID 'Description'
;

hideMessageHeaderBlrapn = DATA LOCAL BOOLEAN ();

FORM exportBlrapnNew FORMEXTID 'BLRAPN'
    PROPERTIES ATTR ='2.0' IF hideMessageHeaderBlrapn() EXTID 'version'
    PROPERTIES IN messageHeader
        messageId = resultMilliseconds() IF NOT hideMessageHeaderBlrapn() EXTID 'MessageID',
        msgDateTime = toChar(blrapnDateTime(),'YYYYMMDDHH24MISS') IF NOT hideMessageHeaderBlrapn() EXTID 'MsgDateTime',
        messageType = 'BLRAPN' IF NOT hideMessageHeaderBlrapn() EXTID 'MessageType',
        msgSenderID = blrapnMsgSender() IF NOT hideMessageHeaderBlrapn() EXTID 'MsgSenderID',
        msgReceiverID = blrapnMsgReceiver() IF NOT hideMessageHeaderBlrapn() EXTID 'MsgReceiverID',
        userID = blrapnUserID() IF NOT hideMessageHeaderBlrapn() EXTID 'UserID'
    PROPERTIES IN acknowledgement
        documentId = resultMilliseconds() EXTID 'DocumentID',
        functionCode = '6' EXTID 'FunctionCode',
        creationdateTime = toChar(blrapnDateTime(),'YYYYMMDDHH24MISS') EXTID 'CreationDateTime',
        blrapnDeliveryNoteId() EXTID 'DeliveryNoteID',
        deliveryNoteDate = toChar(blrapnDeliveryNoteDate(),'YYYYMMDD') EXTID 'DeliveryNoteDate',
        blrapnReferenceDocumentType() EXTID 'Type' IN blrapnReferenceDocument,
        blrapnReferenceDocumentId() EXTID 'ID' IN blrapnReferenceDocument,
        date = toChar(blrapnReferenceDocumentDate(),'YYYYMMDDHH24MISS') EXTID 'Date' IN blrapnReferenceDocument,
        blrapnShipper() EXTID 'GLN' IN blrapnShipper,
        blrapnReceiver() EXTID 'GLN' IN blrapnReceiver,
        blrapnCode() EXTID 'Code' IN blrapnErrorOrAcknowledgement,
        blrapnDescription() EXTID 'Description' IN blrapnErrorOrAcknowledgement
;
MODULE InvoiceEInvoice;

REQUIRE Invoice, CustomerEInvoiceEDI;

NAMESPACE EDI;

invoice = DATA Invoice(EInvoice) INDEXED;
eInvoice = GROUP LAST EInvoice ei IF NOT isCancel(ei) ORDER deliveryNoteDateTime(ei), ei BY invoice(ei);
numberInvoice 'Номер накладной EDI (приход)' (EInvoice i) = number(invoice(i));

eInvoiceAll = GROUP LAST EInvoice ei AS EInvoice IF ei IS EInvoice ORDER deliveryNoteDateTime(ei), ei BY invoice(ei);

numberEInvoice 'Серия и номер'(Invoice i) = deliveryNoteNumber(eInvoice(i));

EXTEND FORM eInvoices
    PROPERTIES (e) READONLY PurchaseNumberInvoice = numberInvoice
;

currentOrderMessage = GROUP LAST EInvoiceMessage m ORDER dateTime(m), m WHERE
    (type(m) == EInvoiceMessageType.blrapn_blrwbr AND NOT isTN2(eInvoice(m))) OR
    (type(m) == EInvoiceMessageType.blrapn_blrdnr AND isTN2(eInvoice(m)))
    AND NOT kind(m) == EInvoiceMessageKind.out BY invoice(eInvoice(m));
statusDescription 'Статус отправки EDI'(Invoice o) = CASE 
    WHEN code(currentOrderMessage(o)) == '2550' THEN 'Подтверждено получение ответа на электронную накладную'
    WHEN exportedCustomer(eInvoice(o)) AND NOT currentOrderMessage(o) THEN 'Отправлен ответ на электронную накладную'
    ELSE CONCAT ' ', code(currentOrderMessage(o)), description(currentOrderMessage(o)) CHARWIDTH 50;
backgroundStatusDescription (Invoice i) = RGB(255,238,238) IF currentOrderMessage(i) AND NOT good(currentOrderMessage(i));

receiver = ABSTRACT Employee (Invoice);

skipReceiver = ABSTRACT BOOLEAN (Invoice);

CONSTRAINT isPosted(Invoice i) AND NOT receiver(i) AND NOT skipReceiver(i) AND eInvoice(i)
        MESSAGE 'В накладной должен быть указан приёмщик';
        
changeContactString (EInvoice e, Employee em)  { 
    contactCustomerStock(e) <- STRING[150](CONCAT ', ', shortName(em), namePosition(em));
}

WHEN SETCHANGED (receiver(Invoice i)) OR SETCHANGED (eInvoice(i)) DO 
    changeContactString(eInvoice(i), receiver(i));

isReceived 'Принят' (EInvoice e) = isPosted(invoice(e));
EXTEND FORM eInvoices
    PROPERTIES (e) READONLY isReceived BEFORE deliveryNoteDateTime(e)
    FILTERGROUP isReceived FILTER 'Принятые' isReceived(e) 
;
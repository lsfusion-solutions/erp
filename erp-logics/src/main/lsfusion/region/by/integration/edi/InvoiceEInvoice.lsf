MODULE InvoiceEInvoice;

REQUIRE Invoice, CustomerEInvoice;

NAMESPACE EDI;

invoice = DATA Invoice(EInvoice);
INDEX invoice(EInvoice i), deliveryNoteDate(i), i;
eInvoice = GROUP LAST EInvoice ei IF NOT isCancel(ei) ORDER deliveryNoteDate(ei), ei BY invoice(ei) MATERIALIZED INDEXED;
numberInvoice 'Номер накладной EDI (приход)' (EInvoice i) = number(invoice(i));

eInvoiceAll = GROUP LAST EInvoice ei AS EInvoice IF ei IS EInvoice ORDER deliveryNoteDate(ei), ei BY invoice(ei) MATERIALIZED INDEXED;

numberEInvoice 'Серия и номер'(Invoice i) = deliveryNoteNumber(eInvoice(i));

EXTEND FORM eInvoices
    PROPERTIES (e) READONLY PurchaseNumberInvoice = numberInvoice
;

currentOrderMessage = GROUP LAST EInvoiceMessage m ORDER dateTime(m), m WHERE
    isCustomer(m) AND kind(m) == EInvoiceMessageKind.in BY invoice(eInvoice(m));
statusDescription 'Статус отправки EDI'(Invoice o) = CASE 
    WHEN code(currentOrderMessage(o)) == '2550' THEN 'Подтверждено получение ответа на электронную накладную'
    WHEN exportedCustomer(eInvoice(o)) AND NOT currentOrderMessage(o) THEN 'Отправлен ответ на электронную накладную'
    WHEN exportedNoticeCustomer(eInvoice(o)) AND NOT currentOrderMessage(o) THEN 'Отправлено уведомление об изменении'
    ELSE CONCAT ' ', code(currentOrderMessage(o)), description(currentOrderMessage(o)) CHARWIDTH 50;
backgroundStatusDescription (Invoice i) = RGB(255,238,238) IF currentOrderMessage(i) AND NOT good(currentOrderMessage(i));

receiver = ABSTRACT Employee (Invoice);

skipReceiver = ABSTRACT VALUE BOOLEAN (Invoice);

CONSTRAINT isPosted(Invoice i) AND NOT receiver(i) AND NOT skipReceiver(i) AND eInvoice(i)
        MESSAGE 'В накладной должен быть указан приёмщик';

customChangeContactString = ABSTRACT BOOLEAN (EInvoice);
overChangeContactString ABSTRACT (EInvoice, Employee);

changeContactString (EInvoice e, Employee em) {
    IF customChangeContactString(e) THEN
        overChangeContactString(e, em);
    ELSE
        contactCustomerStock(e) <- STRING[150](CONCAT ', ', shortName(em), namePosition(em));
}

WHEN SETCHANGED (receiver(Invoice i)) OR SETCHANGED (eInvoice(i)) DO 
    changeContactString(eInvoice(i), receiver(i));

isReceived 'Принят' (EInvoice e) = isPosted(invoice(e));
EXTEND FORM eInvoices
    PROPERTIES (e) READONLY isReceived BEFORE deliveryNoteDate(e)
    FILTERGROUP isReceived FILTER 'Принятые' isReceived(e) 
;

useECriditNote 'Использовать электронные акты приемки' = DATA BOOLEAN(LegalEntity);

EXTEND FORM legalEntity PROPERTIES(l) SHOWIF isSupplier(l) useECriditNote;
DESIGN legalEntity {
    EInvoice {
        MOVE PROPERTY(useECriditNote(l));
    }
}
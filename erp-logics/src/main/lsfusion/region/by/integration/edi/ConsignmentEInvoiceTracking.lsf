MODULE ConsignmentEInvoiceTracking;

REQUIRE ConsignmentEInvoice, ItemTracking, PurchaseEInvoice, BatchTracking;

NAMESPACE Consignment;

isTracking = ABSTRACT VALUE BOOLEAN (ConsignmentDetail);
isTracking(ConsignmentDetail cd) += isTrackingCustomsGroup(sku(cd));

trackingUOM = ABSTRACT VALUE STRING[3] (ConsignmentDetail);
trackingUOM(ConsignmentDetail cd) += trackingUOMCustomsGroup(sku(cd));

overCreate(EInvoiceDetail eid, ConsignmentDetail cd) + {
    IF (currentDate() < dateStartSPT() OR NOT dateStartSPT() )THEN {
        itemCustomCode(eid) <- codeCustomsGroupDefaultCountry(sku(cd)) IF isTracking(cd);
        deliveryTypePrev(eid) <- (OVERRIDE (IF isTN2(eInvoice(eInvoiceDetail(invoiceDetail(batch(cd))))) THEN 2 ELSE IF eInvoice(eInvoiceDetail(invoiceDetail(batch(cd)))) THEN 1), 3) IF isTracking(cd);
        deliveryNotePrev(eid) <- deliveryNoteNumber(eInvoice(eInvoiceDetail(invoiceDetail(batch(cd))))) IF isTracking(cd);
        deliveryNoteDatePrev(eid) <- DATETIME (deliveryNoteDate(eInvoice(eInvoiceDetail(invoiceDetail(batch(cd)))))) IF isTracking(cd);
        deliveryNotePrevLineID(eid) <- (OVERRIDE numberEInvoiceDetail(invoiceDetail(batch(cd))), index(invoiceDetail(batch(cd)))) IF isTracking(cd);
        quantityDespatchedSPT(eid) <- (OVERRIDE despatchUnitQuantityDespatched(cd), NUMERIC[12,5](packQuantity(cd)), 1.0) IF isTracking(cd);
        lineItemQuantitySPT(eid) <- (OVERRIDE trackingUOM(cd), '796') IF isTracking(cd);
        lineItemPriceSPT(eid) <- (OVERRIDE lineItemPriceSPT(eInvoiceDetail(invoiceDetail(batch(cd)))), NUMERIC[12,2](price(cd))) IF isTracking(cd);
    }
}

overCreate(EInvoiceDetail eid, ConsignmentDetail cd) + {
    IF isTracking(cd) AND currentDate() >= dateStartSPT() THEN {
        itemCustomCode(eid) <- itemCustomCode(batch(cd));
        itemCustomCodeOther(eid) <- itemCustomCodeOther(batch(cd));
        deliveryTypePrev(eid) <- deliveryTypePrev(batch(cd));// обязательно добавить наполнение типом 3 по партиям из ведомостей по остаткам
        deliveryNotePrev(eid) <- deliveryNotePrev(batch(cd));
        deliveryNoteDatePrev(eid) <- DATETIME(deliveryNoteDate(batch(cd)));
        deliveryNotePrevLineID(eid) <- deliveryNotePrevLineID(batch(cd));
        lineItemQuantitySPT(eid) <- lineItemQuantitySPT(batch(cd));
        quantityDespatchedSPT(eid) <- NUMERIC[12,5](CASE
            WHEN lineItemQuantitySPT(batch(cd))=='112'  THEN ((OVERRIDE despatchUnitQuantityDespatched(cd), NUMERIC[12,5](packQuantity(cd)), 1.0)* volume(sku(cd))) 
            WHEN lineItemQuantitySPT(batch(cd))=='166'  THEN ((OVERRIDE despatchUnitQuantityDespatched(cd), NUMERIC[12,5](packQuantity(cd)), 1.0)* netWeight(sku(cd)))
            ELSE (OVERRIDE despatchUnitQuantityDespatched(cd), NUMERIC[12,5](packQuantity(cd)), 1.0));
        lineItemPriceSPT(eid) <- NUMERIC[12,2] (CASE
            WHEN lineItemQuantitySPT(batch(cd))=='112'  THEN (price(cd)/ volume(sku(cd))) 
            WHEN lineItemQuantitySPT(batch(cd))=='166'  THEN (price(cd)/ netWeight(sku(cd)))
            ELSE price(cd));
    }
}

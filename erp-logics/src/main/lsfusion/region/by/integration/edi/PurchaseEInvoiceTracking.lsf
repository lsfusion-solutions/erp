MODULE PurchaseEInvoiceTracking;

REQUIRE PurchaseEInvoice, PurchaseInvoiceTracking;

NAMESPACE Purchase;

skipFillExtraCode 'Не импортировать 4-зн. код в карточку товара из ЭТТН' () = DATA BOOLEAN ;

EXTEND FORM options
    PROPERTIES () skipFillExtraCode
;
DESIGN options {
    customs {
        MOVE PROPERTY(skipFillExtraCode());
    }
}

overCopy(EInvoiceDetail eid, UserInvoiceDetail id) + {
    dataExtraCode(sku(id)) <- extraCode(customsGroup(itemCustomCode(eid)), itemCustomCodeOther(eid))  WHERE itemCustomCodeOther(eid) AND NOT skipFillExtraCode() AND NOT extraCode(sku(id)) ;        
    deliveryTypePrev(id) <- CASE WHEN isTN2(eInvoice(eid)) THEN 2 ELSE 1 ;// определяем сами, т.к. это мы создаем в лсф прослеживаемость на основе электр. накладной
    deliveryNotePrev(id) <- deliveryNoteNumber(eInvoice(eid));
    deliveryNoteDate(id) <- deliveryNoteDate(eInvoice(eid));
    deliveryNotePrevLineID(id) <- lineItemNumber(eid);
    itemCustomCode(id) <- itemCustomCode(eid) IF NOT code(customsGroup(defaultCountry(),sku(id)));
    itemCustomCodeOther(id) <- itemCustomCodeOther(eid) IF NOT id(extraCode(sku(id)));
    lineItemQuantitySPT(id) <- lineItemQuantitySPT(eid);
    lineItemPriceSPT(id) <- lineItemPriceSPT(eid);// записываем для инфо, но при расходе цену рассчитываем свою исходя из ед изм
    quantityDespatchedSPT(id) <- quantityDespatchedSPT(eid);// записываем для инфо, но при расходе кол-во рассчитываем свое исходя из ед изм
}

//проверка данных из электронной накладной и в карточке товара
skipCheckESPT = ABSTRACT VALUE BOOLEAN (InvoiceDetail);

skipCheckESPT  'Не проверять соотвествие кода ТН ВЭД, 4-зн. код, ед. изм СПТ из электронной накладной' = DATA BOOLEAN ();

skipCheckESPT(InvoiceDetail id) += skipCheckESPT() IF id IS InvoiceDetail; 

CONSTRAINT (SET(isPosted(UserInvoiceDetail id)) OR CHANGED (itemCustomCode(id))) AND isPosted(id) AND isTracking(id) AND eInvoice(invoice(id))
    AND customsGroup(defaultCountry(),sku(id)) AND NOT itemCustomCode(id)==code(customsGroup(defaultCountry(),sku(id))) AND NOT skipCheckESPT(id)
    MESSAGE 'Код ТН ВЭД товара не соответствует коду ТН ВЭД электронной накладной' ;

CONSTRAINT (SET(isPosted(UserInvoiceDetail id)) OR CHANGED (itemCustomCodeOther(id))) AND isPosted(id) AND isTracking(id) AND eInvoice(invoice(id))
    AND extraCode(sku(id)) AND NOT itemCustomCodeOther(id)==id(extraCode(sku(id))) AND NOT skipCheckESPT(id)
    MESSAGE '4-зн. код к ТН ВЭД товара не соответствует 4-зн. коду электронной накладной' ;

CONSTRAINT (SET(isPosted(UserInvoiceDetail id)) OR CHANGED (lineItemQuantitySPT(id))) AND isPosted(id) AND isTracking(id) AND eInvoice(invoice(id))
    AND trackingUOMCustomsGroup(sku(id)) AND NOT lineItemQuantitySPT(id)==trackingUOMCustomsGroup(sku(id)) AND NOT skipCheckESPT(id)
    MESSAGE 'Код ед изм СПТ к ТН ВЭД товара не соответствует коду ед изм СПТ электронной накладной' ;
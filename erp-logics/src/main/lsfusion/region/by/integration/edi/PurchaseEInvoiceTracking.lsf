MODULE PurchaseEInvoiceTracking;

REQUIRE PurchaseEInvoiceBy, PurchaseInvoiceTracking;

NAMESPACE Purchase;

skipFillExtraCode 'Не импортировать 4-зн. код в карточку товара из ЭТТН' = DATA BOOLEAN () PREREAD;

EXTEND FORM options
    PROPERTIES () skipFillExtraCode
;
DESIGN options {
    customs {
        MOVE PROPERTY(skipFillExtraCode());
    }
}

overCopy(EInvoiceDetail eid, UserInvoiceDetail id) + {
    IF date(id) >= dateStartSPT(id) AND NOT skipFillExtraCode() THEN {
        dataExtraCode(sku(id)) <- extraCode(customsGroup(itemCustomCode(eid)), itemCustomCodeOther(eid)) WHERE itemCustomCodeOther(eid) AND NOT extraCode(sku(id)) ;
    }        
}

itemCustomCode (InvoiceDetail d) += itemCustomCode(eInvoiceDetail(d));// записываем для инфо, но при расходе отображаем из карточки товара
itemCustomCodeOther (InvoiceDetail d) += itemCustomCodeOther(eInvoiceDetail(d)); // записываем для инфо, но при расходе отображаем из карточки товара

deliveryTypePrev (InvoiceDetail d) += CASE WHEN isTN2(eInvoice(eInvoiceDetail(d))) THEN 2
                                             WHEN eInvoice(eInvoiceDetail(d)) THEN 1;

deliveryNotePrev (InvoiceDetail d) += deliveryNoteNumber(eInvoice(eInvoiceDetail(d)));
deliveryNoteDate (InvoiceDetail d) += deliveryNoteDate(eInvoice(eInvoiceDetail(d)));
deliveryNotePrevLineID (InvoiceDetail d) += lineItemNumber(eInvoiceDetail(d));
lineItemQuantitySPT (InvoiceDetail d) += lineItemQuantitySPT(eInvoiceDetail(d));// записываем для инфо, но при расходе отображаем из ТН ВЭДа из карточки товара
quantityDespatchedSPT (InvoiceDetail d) += quantityDespatchedSPT(eInvoiceDetail(d));// записываем для инфо, но при расходе кол-во рассчитываем свое исходя из ед изм
lineItemPriceSPT (InvoiceDetail d) += lineItemPriceSPT(eInvoiceDetail(d));        // записываем для инфо, но при расходе цену рассчитываем свою исходя из ед изм

//проверка данных из электронной накладной и в карточке товара

checkESPT  'Проверять соответствие кода ТН ВЭД, 4-зн. код, ед. изм СПТ из электронной накладной' = DATA BOOLEAN () PREREAD ;

EXTEND FORM options
    PROPERTIES checkESPT()
;

DESIGN options { 
    EDI { 
        MOVE PROPERTY(checkESPT()); 
    } 
}

CONSTRAINT (SET(isPosted(UserInvoiceDetail id)) OR CHANGED (itemCustomCode(id))) AND isPosted(id) AND isTracking(id) AND eInvoice(invoice(id))
    AND customsGroup(defaultCountry(),sku(id)) AND itemCustomCode(id) AND NOT itemCustomCode(id)==code(customsGroup(defaultCountry(),sku(id))) AND checkESPT()
    MESSAGE 'Код ТН ВЭД товара не соответствует коду ТН ВЭД электронной накладной' ;

CONSTRAINT (SET(isPosted(UserInvoiceDetail id)) OR CHANGED (itemCustomCodeOther(id))) AND isPosted(id) AND isTracking(id) AND eInvoice(invoice(id))
    AND extraCode(sku(id)) AND itemCustomCodeOther(id) AND NOT itemCustomCodeOther(id)==id(extraCode(sku(id))) AND checkESPT()
    MESSAGE '4-зн. код к ТН ВЭД товара не соответствует 4-зн. коду электронной накладной' ;

CONSTRAINT (SET(isPosted(UserInvoiceDetail id)) OR CHANGED (lineItemQuantitySPT(id))) AND isPosted(id) AND isTracking(id) AND eInvoice(invoice(id))
    AND trackingUOMCustomsGroup(sku(id)) AND lineItemQuantitySPT(id) AND NOT lineItemQuantitySPT(id)==trackingUOMCustomsGroup(sku(id)) AND checkESPT()
    MESSAGE 'Код ед изм СПТ к ТН ВЭД товара не соответствует коду ед изм СПТ электронной накладной' ;


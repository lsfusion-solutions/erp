MODULE PurchaseEInvoice;

REQUIRE InvoiceEInvoice, PurchaseInvoice, PurchaseCreditNote;

NAMESPACE Purchase;

eOperation = DATA Operation(LegalEntity);
nameEOperation 'Операция (Закупка) для электронных накладных'(LegalEntity l) = name(eOperation(l));

EXTEND FORM legalEntity
    PROPERTIES (l) PurchaseInvoiceNameEOperation = nameEOperation;

DESIGN legalEntity {
    EDI{
        MOVE PROPERTY (PurchaseInvoiceNameEOperation);
    }
} 

receiver = DATA Employee (UserInvoice);
receiver(UserInvoice i) += receiver(i);

nameReceiver 'Приёмщик'(Invoice i) = name(receiver(i));

eInvoiceDetail 'Строка ЭТТН' = DATA EInvoiceDetail (Purchase.InvoiceDetail);
numberEInvoiceDetail 'Строка ЭТТН' (Purchase.InvoiceDetail d) = lineItemNumber(eInvoiceDetail(d));

prevList (PriceListType t, UserInvoiceDetail d) += WHEN t == priceListType(d) AND eInvoiceDetail(d) THEN lineItemPrice(eInvoiceDetail(d));

filterDate 'Дата' = DATA LOCAL DATE ();
filterDate (EInvoice i) = DATE(deliveryNoteDateTime(i)) == filterDate() OR NOT filterDate();      

filterSupplier = DATA LOCAL LegalEntity ();
filterSupplier (EInvoice i) = supplier(i)= filterSupplier() OR (i IS EInvoice AND NOT filterSupplier()) OR NOT supplier(i);

filterCustomer = DATA LOCAL LegalEntity ();
filterCustomer (EInvoice i) = customer(i)= filterCustomer() OR (i IS EInvoice AND NOT filterCustomer()) OR NOT customer(i);

filterStock = DATA LOCAL Stock ();
filterStock (EInvoice i) = customerStock(i)= filterStock() OR (i IS EInvoice AND NOT filterStock()) OR NOT customerStock(i);

FORM selectEInvoice 'Накладные EDI'
    PROPERTIES filterDate()
    
    OBJECTS ei = EInvoice
    PROPERTIES(ei) READONLY dateSupplier, numberSupplier, deliveryNoteNumber, nameSupplier, nameCustomer
    FILTERS filterDate(ei), 
            filterSupplier(ei),
            filterCustomer(ei),
            filterStock(ei),
            NOT invoice(ei),
            importedCustomer(ei)            
;

DESIGN selectEInvoice {
    NEW filter {
        caption = 'Фильтр';
        MOVE PROPERTY (filterDate());
    }
    
    MOVE BOX(ei);
    MOVE TOOLBARBOX;
}

overCopy ABSTRACT LIST ( EInvoice, UserInvoice);
overCopy  ABSTRACT LIST ( EInvoiceDetail, UserInvoiceDetail);
ignoreDetail = ABSTRACT BOOLEAN (EInvoiceDetail);
copyData(EInvoice ei, Invoice i)  { 
    invoice(EInvoice e) <- NULL WHERE invoice(e) == i AND NOT isCancel(e);
    invoice(ei) <- i;
    number(i) <- deliveryNoteNumber48(ei);
    series(i) <- NULL;
    operation(i) <- eOperation(supplier(ei)) WHERE eOperation(supplier(ei));
    supplier(i) <- supplier(ei);
    supplierStock(i) <- OVERRIDE supplierStock(ei), defaultStock(supplier(ei));
    customer(i) <- customer(ei);
    customerStock(i) <- customerStock(ei);
    date(i) <- DATE(deliveryNoteDateTime(ei));
    time(i) <- TIME(deliveryNoteDateTime(ei));
    overCopy(ei,i);
    
    FOR eInvoice(EInvoiceDetail eid) == eInvoice(i) AND NOT ignoreDetail(eid) NEW id = UserInvoiceDetail DO {
        userInvoice(id) <- i;
        sku(id) <- OVERRIDE sku(lineItemBuyerID(eid)), skuGTIN(lineItemID(eid)), sku(lineItemID(eid));
        quantity(id) <- quantityDespatched(eid);
        price(id) <- lineItemPrice(eid);
        VAT(id) <- valueCurrentVAT(country(defaultStock(supplier(ei))), overValueVAT(eid));;
        valueVAT(id) <- NUMERIC[10,5](overValueVAT(eid)); 
        invoicePrice(id) <- round2(price(id) * (100 + valueVAT(id))/100);
        sum(id) <- lineItemAmountWithoutCharges(eid) WHERE isEDI(ei);
        VATSum(id) <- lineItemAmountCharges(eid) WHERE isEDI(ei);
        invoiceSum(id) <- lineItemAmount(eid) WHERE isEDI(ei);
        eInvoiceDetail(id) <- eid;

        overCopy(eid,id); 
    }
}

importFromEInvoice 'Импорт из накладной EDI' (UserInvoice i)  { 
    IF isPosted(i) THEN {
        MESSAGE 'Вы пытаетесь импортировать в проведенный документ. Распроведите его.';
        RETURN;
    }
    
    filterDate() <- date(i);
    filterSupplier() <- supplier(i);
    filterCustomer() <- customer(i);    
    filterStock() <- customerStock(i);
    
    DIALOG selectEInvoice OBJECTS ei INPUT DO {
        IF ei THEN
            copyData(ei,i);
    }
}

createInvoice 'Создать на основе накладных EDI' (DATE d) {
    FOR DATE(deliveryNoteDateTime(EInvoice ei)) == d AND NOT invoice(ei) DO {
        NEWSESSION NEW i = UserInvoice {
            copyData(ei, i);
            IF [GROUP SUM 1 IF invoice(UserInvoiceDetail id) == i]() == [GROUP SUM 1 IF invoice(UserInvoiceDetail id) == i AND prevPriceB(priceListType('own'), sku(id), customerStock(id), dateTime(id))]() THEN {
                operation(i) <- operation('own');
            }
            executeLocalEvents('Purchase.userInvoice');
            APPLY;
        }
    }
}

FORM dialogEInvoiceCustomer 'Выбор электронной накладной'
    OBJECTS i = EInvoice
    PROPERTIES(i) READONLY dateSupplier, numberSupplier, deliveryNoteNumber, nameSupplier, nameCustomer, nameCustomerStock
    FILTERS importedCustomer(i)
;
@extendFormFilterAccessStock(dialogEInvoiceCustomer, i, dialogEInvoiceCustomer, customerStock, Company);

overChangeEInvoiceCustomer  ABSTRACT LIST (EInvoice, UserInvoice);
changeEInvoiceCustomer(Invoice in) {
    DIALOG dialogEInvoiceCustomer OBJECTS i = eInvoice(in) NULL INPUT NULL DO {
        invoice(EInvoice e) <- NULL WHERE e == eInvoice(in) AND NOT isCancel(e);
        invoice(i) <- in;
        overChangeEInvoiceCustomer(i,in);
    }
}

EXTEND FORM userInvoice 
    PROPERTIES READONLYIF exportedCustomer(eInvoice(i)) numberEInvoice(i) ON CHANGE changeEInvoiceCustomer(i)
    PROPERTIES(i) READONLYIF exportedCustomer(eInvoice(i)) importFromEInvoice, nameReceiver
;

DESIGN userInvoice {
    import {
        NEW importEDI {
            caption = 'Накладная EDI';
            flex = 1;
            type = CONTAINERV;
            MOVE PROPERTY(numberEInvoice(i));
            MOVE PROPERTY(nameReceiver(i));
            MOVE PROPERTY(importFromEInvoice(i));
        }
    }
}

signAndSendCustomerEDI 'Подписать и отправить (покупатель)'(Invoice i)  { 
    NEWSESSION {
        in(EInvoice e) <- e = eInvoice(i);
        signAndSendCustomerEDI();
        in(EInvoice e) <- NULL;
    }
}
signAndSendNoticeCustomerEDI 'Подписать и отправить уведомление об изменении (покупатель)'(Invoice i)  { 
    NEWSESSION {
        in(EInvoice e) <- e = eInvoice(i);
        signAndSendNoticeCustomerEDI();
        in(EInvoice e) <- NULL;
    }
}

hideSignAndSendCustomerEDI = ABSTRACT BOOLEAN (Invoice);
hideSignAndSendNoticeCustomerEDI = ABSTRACT BOOLEAN (Invoice);

EXTEND FORM invoices
    PROPERTIES READONLY numberEInvoice = numberEInvoice(i), statusDescription = statusDescription(i) BACKGROUND backgroundStatusDescription(i)
    PROPERTIES numberEInvoice(i) SHOWIF eInvoice(i) PANEL READONLY,
               signAndSendCustomerEDI(i) SHOWIF (eInvoice(i) AND NOT exportedCustomer(eInvoice(i)) AND NOT exportedNoticeCustomer(eInvoice(i)) AND NOT hideSignAndSendCustomerEDI(i)),
               signAndSendNoticeCustomerEDI(i) SHOWIF (eInvoice(i) AND NOT exportedCustomer(eInvoice(i)) AND NOT exportedNoticeCustomer(eInvoice(i)) AND NOT hideSignAndSendNoticeCustomerEDI(i)),
               statusDescription(i) SHOWIF eInvoice(i) PANEL
;

DESIGN invoices {
    printTabScroll {
        NEW tabConsignment{
            type = TABBED;
            MOVE printTab {caption = 'Накладная';};
            NEW eInvoice {
                caption = 'Электронная накладная';
                NEW createEI {
                    type = CONTAINERH;
                    MOVE PROPERTY(numberEInvoice(i));
                    MOVE PROPERTY(signAndSendCustomerEDI(i));
                    MOVE PROPERTY(signAndSendNoticeCustomerEDI(i));
                    MOVE PROPERTY(statusDescription(i));
                }
            }
        }
    }    
}

skipNumberCheck(Invoice i) += IF (GROUP SUM 1 IF invoice(EInvoice ei) == i) THEN TRUE;
 
@defineOption(createPurchaseInvoiceFromEInvoice, 'Автоматически создавать накладную (закупка) при создании накладной EDI', EDI);

skipCreatePurchaseInvoice = ABSTRACT BOOLEAN (EInvoice);

WHEN SET(EInvoice ei IS EInvoice) AND createPurchaseInvoiceFromEInvoice() AND NOT skipCreatePurchaseInvoice(ei) DO {
    NEW i = UserInvoice {
        copyData(ei,i);
        executeLocalEvents('Purchase.userInvoice');
    }        
}

creditNote = DATA CreditNote (ECreditNote);
creditNoteDetail = DATA CreditNoteDetail(ECreditNoteDetail);
eCreditNote = GROUP LAST ECreditNote ec IF NOT isCancel(ec) ORDER dateTime(ec), ec BY creditNote(ec);
eCreditNoteDetail = GROUP LAST ECreditNoteDetail ecd IF NOT isCancel(eCreditNote(ecd)) ORDER dateTime(eCreditNote(ecd)), ecd BY creditNoteDetail(ecd);
numberCreditNote 'Номер акта приемки EDI' (ECreditNote c) = number(creditNote(c));
EXTEND FORM eCreditNotes
    PROPERTIES (c) READONLY numberCreditNote
;

CONSTRAINT DROPPED (CreditNote c IS CreditNote) AND exportedCustomer(eCreditNote(c)) 
    MESSAGE 'Запрещено удалять акт расхождения по которому был отправлен электронный акт приемки. Сначала необходимо отменить электронный акт приемки';

numberECreditNote 'Номер электронного акта приемки'(CreditNote c) = actDifNumber(eCreditNote(c));
isECreditNote 'Электронный акт приемки' (CreditNote c) = TRUE IF eCreditNote(c);
signAndSendCustomerCreditNoteEDI 'Подписать и отправить (покупатель)'(CreditNote c)  { 
    NEWSESSION {
        in(ECreditNote e) <- e = eCreditNote(c);
        signAndSendCustomerCreditNoteEDI();
        in(ECreditNote e) <- NULL;
    }
}

cancelSignAndSendCustomerCreditNoteEDII 'Отменить (покупатель)'(CreditNote c)  { 
    NEWSESSION {
        in(ECreditNote e) <- e = eCreditNote(c);
        exportedCustomer(ECreditNote e) <- NULL WHERE e = eCreditNote(c);
        isCancel(ECreditNote e) <- TRUE WHERE e = eCreditNote(c);
        signAndSendCustomerCreditNoteEDI();
        IF (GROUP SUM 1 IF exportedCustomer(ECreditNote e) AND in(e)) THEN APPLY;
    }
}

overCreate ABSTRACT LIST(ECreditNote, CreditNote);
overCreate ABSTRACT LIST(ECreditNoteDetail, CreditNoteDetail);

hideCreateECreditNote = ABSTRACT BOOLEAN (CreditNote);

createECreditNote 'Создать электронный акт приемки' (CreditNote c)  { 
    IF NOT eCreditNote(c) THEN NEWSESSION {
        NEW ec = ECreditNote {
            creditNote(ec) <- c;
            number(ec) <- number(c);
            dateTime(ec) <- dateTime(c);
            actDifDateTime(ec) <- dateTime(c);
            deliveryNoteNumber(ec) <- GROUP MAX deliveryNoteNumber(eInvoice(Invoice i)) IF quantity(i, c);
            deliveryNoteDateTime(ec) <- GROUP MAX deliveryNoteDateTime(eInvoice(Invoice i)) IF quantity(i, c);
            supplier(ec) <- supplier(c);
            customer(ec) <- customer(c);
            customerStock(ec) <- customerStock(c);
            contractId(ec) <- seriesNumberContractSku(c);
            contractDate(ec) <- dateFromContractSku(c);
            currency(ec) <- shortNameCurrency(c);
            
            FOR creditNote(CreditNoteDetail d) == c NEW ed = ECreditNoteDetail  DO {
                eCreditNote(ed) <- ec;
                creditNoteDetail(ed) <- d;
                lineItemNumber(ed) <- index(d);
                lineItemID(ed) <- id(barcode(sku(d))) IF length(id(barcode(sku(d)))) > 7;
                lineItemBuyerID(ed) <- id(sku(d));
                lineItemName(ed) <- STRING[500](nameSku(d));
                lineItemQuantityUOM(ed) <- OVERRIDE extraCodeUOM(UOM(sku(d))), 'PCE';
                quantityOrdered(ed) <- OVERRIDE quantity(invoiceDetail(d)), 0.0;
                quantityReceived(ed) <- OVERRIDE shipped(invoiceDetail(d)), 0.0;
                valueVAT(ed) <- NUMERIC[8,2](valueVAT(d));
                lineItemPrice(ed) <- price(d);
                amountOrdered(ed) <- OVERRIDE invoiceSum(invoiceDetail(d)), 0.0;
                amountReceived(ed) <- OVERRIDE invoiceSum(invoiceDetail(d)) (-) invoiceSum(d), 0.0;
                //amountReceived(ed) <- NUMERIC[18,4](Utils.round(quantityReceived(ed) * price(d), priceRound(invoiceDetail(d)))) (+)
                                      //round((NUMERIC[18,4](Utils.round(quantityReceived(ed) * price(d), priceRound(invoiceDetail(d)))) * calcValueVAT(invoiceDetail(d)) / 100), currency(invoiceDetail(d)));
                overCreate(ed,d);
            }
            
            overCreate(ec,c);
        }
        APPLY;
    }
}

EXTEND FORM creditNotes
        PROPERTIES READONLY PANEL numberECreditNote(c)
        PROPERTIES(c) createECreditNote SHOWIF (NOT eCreditNote(c) AND NOT hideCreateECreditNote(c)),// statusDescription SHOWIF eInvoice(i) PANEL, 
                      signAndSendCustomerCreditNoteEDI SHOWIF (eCreditNote(c) AND NOT exportedCustomer(eCreditNote(c))), 
                      cancelSignAndSendCustomerCreditNoteEDII SHOWIF (eCreditNote(c) AND exportedCustomer(eCreditNote(c)))
;

DESIGN creditNotes {
    documentDetail {
        NEW tabCreditNote AFTER printTab {
            caption = 'Печатные формы';
            type = TABBED;
            MOVE printTab {caption = 'Акт расхождений';};
            NEW eCreditNote {
                caption = 'Электронный акт приемки';
                NEW createCN {
                    type = CONTAINERH;
                    MOVE PROPERTY(numberECreditNote(c));
                    MOVE PROPERTY(createECreditNote(c));
                    MOVE PROPERTY(signAndSendCustomerCreditNoteEDI(c));
                    MOVE PROPERTY(cancelSignAndSendCustomerCreditNoteEDII(c));
                }
            }
        }
    }
}

numberECreditNote 'Номер электронного акта приемки'(Invoice i) = actDifNumber(eCreditNote(invoiceCreditNote(i)));

signAndSendCustomerCreditNoteEDI 'Подписать и отправить (покупатель)'(Invoice i)  { 
    signAndSendCustomerCreditNoteEDI(invoiceCreditNote(i));
}

cancelSignAndSendCustomerCreditNoteEDII 'Отменить (покупатель)'(Invoice i)  { 
    cancelSignAndSendCustomerCreditNoteEDII(invoiceCreditNote(i));
}
createECreditNote 'Создать электронный акт приемки' (Invoice i)  {
    createECreditNote(invoiceCreditNote(i));
}

EXTEND FORM invoices
        PROPERTIES READONLY PANEL numberECreditNote(i) SHOWIF invoiceCreditNote(i)
        PROPERTIES(i) createECreditNote SHOWIF (invoiceCreditNote(i) AND NOT eCreditNote(invoiceCreditNote(i)) AND NOT hideCreateECreditNote(invoiceCreditNote(i))),// statusDescription SHOWIF eInvoice(i) PANEL, 
                      signAndSendCustomerCreditNoteEDI SHOWIF (invoiceCreditNote(i) AND eCreditNote(invoiceCreditNote(i)) AND NOT exportedCustomer(eCreditNote(invoiceCreditNote(i)))), 
                      cancelSignAndSendCustomerCreditNoteEDII SHOWIF (invoiceCreditNote(i) AND eCreditNote(invoiceCreditNote(i)) AND exportedCustomer(eCreditNote(invoiceCreditNote(i))))
;

DESIGN invoices {
    tabConsignment{
        NEW eCreditNote {
            caption = 'Электронный акт приемки';
            NEW createCN {
                type = CONTAINERH;
                MOVE PROPERTY(numberECreditNote(i));
                MOVE PROPERTY(createECreditNote(i));
                MOVE PROPERTY(signAndSendCustomerCreditNoteEDI(i));
                MOVE PROPERTY(cancelSignAndSendCustomerCreditNoteEDII(i));
            }
        }
    }
}
MODULE PurchaseOrderEDI;

REQUIRE PurchaseOrder, EDI;

NAMESPACE EDI;

EXTEND CLASS Purchase.UserOrder : EOrder;

isClosed(Purchase.UserOrder o) += Purchase.isClosed(o);
isOpened(Purchase.UserOrder o) += Purchase.isOpened(o);
dateTime(Purchase.UserOrder o) += Purchase.dateTime[UserOrder](o);
shipmentDateTime(Purchase.UserOrder o) += Purchase.shipmentDateTime[UserOrder](o);
number(Purchase.UserOrder o) += Purchase.number(o);
supplier(Purchase.UserOrder o) += Purchase.supplier(o);
supplierStock(Purchase.UserOrder o) += Purchase.supplierStock(o);
customer(Purchase.UserOrder o) += Purchase.customer(o);
customerStock(Purchase.UserOrder o) += Purchase.customerStock(o);
note(Purchase.UserOrder o) += Purchase.note(o);

EXTEND CLASS Purchase.UserOrderDetail : EOrderDetail;

order(Purchase.UserOrderDetail o) += Purchase.userOrder(o);
sku(Purchase.UserOrderDetail o) += Purchase.sku(o);
quantity(Purchase.UserOrderDetail o) += Purchase.quantity(o);
price(Purchase.UserOrderDetail o) += Purchase.price(o);
valueVAT(Purchase.UserOrderDetail o) += Purchase.valueVAT(o);

skipEDI 'Не использовать EDI для заказов' = DATA BOOLEAN (Warehouse);
EXTEND FORM warehouse
    PROPERTIES (w) skipEDI;

DESIGN warehouse {
    EDI{
        caption = 'EDI';
        MOVE PROPERTY (skipEDI(w));
    }
}

skipEDI 'Не использовать EDI для заказов' = DATA BOOLEAN (DepartmentStore);
EXTEND FORM departmentStore
    PROPERTIES (d) skipEDI;

DESIGN departmentStore {
    EDI{
        MOVE PROPERTY (skipEDI(d));
    }
}

skipEDI = ABSTRACT BOOLEAN (Stock);
skipEDI(Warehouse w) += skipEDI(w);
skipEDI(DepartmentStore d) += skipEDI(d);

isEOrder(Purchase.Order o) = EDIProvider(supplier(o)) IS EDIProvider AND NOT skipEDI(supplierStock(o)) AND NOT skipEDI(customerStock(o)) AND NOT isCompany(supplier(o));
isEOrder(Purchase.UserOrder o) = EDIProvider(supplier(o)) IS EDIProvider AND NOT skipEDI(supplierStock(o)) AND NOT skipEDI(customerStock(o)) AND NOT isCompany(supplier(o));
isEOrder(Purchase.OrderDetail d) = isEOrder(order(d));
isEOrder(Purchase.UserOrderDetail d) = isEOrder(userOrder(d));

response = GROUP MAX EOrderResponse e BY eOrder(e);
responseNote 'Примечание поставщика' (Purchase.Order o) = note(response(o)) IN documentPrm;
 
indexSku(EOrderResponseDetail d) = PARTITION SUM 1 ORDER quantityOrdered(d), -quantityAccepted(d), d BY sku(d), eOrder(orderResponse(d)) MATERIALIZED ;
//сортировка по принятым откланенным, принятые первыми

dataIndexSku = DATA INTEGER (Purchase.OrderDetail);
indexSku(Purchase.OrderDetail d) = OVERRIDE dataIndexSku(d), 1;

responseDetail (Purchase.OrderDetail d) = GROUP 
    MAX EOrderResponseDetail ed 
    IF eOrder(ed) == order(d) AND sku(ed) == sku(d) AND indexSku(d) == indexSku(ed);

//тереоретически поставщик может поменять порядок строк, но пока для упрощения попробуем исходить из того что не поменяет
//quantityAccepted = GROUP SUM quantityAccepted(EOrderResponseDetail d) BY sku(d), eOrder(orderResponse(d));
//quantityResponseEDI 'Подтвержденное количество (EDI)'(Purchase.OrderDetail d) = PARTITION UNGROUP quantityAccepted LIMIT STRICT PREV(quantity(d)) BY sku(d), order(d);
quantityResponseEDI 'Подтвержденное количество (EDI)'(Purchase.OrderDetail d) = quantityAccepted(responseDetail(d));
backgroundQuantityResponseEDI = ABSTRACT COLOR (Purchase.OrderDetail);
quantityResponseEDI = GROUP SUM quantityResponseEDI(Purchase.OrderDetail d) BY order(d);

priceResponseEDI 'Подтвержденная цена (EDI)'(Purchase.OrderDetail d) = price(responseDetail(d));
backgroundPriceResponseEDI(Purchase.OrderDetail d) = RGB(255,0,0) IF priceResponseEDI(d) != price(d);

EXTEND FORM userOrder
    PROPERTIES (o) READONLY SHOWIF isEOrder(o) responseNote 
    PROPERTIES (d) READONLY SHOWIF isEOrder(d) quantityResponseEDI BEFORE quantity(d) BACKGROUND backgroundQuantityResponseEDI(d), 
                                               priceResponseEDI BEFORE price(d) BACKGROUND backgroundPriceResponseEDI(d); 

EXTEND FORM orders
    PROPERTIES (o) READONLY responseNote 
    PROPERTIES (d) READONLY SHOWIF isEOrder(d) quantityResponseEDI BEFORE quantity(d) BACKGROUND backgroundQuantityResponseEDI(d), 
                                               priceResponseEDI BEFORE price(d) BACKGROUND backgroundPriceResponseEDI(d); 

skipSetShipmentDateEDI = ABSTRACT BOOLEAN (Purchase.UserOrder);

WHEN SET (UserOrder o == eOrder(EOrderResponse od)) AND deliveryDateTime(od) AND NOT skipSetShipmentDateEDI(o) DO {
    shipmentDate(o) <- DATE(deliveryDateTime(od));
    shipmentTime(o) <- TIME(deliveryDateTime(od)) WHERE NOT shipmentTime(o);
}

despatchAdviceDetail (UserOrderDetail d) = GROUP MAX EOrderDespatchAdviceDetail ed IF eOrder(orderDespatchAdvice(ed)) == order(d) AND sku(ed) == sku(d);

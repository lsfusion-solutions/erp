MODULE TopBy;

REQUIRE TopByApi;

NAMESPACE EDI;

EXTEND CLASS EDIProvider {
    topBy 'СТТ'
}
prefix(EDIProvider e) += WHEN e == EDIProvider.topBy THEN '001-';
name(EDIProvider e) += WHEN e == EDIProvider.topBy THEN 'ООО "Современные технологии торговли"';

loginTopBy 'Имя пользователя TopBy' = DATA STRING[100] (LegalEntity);
passwordTopBy 'Пароль TopBy' = DATA STRING[100] (LegalEntity) ECHO;
hostTopBy 'Хост' = DATA STRING[100] (LegalEntity);
portTopBy 'Порт' = DATA INTEGER (LegalEntity);
archiveDirTopBy 'Папка принятых сообщений' = DATA STRING[100] ();
outputDirTopBy 'Папка отправленных сообщений' = DATA STRING[100] ();
disableConfirmationTopBy 'Отключить подтверждение сообщений' = DATA BOOLEAN ();
receiveSupplierMessagesTopBy 'Получать сообщения для поставщика' = DATA BOOLEAN ();

@lockedObjects(TopBy,LegalEntity);

onStarted()+ { 
    NEWSESSION {
        lockedTopBy(LegalEntity l) <- NULL WHERE lockedTopBy(l) AND l IS LegalEntity;
        APPLY;      
    }
}

receiveMessagesTopBy () {
    FOR LegalEntity le == [GROUP MIN LegalEntity l BY loginTopBy(l)](STRING login) AND passwordTopBy(le) DO {
        NEWSESSION {
            lockTopBy(le);
            IF lockResultTopBy() THEN {
                TRY {
                    receiveMessages(login, passwordTopBy(le), ('http://' +  hostTopBy(le) + ':' + portTopBy(le) + '/DmcService'), archiveDirTopBy(), disableConfirmationTopBy(), NULL, 'TopBy');
                } FINALLY {
                    unlockTopBy(le);
                }
            } ELSE {
                MESSAGE 'Уже выполняется получение сообщений TopBy для ' + login + '. Подождите...' NOWAIT;
            }
        }
    }
}

receiveTopBy 'Получить сообщения' {
    checkIsServer();
    IF isServer() THEN {receiveMessagesTopBy(); };
}

EXTEND FORM integrationData
    PROPERTIES() archiveDirTopBy, outputDirTopBy, disableConfirmationTopBy, receiveTopBy
    
    OBJECTS let = LegalEntity
    FILTERS isCompany(let)
    PROPERTIES(let) name READONLY, loginTopBy, passwordTopBy, hostTopBy, portTopBy
;
    
DESIGN integrationData {
    edi {
        NEW vs {
            caption = 'TopBy';
            type = CONTAINERH;
            NEW col1TopBy {
                NEW params {
                    caption = 'Общие';
                    MOVE PROPERTY(archiveDirTopBy());
                    MOVE PROPERTY(outputDirTopBy());
                    MOVE PROPERTY(disableConfirmationTopBy());
                    MOVE PROPERTY(receiveTopBy());
                }
            }
            NEW col2TopBy {
                fill = 1;
                MOVE BOX(let);
            }
        }
    }
}

MODULE TopBy;

REQUIRE TopByApi;

NAMESPACE EDI;

EXTEND CLASS EDIProvider {
    topBy 'СТТ'
}
prefix(EDIProvider e) += WHEN e == EDIProvider.topBy THEN '001-';
name(EDIProvider e) += WHEN e == EDIProvider.topBy THEN 'ООО "Современные технологии торговли"';

loginTopBy 'Имя пользователя' = DATA STRING[100] (LegalEntity);
passwordTopBy 'Пароль' = DATA STRING[100] (LegalEntity) ECHO;
hostTopBy 'Хост' = DATA STRING[100] (LegalEntity);
portTopBy 'Порт' = DATA INTEGER (LegalEntity);

archiveDirTopBy 'Папка принятых сообщений' = DATA STRING[100] ();
outputDirTopBy 'Папка отправленных сообщений' = DATA STRING[100] ();
disableConfirmationTopBy 'Отключить подтверждение сообщений' = DATA BOOLEAN ();
receiveSupplierMessagesTopBy 'Получать сообщения для поставщика' = DATA BOOLEAN ();

lockedTopBy = DATA User (STRING) TABLE stringEDI;
lockResultTopBy = DATA LOCAL NESTED BOOLEAN ();

lockTopBy(STRING object)  { 
    NEWSESSION {    
        lockResultTopBy() <- NULL;
        APPLY SERIALIZABLE {
            IF NOT object THEN
                RETURN;
            IF lockedTopBy(object) THEN {
                cancel();
            } ELSE {
                lockedTopBy(object) <- currentUser();
                lockResultTopBy() <- TRUE;
            }
        }
    }
}

unlockTopBy(STRING object)  { 
    NEWSESSION { 
        APPLY lockedTopBy(object) <- NULL;
    }
}

onStarted()+ { 
    NEWSESSION {
        lockedTopBy(STRING l) <- NULL WHERE lockedTopBy(l);
        APPLY;      
    }
}

receiveMessagesTopBy () {
    FOR LegalEntity le == [GROUP MIN LegalEntity l BY loginTopBy(l)](STRING login) AND passwordTopBy(le) DO {
        NEWSESSION {
            lockTopBy(login);
            IF lockResultTopBy() THEN {
                TRY {
                    newNS() <- TRUE;
                    receiveMessages(login, passwordTopBy(le), ('http://' +  hostTopBy(le) + ':' + portTopBy(le) + '/DmcService'), archiveDirTopBy(), disableConfirmationTopBy(), 'TopBy');
                } FINALLY {
                    unlockTopBy(login);
                }
            } ELSE {
                MESSAGE 'Уже выполняется получение сообщений TopBy для ' + login + '. Подождите...' NOWAIT;
            }
        }
    }
}

receiveTopBy 'Получить сообщения' {
    checkIsServer();
    IF isServer() THEN {receiveMessagesTopBy(); };
}

listMessagesTopBy 'Просмотр очереди' (LegalEntity le) {
    listMessages(loginTopBy(le), passwordTopBy(le), ('http://' +  hostTopBy(le) + ':' + portTopBy(le) + '/DmcService'), archiveDirTopBy(), disableConfirmationTopBy(), 'TopBy');
} TOOLBAR;

EXTEND FORM integrationData
    PROPERTIES() archiveDirTopBy, outputDirTopBy, disableConfirmationTopBy, receiveTopBy
    
    OBJECTS let = LegalEntity
    FILTERS isCompany(let)
    PROPERTIES(let) name READONLY, loginTopBy, passwordTopBy, hostTopBy, portTopBy, listMessagesTopBy
;
    
DESIGN integrationData {
    edi {
        NEW vs {
            caption = 'СТТ';
            horizontal = TRUE;
            NEW col1TopBy {
                NEW params {
                    caption = 'Общие';
                    MOVE PROPERTY(archiveDirTopBy());
                    MOVE PROPERTY(outputDirTopBy());
                    MOVE PROPERTY(disableConfirmationTopBy());
                    MOVE PROPERTY(receiveTopBy());
                }
            }
            NEW col2TopBy {
                fill = 1;
                MOVE BOX(let);
            }
        }
    }
}

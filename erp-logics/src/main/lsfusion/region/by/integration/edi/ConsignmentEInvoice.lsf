MODULE ConsignmentEInvoice;

REQUIRE SupplierEInvoiceEDI, Invoice, ConsignmentBy;

NAMESPACE Consignment;

defaultNumeratorDeliveryNoteNumber = DATA Numerator ();
nameDefaultNumeratorDeliveryNote 'DeliveryNoteNumber Накладные EDI' = name(defaultNumeratorDeliveryNoteNumber()) IN defaultNumerator;

EXTEND FORM defaultNumerators
    PROPERTIES() nameSaleDefaultNumeratorDeliveryNote = nameDefaultNumeratorDeliveryNote
;

loadDefaultNumerators() + { 
    NEW n = Numerator {
        name(n) <- 'DeliveryNoteNumber Накладные EDI';
        series(n) <- '';
        minValue(n) <- 1L;
        maxValue(n) <- 9999999999L;
        stringLength(n) <- 10;

        defaultNumeratorDeliveryNoteNumber() <- n;
    }
}

generateSeriesNumber 'Сгенерировать номер'(EInvoice o)  { 
    deliveryNoteNumber(o) <- '001-' + GLN(supplier(o)) + '-' + curStringValue(defaultNumeratorDeliveryNoteNumber()); //ССС-GGGGGGGGGGGGG-NNNNNNNNNN
    incrementValueSession(defaultNumeratorDeliveryNoteNumber());
}

skipGenerateDeliveryNoteNumber = ABSTRACT BOOLEAN (EInvoice);

WHEN CHANGED (supplier(EInvoice o)) AND NOT deliveryNoteNumber(o) AND NOT skipGenerateDeliveryNoteNumber(o) DO {
    generateSeriesNumber(o);
}

//исходящие (Sale OR PurchaseReturn)
invoice= DATA Invoice(EInvoice);
invoiceDetail= DATA InvoiceDetail(EInvoiceDetail);
eInvoice= GROUP LAST EInvoice ei IF NOT isCancel(ei) ORDER dateTime(ei), ei BY invoice(ei);
numberInvoice'Номер накладной EDI (расход)' (EInvoice i) = number(invoice(i));
EXTEND FORM eInvoices
    PROPERTIES (e) READONLY SaleNumberInvoice = numberInvoice
;

CONSTRAINT DROPPED (Invoice i IS Invoice) AND exportedSupplier(eInvoice(i)) 
    MESSAGE 'Запрещено удалять накладную по которой была отправлена ЭТТН. Сначала необходимо отменить ЭТТН';

numberEInvoice 'Номер ЭТТН'(Invoice i) = deliveryNoteNumber(eInvoice(i));
isEInvoice 'ЭТТН'  (Invoice i) = TRUE IF eInvoice(i);
signAndSendSupplierEDI 'Подписать и отправить (поставщик)'(Invoice i)  { 
    NEWSESSION {
        in(EInvoice e) <- e = eInvoice(i);
        signAndSendSupplierEDI();
        in(EInvoice e) <- NULL;
    }
}

cancelSignAndSendSupplierEDI 'Отменить (поставщик)'(Invoice i)  { 
    NEWSESSION {
        in(EInvoice e) <- e = eInvoice(i);
        exportedSupplier(EInvoice e) <- NULL WHERE e = eInvoice(i);
        isCancel(EInvoice e) <- TRUE WHERE e = eInvoice(i);
        signAndSendSupplierEDI();
        IF (GROUP SUM 1 IF exportedSupplier(EInvoice e) AND in(e)) THEN APPLY;
    }
}

currentOrderMessage = GROUP LAST EInvoiceMessage m ORDER dateTime(m), m BY invoice(eInvoice(m));
statusDescription 'Статус отправки EDI'(Invoice o) = CONCAT ' ', code(currentOrderMessage(o)), description(currentOrderMessage(o)) CHARWIDTH 15;
backgroundStatusDescription (Invoice i) = RGB(255,238,238) IF currentOrderMessage(i) AND NOT good(currentOrderMessage(i));

WHEN SETCHANGED(payer(invoice(EInvoice e))) AND NOT (CHANGED(freightPayer(e)))  DO {
    freightPayer(e) <- payer(invoice(e));    
}
WHEN SETCHANGED(nameDriver(invoice(EInvoice e))) AND NOT (CHANGED(transportContact(e)))  DO {
    transportContact(e) <- STRING[150](nameDriver(invoice(e)));    
}
WHEN SETCHANGED(overWaybill(invoice(EInvoice e))) AND NOT (CHANGED(waybillId(e)))  DO {
    waybillId(e) <- STRING[70](trim(overWaybill(invoice(e)))); 
}
WHEN SETCHANGED(nameIssuanceAllowed(invoice(EInvoice e))) AND NOT (CHANGED(contactSupplier(e)))  DO {
    contactSupplier(e) <- STRING[150](nameIssuanceAllowed(invoice(e)));     
}
WHEN SETCHANGED(forwarder(invoice(EInvoice e))) AND NOT (CHANGED(deliveryContact(e)))  DO {
    deliveryContact(e) <- STRING[150](forwarder(invoice(e)));
}
WHEN SETCHANGED(warrant(invoice(EInvoice e))) AND NOT (CHANGED(proxyID(e)))  DO {
    proxyID(e) <- STRING[70](warrant(invoice(e)));
}
WHEN SETCHANGED(warrantHolder(invoice(EInvoice e))) AND NOT (CHANGED(partyIssuingProxyName(e)))  DO {
    partyIssuingProxyName(e) <- STRING[175](warrantHolder(invoice(e)));
}
WHEN SETCHANGED(raceQuantity(invoice(EInvoice e))) AND NOT (CHANGED(quantityTrip(e)))  DO {
    quantityTrip(e) <- STRING[70](raceQuantity(invoice(e)));
}
WHEN SETCHANGED(nameTruck(invoice(EInvoice e))) AND NOT (CHANGED(transportID(e)))  DO {
    transportID(e) <- STRING[35](nameTruck(invoice(e)));
}
WHEN SETCHANGED (trailer(invoice(EInvoice e))) AND NOT (CHANGED(trailerID(e)))  DO {
    trailerID(e) <- STRING[35](trailer(invoice(e)));
}
WHEN SETCHANGED (overGoodsAccepted(invoice(EInvoice e))) AND NOT (CHANGED(contactCustomerStock(e)))  DO {
    contactCustomerStock(e) <- STRING[150](overGoodsAccepted(invoice(e)));
}

WHEN SETCHANGED (sealNumberSupplier(invoice(EInvoice e))) AND NOT (CHANGED(sealIDReceiver(e)))  DO {
    sealIDReceiver(e) <- STRING[35](sealNumberSupplier(invoice(e)));
}

WHEN SETCHANGED (nameIssuanceExecuted(invoice(EInvoice e))) AND NOT (CHANGED(contactSupplierStock(e)))  DO {
    contactSupplierStock(e) <- STRING[35](nameIssuanceExecuted(invoice(e)));    
}

WHEN LOCAL FORMS consignment CHANGED(grossWeightTon(invoiceDetail(EInvoiceDetail ed))) AND NOT (CHANGED(grossWeightValue(ed))) DO {
    grossWeightValue(ed) <- NUMERIC[12,6](grossWeightTon(invoiceDetail(ed)));
}

//transportOwnerName 'Владелец автомобиля' = DATA STRING[175] (EInvoice);
overCreate ABSTRACT LIST(EInvoice, Consignment);
overCreate ABSTRACT LIST(EInvoiceDetail, ConsignmentDetail);

hideCreateEInvoice = ABSTRACT BOOLEAN (Consignment);

createEInvoice 'Создать электронную накладную' (Consignment invoice)  { 
    IF NOT eInvoice(invoice) THEN NEWSESSION {
        NEW ei = EInvoice {
            invoice(ei) <- invoice;
            number(ei) <- number(invoice);
            dateTime(ei) <- dateTime[Invoice](invoice);
            deliveryNoteDateTime(ei) <- dateTime[Invoice](invoice);
            supplier(ei) <- supplier(invoice);
            supplierStock(ei) <- supplierStock(invoice);
            customer(ei) <- customer(invoice);
            customerStock(ei) <- customerStock(invoice);
            contractId(ei) <- seriesNumberContractSku(invoice);
            contractDate(ei) <- dateFromContractSku(invoice);
            currency(ei) <- shortNameCurrency(invoice);
            
            FOR consignment(ConsignmentDetail detail) == invoice NEW ed = EInvoiceDetail  DO {
                eInvoice(ed) <- ei;
                invoiceDetail(ed) <- detail;
                lineItemID(ed) <- id(barcode(sku(detail))) IF length(id(barcode(sku(detail)))) > 7;
                lineItemBuyerID(ed) <- id(sku(detail));
                lineItemName(ed) <- STRING[500](nameSku(detail));
                quantityDespatched(ed) <- quantity(detail);
                valueVAT(ed) <- NUMERIC[8,2](vat(detail));
                lineItemPrice(ed) <- price(detail);
                lineItemAmount(ed) <- dataSumInvoice(detail);
                lineItemAmountCharges(ed) <- sumVATIaW(detail);
                lineItemAmountWithoutCharges(ed) <- dataSumInvoice(detail) (-) sumVATIaW(detail);
                grossWeightValue(ed) <- NUMERIC[12,6](grossWeightTon(detail));
                lineItemQuantityUOM(ed) <- OVERRIDE extraCodeUOM(UOM(sku(detail))), 'PCE';
                despatchUnitQuantityDespatched(ed) <- OVERRIDE NUMERIC[12,5](packQuantity(detail)), 1.0;
                overCreate(ed,detail);
            }
            
            overCreate(ei,invoice);
        }
        APPLY;
    }
}

UNPfullNamePayer (Invoice i) = CONCAT ', ', UNPPayer(i), fullNamePayer(i);
UNPfullNameSupplier (Invoice i) = CONCAT ', ', UNPSupplier(i), fullNameSupplier[Consignment](i);
addressSupplierAddressSupplierStock (Invoice i) = CONCAT ', ', addressSupplier[Consignment](i), addressSupplierStock[Consignment](i);
infoCustomerBase (Invoice i) = CONCAT ', ', UNPCustomer(i), fullNameCustomer[Consignment](i), addressCustomer[Consignment](i), shipmentBase(i);
numberDate (Invoice i) = CONCAT ', ', number(i), toDateDDMMYY(date(i));

//-- old
FORM consignmentEInvoice 'РЕЕСТР'
    OBJECTS i = Invoice PANEL SUBREPORT
    PROPERTIES(i) READONLY UNPfullNamePayer, addressPayer, UNPfullNameSupplier, addressSupplierAddressSupplierStock,
                           date, nameTruck, overWaybill, nameDriver, trailer, number, numberDate, infoCustomerBase,
                           grossWeightConsignmentDetail, overPackQuantityConsignmentDetail, sumVATConsignmentDetail,
                           sumInvoiceConsignmentDetail, forwarder, overGoodsAccepted, numberEInvoice
;

FORM attachmentEInvoice 'Приложение'
    OBJECTS i = Invoice PANEL SUBREPORT
    PROPERTIES(i) READONLY number, date
    
    OBJECTS d = InvoiceDetail
    PROPERTIES(d) READONLY index, idBarcodeSku, nameSku, shortNameUOMSku, quantity, price, sum, valueVAT, VATSum, invoiceSum,
                           packQuantity, sumGrossWeight, idSku
    FILTERS invoice(d) == i
;

UNPfullNamePayer (EInvoice i) = CONCAT ', ', UNP(freightPayer(i)), fullName(freightPayer(i));
UNPfullNameSupplier (EInvoice i) = CONCAT ', ', unpSupplier(i), nameSupplier(i);
addressSupplierAddressSupplierStock (EInvoice i) = CONCAT ', ', addressSupplier(i), addressSupplierStock(i);
numberDate (EInvoice i) = CONCAT ', ', deliveryNoteNumber(i), toDateDDMMYY(deliveryNoteDateTime(i)), (OVERRIDE seriesNumber(invoice(i)),number(i));
infoCustomerBase (EInvoice i) = CONCAT ', ', unpCustomer(i), nameCustomer(i), addressCustomer(i), (CONCAT ' ', contractId(i),contractDate(i)), ('разгрузка: '+addressCustomerStock(i));

include 'Вкл.' = DATA LOCAL BOOLEAN (EInvoice);
multy  = DATA LOCAL BOOLEAN ();

FORM EInvoiceConsignment 'РЕЕСТР'
    OBJECTS e = EInvoice  
    PROPERTIES(e)  UNPfullNamePayer, addressFreightPayer, UNPfullNameSupplier, addressSupplierAddressSupplierStock, deliveryNoteDateTime,deliveryNoteNumber,
                   transportContact, waybillId, deliveryContact, transportID, trailerID, numberDate, infoCustomerBase, contactCustomerStock    
    PROPERTIES(e) totalAmountWithoutCharges,totalAmountCharges, totalAmount,totalLineItem, totalLineItemQuantity, totalGrossWeight, 
                totalDespatchUnitQuantity, totalAmountExcise, sealIDReceiver
    PROPERTIES multy()
    FILTERS include(e)
;
FORM EInvoiceAttachment 'Приложение'
    OBJECTS e = EInvoice  
    PROPERTIES(e) READONLY number, dateTime, deliveryNoteDateTime, deliveryNoteNumber
    PROPERTIES multy()
    
    OBJECTS d = EInvoiceDetail
    PROPERTIES(d) READONLY lineItemID, lineItemBuyerID, lineItemName, quantityDespatched, lineItemPrice, lineItemAmountWithoutCharges, overValueVAT, 
                   lineItemAmountCharges, lineItemAmount, despatchUnitQuantityDespatched, grossWeightValue
    PROPERTIES shortNameUOMSku = shortName(UOMExtraCode(lineItemQuantityUOM(d)))
    FILTERS eInvoice(d) == e
    FILTERS include(e)
;

hidePrintEInvoice = ABSTRACT BOOLEAN (EInvoice);
hidePrintEInvoice(Invoice i) = hidePrintEInvoice(eInvoice(i));

printConsignmentEInvoiceAttachment 'Реестр ТТН с приложением' (Invoice i)  { 
    include(EInvoice e) <- NULL;
    multy() <- NULL;
    include(EInvoice e) <- TRUE WHERE e = eInvoice(i);    
    PRINT EInvoiceConsignment;
    PRINT EInvoiceAttachment;
} IMAGE 'print.png';

printConsignmentEInvoice 'Реестр ТТН' (Invoice i)  { 
    include(EInvoice e) <- NULL;
    multy() <- NULL;
    include(EInvoice e) <- TRUE WHERE e = eInvoice(i);    
    PRINT EInvoiceConsignment;
} IMAGE 'print.png';

printConsignmentEAttachment 'Приложение' (Invoice i)  { 
    include(EInvoice e) <- NULL;
    multy() <- NULL;
    include(EInvoice e) <- TRUE WHERE e = eInvoice(i);    
    PRINT EInvoiceAttachment;
} IMAGE 'print.png';

printConsignmentEAttachmentXLSX 'Приложение (xlsx)' (Invoice i)  { 
    include(EInvoice e) <- NULL;
    multy() <- NULL;
    include(EInvoice e) <- TRUE WHERE e = eInvoice(i);    
    PRINT EInvoiceAttachment XLSX;
} IMAGE 'print.png';



include 'Вкл.' = DATA LOCAL BOOLEAN (Invoice);
countInclude() = GROUP SUM 1 IF include(Invoice i);
maxInclude() = GROUP MAX Invoice i IF include(i);

printConsignmentEInvoices 'Реестр ТТН (несколько)' ()  { 
    IF NOT ([ GROUP SUM 1 IF [GROUP SUM 1 IF include(Invoice i) BY fromStock(i)](Stock st)]() >1 OR 
            [ GROUP SUM 1 IF [GROUP SUM 1 IF include(Invoice i) BY toStock(i)](Stock st)]()>1 OR 
            [ GROUP SUM 1 IF [GROUP SUM 1 IF include(Invoice i) BY Invoice.date(i)](DATE dt)]()>1)  THEN {
        include(EInvoice e) <- NULL;
        multy() <- TRUE;
        include(EInvoice e) <- TRUE WHERE e = eInvoice(invoice(e)) AND include(invoice(e));    
        
        PRINT EInvoiceConsignment;
        PRINT EInvoiceAttachment;
        include(Invoice i) <- NULL;    
    } ELSE {
        MESSAGE 'Выбраны накладные с разными складами и/или датами';
    }

} IMAGE 'print.png';

UNPfullNamePayer () = CONCAT ', ', UNPPayer(maxInclude()), fullNamePayer(maxInclude());
addressPayer () = addressPayer(maxInclude());
UNPfullNameSupplier () = CONCAT ', ', UNPSupplier(maxInclude()), fullNameSupplier[Consignment](maxInclude());
addressSupplier () = addressSupplier[Consignment](maxInclude());
date () = date(maxInclude());
nameTruck () = nameTruck(maxInclude());
overWaybill () = overWaybill(maxInclude());
nameDriver () = nameDriver(maxInclude());
forwarder () = forwarder(maxInclude());
infoCustomer (Invoice i) = CONCAT ', ', UNPCustomer(i), fullNameCustomer[Consignment](i), addressCustomer[Consignment](i);

FORM consignmentEInvoices 'РЕЕСТР'
    PROPERTIES() UNPfullNamePayer, addressPayer, UNPfullNameSupplier, addressSupplier, date, nameTruck, overWaybill, nameDriver, forwarder
    OBJECTS i = Invoice SUBREPORT
    PROPERTIES(i) READONLY numberDate, infoCustomer, overGoodsAccepted
    FILTERS include(i)
;

//printConsignmentEInvoicesXLSX 'Реестр ТТН (xlsx) (несколько)' ()  { 
//    PRINT consignmentEInvoices XLSX;
//    FOR include(Invoice i) DO {
//        PRINT attachmentEInvoice OBJECTS i = i XLSX;
//    }
//} IMAGE 'print.png';
@addRoleApplicationSetting(showEditEInvoice, 'Показывать форму редактирования электронной ТТН', BOOLEAN);
showEditEInvoiceCurrent () = showEditEInvoice(mainRole(currentUser()));

editAttributesEInvoice 'Заполнить атрибуты ЭТТН' (Invoice i)  { 
	NEWSESSION {   
	    SHOW eInvoice OBJECTS e = eInvoice(i) DOCKED; 
	}
} IMAGE 'edit.png';

META defineInvoiceEinvoice (dumb)
    EXTEND FORM invoices
        PROPERTIES READONLY PANEL numberEInvoice(i)
        PROPERTIES READONLY numberEInvoice = numberEInvoice(i), statusDescription = statusDescription(i) BACKGROUND backgroundStatusDescription(i)
        PROPERTIES(i) createEInvoice SHOWIF (NOT eInvoice(i) AND NOT hideCreateEInvoice(i)), statusDescription SHOWIF eInvoice(i) PANEL, 
                      signAndSendSupplierEDI SHOWIF (eInvoice(i) AND NOT exportedSupplier(eInvoice(i))), 
                      cancelSignAndSendSupplierEDI SHOWIF (eInvoice(i) AND exportedSupplier(eInvoice(i)))
        PROPERTIES (i) editAttributesEInvoice SHOWIF (eInvoice(i) AND showEditEInvoiceCurrent())
        PROPERTIES    editAttributes = editAttributes(i) SHOWIF eInvoice(i)
        PROPERTIES(i) SHOWIF (eInvoice(i) AND NOT hidePrintEInvoice(i))
                      printConsignmentEInvoiceAttachment,  printConsignmentEInvoice, 
                      printConsignmentEAttachment, printConsignmentEAttachmentXLSX
    ;
    
    DESIGN invoices {
        printTabScroll {
            NEW tabConsignment{
                type = TABBED;
                MOVE printTab {caption = 'Накладная';};
                NEW eInvoice {
                    caption = 'Электронная накладная';
                    NEW createEI {
                        type = CONTAINERH;
                        MOVE PROPERTY(numberEInvoice(i));
                        MOVE PROPERTY(createEInvoice(i));
                        MOVE PROPERTY(editAttributes);          
                        MOVE PROPERTY(editAttributesEInvoice(i));                        
                        MOVE PROPERTY(signAndSendSupplierEDI(i));
                        MOVE PROPERTY(cancelSignAndSendSupplierEDI(i));
                        MOVE PROPERTY(statusDescription(i));
                    }
                    NEW printEI {
                        type = CONTAINERH;
                        MOVE PROPERTY (printConsignmentEInvoiceAttachment(i));
                        MOVE PROPERTY (printConsignmentEInvoice(i));
                        MOVE PROPERTY (printConsignmentEAttachment(i));
                    }
                    NEW exportEI {
                        type = CONTAINERH;
                        MOVE PROPERTY (printConsignmentEAttachmentXLSX(i));
                    }
                }
            }
    }    
    }
END

printConsignmentEInvoiceAttachment 'Реестр ТТН с приложением' (EInvoice i)  { 
    include(EInvoice e) <- NULL;
    multy() <- NULL;
    include(EInvoice e) <- TRUE WHERE e = i;    
    PRINT EInvoiceConsignment;
    PRINT EInvoiceAttachment;
} IMAGE 'print.png';

printConsignmentEInvoice 'Реестр ТТН' (EInvoice i)  { 
    include(EInvoice e) <- NULL;
    multy() <- NULL;
    include(EInvoice e) <- TRUE WHERE e = i;    
    PRINT EInvoiceConsignment;
} IMAGE 'print.png';

printConsignmentEAttachment 'Приложение' (EInvoice i)  { 
    include(EInvoice e) <- NULL;
    multy() <- NULL;
    include(EInvoice e) <- TRUE WHERE e = i;    
    PRINT EInvoiceAttachment;
} IMAGE 'print.png';

printConsignmentEAttachmentXLSX 'Приложение (xlsx)' (EInvoice i)  { 
    include(EInvoice e) <- NULL;
    multy() <- NULL;
    include(EInvoice e) <- TRUE WHERE e = i;    
    PRINT EInvoiceAttachment XLSX;
} IMAGE 'print.png';


printConsignmentEInvoicesAll 'Реестр ТТН (несколько)' ()  { 
    IF NOT ([ GROUP SUM 1 IF [GROUP SUM 1 IF in(EInvoice i) BY supplierStock(i)](Stock st)]() >1 OR 
            [ GROUP SUM 1 IF [GROUP SUM 1 IF in(EInvoice i) BY customerStock(i)](Stock st)]()>1 OR 
            [ GROUP SUM 1 IF [GROUP SUM 1 IF in(EInvoice i) BY DATE(dateTime(i))](DATE dt)]()>1)  THEN {
        include(EInvoice e) <- NULL;
        multy() <- TRUE;
        include(EInvoice e) <- TRUE WHERE in(e);    
        
        PRINT EInvoiceConsignment;
        PRINT EInvoiceAttachment;
    } ELSE {
        MESSAGE 'Выбраны накладные с разными складами и/или датами';
    }

} IMAGE 'print.png';

EXTEND FORM eInvoices
    PROPERTIES(e) SHOWIF NOT hidePrintEInvoice(e)
                  printConsignmentEInvoiceAttachment,  printConsignmentEInvoice, 
                  printConsignmentEAttachment, printConsignmentEAttachmentXLSX
    PROPERTIES    printConsignmentEInvoicesAll() SHOWIF ((GROUP SUM 1 IF in(EInvoice iii)) AND NOT hidePrintEInvoice(e))
;
DESIGN eInvoices {
    tab {
        NEW printTab {
            fill = 1;
                caption = 'Печатные формы';
                NEW printEI {
                    type = CONTAINERH;
                    MOVE PROPERTY (printConsignmentEInvoiceAttachment(e));
                    MOVE PROPERTY (printConsignmentEInvoice(e));
                    MOVE PROPERTY (printConsignmentEAttachment(e)); 
                    MOVE PROPERTY (printConsignmentEInvoicesAll());                   
                }
                NEW exportEI {
                    type = CONTAINERH;
                    MOVE PROPERTY (printConsignmentEAttachmentXLSX(e));
                    
                }
        }
    }
}
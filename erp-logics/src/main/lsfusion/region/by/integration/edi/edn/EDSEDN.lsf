MODULE EDSEDN;

REQUIRE EDS, EDN;

NAMESPACE EDN;

EXTEND CLASS EDSApplication {
    docudriveProxy 'Прокси сервис Docudrive'
}

dataEDSApplication = DATA EDSApplication();
EDSApplication() = OVERRIDE dataEDSApplication(), EDSApplication.docudriveProxy;
nameEDSApplication 'Приложение ЭЦП' = staticCaption(EDSApplication());

EDSApplication(STRING[100] provider) += WHEN provider == 'EDN' THEN EDSApplication();

aliasEDSService 'Токен ЭЦП - имя контейнера' = DATA STRING[100] (LegalEntity);
passwordEDSService 'Токен ЭЦП - пароль' = DATA STRING[100] (LegalEntity) ECHO;
aliasEDSService 'Токен ЭЦП - имя контейнера' = DATA STRING[100] (Stock);
passwordEDSService 'Токен ЭЦП - пароль' = DATA STRING[100] (Stock) ECHO;
aliasEDSService 'Токен ЭЦП - имя контейнера' = DATA STRING[100] (Employee);
passwordEDSService 'Токен ЭЦП - пароль' = DATA STRING[100] (Employee) ECHO;

aliasEDSServiceCustomer (EInvoice i) = OVERRIDE aliasEDSService(currentUser()), aliasEDSService(customerStock(i)), aliasEDSService(customer(i));
passwordEDSServiceCustomer (EInvoice i) = OVERRIDE passwordEDSService(currentUser()), passwordEDSService(customerStock(i)), passwordEDSService(customer(i));

aliasEDSServiceSupplier (EInvoice i) = OVERRIDE aliasEDSService(currentUser()), aliasEDSService(supplierStock(i)), aliasEDSService(supplier(i));
passwordEDSServiceSupplier (EInvoice i) = OVERRIDE passwordEDSService(currentUser()), passwordEDSService(supplierStock(i)), passwordEDSService(supplier(i));

EDSService 'Прокси сервис Docudrive' = DATA EDSService (LegalEntity);
descriptionEDSService 'Прокси сервис Docudrive' (LegalEntity o) = description(EDSService(o));
EDSService 'Прокси сервис Docudrive' = DATA EDSService (Stock);
descriptionEDSService 'Прокси сервис Docudrive' (Stock o) = description(EDSService(o));
EDSService 'Прокси сервис Docudrive' = DATA EDSService (Employee);
descriptionEDSService 'Прокси сервис Docudrive' (Employee o) = description(EDSService(o));

EDSServiceCustomer(EDSApplication app, EInvoice i) += WHEN app == EDSApplication.docudriveProxy AND i IS EInvoice 
        THEN OVERRIDE EDSService(currentUser()), EDSService(customerStock(i)), EDSService(customer(i));
EDSServiceSupplier(EDSApplication app, EInvoice i) += WHEN app == EDSApplication.docudriveProxy AND i IS EInvoice 
        THEN OVERRIDE EDSService(currentUser()), EDSService(supplierStock(i)), EDSService(supplier(i));

EXTEND FORM integrationData
    PROPERTIES nameEDSApplication()
    OBJECTS lee = LegalEntity
    FILTERS isCompany(lee)
    PROPERTIES(lee) SHOWIF NOT useDocuDrive() name READONLY, aliasEDSService, passwordEDSService, descriptionEDSService
    
    OBJECTS sedse = Stock
    PROPERTIES(sedse) SHOWIF NOT useDocuDrive() name READONLY, aliasEDSService, passwordEDSService, descriptionEDSService
    FILTERS isCompany(sedse)
    
    OBJECTS eedse = Employee
    PROPERTIES(eedse) SHOWIF NOT useDocuDrive() READONLY lastName, firstName, namePosition
    PROPERTIES(eedse) SHOWIF NOT useDocuDrive() aliasEDSService, passwordEDSService, descriptionEDSService
    FILTERS active(eedse)
;

DESIGN integrationData {
    ivsEDN {
        MOVE PROPERTY (nameEDSApplication());
        NEW EDSServiceEDN {
            showIf = EDSApplication() == EDSApplication.docudriveProxy;
            type = TABBED;
            fill = 1;
            MOVE BOX(lee);
            MOVE BOX(sedse);
            MOVE BOX(eedse);
        }
    }
}

sendRequestEDSCustomer(STRING[100] provider, EInvoice ei, XMLFILE file) {
    sendRequestEDI(NULL, NULL, ('http://' + host(EDSServiceCustomer(provider,ei)) + ':' + port(EDSServiceCustomer(provider,ei))  + '/webservice/soap?wsdl'), file, NULL, client(EDSServiceCustomer(provider,ei)) );
}
    
sendRequestEDSSupplier(STRING[100] provider, EInvoice ei, XMLFILE file) {
    sendRequestEDI(NULL, NULL, ('http://' + host(EDSServiceSupplier(provider,ei)) + ':' + port(EDSServiceSupplier(provider,ei)) + '/webservice/soap?wsdl'), file, NULL, client(EDSServiceSupplier(provider,ei)) );
}

GROUP sign EXTID 'urn:Sign' : body;

header = DATA LOCAL STRING();
name = DATA LOCAL STRING();
data = DATA LOCAL XMLFILE();
cryptoContainerId = DATA LOCAL STRING();
cryptoContainerPassword = DATA LOCAL STRING();
attributeCertificate = DATA LOCAL STRING();

FORM envelopeEDN FORMEXTID 'soapenv=http://schemas.xmlsoap.org/soap/envelope/:Envelope'
PROPERTIES ATTR ='urn:webservice' EXTID 'xmlns:urn'
PROPERTIES() header EXTID 'soapenv:Header'
PROPERTIES() IN sign 
    name EXTID 'urn:Name',
    documentType EXTID 'urn:DocumentType',
    data EXTID 'urn:Data',
    cryptoContainerId EXTID 'urn:CryptoContainerId',
    cryptoContainerPassword EXTID 'urn:CryptoContainerPassword',
    attributeCertificate EXTID 'urn:AttributeCertificate';

GROUP body EXTID 'soap:Body';
GROUP signResponse EXTID '=urn:webservice:SignResponse' : body;
GROUP signResult EXTID '=urn:webservice:SignResult' : signResponse;

result = DATA LOCAL STRING();
fileName = DATA LOCAL STRING();
fileData = DATA LOCAL XMLFILE ();

FORM envelopeResponseEDN FORMEXTID 'soap=http://schemas.xmlsoap.org/soap/envelope/:Envelope'
PROPERTIES ATTR IN signResponse ='urn:webservice' EXTID 'xmlns:'
PROPERTIES() IN signResult 
    result EXTID '=urn:webservice:Result', 
    fileName EXTID '=urn:webservice:FileName', 
    fileData EXTID '=urn:webservice:FileData';


getSignedDocumentCustomer(STRING[100] provider, EInvoice eInvoice, FILE contentSubXML, BOOLEAN interactive) + {
    IF EDSApplication(provider) == EDSApplication.docudriveProxy THEN {
        signError() <- NULL;
        signResult() <- NULL;
        
        LOCAL xml = FILE();
        name() <- 'blrapn-' + toChar(blrapnDateTime(),'YYYY-MM-DD-HH24-MI-SS-MS');
        documentType() <- 'BLRAPN';
        data() <- XMLFILE(contentSubXML);
        cryptoContainerId() <- aliasEDSServiceCustomer(eInvoice);
        cryptoContainerPassword() <- passwordEDSServiceCustomer(eInvoice);
        EXPORT envelopeEDN XML TO xml;
        sendRequestEDSCustomer(provider, eInvoice, XMLFILE(xml()));
        IMPORT envelopeResponseEDN FROM sendRequestEDIResponse();

        signError() <- (CONCAT ' ', result(), fileName()) IF INTEGER (result()) > 0;
        IF fileData() THEN {
            fileToString(FILE(fileData()));
            encodeBase64(resultString());
            signResult() <- encodedBase64();
        }
        RETURN;
    }
}

getSignedDocumentSupplier(STRING[100] provider, EInvoice eInvoice, FILE contentSubXML, BOOLEAN interactive) + {
    IF EDSApplication(provider) == EDSApplication.docudriveProxy THEN {
        signError() <- NULL;
        signResult() <- NULL;
        
        LOCAL xml = FILE();
        name() <- 'blrapn-' + toChar(blrapnDateTime(),'YYYY-MM-DD-HH24-MI-SS-MS');
        documentType() <- 'BLRAPN';
        data() <- XMLFILE(contentSubXML);
        cryptoContainerId() <- aliasEDSServiceSupplier(eInvoice);
        cryptoContainerPassword() <- passwordEDSServiceSupplier(eInvoice);
        EXPORT envelopeEDN XML TO xml;
        sendRequestEDSSupplier(provider, eInvoice, XMLFILE(xml()));
        IMPORT envelopeResponseEDN FROM sendRequestEDIResponse();
        
        signError() <- (CONCAT ' ', result(), fileName()) IF INTEGER (result()) > 0;
        IF fileData() THEN {
            fileToString(FILE(fileData()));
            encodeBase64(resultString());
            signResult() <- encodedBase64();
        }
        RETURN;
    }
}
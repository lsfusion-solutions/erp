MODULE SaleEInvoiceManufacturingPrice;

REQUIRE SaleEInvoice, SaleManufacturingPrice, PurchaseShipment;

NAMESPACE Sale;

invoiceDetailPurchase (Sale.InvoiceDetail d) = OVERRIDE invoiceDetail(batch(d)), invoiceDetail(lastOrderBatchB(sku(d), supplierStock(d), dateTime(d)));

overManufacturingPrice (Sale.InvoiceDetail d) = IF d IS Sale.InvoiceDetail AND NOT (supplierStock(d) IS Warehouse)
    THEN (OVERRIDE manufacturingPrice(invoiceDetailPurchase(d)), price(invoiceDetailPurchase(d)))
    ELSE (OVERRIDE manufacturingPrice(d), price(d));

overManufacturingMarkup (Sale.InvoiceDetail d) = round4([(X-Y)/Y*100](price(d),overManufacturingPrice(d))) IF overManufacturingPrice(d);

overCreate(EInvoiceDetail eid, ConsignmentDetail cd) + {
    IF overManufacturingPrice[InvoiceDetail](cd) THEN NEW ief = ItemExtraField {
        name(ief) <- 'Отпускная цена';
        code(ief) <- 'PT0022';
        value(ief) <- trim(toChar(overManufacturingPrice[InvoiceDetail](cd),'FM999990.0099'));
        eInvoiceDetail(ief) <- eid;
    }
    IF overManufacturingMarkup[InvoiceDetail](cd) THEN NEW ief = ItemExtraField {
        name(ief) <- 'Оптовая надбавка';
        code(ief) <- 'PT0023';
        value(ief) <- trim(toChar(overManufacturingMarkup[InvoiceDetail](cd),'FM990.9999'));
        eInvoiceDetail(ief) <- eid;
    }
}
MODULE StoreTypeImport;

REQUIRE Store;

idStoreType 'Код формата магазина' (INTEGER i) = 'format1' IF i IS INTEGER;
nameStoreType 'Наименование формата магазина' (INTEGER i) = 'Формат 1' IF i IS INTEGER;
idChainStores 'Код торговой сети' (INTEGER i) = 'gippo' IF i IS INTEGER;
nameChainStores 'Наименование торговой сети' (INTEGER i) = 'Гиппо' IF i IS INTEGER;

FORM storeTypeImportTemplate 'Импорт форматов магазина (шаблон)'
    OBJECTS i=INTEGER SUBREPORT 
    PROPERTIES (i) idStoreType, nameStoreType, idChainStores, nameChainStores
    FILTERS i == 1
;

storeTypeImportTemplateXLS 'Создать шаблон' ()  { 
    PRINT storeTypeImportTemplate XLS;       
} IMAGE 'print.png';

storeTypeImport 'Импортировать' ()  { 
    NEWSESSION {
         LOCAL NESTED idStoreTypeField = STRING[100] (INTEGER);
         LOCAL NESTED nameStoreTypeField = ISTRING[100] (INTEGER);
         LOCAL NESTED idChainStoresField = STRING[100] (INTEGER);
         LOCAL NESTED nameChainStoresField = ISTRING[100] (INTEGER);
         
         INPUT f = EXCELFILE DO {
            IMPORT XLS NOHEADER FROM f AS EXCELFILE TO idStoreTypeField=B, nameStoreTypeField=C, idChainStoresField=D, nameChainStoresField=E;
            
            FOR INTEGER i == [GROUP MIN INTEGER ii IF ii > 1 BY idChainStoresField(ii)](STRING[100] id) AND 
                NOT [GROUP SUM 1 BY id(ChainStores chainStores)](id) AND 
                nameChainStoresField(i) NEW chainStores = ChainStores DO {
                    id(chainStores) <- idChainStoresField(i);
                    name(chainStores) <- nameChainStoresField(i);
                }
            
            FOR imported(INTEGER i) AND i > 1 AND idStoreTypeField(i) AND nameStoreTypeField(i) AND idChainStoresField(i) AND nameChainStoresField(i)
                NEW storeType = StoreType DO {
                    id(storeType) <- idStoreTypeField(i);
                    name(storeType) <- nameStoreTypeField(i);
                    chainStores(storeType) <- chainStores(idChainStoresField(i));
                }
            apply();
         }
    }
}

EXTEND FORM migrationData
    PROPERTIES () storeTypeImportTemplateXLS, storeTypeImport
;

DESIGN migrationData{
    commons {
        NEW storeTypeImport {
            type = CONTAINERH;
            caption = 'Форматы магазина';
            MOVE PROPERTY(storeTypeImportTemplateXLS()); 
            MOVE PROPERTY(storeTypeImport());
        } 
    }
}
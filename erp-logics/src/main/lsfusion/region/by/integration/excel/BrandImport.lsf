MODULE BrandImport;

REQUIRE Item;

id 'Код' (INTEGER i) = 'Бабушка Аня' IF i IS INTEGER;
name 'Наименование' (INTEGER i) = 'Бабушка Аня' IF i IS INTEGER;
site 'Адрес сайта' (INTEGER i) = '' IF i IS INTEGER;

FORM brandImportTemplate 'Импорт брендов (шаблон)'
    OBJECTS i=INTEGER SUBREPORT 
    PROPERTIES (i) id, name, site
    FILTERS i == 1
;

brandImportTemplateXLS 'Создать шаблон' ()  { 
    PRINT brandImportTemplate XLS;       
} IMAGE 'print.png';

brandImport 'Импортировать' ()  { 
    NEWSESSION {
        LOCAL NESTED idField = STRING[100] (INTEGER);
        LOCAL NESTED nameField = ISTRING[50] (INTEGER);
        LOCAL NESTED siteField = ISTRING[100] (INTEGER);
        
        INPUT f = EXCELFILE DO {
            IMPORT XLS NOHEADER FROM f AS EXCELFILE TO idField=B, nameField=C, siteField=D;
            
            FOR imported(INTEGER i) AND i > 1 AND idField(i) AND nameField(i) NEW brand = Brand DO {
                id(brand) <- idField(i);
                name(brand) <- nameField(i);
                site(brand) <- siteField(i);
            }
            apply();
        }
    }
}

EXTEND FORM migrationData
    PROPERTIES () brandImportTemplateXLS, brandImport
;

DESIGN migrationData{
    commons {
        NEW brandImport {
            type = CONTAINERH;
            caption = 'Бренды';
            MOVE PROPERTY(brandImportTemplateXLS()); 
            MOVE PROPERTY(brandImport());
        } 
    }
}
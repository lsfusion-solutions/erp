MODULE LegalEntityImport;

REQUIRE LegalEntity;

id 'Код' (INTEGER i) = 'ПК99990' IF i IS INTEGER;
name 'Наименование' (INTEGER i) = '1-МИНСКАЯ ПТИЦЕФАБРИКА ОАО' IF i IS INTEGER;
address 'Юридический адрес' (INTEGER i) = '223043 Минская область.Минский район. а.г Большевик' IF i IS INTEGER;
phone 'Телефон/факс' (INTEGER i) = '017 504 87 90' IF i IS INTEGER;
isSupplier 'Явл. поставщиком' (INTEGER i) = '' IF i IS INTEGER;
isCustomer 'Явл. покупателем' (INTEGER i) = 'да' IF i IS INTEGER;

FORM legalEntityImportTemplate 'Импорт организаций (шаблон)'
    OBJECTS i=INTEGER SUBREPORT 
    PROPERTIES (i) id, name, address, phone, isSupplier, isCustomer
    FILTERS i == 1
;

legalEntityImportTemplateXLS 'Создать шаблон' ()  { 
    PRINT legalEntityImportTemplate XLS;       
} IMAGE 'print.png';

legalEntityImport 'Импортировать' ()  { 
    NEWSESSION {
        LOCAL NESTED idField = STRING[100] (INTEGER);
        LOCAL NESTED nameField = ISTRING[150] (INTEGER);
        LOCAL NESTED addressField = STRING[150] (INTEGER);
        LOCAL NESTED phoneField = STRING[100] (INTEGER);
        LOCAL NESTED isSupplierField = STRING[10] (INTEGER);
        LOCAL NESTED isCustomerField = STRING[10] (INTEGER); 
        
        INPUT f = EXCELFILE DO {
            IMPORT XLS NOHEADER FROM f AS EXCELFILE TO idField=B, nameField=C, addressField=D, phoneField=E, isSupplierField=F, isCustomerField=G;
            
            FOR imported(INTEGER i) AND i > 1 AND idField(i) AND nameField(i) NEW legalEntity = LegalEntity DO {
                id(legalEntity) <- idField(i);
                name(legalEntity) <- nameField(i);
                fullName(legalEntity) <- nameField(i);
                legalEntityGroup(legalEntity) <- CASE WHEN lower(isSupplierField(i)) == 'да' THEN legalEntityGroup('suppliers')
                                                      WHEN lower(isCustomerField(i)) == 'да' THEN legalEntityGroup('customers')
                                                      ELSE legalEntityGroup('all');
                dataAddress(legalEntity, DATE date) <- addressField(i) IF date == currentDate();
                dataPhone(legalEntity, DATE date) <- phoneField(i) IF date == currentDate();
                isSupplier(legalEntity) <- lower(isSupplierField(i)) == 'да';
                isCustomer(legalEntity) <- lower(isCustomerField(i)) == 'да';
            }
            apply();
        }
    }
}

EXTEND FORM migrationData
    PROPERTIES () legalEntityImportTemplateXLS, legalEntityImport
;

DESIGN migrationData{
    commons {
        NEW legalEntityImport {
            type = CONTAINERH;
            caption = 'Организации';
            MOVE PROPERTY(legalEntityImportTemplateXLS()); 
            MOVE PROPERTY(legalEntityImport());
        } 
    }
}
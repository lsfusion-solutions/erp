MODULE WarehouseImport;

REQUIRE Warehouse;

id 'Код' (INTEGER i) = 'ПК99990' IF i IS INTEGER;
name 'Наименование' (INTEGER i) = 'Склад 1-МИНСКАЯ ПТИЦЕФАБРИКА ОАО' IF i IS INTEGER;
address 'Адрес' (INTEGER i) = '' IF i IS INTEGER;
idLegalEntity 'Код организации' (INTEGER i) = 'ПК99990' IF i IS INTEGER;
isCompany 'Собственный склад' (INTEGER i) = 'да' IF i IS INTEGER;

FORM warehouseImportTemplate 'Импорт складов (шаблон)'
    OBJECTS i=INTEGER SUBREPORT 
    PROPERTIES (i) id, name, address, idLegalEntity, isCompany
    FILTERS i == 1
;

warehouseImportTemplateXLS 'Создать шаблон' ()  { 
    PRINT warehouseImportTemplate XLS;       
} IMAGE 'print.png';

warehouseImport 'Импортировать' ()  { 
    NEWSESSION {
        LOCAL NESTED idField = STRING[100] (INTEGER);
        LOCAL NESTED nameField = ISTRING[150] (INTEGER);
        LOCAL NESTED addressField = STRING[100] (INTEGER);
        LOCAL NESTED idLegalEntityField = STRING[100] (INTEGER);
        LOCAL NESTED isCompanyField = STRING[10] (INTEGER);
        
        INPUT f = EXCELFILE DO {
            IMPORT XLS NOHEADER FROM f AS EXCELFILE TO idField=B, nameField=C, addressField=D, idLegalEntityField=E, isCompanyField=F;
                    
            FOR imported(INTEGER i) AND i > 1 AND idField(i) AND nameField(i) AND idLegalEntityField(i) NEW warehouse = Warehouse DO {
                id(warehouse) <- idField(i);
                name(warehouse) <- nameField(i);
                address(warehouse) <- addressField(i);
                legalEntity(warehouse) <- legalEntity(idLegalEntityField(i));
                warehouseGroup(warehouse) <- IF lower(isCompanyField(i)) == 'да' THEN warehouseGroup('company') ELSE warehouseGroup('external');
            }
            apply();
        }
    }
}

EXTEND FORM migrationData
    PROPERTIES () warehouseImportTemplateXLS, warehouseImport
;

DESIGN migrationData{
    commons {
        NEW warehouseImport {
            type = CONTAINERH;
            caption = 'Склады';
            MOVE PROPERTY(warehouseImportTemplateXLS()); 
            MOVE PROPERTY(warehouseImport());
        } 
    }
}
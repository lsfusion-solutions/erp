MODULE StoreImport;

REQUIRE Store;

idStore 'Код магазина' (INTEGER i) = 'ГМ1' IF i IS INTEGER;
nameStore 'Наименование магазина' (INTEGER i) = 'Держинского' IF i IS INTEGER;
idStoreType 'Код формата магазина' (INTEGER i) = 'format1' IF i IS INTEGER;
idLegalEntity 'Код компании' (INTEGER i) = 'РПКП000000' IF i IS INTEGER;
address 'Адрес' (INTEGER i) = 'Держинского 1' IF i IS INTEGER;
idDepartmentStore 'Код отдела' (INTEGER i) = 'ГМ1' IF i IS INTEGER;
nameDepartmentStore 'Наименование отдела' (INTEGER i) = 'Держинского' IF i IS INTEGER;
fullNameDepartmentStore 'Полное наименование отдела' (INTEGER i) = 'Торговый центр по ул. Держинского' IF i IS INTEGER;

FORM storeImportTemplate 'Импорт магазинов (шаблон)'
    OBJECTS i=INTEGER SUBREPORT 
    PROPERTIES (i) idStore, nameStore, idStoreType, idLegalEntity, address, idDepartmentStore, nameDepartmentStore, fullNameDepartmentStore
    FILTERS i == 1
;

storeImportTemplateXLS 'Создать шаблон' ()  { 
    PRINT storeImportTemplate XLS;       
} IMAGE 'print.png';

storeImport 'Импортировать' ()  { 
    NEWSESSION {
        LOCAL NESTED idStoreField = STRING[100] (INTEGER);
        LOCAL NESTED nameStoreField = ISTRING[100] (INTEGER);
        LOCAL NESTED idStoreTypeField = STRING[100] (INTEGER);
        LOCAL NESTED idLegalEntityField = ISTRING[150] (INTEGER);
        LOCAL NESTED addressField = STRING[100] (INTEGER);
        LOCAL NESTED idDepartmentStoreField = STRING[100] (INTEGER);
        LOCAL NESTED nameDepartmentStoreField = ISTRING[150] (INTEGER);
        LOCAL NESTED fullNameDepartmentStoreField = ISTRING[150] (INTEGER);
            
        INPUT f = EXCELFILE DO {
            IMPORT XLS NOHEADER FROM f AS EXCELFILE TO idStoreField=B, nameStoreField=C, idStoreTypeField=D, idLegalEntityField=E, addressField=F, 
                          idDepartmentStoreField=G, nameDepartmentStoreField=H, fullNameDepartmentStoreField=I;
                          
            FOR INTEGER i == [GROUP MIN INTEGER ii IF ii > 1 BY idStoreField(ii)](STRING[100] id) AND 
                NOT [GROUP SUM 1 BY id(Store store)](id) AND 
                nameStoreField(i) AND idLegalEntityField(i) NEW store = Store DO {
                    id(store) <- idStoreField(i);
                    name(store) <- nameStoreField(i);
                    storeType(store) <- storeType(idStoreTypeField(i));
                    legalEntity(store) <- legalEntity(idLegalEntityField(i));
                    address(store) <- addressField(i);
                }
            
            FOR imported(INTEGER i) AND i > 1 AND idDepartmentStoreField(i) AND nameDepartmentStoreField(i) AND 
                idStoreField(i) AND nameStoreField(i) AND idLegalEntityField(i)
                NEW departmentStore = DepartmentStore DO {
                    id(departmentStore) <- idDepartmentStoreField(i);
                    name(departmentStore) <- nameDepartmentStoreField(i);
                    fullName(departmentStore) <- fullNameDepartmentStoreField(i);
                    store(departmentStore) <- store(idStoreField(i));
                }
            apply();
        }
    }
}

EXTEND FORM migrationData
    PROPERTIES () storeImportTemplateXLS, storeImport
;

DESIGN migrationData{
    commons {
        NEW storeImport {
            type = CONTAINERH;
            caption = 'Магазины';
            MOVE PROPERTY(storeImportTemplateXLS()); 
            MOVE PROPERTY(storeImport());
        } 
    }
}
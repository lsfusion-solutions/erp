MODULE AccountImport;

REQUIRE Bank;

idLegalEntity 'Код организации' (INTEGER i) = 'ПК99990' IF i IS INTEGER;
number 'Номер расчетного счета' (INTEGER i) = '000000000' IF i IS INTEGER;
numberNew 'Номер расчетного счета IBAN' (INTEGER i) = '' IF i IS INTEGER;

FORM accountImportTemplate 'Импорт расчетных счетов (шаблон)'
    OBJECTS i=INTEGER SUBREPORT 
    PROPERTIES (i) idLegalEntity, number, numberNew
    FILTERS i == 1
;

accountImportTemplateXLS 'Создать шаблон' ()  { 
    PRINT accountImportTemplate XLS;       
} IMAGE 'print.png';

accountImport 'Импортировать' ()  { 
    NEWSESSION {
        LOCAL NESTED idField = STRING[100] (INTEGER);
        LOCAL NESTED numberField = STRING[50] (INTEGER);
        LOCAL NESTED numberNewField = STRING[50] (INTEGER);
        
        INPUT f = EXCELFILE DO {
            IMPORT XLS NOHEADER FROM f AS EXCELFILE TO idField=B, numberField=C, numberNewField=D;
            
            FOR imported(INTEGER i) AND i > 1 AND idField(i) AND (numberField(i) OR numberNewField(i)) AND 
                LegalEntity legalEntity == [GROUP MIN LegalEntity llegalEntity BY id(llegalEntity)](idField(i)) 
                NEW account = Bank.Account DO {
                    number(account) <- numberField(i);
                    numberNew(account) <- numberNewField(i);
                    currency(account) <- defaultCurrency();
                    legalEntity(account) <- legalEntity;
                }
            apply();
        }
    }
}

EXTEND FORM migrationData
    PROPERTIES () accountImportTemplateXLS, accountImport
;

DESIGN migrationData{
    commons {
        NEW accountImport {
            type = CONTAINERH;
            caption = 'Расчетные счета';
            MOVE PROPERTY(accountImportTemplateXLS()); 
            MOVE PROPERTY(accountImport());
        } 
    }
}
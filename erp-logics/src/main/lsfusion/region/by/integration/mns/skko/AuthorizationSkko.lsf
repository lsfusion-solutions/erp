MODULE AuthorizationSkko;

REQUIRE Utils, LegalEntity;

NAMESPACE Skko;

httpSkkoHeaders = DATA LOCAL NESTED TEXT(TEXT);

skkoUrl 'URL СККО' = DATA STRING();
EXTEND FORM integrationData PROPERTIES skkoUrl();

DESIGN integrationData {
    pane {
        NEW mnsSKKO {
            caption = 'СККО';
            MOVE PROPERTY (skkoUrl());
        }
    }
}

// token
skkoToken 'Токен' = DATA STRING() ECHO;
apiKey 'Ключ авторизации' = DATA STRING();


FORM authorization
    OBJECTS apiKey = STRING EXTID 'value'
    PROPERTIES() apiKey EXTID 'api_key'
;

// импорт
ui = DATA LOCAL STRING();
error_description = DATA LOCAL STRING();
error_code = DATA LOCAL INTEGER();
status = DATA LOCAL BOOLEAN ();
access_token = DATA LOCAL STRING();
time_out = DATA LOCAL NUMERIC();

FORM importToken
    PROPERTIES()
    access_token EXTID 'access_token',
        error_code EXTID 'error_code',
        ui EXTID 'ui',
        error_description EXTID 'error_description',
        time_out EXTID 'time_out',
        status EXTID 'status'    
;

setHttpSkkoHeaders 'Задать заголовки' (STRING token){
    httpSkkoHeaders('Content-Type') <- 'application/json; charset=utf-8';

    IF token THEN {
        httpSkkoHeaders('x-access-token') <- token;
    }
}

getSkkoToken 'Авторизация' (STRING apiKey) {    
    EXPORT authorization OBJECTS apiKey = apiKey JSON;
    fileToString(exportFile());
    
    LOCAL response = FILE();
    TRY {
        access_token() <- NULL;
        insecureSSL() <- TRUE;
        EXTERNAL HTTP POST skkoUrl() + '/login' PARAMS exportFile() TO response;
        IMPORT importToken JSON FROM response();
    } CATCH {
        IMPORT importToken JSON FROM response();
        MESSAGE 'Ошибка получения токена: ' + CONCAT '\n', error_description(), messageCaughtException() NOWAIT;
    }
}

getSkkoToken 'Авторизация' () {
    getSkkoToken(apiKey());
    skkoToken() <- access_token();
    APPLY;
}

EXTEND FORM integrationData
    PROPERTIES () apiKey
    PROPERTIES READONLY skkoToken()
    PROPERTIES getSkkoToken()
;

DESIGN integrationData {
    pane {
        mnsSKKO {
            MOVE PROPERTY (apiKey());
            MOVE PROPERTY (skkoToken());
            MOVE PROPERTY (getSkkoToken());
        }
    }
}

useSkkoLegalEntity 'Использовать организации' = DATA BOOLEAN ();

dataSkkoApiKey 'Ключ авторизации' = DATA STRING(LegalEntity);
dataSkkoToken 'Токен' = DATA STRING (LegalEntity) ECHO;

EXTEND FORM integrationData
    PROPERTIES useSkkoLegalEntity()

    OBJECTS skkol = LegalEntity
    PROPERTIES(skkol) READONLY id, name
    PROPERTIES(skkol) dataSkkoApiKey, dataSkkoToken READONLY
    FILTERS isCompany(skkol)
;

DESIGN integrationData {
    mnsSKKO {
        MOVE PROPERTY(useSkkoLegalEntity());
        MOVE BOX(skkol) { showIf = useSkkoLegalEntity(); }
    }
}

apiKey(LegalEntity l) = OVERRIDE dataSkkoApiKey(l), (apiKey() IF l IS LegalEntity);
skkoToken(LegalEntity l) = OVERRIDE dataSkkoToken(l), (skkoToken() IF l IS LegalEntity);

getSkkoToken 'Авторизация' (LegalEntity l) {
    IF apiKey(l) THEN {
        getSkkoToken(apiKey(l));
        dataSkkoToken(l) <- access_token();
        APPLY;
    }
}

EXTEND FORM integrationData
    PROPERTIES(skkol) getSkkoToken GRID
;
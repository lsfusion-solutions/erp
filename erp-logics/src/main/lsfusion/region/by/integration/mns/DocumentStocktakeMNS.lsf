MODULE DocumentStocktakeMNS;

REQUIRE DocumentMNS, Inventory, LegalEntityBy, ItemTracking, Lot, InventoryLot, BatchInventoryTracking, LegalEntityMNS;

NAMESPACE Inventory;

CLASS Stocktake 'Документ выгрузки в МНС';

id (Stocktake s) = STRING[50](s);
dateTime 'Дата отправки' =  DATA DATETIME (Stocktake);
inventory = DATA Inventory (Stocktake) NONULL DELETE;
legalEntity = DATA  LegalEntity(Stocktake);
prevStocktake = DATA LOCAL Stocktake(Stocktake);

district 'Наименование района, города, района в городе' = DATA STRING(Stocktake);
district2 'Наименование района' = DATA STRING(Stocktake);
dateTimeInventory 'Дата/время' = DATA STRING (Stocktake);
seriesNumber 'Серия/номер' = DATA STRING(Stocktake);
correctionNumber 'Номер корректировки' = DATA INTEGER (Stocktake);
nameLegalEntity 'Организация' = DATA STRING(Stocktake);
nameChief 'ФИО руководителя' = DATA STRING(Stocktake);
dateTimeSign 'Дата/время' = DATA DATETIME (Stocktake);
dateTimeSignXML 'Дата представляемых сведений'  = DATA STRING(Stocktake);
prevDocNumber 'Регистрационный номер представляемых сведений' = DATA STRING(Stocktake);

refRecordId 'Уникальный идентифика-тор исходного документа' = DATA INTEGER (Stocktake);

// Ответ
recordId 'Идентификатор записи (ответ)' = DATA  INTEGER(Stocktake);
statusCode 'Статус документа (ответ)' = DATA  STRING(Stocktake);
documentReplyDateTime 'Дата и время подтверждения портала о приёме документа (ответ)' = DATA DATETIME(Stocktake);
reply 'XML-документ квитанция подтверждение портала о приёме документа (ответ)' = DATA STRING(Stocktake);
SPTInternalDateTime 'Дата и время создания документа в системе прослеживаемости (ответ)' = DATA DATETIME(Stocktake);
resultDescription 'Поясняющий текст к результату обработки (ответ)' = DATA  STRING(Stocktake);
resultCode 'Код результата обработки (ответ)' = DATA INTEGER(Stocktake);

CLASS StocktakeDetail 'Строка документа выгрузки в МНС';
stocktake = DATA Stocktake(StocktakeDetail) NONULL DELETE;
batch = DATA Batch(StocktakeDetail);
numberBatch 'Партия' (StocktakeDetail d)  = number(batch(d));
index 'Номер строки' = DATA INTEGER (StocktakeDetail);
codeSku 'Код товара' = DATA STRING(StocktakeDetail);
extraCodeSku 'Дополнительный четырехзначный код' = DATA STRING[4](StocktakeDetail);
gtin 'GTIN' = DATA STRING(StocktakeDetail);
nameSku 'Наименование товара' = DATA STRING(StocktakeDetail);
sidOrigin2Country 'Код страны' = DATA STRING(StocktakeDetail);
uomSku 'Единица измерения' = DATA STRING(StocktakeDetail);
quantityUomSku 'Количество товара' =  DATA INTEGER (StocktakeDetail); // TODO вернуть после теста на DATA NUMERIC[16,4] (StocktakeDetail);
quantityUomSku0(StocktakeDetail d) = INTEGER((round0(quantityUomSku(d)))) IF NUMERIC[16,3](round0(quantityUomSku(d))) == quantityUomSku(d);
quantityUomSku1(StocktakeDetail d) = NUMERIC[16,1](round1(quantityUomSku(d))) IF NUMERIC[16,3](round1(quantityUomSku(d))) == quantityUomSku(d) AND NOT quantityUomSku0(d);
quantityUomSku2(StocktakeDetail d) = NUMERIC[16,2](round2(quantityUomSku(d))) IF NUMERIC[16,3](round2(quantityUomSku(d))) == quantityUomSku(d) AND NOT quantityUomSku0(d) AND NOT quantityUomSku1(d);
quantityUomSku3(StocktakeDetail d) = NUMERIC[16,3](round3(quantityUomSku(d))) IF NUMERIC[16,3](round3(quantityUomSku(d))) == quantityUomSku(d) AND NOT quantityUomSku0(d) AND NOT quantityUomSku1(d) AND NOT quantityUomSku2(d);

trackingUOMCustomsGroup 'Единица измерения (электронных накладных)' = DATA STRING[3](StocktakeDetail);
pricePageInventoryDetail 'Учетная цена (ед. изм. в эл. накладных)' = DATA NUMERIC[16,4](StocktakeDetail);
sumPageInventoryDetail 'Учетная стоимость товаров' = DATA NUMERIC[16,4](StocktakeDetail);
quantityPageInventoryDetail 'Количество товара (ед. изм. в эл. накладных)' = DATA INTEGER (StocktakeDetail); // TODO вернуть после теста на DATA NUMERIC[16,4](StocktakeDetail); 
quantityPageInventoryDetail0 (StocktakeDetail d) = INTEGER(round0(quantityPageInventoryDetail(d))) IF NUMERIC[16,3](round0(quantityPageInventoryDetail(d))) == quantityPageInventoryDetail(d);
quantityPageInventoryDetail1 (StocktakeDetail d) = NUMERIC[16,1](round1(quantityPageInventoryDetail(d))) IF NUMERIC[16,3](round1(quantityPageInventoryDetail(d))) == quantityPageInventoryDetail(d) AND NOT quantityPageInventoryDetail0(d);
quantityPageInventoryDetail2 (StocktakeDetail d) = NUMERIC[16,2](round2(quantityPageInventoryDetail(d))) IF NUMERIC[16,3](round2(quantityPageInventoryDetail(d))) == quantityPageInventoryDetail(d) AND NOT quantityPageInventoryDetail0(d) AND NOT quantityPageInventoryDetail1(d);
quantityPageInventoryDetail3 (StocktakeDetail d) = NUMERIC[16,3](round3(quantityPageInventoryDetail(d))) IF NUMERIC[16,3](round3(quantityPageInventoryDetail(d))) == quantityPageInventoryDetail(d) AND NOT quantityPageInventoryDetail0(d) AND NOT quantityPageInventoryDetail1(d) AND NOT quantityPageInventoryDetail2(d);

CLASS LotStocktakeDetail 'Строка марки документа выгрузки в МНС';
stocktakeDetail = DATA StocktakeDetail (LotStocktakeDetail) NONULL DELETE;
lot = DATA Lot(LotStocktakeDetail);
encodeBase64Code 'Код маркировки единицы товара' = DATA STRING(LotStocktakeDetail);

version = DATA LOCAL INTEGER ();
type = DATA LOCAL STRING();
rectification = DATA LOCAL BOOLEAN ();
kodIMNS = DATA LOCAL STRING();
UNP = DATA LOCAL STRING();
year = DATA LOCAL INTEGER();

lastStocktake (Inventory i) = GROUP LAST Stocktake s ORDER dateTime(s) BY inventory(s);

GROUP district EXTID 'LetterTraceabilityLeftovers_v1_f001';
GROUP district2 EXTID 'LetterTraceabilityLeftovers_v1_f001A';
GROUP groupInventory EXTID 'LetterTraceabilityLeftovers_v1_f002';
GROUP groupDetail EXTID 'LetterTraceabilityLeftovers_v1_t001';

FORM originalDocument FORMEXTID '=http://mns/edeclaration/xml/letters/traceabilityleftovers/ver1:LetterTraceabilityLeftovers'
        
    PROPERTIES()        
        version ATTR, // required
        type ATTR, // required
        rectification ATTR, // required
        kodIMNS ATTR, // required
        UNP ATTR, // required
        year ATTR // required
    
    OBJECTS s = Stocktake EXTID ''
//    PROPERTIES ATTR IN district ='' EXTID 'xmlns:'
    PROPERTIES(s) IN district district EXTID 'value'  // required

//    PROPERTIES ATTR IN district2 ='' EXTID 'xmlns:'
    PROPERTIES(s) IN district2 district2 EXTID 'value'  // required

//    PROPERTIES ATTR IN groupInventory ='' EXTID 'xmlns:'
    PROPERTIES(s) IN groupInventory
        dateTimeInventory EXTID 'LetterTraceabilityLeftovers_v1_f002_s1', // required
        seriesNumber EXTID 'LetterTraceabilityLeftovers_v1_f002_s2', // required
        nameLegalEntity EXTID 'LetterTraceabilityLeftovers_v1_f002_s3', // required
        nameChief EXTID 'LetterTraceabilityLeftovers_v1_f002_s4', // required
        dateTimeSignXML EXTID 'LetterTraceabilityLeftovers_v1_f002_s5',  // required
//        prevDocNumber EXTID 'LetterTraceabilityLeftovers_v1_f002_s6'
        sn = seriesNumber EXTID 'LetterTraceabilityLeftovers_v1_f002_s6' // required

    
    OBJECTS d = StocktakeDetail IN groupDetail EXTID 'LetterTraceabilityLeftovers_v1_t001_ri'
    PROPERTIES 
    'index'=STRING(index(d)) EXTID 'LetterTraceabilityLeftovers_v1_t001_ric1'  // required
    PROPERTIES(d) 
        codeSku EXTID 'LetterTraceabilityLeftovers_v1_t001_ric2',  // required
        extraCodeSku EXTID 'LetterTraceabilityLeftovers_v1_t001_ric2a',  // required
        gtin EXTID 'LetterTraceabilityLeftovers_v1_t001_ric2b', // required
        nameSku EXTID 'LetterTraceabilityLeftovers_v1_t001_ric3', // required
        sidOrigin2Country EXTID 'LetterTraceabilityLeftovers_v1_t001_ric3a', // required
        uomSku EXTID 'LetterTraceabilityLeftovers_v1_t001_ric4', // required
        quantityUomSku0 EXTID 'LetterTraceabilityLeftovers_v1_t001_ric5', // required
        quantityUomSku1 EXTID 'LetterTraceabilityLeftovers_v1_t001_ric5', // required
        quantityUomSku2 EXTID 'LetterTraceabilityLeftovers_v1_t001_ric5', // required
        quantityUomSku3 EXTID 'LetterTraceabilityLeftovers_v1_t001_ric5', // required
        trackingUOMCustomsGroup EXTID 'LetterTraceabilityLeftovers_v1_t001_ric6', // required
        pricePageInventoryDetail EXTID 'LetterTraceabilityLeftovers_v1_t001_ric7', // required
        sumPageInventoryDetail EXTID 'LetterTraceabilityLeftovers_v1_t001_ric8', // required
        quantityPageInventoryDetail0 EXTID 'LetterTraceabilityLeftovers_v1_t001_ric9', // required
        quantityPageInventoryDetail1 EXTID 'LetterTraceabilityLeftovers_v1_t001_ric9', // required
        quantityPageInventoryDetail2 EXTID 'LetterTraceabilityLeftovers_v1_t001_ric9', // required
        quantityPageInventoryDetail3 EXTID 'LetterTraceabilityLeftovers_v1_t001_ric9' // required
    FILTERS stocktake(d) == s

    OBJECTS l = LotStocktakeDetail EXTID 'LetterTraceabilityLeftovers_v1_t001_ric10'
    PROPERTIES(l) 
        encodeBase64Code EXTID 'LetterTraceabilityLeftovers_v1_t001_ric10a'
    FILTERS stocktakeDetail(l) == d;

countLegalEntity(Inventory i) = GROUP SUM 1 IF LegalEntity le AND include(Stock s, i) AND le == legalEntity(s);
notTracking(Inventory i) = GROUP SUM 1 IF NOT isTrackingOver(sku(PageInventoryDetail d)) AND inventory(d)==i;

exportXMLFile = DATA LOCAL FILE ();

fillTrackingBatch ABSTRACT LIST (Inventory);

fillTrackingBatch(Inventory i) + {
    FOR lastStocktake(i) == Stocktake t AND Stock st == (GROUP MIN  Stock s IF include(s,i)) DO {
        LOCAL correct = INTEGER (Batch);
        correct(Batch bt) <- GROUP SUM 1 IF StocktakeDetail std AND batch(std)==bt AND inventory(stocktake(std))==i ;
        isTracking(Batch bb, st) <- NULL WHERE correct(bb);
        deliveryTypePrev (Batch bb, st) <- NULL WHERE correct(bb);
        deliveryNotePrev (Batch bb, st) <- NULL WHERE correct(bb);
        deliveryNoteDate (Batch bb, st) <- NULL WHERE correct(bb);
        deliveryNotePrevLineID (Batch bb, st) <- NULL WHERE correct(bb);
        lineItemQuantitySPT (Batch bb, st) <- NULL WHERE correct(bb);
        itemCustomCode (Batch bb, st) <- NULL WHERE correct(bb);
        itemCustomCodeOther (Batch bb, st) <- NULL WHERE correct(bb);

        FOR batch(StocktakeDetail td) == Batch b AND stocktake(td)==t AND quantityUomSku(td)>0 DO{
            isTracking (b, st) <- TRUE;
            deliveryTypePrev (b, st) <- 3 ;
            deliveryNotePrev (b, st) <- seriesNumber(stocktake(td));
            deliveryNoteDate (b, st) <- DATE(dateTimeInventory(stocktake(td)));
            deliveryNotePrevLineID (b, st) <- index(td);
            lineItemQuantitySPT (b, st) <- trackingUOMCustomsGroup(td);
            itemCustomCode (b, st) <- codeSku(td);
            itemCustomCodeOther (b, st) <- extraCodeSku(td);
        }
    }
}

createdStocktake = DATA LOCAL Stocktake();

countDetail 'Кол-во позиций' (Stocktake d) = GROUP SUM 1 IF stocktake(StocktakeDetail dd) == d;

allXMLRequiredFields 'Все обязательные поля' (StocktakeDetail dd) =
    index(dd) AND codeSku(dd) AND extraCodeSku(dd) AND gtin(dd) AND nameSku(dd) AND sidOrigin2Country(dd) AND uomSku(dd) AND 
    quantityUomSku(dd) AND trackingUOMCustomsGroup(dd) AND pricePageInventoryDetail(dd) AND sumPageInventoryDetail(dd) AND quantityPageInventoryDetail(dd);

missedDetailFields (Stocktake d) = GROUP MIN TRUE IF stocktake(StocktakeDetail dd) == d AND NOT allXMLRequiredFields(dd);

allXMLRequiredFields 'Все обязательные поля' (Stocktake d) =
    d IS Stocktake AND version() AND type() AND kodIMNS() AND UNP() AND year() AND

    district(d) AND district2(d) AND dateTimeInventory(d) AND seriesNumber(d) AND dateTimeSignXML(d) AND nameLegalEntity(d) AND
    nameChief(d) AND dateTimeSignXML(d) AND

    countDetail(d) AND NOT missedDetailFields(d);

missedFieldsDescription (StocktakeDetail dd) =
    ((CONCAT ' ', ' - для позиции документа с', 'номером строки ' + index(dd), 'товар ' + nameSku(dd), 'ш/к ' + idBarcode(sku(batch(dd))), 'отсутствует следующая инофрмация: \n') +
        CONCAT ', ',
            'номер строки'  IF NOT index(dd),
            'код ТН ВЭД' IF NOT codeSku(dd),
            'дополнительный четырехзначный код' IF NOT extraCodeSku(dd),
            'GTIN' IF NOT gtin(dd),
            'наименование товара' IF NOT nameSku(dd),
            'код страны товара' IF NOT sidOrigin2Country(dd),
            'единица измерения' IF NOT uomSku(dd),
            'количество товара' IF NOT quantityUomSku(dd),
            'единица измерения (электронных накладных)' IF NOT trackingUOMCustomsGroup(dd),
            'учетная цена (ед. изм. в эл. накладных)' IF NOT pricePageInventoryDetail(dd),
            'количество товара (ед. изм. в эл. накладных)' IF NOT quantityPageInventoryDetail(dd),
            'учетная стоимость товаров' IF NOT sumPageInventoryDetail(dd)) IF dd IS StocktakeDetail AND NOT allXMLRequiredFields(dd);

missedFieldsDescription (Stocktake d) =
    ((CONCAT ' ', 'Для документа ' + seriesNumber(inventory(d)), 'от ' + date(inventory(d)), 'отсутствует следующая информация: \n') +
        CONCAT '\n ',
            '- версия' IF NOT version(),
            '- тип' IF NOT type(),
            '- код МНС' IF NOT kodIMNS(),
            '- УНП' IF NOT UNP(),
            //        '- признак уточнения' IF NOT rectification(),
            '- год' IF NOT year(),
            '- наименовние района, города, района в городе' IF NOT district(d),
            '- наименовние района' IF NOT district2(d),
            '- дата акта инвентаризации' IF NOT dateTimeInventory(d),
            '- серия/номер' IF NOT seriesNumber(d),
            '- наименование лица, передавшего сведения' IF NOT nameLegalEntity(d),
            '- руководитель организации' IF NOT nameChief(d),
            '- дата представляемых сведений' IF NOT dateTimeSignXML(d),

            GROUP CONCAT missedFieldsDescription(StocktakeDetail dd) IF stocktake( dd) == d AND NOT allXMLRequiredFields(dd), '\n' ORDER dd

        ) IF d IS Stocktake AND NOT allXMLRequiredFields(d);
;

batch(Lot l, Inventory inv) = OVERRIDE (batch(l) IF inv IS Inventory), (GROUP MIN batch(PageInventoryDetail d) IF quantity(d, l) AND inventory(d) == inv);

createStocktake (Inventory inv) {
    IF countLegalEntity(inv) > 1 THEN {
        MESSAGE 'Инвентаризация включает склады нескольких организаций';        
        RETURN;
    }

    LOCAL legalEntity = LegalEntity();
    legalEntity() <- GROUP MIN legalEntity(Stock s) IF include(s, inv);
    
    createdStocktake() <- NULL;
    
    NEW s = Stocktake {
        createdStocktake() <- s;
        inventory(s) <- inv;
        dateTime(s) <- currentDateTime();
        legalEntity(s) <- GROUP MIN legalEntity(Stock stock) IF include(stock, inv);
        prevStocktake(s) <- PREV(lastStocktake(inv));
        refRecordId (s) <- recordId(prevStocktake(s));

        district(s) <- district(legalEntity(s));
        district2(s) <- district(legalEntity(s));
        dateTimeInventory(s) <- toChar(dateTime(inv), 'YYYY-MM-DD+03:00');
        
        nameLegalEntity(s) <- fullName(legalEntity(s));
        nameChief(s) <- nameChief(legalEntity(s));
        dateTimeSign(s) <- OVERRIDE dateTimeSign(prevStocktake(s)), currentDateTime();
        dateTimeSignXML(s) <- toChar(dateTimeSign(s), 'yyyy-MM-dd+03:00');
        prevStocktake(s) <- PREV (lastStocktake(inv));
        correctionNumber(s) <- correctionNumber(prevStocktake(s)) (+) 1 IF prevStocktake(s);
        seriesNumber(s) <- seriesNumber(inventory(s));
        prevDocNumber(s) <- OVERRIDE seriesNumber(prevStocktake(s)), seriesNumber(inv) IF s IS Stocktake;

        // переносим корректируемые документы
        FOR stocktake(StocktakeDetail d) == prevStocktake(s) DO NEW nd = StocktakeDetail {
            stocktake(nd) <- s;
            batch(nd) <- batch(d);
            index(nd) <- index(d);
            codeSku(nd) <- codeSku(d);
            extraCodeSku(nd) <- extraCodeSku(d);
            gtin(nd) <- gtin(d);
            
            nameSku(nd) <-name(sku(batch(nd)));
            sidOrigin2Country(nd) <- sidOrigin2Country(sku(batch(nd)));
            uomSku(nd) <- trackingUOMCustomsGroup(sku(batch(nd)));
            trackingUOMCustomsGroup(nd) <- trackingUOMCustomsGroup(d);
            quantityUomSku(nd) <- OVERRIDE ((GROUP SUM quantityPageInventoryDetail(CollationSheet cs, batch(nd)) 
                IF inventory(cs) == inv) IF codeSku(nd) == codeCustomsGroupDefaultCountry(sku(batch(nd))) AND extraCodeSku(nd) == id(extraCode(sku(batch(nd))))), 0.0;
            pricePageInventoryDetail(nd) <- OVERRIDE (GROUP MIN pricePageInventoryDetail(CollationSheet cs, batch(nd)) / trackingUOMRatio(sku(batch(nd))) IF inventory(cs) == inv), 0.0;
            sumPageInventoryDetail(nd) <- OVERRIDE (GROUP SUM sumPageInventoryDetail(CollationSheet cs, batch(nd)) IF inventory(cs) == inv), 0.0;
            quantityPageInventoryDetail(nd) <- quantityUomSku(nd) * trackingUOMRatio(sku(batch(nd)));

            FOR batch(Lot l, inv) = batch(nd)  AND (GROUP MIN inventory(CollationSheet cs) == inv IF include(cs, l) AND quantityPageInventoryDetail(cs, l)) DO NEW ld = LotStocktakeDetail{
                stocktakeDetail(ld) <- nd;
                lot(ld) <- l;
                encodeBase64Code(ld) <- replace(replace((DocumentMNS.encodeText(code(l), 'base64')), '\n', ''), '\r', '');
            }
        }

        LOCAL needNewDetail = BOOLEAN (Batch);
        needNewDetail(Batch b) <- isTrackingOver(sku(b)) AND (GROUP MIN inventory(CollationSheet cs) == inv IF include(cs, b)) AND
            (GROUP SUM quantityPageInventoryDetail(CollationSheet cs, b) IF inventory(cs) == inv) AND
            (NOT GROUP MIN StocktakeDetail d IF stocktake(d) == s AND batch(d) == b AND codeSku(d) == codeCustomsGroupDefaultCountry(sku(batch(d))) AND extraCodeSku(d) == id(extraCode(sku(batch(d)))));
        
        // создаем новые
        FOR needNewDetail(Batch b) DO NEW nd = StocktakeDetail {
            stocktake(nd) <- s;
            batch(nd) <- b;
            codeSku(nd) <- codeCustomsGroupDefaultCountry(sku(b)); 
            extraCodeSku(nd) <- id(extraCode(sku(b)));
            gtin(nd) <- gtin(sku(b));
            nameSku(nd) <- name(sku(b));
            sidOrigin2Country(nd) <- sidOrigin2Country(sku(b));
            uomSku(nd) <- trackingUOMCustomsGroup(sku(b));
            quantityUomSku(nd) <- OVERRIDE (GROUP SUM quantityPageInventoryDetail(CollationSheet cs, b) IF inventory(cs) == inv), 0.0;
            trackingUOMCustomsGroup(nd) <- trackingUOMCustomsGroup(sku(b));
            pricePageInventoryDetail(nd) <- OVERRIDE (GROUP MIN pricePageInventoryDetail(CollationSheet cs, b) / trackingUOMRatio(sku(b)) IF inventory(cs) == inv), 0.0;
            sumPageInventoryDetail(nd) <- OVERRIDE (GROUP SUM sumPageInventoryDetail(CollationSheet cs, b) IF inventory(cs) == inv), 0.0;
            quantityPageInventoryDetail(nd) <- OVERRIDE (GROUP SUM quantityPageInventoryDetail(CollationSheet cs, b) * trackingUOMRatio(sku(b)) IF inventory(cs) == inv), 0.0;
            FOR batch(Lot l, inv) = batch(nd)  AND (GROUP MIN inventory(CollationSheet cs) == inv IF include(cs, l) AND quantityPageInventoryDetail(cs, l)) DO NEW ld = LotStocktakeDetail{
                stocktakeDetail(ld) <- nd;
                lot(ld) <- l;
                encodeBase64Code(ld) <- replace(replace(DocumentMNS.encodeText(code(l), 'base64'), '\n', ''), '\r', '');
            }
        }

        LOCAL locIndex = INTEGER (StocktakeDetail);
        locIndex (StocktakeDetail d) <- PARTITION SUM 1 ORDER d BY stocktake(d) WHERE s == stocktake(d);        
        index(StocktakeDetail d) <- locIndex(d) WHERE s == stocktake(d);
    }
}

exportToXML(Stocktake s) {
    version() <- 1 (+) correctionNumber(s);
    type() <- 'LETTERTRACEABILITYLEFTOVERS';
    rectification() <- NULL;
    kodIMNS() <- codeMNS(legalEntity(s));
    UNP() <- UNP(legalEntity(s));
    year() <- extractYear(DATE(createdTime(inventory(s))));
    
    EXPORT originalDocument OBJECTS  s = s XML TO exportXMLFile;
}

// Основной JSON
VATRegistrationNumber = DATA LOCAL STRING();
originalDocument = DATA LOCAL STRING();
creationDateTime = DATA LOCAL STRING();
documentId = DATA LOCAL STRING();
originalDocumentSign = DATA LOCAL STRING();
IMNS = DATA LOCAL STRING();
documentNumber1 = DATA LOCAL STRING();
documentDate = DATA LOCAL STRING ();
documentName = DATA LOCAL STRING();
refRecordId = DATA LOCAL INTEGER ();
correctionDate = DATA LOCAL STRING();

//documentNumber(StocktakeDetail d) = seriesNumber(prevStocktake(stocktake(d)));
documentNumber(StocktakeDetail d) = seriesNumber(stocktake(d));

FORM exportJSON
    PROPERTIES() 
        VATRegistrationNumber,
        creationDateTime EXTID 'CreationDateTime', 
        documentId EXTID 'DocumentId',
        IMNS, 
        documentNumber1 EXTID 'DocumentNumber', 
        documentDate EXTID 'DocumentDate', 
        documentName EXTID 'DocumentName', 
        refRecordId EXTID 'RefRecordId', 
        correctionDate EXTID 'CorrectionDate',

        originalDocument,
        originalDocumentSign

    OBJECTS d = StocktakeDetail EXTID 'Items'
    PROPERTIES
        index = STRING (index(d)) EXTID 'lineItemNumber',
        codeSku(d) EXTID 'itemCustomCode',
        extraCodeSku(d) EXTID 'itemAdditionalCode',
        gtin(d) EXTID 'gtinCode',
        trackingUOMCustomsGroup(d) EXTID 'lineItemQuantitySPT',
        quantityPageInventoryDetail(d) EXTID 'quantityDespatchedSPT',
        documentNumber(d) EXTID 'documentNumber'
    
    FILTERS stocktake(d) == createdStocktake()
    ORDERS index
;

sendedMNS 'Отправлена в МНС' (Inventory i) = GROUP MIN TRUE BY inventory(Stocktake s);

sign 'Подписать' ABSTRACT LIST (Stocktake, STRING, BOOLEAN);

sign(Stocktake e, STRING contentSubXML, BOOLEAN interactive) + {
    IF NOT signatureApp() THEN {
        MESSAGE 'Не задано приложение для подписи' NOWAIT;
    }
}
exportJSONFile = DATA LOCAL FILE ();
generateJSON (Inventory inv) {
    createStocktake(inv);
    exportToXML(createdStocktake());

    fileToString(exportXMLFile());
    resultString() <- replace(replace(replace(replace(replace(resultString(), '<LetterTraceabilityLeftovers_v1_f001>', '<LetterTraceabilityLeftovers_v1_f001 xmlns="">'),
        '<LetterTraceabilityLeftovers_v1_f001A>', '<LetterTraceabilityLeftovers_v1_f001A xmlns="">'),
        '<LetterTraceabilityLeftovers_v1_f002>', '<LetterTraceabilityLeftovers_v1_f002 xmlns="">'),
        '<LetterTraceabilityLeftovers_v1_t001>', '<LetterTraceabilityLeftovers_v1_t001 xmlns="">'),
        '<LetterTraceabilityLeftovers_v1_t001_ric10>', '<LetterTraceabilityLeftovers_v1_t001_ric10 xmlns="">');

    resultFile() <- NULL;
    
    originalDocument() <- replace(replace(DocumentMNS.encodeText(resultString(), 'base64'), '\n', ''), '\r', '');
    
    sign(createdStocktake(), originalDocument(), TRUE);
    originalDocumentSign() <- base64Signature();
    
    IF NOT originalDocumentSign() THEN RETURN;

    documentId() <- id(createdStocktake());    
//    documentNumber1() <- seriesNumber(prevStocktake(createdStocktake()));
    documentNumber1() <- seriesNumber(createdStocktake());
    VATRegistrationNumber() <- UNP(legalEntity(createdStocktake()));
    IMNS() <- codeMNS(legalEntity(createdStocktake()));;
    documentDate() <- toChar(dateTimeSign(createdStocktake()), 'YYYYMMDD');
    documentName() <- 'Сведения об остатках';
    refRecordId() <- refRecordId(createdStocktake()); // TODO
    correctionDate() <- toChar(dateTime(createdStocktake()) , 'YYYYMMDD') IF PREV(sendedMNS(inv)); // TODO     
    creationDateTime() <- toChar(dateTime(createdStocktake()), 'YYYY-MM-DD HH:mm:ss');

    IF NOT allXMLRequiredFields(createdStocktake()) THEN {
        MESSAGE missedFieldsDescription(createdStocktake()) NOWAIT;
        RETURN;
    }
    
    EXPORT exportJSON JSON TO exportJSONFile;
}


// Ответ JSON
GROUP documentReply EXTID 'DocumentReply';
GROUP result EXTID 'Result';

recordId = DATA LOCAL NUMERIC();
statusCode = DATA LOCAL STRING();
documentReplyDateTime = DATA LOCAL DATETIME();
reply = DATA LOCAL STRING();
SPTInternalDateTime = DATA LOCAL DATETIME();
resultDescription = DATA LOCAL STRING();
resultCode = DATA LOCAL NUMERIC();

FORM importJSON
    PROPERTIES() recordId EXTID 'RecordId', statusCode EXTID 'StatusCode'
    PROPERTIES() IN documentReply documentReplyDateTime EXTID 'DocumentReplyDateTime', reply EXTID 'Reply'
    PROPERTIES() IN result SPTInternalDateTime, resultDescription EXTID 'ResultDescription', resultCode EXTID 'ResultCode';
;

stocktakeMNS 'Отправить в МНС' (Inventory inv) {

    IF notTracking(inv) THEN {
        MESSAGE 'В инвентаризации присутствует товар не подлежащий прослеживаемости \n' + 
            (GROUP CONCAT (CONCAT ' ', 'строка ' + index(PageInventoryDetail d), 'ш/к '+idBarcodeSku(d), 'товар ' + nameSku(d)) IF NOT isTrackingOver(sku(d)) AND inventory(d)==inv, '\n' ORDER d) NOWAIT;
        RETURN;
    }
    
    NEWSESSION {
        generateJSON(inv);
        
        IF NOT exportJSONFile() THEN {
            MESSAGE 'Инвентаризация не была отправлена' NOWAIT;
            RETURN;
        }
        
        fileToString(exportJSONFile());

        LOCAL response = FILE();
        TRY {
            insecureSSL() <- TRUE;
            EXTERNAL HTTP POST mnsService() + '/document/stocktake' PARAMS exportJSONFile() TO response;
            fileToString(response());
            fileToString(response());

            IMPORT importJSON JSON FROM response();
            
            IF NOT recordId() THEN { 
                MESSAGE resultDescription();
                RETURN;
            } 
            
            recordId(createdStocktake()) <- recordId();
            statusCode(createdStocktake()) <- statusCode();
            documentReplyDateTime(createdStocktake()) <- documentReplyDateTime();
            reply(createdStocktake()) <- reply();
            SPTInternalDateTime(createdStocktake()) <- SPTInternalDateTime();
            resultDescription(createdStocktake()) <- resultDescription();
            resultCode(createdStocktake()) <- resultCode();
            fillTrackingBatch(inv);
            
            APPLY;
        } CATCH {
            fileToString(response());
            printToLog(CONCAT '||', 'ExportStocktakeMNS', currentDateTime(), '\n',
                resultString(), '\n');
            MESSAGE 'Ошибка отправки: ' + messageCaughtException() NOWAIT;
        }
    }
}

showStocktakeMNS(Inventory i) = isBatch(i) AND isPosted(i) AND useLot(operation(i)) AND NOT sendedMNS(i);

correctingStocktakeMNS 'Отправить корректировку в МНС' (Inventory i) {
    stocktakeMNS(i);
}

showCorrectingStocktakeMNS(Inventory i) = isBatch(i) AND isPosted(i) AND useLot(operation(i)) AND sendedMNS(i);

EXTEND FORM inventories
//    PROPERTIES(in) PANEL generateJSON
    
    PROPERTIES(in) READONLY sendedMNS 
    PROPERTIES(in) PANEL stocktakeMNS SHOWIF showStocktakeMNS(in), correctingStocktakeMNS SHOWIF showCorrectingStocktakeMNS(in)  

    OBJECTS st = Stocktake
    PROPERTIES(st)  READONLY  id, dateTime, nameLegalEntity, district, district2, dateTimeInventory, seriesNumber,
        nameChief, dateTimeSignXML, prevDocNumber, refRecordId,
        //Данные ответа
        recordId, statusCode, documentReplyDateTime, reply, SPTInternalDateTime, resultDescription, resultCode
    FILTERS inventory(st) == in
    ORDERS dateTime(st)
    
    OBJECTS std = StocktakeDetail
    PROPERTIES(std)  READONLY index, codeSku, extraCodeSku, gtin, nameSku, numberBatch, sidOrigin2Country, uomSku,
        quantityUomSku, trackingUOMCustomsGroup, pricePageInventoryDetail, sumPageInventoryDetail, quantityPageInventoryDetail
    FILTERS  stocktake(std) == st
    
    OBJECTS lstd = LotStocktakeDetail
    PROPERTIES lot'Марка' = STRING(DocumentMNS.decodeText(encodeBase64Code(lstd), 'base64'))
    FILTERS stocktakeDetail(lstd) == std
;

DESIGN inventories {
    wor {  
        NEW MNS {
            caption = 'Отправка в МНС';            
            
            horizontal = TRUE;
            MOVE BOX(st);
            NEW stocktakeDetailMNS {
                fill = 1;

                MOVE BOX(std);
                MOVE BOX(lstd);
            }
        }
    }
}

MODULE NTClientSoftware;

REQUIRE Utils, Integration, SignMNS;

//4.4. Выработка ЭЦП через идентификационную карту (КТА) (URL): http://127.0.0.1:8084/sign.

EXTEND CLASS SignatureApp {
    ntClientSoftware 'NTClientSoftware'
}

urlNTClientSoftware'Url' = DATA STRING[255] ();

EXTEND FORM integrationData
    PROPERTIES () urlNTClientSoftware
;

DESIGN integrationData{
    pane{
        NEW NTClientSoftware {
            caption = 'NTClientSoftware';
            MOVE PROPERTY (urlNTClientSoftware());
        }
    }
}

base64Data = DATA LOCAL STRING();

FORM signing
    PROPERTIES() base64Data EXTID 'data'
;

cms = DATA LOCAL STRING();

FORM importSignedData
    PROPERTIES() cms EXTID 'cms'
;

sign (STRING base64Data) {
    TRY {
        LOCAL f = FILE ();
        base64Data() <- base64Data;
        EXPORT signing JSON TO f;

        fileToString(f());

        LOCAL response = FILE ();
        timeoutHttp() <- 120000;
        EXTERNAL HTTP CLIENT POST urlNTClientSoftware() + '/sign' PARAMS f() TO response;

        fileToString(response());
        //        MESSAGE resultString();

        IMPORT importSignedData JSON FROM response();

        base64Signature() <- cms();        
    } CATCH {
        MESSAGE CONCAT '\n', 'Документ не подписан', messageCaughtException() NOWAIT;
        logToFile('MNS', (CONCAT ':',' Ошибка при получении подписи от NTClientSoftware', messageCaughtException()));
    }
}

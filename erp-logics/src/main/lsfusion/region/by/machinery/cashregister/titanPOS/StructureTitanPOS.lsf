MODULE StructureTitanPOS;
// Здесь описаны структуры только для розницы

REQUIRE POS, ZReportLot, Utils, StructureTitanPOSCommon, POSLotBy;

// Чек продажи / возврат
// Особенности:
// detailDiscountLabel: как DiscountLabel - не используем, возможно отменят в 24г., используем как Footer 
// в КСА 3 счктчика оплаты: 1 - наличные, 2 - банковская карта, 3 - прочие
GROUP goods EXTID 'Goods';

receiptRefund 'Признак: false - продажа, true - возврат' = DATA LOCAL TBOOLEAN ();
receiptDiscountLabel 'Описание скидки на чек' = DATA LOCAL STRING();
receiptDiscountSum 'Сумма скидки на чек' = DATA LOCAL NUMERIC();
receiptTotalSum 'Общая сумма чека с учетом скидок' = DATA LOCAL NUMERIC();
receiptSendEmail 'Флаг отправки почты' = DATA LOCAL TBOOLEAN ();
receiptEmail 'Почтовый адрес' = DATA LOCAL STRING();
receiptHeader 'Текст перед чеком'= DATA LOCAL STRING();
receiptFooter 'Текст после чека'= DATA LOCAL STRING();
// ---------------------------------------------------
// см. POSElemaRB - типы платежей, строка 188
paymentSum 'Сумма' (Payment p) = IF receiptRefund() = TTRUE THEN abs(sum(p)) ELSE sum(p);

paymentDescription 'Название оплаты' (Payment p) = namePaymentType(p);
//paymentDescription 'Название оплаты' (Payment p) = namePaymentType(p) + ', ' + sidPaymentType(p); // отладка 

paymentTypeFlag 'Тип расчета' = ABSTRACT VALUE INTEGER(Payment);
paymentTypeFlag (Payment p) += IF sidPaymentType(p) = 'cash' THEN 1 ELSE IF sidPaymentType(p) = 'card' THEN 2 ELSE 3; 
// ---------------------------------------------------
detailQuantity 'Количество товара' (ReceiptDetail o) = quantity(o);
detailPrice 'Цена товара' (ReceiptDetail o) = price(o);

// печатается, как Footer, возможно не будет поддерживаться, как и DiscountLabel
detailDiscountLabel 'Описание скидки на товара'  = ABSTRACT VALUE STRING(ReceiptDetail); 

detailDiscountSum 'Сумма скидки на товар' (ReceiptDetail o) = discountSum(o) (+) bonusPaid(o);
detailTotalSum 'Сумма товара' (ReceiptDetail o) = sum(o);

detailName 'Название товара' (ReceiptDetail o) = 
    IF type(o) = 'Сертификат' THEN 'Подарочный сертификат' ELSE overNameSku(o); //CONCAT '',overNameSku(o),IF idBarcode(o) THEN ' / '+ idBarcode(o);

// Вид кода товара: 0 - без кода, 1 - EAN13 (товар), 3 - услуга, 4 - сертификат
//detailCodeType  'Вид кода товара' (ReceiptDetail o) = IF idBarcode(o) THEN 1 ELSE 1;
detailCodeType  'Вид кода товара' (ReceiptDetail o) = 
    IF type(o) = 'Сертификат' THEN 4 ELSE                       // продажа сертификат
    IF skuType(sku(o)) = SkuType.skuTypeItem THEN 1 ELSE        // товар
    IF skuType(sku(o)) = SkuType.skuTypeCharge THEN 3 ELSE 0;   // услуга, иначе 0 

//detailBarcode 'Код товара' (ReceiptDetail o) = IF titanBarcode(currentCashRegister()) THEN idBarcode(o);
//detailBarcode 'Код товара' (ReceiptDetail o) = 
//    IF NOT(vid() > 2.1) THEN (IF titanBarcode(currentCashRegister()) THEN idBarcode(o) ELSE NULL) ELSE idBarcode(o);
detailBarcode 'Код товара' (ReceiptDetail o) = 
    IF NOT(vid() > 2.1) THEN (IF titanBarcode(currentCashRegister()) THEN GTINBarcode(sku(o)) ELSE NULL) ELSE GTINBarcode(sku(o));

detailHideCodes 'Скрывать код товара в чеке, версия старше 2.1' (ReceiptDetail o) = 
    IF o THEN IF titanBarcode(currentCashRegister()) THEN TFALSE ELSE TTRUE;

detailTax 'Ставка НДС' = ABSTRACT VALUE INTEGER(ReceiptDetail); 
detailTax (ReceiptDetail o) += IF titanTax(currentCashRegister()) AND valueVAT(o) THEN
    IF valueVAT(o) = 20 THEN 1 ELSE 
    IF valueVAT(o) = 10 THEN 2 ELSE 
    IF valueVAT(o) =  0 THEN 3 ELSE NULL;

detailDepartament 'Код отдела КСА' = ABSTRACT VALUE INTEGER (ReceiptDetail);
detailMarka 'Код марки' (ReceiptDetail o) = IF vid() > 2.1 AND idLot(o) THEN encodeText((CONCAT '', idLot(o), tailLot(o)),'base64');
detailUkz 'Код УКЗ' (ReceiptDetail o) = IF vid() > 2.1 AND ukz(o) THEN encodeText((CONCAT '',ukz(o)),'base64');
   
FORM saleForm 'Продажа и возврат, кроме возврата со скидкой для версии старше 2.1'
    PROPERTIES () titanKey EXTID 'TitanKey'
    PROPERTIES() receiptRefund EXTID 'Refund',
        receiptDiscountSum EXTID 'DiscountSum',
        receiptTotalSum EXTID 'TotalSum', receiptSendEmail EXTID 'SendEmail', receiptEmail EXTID 'Email',
        receiptHeader EXTID 'Header', receiptFooter EXTID 'Footer'  // нет ограничений по длине, только для версии 2.1

    OBJECTS payments = Payment EXTID 'Payments'
    PROPERTIES(payments) paymentTypeFlag EXTID 'TypeFlag',
        paymentSum EXTID 'Sum',
        paymentDescription EXTID 'Description'
    FILTERS IF paymentSum(payments) AND abs(paymentSum(payments)) > 0 THEN TRUE ELSE NULL

    OBJECTS lines = ReceiptDetail EXTID 'Lines'
    PROPERTIES(lines) detailQuantity EXTID 'Quantity', detailPrice EXTID 'Price',
        detailTax EXTID 'Tax',
        detailDiscountSum EXTID 'DiscountSum', detailDiscountLabel EXTID 'Footer',
        detailTotalSum EXTID 'TotalSum',
        detailDepartament EXTID 'Departament',
        detailHideCodes EXTID 'HideCodes',
        detailMarka EXTID 'MarkSI',
        detailUkz EXTID 'MarkUKZ'

    PROPERTIES(lines) IN goods detailBarcode EXTID 'Barcode', detailCodeType EXTID 'CodeType', detailName EXTID 'Name'
;


detailQuantityDsc (ReceiptDetail o) = IF o THEN 1;
detailPriceDsc (ReceiptDetail o) = sum(o);
detailNameDsc (ReceiptDetail o) = 
    IF discountSum(o) (+) bonusPaid(o) THEN detailName(o) + '\n' + 
    '( возврат ' + delChar(quantity(o)) + ' ед. товара по цене ' + delChar(detailPrice(o)) + ' руб. ' +  
    'с учетом ранее предоставленной суммы скидки ' + delChar(discountSum(o) (+) bonusPaid(o)) + ' руб. )'
    ELSE detailName(o);

FORM returnForm 'Возврат товара со скидкой, версия > 2.1'
    PROPERTIES () titanKey EXTID 'TitanKey'
    PROPERTIES() receiptRefund EXTID 'Refund',
        receiptTotalSum EXTID 'TotalSum', receiptSendEmail EXTID 'SendEmail', receiptEmail EXTID 'Email',
        receiptHeader EXTID 'Header', receiptFooter EXTID 'Footer'  // нет ограничений по длине

    OBJECTS payments = Payment EXTID 'Payments'
    PROPERTIES(payments) paymentTypeFlag EXTID 'TypeFlag',
        paymentSum EXTID 'Sum',
        paymentDescription EXTID 'Description'
    FILTERS IF paymentSum(payments) AND abs(paymentSum(payments)) > 0 THEN TRUE ELSE NULL

    OBJECTS lines = ReceiptDetail EXTID 'Lines'
    PROPERTIES(lines) detailQuantityDsc EXTID 'Quantity', detailPriceDsc EXTID 'Price',
        detailTax EXTID 'Tax',
        detailTotalSum EXTID 'TotalSum',
        detailDepartament EXTID 'Departament',
        detailHideCodes EXTID 'HideCodes'

    PROPERTIES(lines) IN goods detailBarcode EXTID 'Barcode', detailCodeType EXTID 'CodeType', detailNameDsc EXTID 'Name'
;


MODULE FiscalSento;

REQUIRE POS, ZReportLot;

// версия для работы с марками, 2025
versionWithLots 'Версия с маркировкой' = ABSTRACT BOOLEAN (CashRegister) IN base;
versionWithLotsCurrentCashRegister 'Sento 2.00 Lot' = BOOLEAN (versionWithLots(currentCashRegister()));

sentoSkuType 'Тип товара' (ReceiptDetail o) =
    IF skuType(sku(o)) = SkuType.skuTypeItem THEN 1 ELSE // товар 
        IF skuType(sku(o)) =  SkuType.skuTypeCharge THEN 3 ELSE // услуга
            IF skuType(sku(o)) = SkuType.skuTypeContainer THEN 5 ELSE 1; // тара  

sentoMarka 'Код марки' = ABSTRACT STRING (ReceiptDetail);
sentoUkz 'Код УКЗ' = ABSTRACT STRING (ReceiptDetail);
sentoDepartment 'Номер отдела' = ABSTRACT INTEGER(ReceiptDetail);
sentoBarcode 'Штрихкод' (ReceiptDetail d) =
    IF skuType(sku(d)) = SkuType.skuTypeItem AND gtin(sku(d)) THEN gtin(sku(d)) ELSE idBarcode(d); 

loadDefaultCashRegisterModels() + {  EXEC loadDefaultCashRegisterModel('Фискальный регистратор Sento', 'Sento', NULL, NULL, TRUE, NULL, NULL ); };

isUnix = osVersion(currentComputer()) == 'Linux';

needDLL() = GROUP SUM 1 IF computer(CashRegister c) == currentComputer() AND sidModel(groupMachinery(c)) == 'Sento';
onDesktopClientStarted() + { 
	IF needDLL() THEN {
        IF is64Java(currentComputer()) THEN
            loadLibrary('lib/win64/sento.dll');
        ELSE
            loadLibrary('lib/win32/sento.dll');
	}
};

isSento() = sidModelCurrentCashRegister() == 'Sento';

fiscalSentoPrint 'Напечатать фискальный чек'  INTERNAL  'lsfusion.erp.region.by.machinery.cashregister.fiscalsento.FiscalSentoPrintReceiptAction' (Receipt);
postPrint(Receipt receipt) + {  IF isSento() THEN fiscalSentoPrint(receipt); }

fiscalSentoReceiptTitle 'Заголовок чека Sento' = ABSTRACT TEXT ();
fiscalSentoTop 'Дополнительный текст перед чеком Sento' = ABSTRACT TEXT (Receipt);
fiscalSentoBottom 'Дополнительный текст после чека Sento' = ABSTRACT TEXT (Receipt);

fiscalSentoPrintCopyReceipt 'Копия чека'  INTERNAL  'lsfusion.erp.region.by.machinery.cashregister.fiscalsento.FiscalSentoPrintCopyReceiptAction' ();
fiscalPrintCopyReceipt() + {  IF isSento() THEN fiscalSentoPrintCopyReceipt(); }

fiscalSentoXReport 'X-отчёт'  INTERNAL  'lsfusion.erp.region.by.machinery.cashregister.fiscalsento.FiscalSentoXReportAction' ();
fiscalXReport() + {  IF isSento() THEN fiscalSentoXReport(); }

fiscalSentoZReport 'Z-отчет'  INTERNAL  'lsfusion.erp.region.by.machinery.cashregister.fiscalsento.FiscalSentoZReportAction' ();
fiscalZReport() + {  IF isSento() THEN fiscalSentoZReport(); }

fiscalSentoReportTop 'Заголовок отчёта Sento' = ABSTRACT TEXT ();

fiscalSentoUpdateData 'Загрузить информацию'  INTERNAL  'lsfusion.erp.region.by.machinery.cashregister.fiscalsento.FiscalSentoUpdateDataAction' ();
fiscalUpdateData() + {  IF isSento() THEN fiscalSentoUpdateData(); }

fiscalSentoService 'Перемещение денег'  INTERNAL  'lsfusion.erp.region.by.machinery.cashregister.fiscalsento.FiscalSentoServiceInOutAction' (CashOperation);
fiscalService(CashOperation cashOperation) + {  IF isSento() THEN fiscalSentoService(cashOperation); }

fiscalSentoDisplayText 'Вывести текст на дисплей'  INTERNAL  'lsfusion.erp.region.by.machinery.cashregister.fiscalsento.FiscalSentoDisplayTextAction' (ReceiptDetail);
fiscalDisplayText (ReceiptDetail receiptDetail) + {  IF isSento() THEN fiscalSentoDisplayText(receiptDetail); }

fiscalSentoCancel 'Отменить чек'  INTERNAL  'lsfusion.erp.region.by.machinery.cashregister.fiscalsento.FiscalSentoCancelReceiptAction' (Receipt);
fiscalCancel(Receipt receipt) + {  IF isSento() THEN fiscalSentoCancel(receipt); }

fiscalSentoReset 'Аннулировать чек'  INTERNAL  'lsfusion.erp.region.by.machinery.cashregister.fiscalsento.FiscalSentoResetReceiptAction' (Receipt);
fiscalResetReceiptCashRegister(Receipt receipt) + {  IF isSento() THEN fiscalSentoReset(receipt); }
showifFiscalResetReceipt() += IF isSento() THEN TRUE;



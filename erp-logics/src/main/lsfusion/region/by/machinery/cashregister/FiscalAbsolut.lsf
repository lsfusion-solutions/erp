MODULE FiscalAbsolut;

REQUIRE System,
        POS;


needDLL() = GROUP SUM 1 IF computer(CashRegister c) == currentComputer() AND sidModel(groupMachinery(c)) == 'Absolut';
onDesktopClientStarted() + { 
	IF needDLL() THEN {
	    IF is64Java(currentComputer()) THEN
	        loadLibrary('lib/win64/absolut.dll');
	    ELSE
	        loadLibrary('lib/win32/absolut.dll');
	}
};

loadDefaultCashRegisterModels() + {  EXEC loadDefaultCashRegisterModel('Фискальный регистратор Absolut', 'Absolut', NULL, NULL, TRUE, NULL, NULL ); };

fiscalAbsolutPrint 'Напечатать фискальный чек'  INTERNAL  'lsfusion.erp.region.by.machinery.cashregister.fiscalabsolut.FiscalAbsolutPrintReceiptAction' (Receipt);
postPrint(Receipt receipt) + {  IF sidCashRegisterModel(receipt) == 'Absolut' THEN fiscalAbsolutPrint(receipt); }

fiscalAbsolutReceiptTitle 'Заголовок чека Absolut' = ABSTRACT TEXT ();
fiscalAbsolutTop 'Дополнительный текст перед чеком Absolut' = ABSTRACT TEXT (Receipt);
fiscalAbsolutBeforePayment 'Дополнительный текст перед оплатой чека Absolut' = ABSTRACT TEXT (Receipt);
fiscalAbsolutAfterPayment 'Дополнительный текст после чека Absolut' = ABSTRACT TEXT (Receipt);

fiscalAbsolutPrintCopyReceipt 'Копия чека'  INTERNAL  'lsfusion.erp.region.by.machinery.cashregister.fiscalabsolut.FiscalAbsolutPrintCopyReceiptAction' ();
fiscalPrintCopyReceipt() + {  IF sidModelCurrentCashRegister() == 'Absolut' THEN fiscalAbsolutPrintCopyReceipt(); }

fiscalAbsolutXReport 'X-отчёт'  INTERNAL  'lsfusion.erp.region.by.machinery.cashregister.fiscalabsolut.FiscalAbsolutXReportAction' ();
fiscalXReport() + {  IF sidModelCurrentCashRegister() == 'Absolut' THEN fiscalAbsolutXReport(); }

fiscalAbsolutZReport 'Z-отчет'  INTERNAL  'lsfusion.erp.region.by.machinery.cashregister.fiscalabsolut.FiscalAbsolutZReportAction' ();
fiscalZReport() + {  IF sidModelCurrentCashRegister() == 'Absolut' THEN fiscalAbsolutZReport(); }

fiscalAbsolutReportTop 'Заголовок отчёта Absolut' = ABSTRACT TEXT ();

fiscalAbsolutService 'Перемещение денег'  INTERNAL  'lsfusion.erp.region.by.machinery.cashregister.fiscalabsolut.FiscalAbsolutServiceInOutAction' (CashOperation);
fiscalService(CashOperation cashOperation) + {  IF sidCashRegisterModel(cashOperation) == 'Absolut' THEN fiscalAbsolutService(cashOperation); }

showDisplayText 'Показывать текст на дисплей' = DATA BOOLEAN (CashRegisterModel);
isAbsolut (CashRegisterModel m) = sid(m) == 'Absolut';
EXTEND FORM cashRegisterModel PROPERTIES(c) showDisplayText SHOWIF isAbsolut(c); 

fiscalAbsolutDisplayText 'Вывести текст на дисплей'  INTERNAL  'lsfusion.erp.region.by.machinery.cashregister.fiscalabsolut.FiscalAbsolutDisplayTextAction' (ReceiptDetail);
fiscalDisplayText (ReceiptDetail receiptDetail) + {  IF sidCashRegisterModel(receiptDetail) == 'Absolut' AND showDisplayText(cashRegisterModel(receipt(receiptDetail))) THEN fiscalAbsolutDisplayText(receiptDetail); }

fiscalAbsolutCancel 'Отменить чек'  INTERNAL  'lsfusion.erp.region.by.machinery.cashregister.fiscalabsolut.FiscalAbsolutCancelReceiptAction' (Receipt);
fiscalCancel(Receipt receipt) + {  IF sidModelCurrentCashRegister() == 'Absolut' THEN fiscalAbsolutCancel(receipt); }

printZeroReceipt 'Нулевой чек при открытии z-отчета (Absolut)' = DATA BOOLEAN ();
fiscalAbsolutOpenZReport 'Z-отчет'  INTERNAL  'lsfusion.erp.region.by.machinery.cashregister.fiscalabsolut.FiscalAbsolutOpenZReportAction' ();
fiscalOpenZReport() + {  IF sidModelCurrentCashRegister() == 'Absolut' AND printZeroReceipt() THEN fiscalAbsolutOpenZReport(); }

saveCommentOnFiscalTapeAbsolut 'Сохранять комментарий для контрольной ленты' = DATA BOOLEAN ();
groupPaymentsByVAT 'Разбивать оплату по ставкам НДС' = DATA BOOLEAN ();
fiscalAbsolutPrefixCode128 'Префикс для Code128' = DATA STRING[10] ();
sumPaymentAbsolut 'Суммовой чек' = DATA BOOLEAN ();
maxLinesAbsolut 'Макс. кол-во строк наименования' = DATA INTEGER ();
printSumWithDiscountAbsolut 'Печатать сумму со скидкой' = DATA BOOLEAN ();
EXTEND FORM options PROPERTIES() saveCommentOnFiscalTapeAbsolut, groupPaymentsByVAT, printZeroReceipt,
                                 fiscalAbsolutPrefixCode128, sumPaymentAbsolut, maxLinesAbsolut, printSumWithDiscountAbsolut;
DESIGN options {
    machinery {
        NEW absolut {
            caption = 'Absolut';
            MOVE PROPERTY(saveCommentOnFiscalTapeAbsolut());
            MOVE PROPERTY(groupPaymentsByVAT());
            MOVE PROPERTY(printZeroReceipt());
            MOVE PROPERTY(fiscalAbsolutPrefixCode128());
            MOVE PROPERTY(sumPaymentAbsolut());
            NEW notSum {
                caption = 'Не суммовой чек';
                MOVE PROPERTY(maxLinesAbsolut());
                MOVE PROPERTY(printSumWithDiscountAbsolut());
            }
        }
    }
}

overChangeBarcodeSale(STRING[30] string, Receipt receipt) + { 
    IF startsWith(string, fiscalAbsolutPrefixCode128()) AND receipt IS Receipt AND string IS STRING[30] THEN {
        FOR STRING[30](Receipt r AS Receipt) == substrFrom(string, 5) DO {
            DIALOG searchSkuReturnReceipt OBJECTS st = departmentStore(r), c = cashRegister(r), s = r INPUT, r INPUT rd DO {
                createReturn(receipt, s, rd);
            }
            consumedChangeBarcodeSaleReceipt() <- TRUE;
        }
    }
};

useSKNOAbsolut 'Работа с СКНО (Absolut)' = DATA BOOLEAN (CashRegister);
useSKNOAbsolutCurrentCashRegister () = useSKNOAbsolut(currentCashRegister());

UNPAbsolut 'УНП (Absolut)' = DATA STRING[9] (Machinery);
UNPAbsolutCurrentCashRegister() = UNPAbsolut(currentCashRegister());
regNumberAbsolut 'Регистрационный номер (Absolut)' = DATA STRING[20] (Machinery);
regNumberAbsolutCurrentCashRegister() = regNumberAbsolut(currentCashRegister());
machineryNumberAbsolut 'Номер КСА (Absolut)' = DATA STRING[20] (Machinery);
machineryNumberAbsolutCurrentCashRegister() = machineryNumberAbsolut(currentCashRegister());

isAbsolut(CashRegister c) = isAbsolut(model(groupCashRegister(c)));
EXTEND FORM cashRegister PROPERTIES(c) useSKNOAbsolut;
EXTEND FORM groupCashRegister PROPERTIES(c) SHOWIF isAbsolut(c) useSKNOAbsolut, UNPAbsolut, regNumberAbsolut, machineryNumberAbsolut;

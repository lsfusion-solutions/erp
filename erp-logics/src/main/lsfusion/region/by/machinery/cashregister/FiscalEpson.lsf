MODULE FiscalEpson;

REQUIRE POS, POSSection;

customImplementationEpson = ABSTRACT BOOLEAN();

fiscalEpsonComment 'Дополнительный текст для чека Epson' = ABSTRACT TEXT (Receipt);
fiscalEpsonComment 'Дополнительный текст для строки чека Epson' = ABSTRACT TEXT (ReceiptDetail);

loadDefaultCashRegisterModels() + {  EXEC loadDefaultCashRegisterModel('Фискальный регистратор Epson', 'Epson', NULL, NULL, TRUE, NULL, NULL ); };

documentNumber 'Сквозной номер чека' (receipt) = DATA INTEGER (Receipt);

fiscalEpsonPrint 'Напечатать фискальный чек'  INTERNAL  'lsfusion.erp.region.by.machinery.cashregister.fiscalepson.FiscalEpsonPrintReceiptAction' (Receipt);
postPrint(Receipt receipt) + {  IF sidCashRegisterModel(receipt) == 'Epson' AND NOT customImplementationEpson() THEN fiscalEpsonPrint(receipt); }

fiscalEpsonElectronicJournalReadOffset = DATA LOCAL NESTED INTEGER ();
fiscalEpsonElectronicJournalReadSize = DATA LOCAL NESTED INTEGER ();
fiscalEpsonSessionNumber = DATA LOCAL NESTED INTEGER ();

fiscalEpsonPrintCopyReceipt 'Копия чека'  INTERNAL  'lsfusion.erp.region.by.machinery.cashregister.fiscalepson.FiscalEpsonPrintCopyReceiptAction' ();
fiscalPrintCopyReceipt() + {  IF sidModelCurrentCashRegister() == 'Epson' THEN fiscalEpsonPrintCopyReceipt(); }

fiscalEpsonXReport 'X-отчёт'  INTERNAL  'lsfusion.erp.region.by.machinery.cashregister.fiscalepson.FiscalEpsonXReportAction' ();
fiscalXReport() + {  IF sidModelCurrentCashRegister() == 'Epson' THEN fiscalEpsonXReport(); }

fiscalEpsonZReport 'Z-отчет'  INTERNAL  'lsfusion.erp.region.by.machinery.cashregister.fiscalepson.FiscalEpsonZReportAction' ();
fiscalZReport() + {  IF sidModelCurrentCashRegister() == 'Epson' THEN fiscalEpsonZReport(); }

fiscalEpsonElectronicJournal 'Печать контрольной ленты'  INTERNAL  'lsfusion.erp.region.by.machinery.cashregister.fiscalepson.FiscalEpsonElectronicJournalAction' ();
fiscalElectronicJournal() + {  IF sidModelCurrentCashRegister() == 'Epson' THEN fiscalEpsonElectronicJournal(); }
showifElectronicJournal() += IF sidModelCurrentCashRegister() == 'Epson' THEN TRUE;

fiscalEpsonElectronicJournalToFile 'Печать контрольной ленты в файл'  INTERNAL  'lsfusion.erp.region.by.machinery.cashregister.fiscalepson.FiscalEpsonElectronicJournalToFileAction' ();
fiscalElectronicJournalToFile() + {  IF sidModelCurrentCashRegister() == 'Epson' THEN fiscalEpsonElectronicJournalToFile(); }

fiscalEpsonService 'Перемещение денег'  INTERNAL  'lsfusion.erp.region.by.machinery.cashregister.fiscalepson.FiscalEpsonServiceInOutAction' (CashOperation);
fiscalService(CashOperation cashOperation) + {  IF sidCashRegisterModel(cashOperation) == 'Epson' THEN fiscalEpsonService(cashOperation); }

fiscalEpsonCancel 'Отменить чек'  INTERNAL  'lsfusion.erp.region.by.machinery.cashregister.fiscalepson.FiscalEpsonCancelReceiptAction' (Receipt);
fiscalCancel(Receipt receipt) + {  IF sidModelCurrentCashRegister() == 'Epson' THEN fiscalEpsonCancel(receipt); }

fiscalEpsonReset 'Аннулировать чек'  INTERNAL  'lsfusion.erp.region.by.machinery.cashregister.fiscalepson.FiscalEpsonResetReceiptAction' (Receipt);
fiscalResetReceiptCashRegister(Receipt receipt) + {  IF sidModelCurrentCashRegister() == 'Epson' THEN fiscalEpsonReset(receipt); }
showifFiscalResetReceipt() += IF sidModelCurrentCashRegister() == 'Epson' THEN TRUE;

blockDesync 'Не открывать Z-отчет при рассинхронизации времени (Epson)' = DATA BOOLEAN ();
maxDesync 'Максимальное время рассинхронизации (c) (Epson)' = DATA LONG ();

fiscalEpsonElectronicJournalReadOffset = DATA INTEGER (ZReport);
fiscalEpsonElectronicJournalReadOffsetCurrentZReport() = fiscalEpsonElectronicJournalReadOffset(currentZReport());

fiscalEpsonCheckOpenZReport 'Проверка перед открытием Z-отчета'  INTERNAL  'lsfusion.erp.region.by.machinery.cashregister.fiscalepson.FiscalEpsonCheckOpenZReportAction' (ZReport);
fiscalCheckOpenZReport(ZReport zReport) + {  IF sidModelCurrentCashRegister() == 'Epson' THEN fiscalEpsonCheckOpenZReport(zReport); }

fiscalEpsonCheckSKNO 'Проверить связь СКНО'  INTERNAL  'lsfusion.erp.region.by.machinery.cashregister.fiscalepson.FiscalEpsonCheckSKNOAction' ();

EXTEND FORM options PROPERTIES() blockDesync, maxDesync;
DESIGN options {
    machinery {
        NEW epson {
            caption = 'Epson';
            MOVE PROPERTY(blockDesync());
            MOVE PROPERTY(maxDesync());
        }
    }
}

EXTEND FORM operationCashRegister
    PROPERTIES() fiscalEpsonCheckSKNO 
;

DESIGN operationCashRegister{
    secondRowActions {
        MOVE PROPERTY(fiscalEpsonCheckSKNO()) BEFORE PROPERTY(numberCurrentZReport()) { font = '24';} 
    }
}

// --- Типы платежей

cardType 'Код платежа картой' = DATA INTEGER (CashRegister) IN base;
giftCardType 'Код платежа подарочным сертификатом' = DATA INTEGER (CashRegister) IN base;

modificationType 'Тип передачи подарочных сертификатов на СКНО (эксперимент, вводить 1 или 2)' = DATA INTEGER (CashRegister);

EXTEND FORM groupCashRegister
    PROPERTIES(c) cardType, giftCardType, modificationType
;

cardTypeCurrentCashRegister 'Код платежа картой' = cardType(currentCashRegister());
giftCardTypeCurrentCashRegister 'Код платежа подарочным сертификатом' = giftCardType(currentCashRegister());

modificationTypeCurrentCashRegister = modificationType(currentCashRegister());

quantityCancelReceipt 'Кол-во аннулированых чеков' (zReport) = GROUP SUM 1 IF skip(Receipt detail) BY zReport(detail);

EXTEND FORM zReports
    PROPERTIES(z) READONLY quantityCancelReceipt  BEFORE quantityReceiptDetail(z)
    PROPERTIES(b) READONLY cancel 'Аннулированый чек'= skip BEFORE date(b)
;

EXTEND FORM zReport
    PROPERTIES(b) READONLY cancel 'Аннулированый чек'= skip BEFORE date(b)
;
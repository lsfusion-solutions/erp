MODULE FiscalEpson;

REQUIRE POS, POSSection;

// версия для работы с марками, 2024
version116 'Новая версия' = ABSTRACT BOOLEAN (CashRegister) IN base;
version116CurrentCashRegister 'Epson TFP 116' = BOOLEAN (version116(currentCashRegister()));

epsonCurrencyCode 'Код валюты' = DATA STRING[3] (CashRegister) IN base;
epsonCurrencyCodeCashRegister 'Код валюты' = (epsonCurrencyCode(currentCashRegister()));

epsonSkuType 'Тип товара' (ReceiptDetail o) = 
    IF skuType(sku(o)) = SkuType.skuTypeItem THEN 1 ELSE // товар 
        IF skuType(sku(o)) =  SkuType.skuTypeCharge THEN 3 ELSE // услуга
            IF skuType(sku(o)) = SkuType.skuTypeContainer THEN 5 ELSE 1; // тара  

epsonIdLot 'Марка' = ABSTRACT STRING (ReceiptDetail);
epsonTailLot 'Хвост марки' = ABSTRACT STRING (ReceiptDetail);

customImplementationEpson = ABSTRACT BOOLEAN();

fiscalEpsonComment 'Дополнительный текст для чека Epson' = ABSTRACT TEXT (Receipt);
fiscalEpsonComment 'Дополнительный текст для строки чека Epson' = ABSTRACT TEXT (ReceiptDetail);

isEpson() = sidModelCurrentCashRegister() == 'Epson';
isEpson(Receipt receipt) = sidCashRegisterModel(receipt) == 'Epson';

loadDefaultCashRegisterModels() + {  EXEC loadDefaultCashRegisterModel('Фискальный регистратор Epson', 'Epson', NULL, NULL, TRUE, NULL, NULL ); };

documentNumber 'Сквозной номер чека' (receipt) = DATA INTEGER (Receipt);

fiscalEpsonPrint 'Напечатать фискальный чек'  INTERNAL  'lsfusion.erp.region.by.machinery.cashregister.fiscalepson.FiscalEpsonPrintReceiptAction' (Receipt);
postPrint(Receipt receipt) + {  IF isEpson(receipt) AND NOT customImplementationEpson() THEN fiscalEpsonPrint(receipt); }

fiscalEpsonElectronicJournalReadOffset = DATA LOCAL NESTED INTEGER ();
fiscalEpsonElectronicJournalReadSize = DATA LOCAL NESTED INTEGER ();
fiscalEpsonSessionNumber = DATA LOCAL NESTED INTEGER ();

fiscalEpsonPrintCopyReceipt 'Копия чека'  INTERNAL  'lsfusion.erp.region.by.machinery.cashregister.fiscalepson.FiscalEpsonPrintCopyReceiptAction' ();
fiscalPrintCopyReceipt() + {  IF isEpson() THEN fiscalEpsonPrintCopyReceipt(); }

fiscalEpsonXReport 'X-отчёт'  INTERNAL  'lsfusion.erp.region.by.machinery.cashregister.fiscalepson.FiscalEpsonXReportAction' ();
fiscalXReport() + {  IF isEpson() THEN fiscalEpsonXReport(); }

fiscalEpsonZReport 'Z-отчет'  INTERNAL  'lsfusion.erp.region.by.machinery.cashregister.fiscalepson.FiscalEpsonZReportAction' ();
fiscalZReport() + {  IF isEpson() THEN fiscalEpsonZReport(); }

fiscalEpsonElectronicJournal 'Печать контрольной ленты'  INTERNAL  'lsfusion.erp.region.by.machinery.cashregister.fiscalepson.FiscalEpsonElectronicJournalAction' ();
fiscalElectronicJournal() + {  IF isEpson() THEN fiscalEpsonElectronicJournal(); }
showifElectronicJournal() += IF isEpson() THEN TRUE;

fiscalEpsonElectronicJournalToFile 'Печать контрольной ленты в файл'  INTERNAL  'lsfusion.erp.region.by.machinery.cashregister.fiscalepson.FiscalEpsonElectronicJournalToFileAction' ();
fiscalElectronicJournalToFile() + {  IF isEpson() THEN fiscalEpsonElectronicJournalToFile(); }

fiscalEpsonService 'Перемещение денег'  INTERNAL  'lsfusion.erp.region.by.machinery.cashregister.fiscalepson.FiscalEpsonServiceInOutAction' (CashOperation);
fiscalService(CashOperation cashOperation) + {  IF isEpson() THEN fiscalEpsonService(cashOperation); }

fiscalEpsonCashSum 'Сумма наличных' INTERNAL 'lsfusion.erp.region.by.machinery.cashregister.fiscalepson.FiscalEpsonCashSumAction' ();
fiscalCashSum() + { IF isEpson() THEN fiscalEpsonCashSum(); }

// отрезать чек
fiscalCutReceipt() + { IF isEpson() THEN MESSAGE 'Операция не поддерживается'; }

// прогон ленты
fiscalAdvancePaper() + { IF isEpson() THEN MESSAGE 'Операция не поддерживается'; }

fiscalEpsonCancel 'Отменить чек'  INTERNAL  'lsfusion.erp.region.by.machinery.cashregister.fiscalepson.FiscalEpsonCancelReceiptAction' (Receipt);
fiscalCancel(Receipt receipt) + {  IF isEpson() THEN fiscalEpsonCancel(receipt); }

fiscalEpsonReset 'Аннулировать чек'  INTERNAL  'lsfusion.erp.region.by.machinery.cashregister.fiscalepson.FiscalEpsonResetReceiptAction' (Receipt);
fiscalResetReceiptCashRegister(Receipt receipt) + {  IF isEpson() THEN fiscalEpsonReset(receipt); }
showifFiscalResetReceipt() += IF isEpson() THEN TRUE;

blockDesync 'Не открывать Z-отчет при рассинхронизации времени (Epson)' = DATA BOOLEAN ();
maxDesync 'Максимальное время рассинхронизации (c) (Epson)' = DATA LONG ();
resetTypeOfGoods 'Сбрасывать typeOfGoods и BarcodeOfGoogs (Epson)' = DATA BOOLEAN();

fiscalEpsonElectronicJournalReadOffset = DATA INTEGER (ZReport);
fiscalEpsonElectronicJournalReadOffsetCurrentZReport() = fiscalEpsonElectronicJournalReadOffset(currentZReport());

fiscalEpsonCheckOpenZReport 'Проверка перед открытием Z-отчета'  INTERNAL  'lsfusion.erp.region.by.machinery.cashregister.fiscalepson.FiscalEpsonCheckOpenZReportAction' (ZReport);
fiscalCheckOpenZReport(ZReport zReport) + {  IF isEpson() THEN fiscalEpsonCheckOpenZReport(zReport); }

fiscalEpsonCheckSKNO 'Проверить связь СКНО'  INTERNAL  'lsfusion.erp.region.by.machinery.cashregister.fiscalepson.FiscalEpsonCheckSKNOAction' ();

EXTEND FORM options PROPERTIES() blockDesync, maxDesync, resetTypeOfGoods;
DESIGN options {
    machinery {
        NEW epson {
            caption = 'Epson';
            MOVE PROPERTY(blockDesync());
            MOVE PROPERTY(maxDesync());
            MOVE PROPERTY(resetTypeOfGoods());
        }
    }
}

EXTEND FORM operationCashRegister
    PROPERTIES() fiscalEpsonCheckSKNO 
;

DESIGN operationCashRegister{
    secondRowActions {
        MOVE PROPERTY(fiscalEpsonCheckSKNO()) BEFORE PROPERTY(numberCurrentZReport()) { font = '24';} 
    }
}

// --- Типы платежей

cardType 'Код платежа картой' = DATA INTEGER (CashRegister) IN base;
giftCardType 'Код платежа подарочным сертификатом' = DATA INTEGER (CashRegister) IN base;

sendSKNO 'Отправлять в СКНО (Epson)' = DATA BOOLEAN (CashRegister);

EXTEND FORM groupCashRegister
    PROPERTIES(c) cardType, giftCardType, sendSKNO
;

cardTypeCurrentCashRegister 'Код платежа картой' = cardType(currentCashRegister());
giftCardTypeCurrentCashRegister 'Код платежа подарочным сертификатом' = giftCardType(currentCashRegister());

sendSKNOCurrentCashRegister = sendSKNO(currentCashRegister());

quantityCancelReceipt 'Кол-во аннулированых чеков' (zReport) = GROUP SUM 1 IF skip(Receipt detail) BY zReport(detail);

EXTEND FORM zReports
    PROPERTIES(z) READONLY quantityCancelReceipt  BEFORE quantityReceiptDetail(z)
    PROPERTIES(b) READONLY cancel 'Аннулированый чек'= skip BEFORE date(b)
;

EXTEND FORM zReport
    PROPERTIES(b) READONLY cancel 'Аннулированый чек'= skip BEFORE date(b)
;

isCommission = ABSTRACT BOOLEAN (ReceiptDetail);

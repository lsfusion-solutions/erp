MODULE ImportMila;

REQUIRE System, Barcode, PriceListOperation, Competitor;

PRIORITY Competitor;

importMilaFile = DATA LOCAL FILE ();
importMila 'Импортировать'  INTERNAL  'lsfusion.erp.region.by.mila.ImportMilaAction' ();

importMilaFull 'Импорт всей информации по товарам (mila.by)' () { importMila(); open(importMilaFile()); }

FORM importMilaEuroopt 'Импорт Mila'
    PROPERTIES() importMilaFull
;

EXTEND FORM integrationData
    PROPERTIES() importMilaFull   
;
DESIGN integrationData {
    pane {
        NEW mila {
            caption = 'Мила';
            MOVE PROPERTY(importMilaFull());          
        }
    }
}

gname = DATA LOCAL STRING[100] (INTEGER);

group = DATA LOCAL INTEGER (INTEGER);
tcode = DATA LOCAL STRING[25] (INTEGER);
barcode = DATA LOCAL STRING[25](INTEGER);

FORM importMila
    OBJECTS groups = INTEGER
    PROPERTIES(groups) gname=gname
    
    OBJECTS goods = INTEGER
    PROPERTIES(goods) tcode, barcode
    FILTERS group(goods) = groups
;

GROUP offer;
GROUP PRICES : offer;
GROUP PRICE : PRICES;
GROUP PRICE_CROSSED_OUT : PRICES;

PRICE1 = DATA LOCAL NUMERIC();
PRICE2 = DATA LOCAL NUMERIC();

FORM milaOffersApi FORMEXTID 'null'
PROPERTIES() IN PRICE PRICE1 EXTID 'PRICE'
PROPERTIES() IN PRICE_CROSSED_OUT PRICE2 EXTID 'PRICE'
;

importPriceList () {
    importMila();
    
    IMPORT importMila JSON FROM importMilaFile();
    
    NEW p = Competitor.PriceList {
        
        IF NOT competitor('Мила') THEN NEW c = Competitor { name(c) <- 'Мила'; }
        
        in(p, Competitor c) <- name(c) == 'Мила';
        date(p) <- currentDate();
        note(p) <- 'Цены Мила';
         
        FOR Sku s = skuBarcode(trim(barcode(INTEGER i))) AND Competitor c == competitor('Мила') DO {
            LOCAL result = JSONFILE ();
            EXTERNAL HTTP POST 'https://api.mila.by/get-all-offer/' BODYURL 'product_id=$1' PARAMS tcode(i) TO result;
            
            IMPORT milaOffersApi JSON FROM result();
        
            NEW d = Competitor.PriceListDetail {
                
                priceList(d) <- p;
                sku(d) <- s;
                price(d, c) <- OVERRIDE (PRICE2() IF PRICE2() != 0), PRICE1();
                promotionPrice(d, c) <- PRICE1() IF PRICE2() != 0;
            }
        }
        APPLY;
    }
}

MODULE ImportTNVEDTracking;

REQUIRE ImportTNVED, ItemTracking;

importExtraCode 'Импортировать 4х значный код (файл TNVED_AD.DBF)' () {
    INPUT f = FILE DO NEWSESSION {
        LOCAL tnved = STRING (INTEGER);
        LOCAL id = STRING[4] (INTEGER);
        LOCAL lvl1 = STRING (INTEGER);
        LOCAL lvl2 = STRING (INTEGER);
        LOCAL lvl3 = STRING (INTEGER);
        LOCAL lvl4 = STRING (INTEGER);
        LOCAL isTracking = STRING (INTEGER);

        IMPORT DBF CHARSET 'CP866' FROM f TO tnved = CODETNVED, id = CODEADD, lvl1 = N1, lvl2 = N2, lvl3 = N3, lvl4 = N4, isTracking = PSM537;

        FOR imported(INTEGER i) AND i > 1 AND customsGroup(tnved(i)) AND ExtraCode ec == extraCode(customsGroup(tnved(i)), id(i)) DO {
            isTracking(ec) <- IF isTracking(i) == '1' THEN TRUE ELSE NULL;
        }

        FOR imported(INTEGER i) AND i > 1 AND customsGroup(tnved(i)) AND NOT extraCode(customsGroup(tnved(i)), id(i)) DO NEW ec = ExtraCode {
            customsGroup(ec) <- customsGroup(tnved(i));
            id(ec) <- id(i);
            description(ec) <- STRING[150](CONCAT '/', lvl1(i), lvl2(i), lvl3(i), lvl4(i));
            isTracking(ec) <- TRUE IF isTracking(i) == '1';
        }

        FOR code(CustomsGroup c) AND NOT countExtraCodes(c) DO NEW ec = ExtraCode {
            customsGroup(ec) <- c;
            id(ec) <- '0000';
        }

        APPLY;
    }
} IN tnved;


EXTEND FORM defaultData
    PROPERTIES()  importExtraCode
;
DESIGN defaultData {
    customs {
        MOVE PROPERTY(importExtraCode());
    }
}

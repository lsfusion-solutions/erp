MODULE ImportTNVED;

REQUIRE DefaultData, Country, CustomsGroup;


//---------------------------------------------- Импорт ТНВЭД -----------------------------------------------------//

GROUP tnved 'ТНВЭД' : public;

importTNVEDClassifierAction 'Импортировать справочник (файл TNVED4.DBF)'  INTERNAL  'lsfusion.erp.region.by.certificate.declaration.ImportTNVEDClassifierAction' () IN tnved;
importTNVEDCustomsRatesAction 'Импортировать платежи (файл TNVED_ST.DBF)'  INTERNAL  'lsfusion.erp.region.by.certificate.declaration.ImportTNVEDCustomsRatesAction' () IN tnved;
importTNVEDCustomsExceptionsAction 'Импортировать льготы (файл TNVED_PL.DBF)'  INTERNAL  'lsfusion.erp.region.by.certificate.declaration.ImportTNVEDCustomsExceptionsAction' () IN tnved;

importTNVEDClassifierNew 'Импортировать справочник (файл TNVED4.DBF) (new)' () {
    INPUT f = FILE DO NEWSESSION {

        LOCAL KOD, NAIM, KR_NAIM, idParent = STRING(INTEGER );
        IMPORT DBF CHARSET 'CP866' FROM f TO KOD, NAIM, KR_NAIM;

        KOD(INTEGER r) <- '-' + r WHERE KOD(r) == '··········';

        LOCAL level = INTEGER (INTEGER);
        level(INTEGER r) <- (length(NAIM(r)) - length(ltrim(NAIM(r), '- ')))/2 (+) 1 WHERE imported(r);
        LOCAL idLevel = STRING (INTEGER);
        FOR imported(INTEGER r) ORDER r NOINLINE DO {
            IF level(r) == 1 THEN {
                idLevel(INTEGER l) <- NULL;
            }
            idLevel(level(r)) <- KOD(r);
            idParent(r) <- idLevel(level(r) - 1);
        }

        DELETE CustomsGroup g WHERE left(code(g),1) == '-' AND NOT (GROUP SUM 1 IF code(g) == KOD(INTEGER r));
        
        IF NOT customsZone('БЕЛАРУСЬ') THEN {
            NEW z = CustomsZone {
                name(z) <- 'БЕЛАРУСЬ';
                currency(z) <- currencyShortName('BYN');
                customsZone(Country c) <- z WHERE country('112');
            }
        }

        FOR imported(INTEGER r) AND NOT customsGroup(KOD(r)) NEW c = CustomsGroup DO {
            code(c) <- KOD(r);
        }

        FOR code(CustomsGroup c) == KOD(INTEGER r) DO {
            name(c) <- NAIM(r);
            number(c) <- r;
            parent(c) <- customsGroup(idParent(r));
            customsZone(c) <- customsZone('БЕЛАРУСЬ');
            hasCode(c) <- TRUE;
        }

        APPLY;
    }
} IN tnved;

importExtraCode 'Импортировать четырехзначный код (DBF)' () {
    INPUT f = FILE DO NEWSESSION {
        LOCAL tnved = STRING (INTEGER);
        LOCAL id = STRING[4] (INTEGER);
        LOCAL lvl1 = STRING (INTEGER);
        LOCAL lvl2 = STRING (INTEGER);
        LOCAL lvl3 = STRING (INTEGER);
        LOCAL lvl4 = STRING (INTEGER);        

        IMPORT DBF CHARSET 'CP866' FROM f TO tnved = CODETNVED, id = CODEADD, lvl1 = N1, lvl2 = N2, lvl3 = N3, lvl4 = N4;
        
        FOR imported(INTEGER i) AND i > 1 AND customsGroup(tnved(i)) AND NOT extraCode(customsGroup(tnved(i)), id(i)) DO NEW ec = ExtraCode {
            customsGroup(ec) <- customsGroup(tnved(i));
            id(ec) <- id(i);
            description(ec) <- STRING[150](CONCAT '/', lvl1(i), lvl2(i), lvl3(i), lvl4(i));
        }
        
        FOR code(CustomsGroup c) AND NOT countExtraCodes(c) DO NEW ec = ExtraCode {
            customsGroup(ec) <- c;
            id(ec) <- '0000';
        }

        APPLY;
    }
} IN tnved;

EXTEND FORM defaultData
    PROPERTIES() importTNVEDClassifierNew, importTNVEDCustomsRatesAction, importTNVEDCustomsExceptionsAction, importExtraCode
;

DESIGN defaultData {
    pane {
        NEW customs {
            caption = 'ТН ВЭД';

            MOVE PROPERTY(importTNVEDClassifierNew());
            MOVE PROPERTY(importTNVEDCustomsRatesAction());
            MOVE PROPERTY(importTNVEDCustomsExceptionsAction());
            MOVE PROPERTY(importExtraCode());
        }
    }
}

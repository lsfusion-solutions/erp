MODULE SaleReturnManufacturingPrice;

REQUIRE SaleReturnInvoice, SaleManufacturingPrice;

NAMESPACE SaleReturn;

@defineDocumentInterfaceProperty (invoice, showManufacturingPrice, 'Цена изготовителя');

// -- Операция
@deriveDocumentOperationProperty(UserInvoice, showManufacturingPrice);

@defineDocumentInterfaceDetailPriceCustomPrefix(invoiceDetail, manufacturing, ' изготовителя');
@defineDocumentInterfaceDetailInclVATCustomPrefix (invoiceDetail);
includeVAT(UserInvoiceDetail detail) <-  fixedPrice(sku(detail)) WHEN CHANGED (sku(detail));

@defineDocumentInterfaceDetailMPVATCustomPrefix (invoiceDetail, manufacturing, );

disableUpdateManufacturingPrice = ABSTRACT VALUE BOOLEAN (UserInvoiceDetail);

WHEN LOCAL (CHANGED(supplierStock(UserInvoiceDetail detail)) OR              
            (CHANGED(dateTime(detail)) AND NOT isPosted(detail)) OR
            CHANGED(batch(detail)) OR DROPPED(invoiceDetail(detail)))
            AND NOT invoiceDetail(detail) AND batch(detail) AND NOT CHANGED(manufacturingPrice(detail)) AND NOT disableUpdateManufacturingPrice(detail) DO {
    manufacturingPrice (detail)  <- round2(prevPriceB(SystemLedgerPriceListType.manufacturingPriceStockPriceListType,
                                                      batch(detail),
                                                      supplierStock(detail),
                                                      dateTime(detail)));
}

WHEN LOCAL (CHANGED(supplierStock(UserInvoiceDetail detail)) OR              
            (CHANGED(dateTime(detail)) AND NOT isPosted(detail)) OR
            DROPPED(batch(detail)) OR DROPPED(invoiceDetail(detail)) OR CHANGED(sku(detail)))
            AND NOT invoiceDetail(detail) AND NOT batch(detail) AND NOT CHANGED(manufacturingPrice(detail)) AND NOT disableUpdateManufacturingPrice(detail) DO {
    manufacturingPrice (detail) <- round2(prevPriceB(SystemLedgerPriceListType.manufacturingPriceStockPriceListType,
                                                     sku(detail),
                                                     supplierStock(detail),
                                                     dateTime(detail)));
}

WHEN LOCAL SETCHANGED(invoiceDetail(UserInvoiceDetail detail)) AND NOT CHANGED(manufacturingPrice(detail)) AND NOT disableUpdateManufacturingPrice(detail) DO {
    manufacturingPrice (detail) <- round2(manufacturingPrice(invoiceDetail(detail)));
}

@defineDocumentInterfaceDetailDataSumPrefix (invoice, manufacturing, ' изготовителя'); // объявляем сумму изготовителя
@deriveDocumentDetailSumPrefix(userInvoice, manufacturing, currency, quantity); // записываем сумму изготовителя

@defineDocumentInterfaceHeaderItemSum (invoice, manufacturing, manufacturing);

EXTEND FORM userInvoice
    PROPERTIES(i)  showManufacturingPrice
    PROPERTIES(d)  SHOWIF showManufacturingPrice(i) AFTER invoiceSum(d)  
                   manufacturingPrice, includeVAT, manufacturingSum 

;
DESIGN userInvoice {
    headerExtraParams {
        NEW headerManufacturing {
            caption = 'Цена изготовителя';
            MOVE PROPERTY(showManufacturingPrice(i));
        }
    }
}

EXTEND FORM invoices
    PROPERTIES(d) READONLY  SHOWIF showManufacturingPrice(i) AFTER invoiceSum(d) 
                  manufacturingPrice, includeVAT, manufacturingSum 
;

// цены в подборе
@extendFormDocumentPriceSku(userOrder, manufacturingPriceStockPriceListType, supplierStock, ' изготовителя', userOrder, o, is);
@extendFormDocumentPriceBatch(userOrder, manufacturingPriceStockPriceListType, supplierStock, ' изготовителя', userOrder, o, is);

@extendFormDocumentPriceSku(userInvoice, manufacturingPriceStockPriceListType, supplierStock, ' изготовителя', userInvoice, i, showManufacturingPrice);
@extendFormDocumentPriceBatch(userInvoice, manufacturingPriceStockPriceListType, supplierStock, ' изготовителя', userInvoice, i, showManufacturingPrice);
MODULE PricingPriceLimit;

REQUIRE PriceLimit, Pricing, PricingOperation;

NAMESPACE Pricing;

//------------------ Предельные надбавки ----------------------//

@defineDocumentInterfaceProperty (pricing, showLimitPrice, 'Предельная надбавка');
@defineDocumentInterfaceDetailMarkupPrefix (pricing, limit, ' предельная');

importerMarkup 'Надбавка импортера' = ABSTRACT NUMERIC[8,4] (PricingDetail);
importerMarkup 'Надбавка импортера' = DATA NUMERIC[8,4] (UserPricingDetail);
importerMarkup(UserPricingDetail d) += importerMarkup(d);

importerMarkup 'Надбавка импортера' = ABSTRACT NUMERIC[8,4] (PriceListLedger);
prevImporterMarkup (PriceListLedger l) = PREV(importerMarkup(l));
importerMarkup(PricingDetail ledger) += importerMarkup(ledger);

priceLimitPriceListType(UserPricingDetail detail) = priceLimitPriceListType(departmentStore(detail));

WHEN LOCAL  (CHANGED(sku(UserPricingDetail detail)) OR
    CHANGED(departmentStore(detail)) OR
    (CHANGED(dateTime(detail)) AND NOT isPosted(detail))) AND NOT CHANGED(importerMarkup(detail)) DO {
    importerMarkup (detail) <- prevImporterMarkup(prevRetailPricingPriceListLedgerB(sku(detail), departmentStore(detail), dateTime(detail)));
}

WHEN LOCAL FORMS userPricing
    (CHANGED(departmentStore(UserPricingDetail detail)) OR CHANGED(sku(detail)) OR CHANGED(importerMarkup(detail)) OR
        SET(showLimitPrice(detail)))
        AND showLimitPrice(detail) DO {

    limitMarkup(detail) <- [floor(((100.0 + X)/(100 + Z)-1.0)*100.0, 0.01)](
            markup(priceLimitPriceListType(detail), sku(detail)),
            (OVERRIDE importerMarkup(detail), 0.0));
}

overLimitBasePrice = ABSTRACT VALUE NUMERIC[16,4] (UserPricingDetail);
limitBasePrice(UserPricingDetail d) = OVERRIDE overLimitBasePrice(d), price(d);

limitPrice (UserPricingDetail detail) = round(
        [ ( X*(Y+100)/100.0 (+) W)*(Z+100)/100.0](
                limitBasePrice(detail),
                limitMarkup(detail),
                retailExtraPackagingCost(detail),
                valueRetailVAT(detail)), roundCondition(priceLimitPriceListType(detail), sku(detail)));

WHEN LOCAL FORMS userPricing GOAFTER retailPrice[UserPricingDetail], limitPrice[UserPricingDetail]
    (SETCHANGED(retailPrice(UserPricingDetail d)) OR CHANGED(limitBasePrice(d)) OR CHANGED(limitMarkup(d))) AND 
        showLimitPrice(d) NOINLINE DO { 
    IF limitPrice(d) < retailPrice(d) THEN {
        retailPrice(d) <- limitPrice(d);
        retailMarkup(d) <- calcRetailMarkup(d);
    }
}

//----------------------------------------------- Операции -----------------------------------------------------//

// Вид цены для расценки для операции
@defineDocumentHeaderProperty (operation, showLimitPrice, 'Предельная надбавка');

EXTEND FORM Pricing.operation
    PROPERTIES(o) showLimitPrice
;

DESIGN Pricing.operation {
    paramsContainer {
        MOVE PROPERTY(showLimitPrice(o));
    }
}

// Записываем необходимо ли показывать предельную надбавку по умолчанию из операции
showLimitPrice (UserPricing pricing) <- showLimitPrice(operation(pricing))
    WHEN CHANGED(operation(pricing));

EXTEND FORM userPricing
    PROPERTIES(p) showLimitPrice
    PROPERTIES(d) SHOWIF showLimitPrice(p) BEFORE retailMarkup(d) importerMarkup, limitMarkup
;
DESIGN userPricing{
    GROUP(documentPrm,p){
        MOVE PROPERTY (showLimitPrice(p));
    }
}

EXTEND FORM pricings
    PROPERTIES(d) READONLY SHOWIF showLimitPrice(p) BEFORE retailMarkup(d) importerMarkup, limitMarkup READONLY
;
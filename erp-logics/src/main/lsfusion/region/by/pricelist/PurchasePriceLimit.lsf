MODULE PurchasePriceLimit;

REQUIRE PricingPriceLimit, PricingPurchase, PurchaseImporterMarkup;

NAMESPACE Purchase;

//------------------ Предельные надбавки ----------------------//

@defineDocumentInterfaceProperty (invoice, showLimitPrice, 'Предельная надбавка');
@defineDocumentInterfaceDetailMarkupPrefix (invoice, limit, ' предельная');

importerMarkup(InvoicePricingDetail detail) += importerMarkup(invoiceDetail(detail));

basePriceLimitPriceListType = ABSTRACT CalcPriceListType ();
priceLimitPriceListType(UserInvoiceDetail detail) = OVERRIDE priceLimitPriceListType(customerStock(detail)), basePriceLimitPriceListType();

WHEN LOCAL FORMS userInvoice
    (CHANGED(customerStock(UserInvoiceDetail detail)) OR
        CHANGED(sku(detail)) OR
        CHANGED(importerMarkup(detail)) OR
        SET(showLimitPrice(detail)))
        AND showLimitPrice(detail) DO {

    limitMarkup(detail) <- [floor(((100.0 + X)/(100 + Z)-1.0)*100.0, 0.01)](
            markup(priceLimitPriceListType(detail), sku(detail)),
            (OVERRIDE importerMarkup(detail), 0.0));

}

overChangedPrice(UserInvoiceDetail detail) += WHEN detail IS UserInvoiceDetail THEN CHANGED(importerMarkup(detail));

//----------------------------------------------- Операции -----------------------------------------------------//

// Записываем необходимо ли показывать предельную надбавку по умолчанию из операции расценки, заданной для операции закупка
showLimitPrice (UserInvoice invoice) <- showLimitPrice(pricingOperation(invoice))
    WHEN CHANGED(pricingOperation(invoice));

showIfLimitPrice = showLimitPrice (UserInvoice invoice) AND createPricing(invoice);

EXTEND FORM userInvoice
    PROPERTIES(i) showLimitPrice SHOWIF createPricing(i) BACKGROUND backgroundRetail(i)
    PROPERTIES(pd) SHOWIF showIfLimitPrice(i) BEFORE retailMarkup(pd)  limitMarkup BACKGROUND backgroundRetail(i)
    PROPERTIES (pd) SHOWIF (showImporterMarkup ( i) AND createPricing(i)) importerMarkup READONLY AFTER pricingPrice(pd)
;
DESIGN userInvoice{
    headerCreatePricing {
        MOVE PROPERTY (showLimitPrice(i));

    }
}

EXTEND FORM invoices
    PROPERTIES(d) READONLY SHOWIF showLimitPrice(i) BEFORE retailMarkup(d) limitMarkup BACKGROUND backgroundRetail(i)
;

// Имплементим для расчета оптимальной розничной цены

overLimitBasePrice = ABSTRACT VALUE NUMERIC[16,4] (UserInvoiceDetail);
limitBasePrice(UserInvoiceDetail d) = OVERRIDE overLimitBasePrice(d), price(d);
 
limitMarkupPrice (UserInvoiceDetail d) =
                                           round(
                                                [ (X+Y)*(Z+100.0)/100.0](
                                                    limitBasePrice(d),
                                                    limitMarkup(d),
                                                    valueRetailVAT(d)), roundCondition(priceLimitPriceListType(d), sku(d)));

limitPrice = ABSTRACT CASE NUMERIC[16,4] (InvoiceDetail);
limitPrice (UserInvoiceDetail detail) += WHEN detail IS UserInvoiceDetail THEN round(
                                           [ ( X*(Y+100)/100.0 (+) W)*(Z+100)/100.0](
                                                    limitBasePrice(detail),
                                                    limitMarkup(detail),
                                                    retailExtraPackagingCost(detail),
                                                    valueRetailVAT(detail)), roundCondition(priceLimitPriceListType(detail), sku(detail)));

skipLocalLimitPrice = ABSTRACT VALUE BOOLEAN (UserInvoiceDetail);
WHEN LOCAL FORMS userInvoice GOAFTER retailPrice[UserInvoiceDetail], limitPrice[UserInvoiceDetail]
            (SETCHANGED(retailPrice(UserInvoiceDetail d)) OR CHANGED(limitBasePrice(d)) OR CHANGED(limitMarkup(d)) OR CHANGED(valueRetailVAT(d))) AND 
             showLimitPrice(d) AND NOT skipLocalLimitPrice(d)  NOINLINE DO { 
    IF limitPrice(d) < retailPrice(d) THEN {
        retailPrice(d) <- limitPrice(d);
        retailMarkup(d) <- calcRetailMarkup(d);
    }
}

showLimitPrice(InvoicePricing pricing) += showLimitPrice(invoice(pricing));
limitMarkup(InvoicePricingDetail detail) += limitMarkup(invoiceDetail(detail));

@defineOperationProperty(isImporterMarkup, 'Контролировать надбавку импортера в накладных', constraintContainer);

skipCheckImporterMarkup = ABSTRACT VALUE BOOLEAN (UserInvoiceDetail);
overIsLimit = ABSTRACT VALUE BOOLEAN (UserInvoiceDetail);
CONSTRAINT (SET(isPosted(UserInvoiceDetail d)) OR DROPPED(importerMarkup(d)) OR CHANGED(sku(d)) OR CHANGED(operation(d)) OR SET(limitMarkup(d))) 
    AND isPosted(d) AND (limitMarkup(d) OR overIsLimit(d)) AND country(sku(d)) != defaultCountry() AND isImporterMarkup(operation(d))
    AND NOT importerMarkup(d) AND NOT skipCheckImporterMarkup(d)
    MESSAGE 'В строке накладной должна быть заполнена надбавка импортера';
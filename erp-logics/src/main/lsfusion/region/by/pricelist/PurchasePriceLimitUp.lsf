MODULE PurchasePriceLimitUp;

REQUIRE PricingPurchase;

NAMESPACE Purchase;

@defineOption(enablePriceLimitUp, 'Запретить рост цен', pricings);

allowPriceUp 'Разрешить рост цены' = DATA BOOLEAN (UserInvoice);
EXTEND FORM userInvoice
    PROPERTIES(i) allowPriceUp SHOWIF enablePriceLimitUp()
;
     
DESIGN userInvoice {
    headerCreatePricing {
         MOVE PROPERTY(allowPriceUp(i));
    }
}

limitRetailPriceUp 'Расценить по текущей' (UserInvoiceDetail dd) {
    retailPrice(UserInvoiceDetail d) <- prevRetailPricingPriceB(sku(d), customerStock(d), dateTime(d)) WHERE userInvoice(d) = userInvoice(dd) AND prevRetailPricingPriceB(sku(d), customerStock(d), dateTime(d));   
} ASON CONTEXTMENU retailPrice[UserInvoiceDetail];

CONSTRAINT (SETCHANGED(retailPrice(UserInvoiceDetail d)) OR SET(isPosted(d)) OR SETCHANGED(dateTime(d)) OR SETCHANGED(customerStock(d)) OR SETCHANGED(sku(d))) 
            AND isPosted(d) AND enablePriceLimitUp() AND NOT allowPriceUp(userInvoice(d)) AND retailPrice(d) > prevRetailPricingPriceB(sku(d), customerStock(d), dateTime(d))
            MESSAGE 'Запрет роста цен. Обратитесь в отдел ЦО';
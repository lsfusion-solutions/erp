MODULE SaleLimitMarkup;

REQUIRE SaleInvoice, PriceListCalc, SaleManufacturingPrice;

NAMESPACE Sale;

limitSaleCalcPriceListType 'Предельная цена от цены изготовителя' = DATA CalcPriceListType() PREREAD;
nameLimitSaleCalcPriceListType 'Предельная цена от цены изготовителя' () = name(limitSaleCalcPriceListType());

EXTEND FORM options
    PROPERTIES() nameLimitSaleCalcPriceListType
;
DESIGN options {
    invoiceSale {
        MOVE PROPERTY(nameLimitSaleCalcPriceListType());
    }
}

dataLimitCalcPriceListType  = DATA CalcPriceListType (Operation);
nameDataLimitCalcPriceListType 'Предельная цена от цены изготовителя' (Operation operation)= name(dataLimitCalcPriceListType(operation));

EXTEND FORM operation
    PROPERTIES(o) nameDataLimitCalcPriceListType
;
DESIGN operation {
    paramsContainer {
        MOVE PROPERTY(nameDataLimitCalcPriceListType(o));
    }
}

limitCalcPriceListType (Operation operation) = OVERRIDE dataLimitCalcPriceListType(operation), limitSaleCalcPriceListType();

markupLimit = ABSTRACT CASE NUMERIC[8,2] (UserOrderDetail);
markupLimitCalcPriceListType 'Предельная надбавка, %' (UserOrderDetail d) = markup(limitCalcPriceListType(operation(d)), sku(d));
overMarkupLimit 'Предельная надбавка, %' (UserOrderDetail d) = OVERRIDE markupLimit(d), markupLimitCalcPriceListType(d);

limitSaleMarkupPrice = ABSTRACT CASE NUMERIC[16,4] (OrderDetail);
limitSaleMarkupPrice(UserOrderDetail d) += WHEN d IS UserOrderDetail THEN round([ X*(Y+100)/100](
                                              manufacturingPrice(d),
                                              markupLimitCalcPriceListType(d)), roundCondition(limitCalcPriceListType(operation(d)))); 

setLimitSaleMarkupPrice ABSTRACT CASE (UserOrderDetail);
WHEN LOCAL FORMS userOrder GOAFTER price[UserOrderDetail]
           (CHANGED(price(UserOrderDetail d)) OR CHANGED(sku(d)) OR CHANGED(manufacturingPrice(d)) OR SET(showManufacturingPrice(d))) AND
           showManufacturingPrice(d) AND limitSaleMarkupPrice(d) < price(d) DO {
    price(d) <- limitSaleMarkupPrice(d);          
    invoicePrice(d) <- round2(limitSaleMarkupPrice(d) * (100.0 (+) valueVAT(d)) / 100.0);
    setLimitSaleMarkupPrice(d);
}

EXTEND FORM userOrder
    PROPERTIES(d) READONLY SHOWIF (limitCalcPriceListType(operation(d)) AND showManufacturingPrice(d)) overMarkupLimit
;

markupLimit = ABSTRACT CASE NUMERIC[8,2] (UserInvoiceDetail);
markupLimitCalcPriceListType 'Предельная надбавка, %' (UserInvoiceDetail d) = markup(limitCalcPriceListType(operation(d)), sku(d));
overMarkupLimit 'Предельная надбавка, %' (UserInvoiceDetail d) = OVERRIDE markupLimit(d), markupLimitCalcPriceListType(d);

limitSaleMarkupPrice = ABSTRACT CASE NUMERIC[16,4] (InvoiceDetail);
limitSaleMarkupPrice(UserInvoiceDetail d) += WHEN d IS UserInvoiceDetail THEN round([ X*(Y+100)/100](
                                              manufacturingPrice(d),
                                              markupLimitCalcPriceListType(d)), roundCondition(limitCalcPriceListType(operation(d)))); 

setLimitSaleMarkupPrice ABSTRACT CASE (UserInvoiceDetail);
WHEN LOCAL FORMS userInvoice GOAFTER price[UserInvoiceDetail]
           (CHANGED(price(UserInvoiceDetail d)) OR CHANGED(sku(d)) OR CHANGED(manufacturingPrice(d)) OR SET(showManufacturingPrice(d))) AND
           showManufacturingPrice(d) AND limitSaleMarkupPrice(d) < price(d) DO {
    price(d) <- limitSaleMarkupPrice(d);          
    invoicePrice(d) <- round2(limitSaleMarkupPrice(d) * (100.0 (+) valueVAT(d)) / 100.0);
    setLimitSaleMarkupPrice(d);
}

EXTEND FORM userInvoice
    PROPERTIES(d) READONLY SHOWIF (limitCalcPriceListType(operation(d)) AND showManufacturingPrice(d)) overMarkupLimit
;

MODULE WarePriceList;

REQUIRE PriceListType, Ware, WareItem;

NAMESPACE PriceList;

CLASS WarePriceListType 'Цена cо стеклопосудой' : PriceListType;

name 'Наименование' = DATA ISTRING[50](WarePriceListType);
name(WarePriceListType type) += name(type) IF type IS WarePriceListType;

basePriceListType(type) = DATA BasePriceListType (WarePriceListType);
nameBasePriceListType 'Базовый вид цены' (WarePriceListType type) = name(basePriceListType(type));

ledgerPriceListType(WarePriceListType type) += basePriceListType(type) AS LedgerPriceListType;

includeVAT(WarePriceListType type) += type IS WarePriceListType;

@defineDocumentHeaderCurrency(warePriceListType);
currency(WarePriceListType warePriceListType) += currency(warePriceListType);

ware (sku) = ABSTRACT Ware (Sku);
ware (Item sku) += Item.ware(sku);

// для товаров
priceB(WarePriceListType type, Sku sku, Stock stock, DATETIME dateTime) =
    priceB(basePriceListType(type), sku, stock, dateTime) (+)
    (price(ware(sku), toDate(dateTime)) IF type IS WarePriceListType AND stock IS Stock);
priceA(WarePriceListType type, Sku sku, Stock stock, DATETIME dateTime) =
    priceA(basePriceListType(type), sku, stock, dateTime) (+)
    (price(ware(sku), toDate(dateTime)) IF type IS WarePriceListType AND stock IS Stock);

priceB(WarePriceListType type, Sku sku, Stock stock, DATETIME dateTime) += priceB(type, sku, stock, dateTime);
priceA(WarePriceListType type, Sku sku, Stock stock, DATETIME dateTime) += priceA(type, sku, stock, dateTime);

// для партий

priceB(WarePriceListType type, Batch batch, Stock stock, DATETIME dateTime) =
    priceB(basePriceListType(type), batch, stock, dateTime) (+)
    (price(ware(sku(batch)), toDate(dateTime)) IF type IS WarePriceListType AND stock IS Stock);
priceA(WarePriceListType type, Batch batch, Stock stock, DATETIME dateTime) =
    priceA(basePriceListType(type), batch, stock, dateTime) (+)
    (price(ware(sku(batch)), toDate(dateTime)) IF type IS WarePriceListType AND stock IS Stock);

priceB(WarePriceListType type, Batch batch, Stock stock, DATETIME dateTime) += priceB(type, batch, stock, dateTime);
priceA(WarePriceListType type, Batch batch, Stock stock, DATETIME dateTime) += priceA(type, batch, stock, dateTime);

FORM warePriceListType 'Цена cо стеклопосудой'
    OBJECTS w = WarePriceListType PANEL
    PROPERTIES(w) name, includeVAT, nameCurrency, nameBasePriceListType

    EDIT WarePriceListType OBJECT w
;

DESIGN warePriceListType {
    BOX(w){
        MOVE PROPERTY(name(w));
        MOVE PROPERTY(includeVAT(w));
        MOVE PROPERTY(nameCurrency(w));
        MOVE PROPERTY(nameBasePriceListType(w));
    }
}

EXTEND FORM priceListTypes
    PROPERTIES(pt) NEWSESSION addWarePriceListType = NEW[WarePriceListType]
;

DESIGN priceListTypes {
    PROPERTY (addCalcPriceListType) {
        caption = 'Добавить цену cо стеклопосудой';
    }
}

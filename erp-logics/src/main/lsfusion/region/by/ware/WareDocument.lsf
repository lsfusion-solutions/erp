MODULE WareDocument;

REQUIRE Ware;

NAMESPACE Ware;

strictWareDocument 'Обязательное наличие строки посуды для строки документа' = DATA BOOLEAN();
EXTEND FORM options
    PROPERTIES() strictWareDocument
;
DESIGN options {
    stock {
        NEW ware {
            horizontal = TRUE;
            caption = 'Стеклопосуда';
        MOVE PROPERTY(strictWareDocument());
        }
    }
}

META defineDocumentWare(document, NS)
    skipCreateWare = DATA BOOLEAN (NS.User###document##Detail);

    wareUser###document##Detail = DATA NS.User###document##Detail(NS.User###document##Detail) INDEXED;
    itemWareUser###document##Detail (user###document##Detail) =
        GROUP AGGR User###document##Detail uuser###document##Detail BY wareUser###document##Detail(uuser###document##Detail) MATERIALIZED INDEXED;

    createWare(User###document##Detail user###document##Detail)  { 
        IF wareUser###document##Detail(user###document##Detail) THEN {
            NS.quantity[User###document##Detail](User###document##Detail d) <- NS.quantity[User###document##Detail](user###document##Detail) WHERE d == wareUser###document##Detail(user###document##Detail);
        } ELSE
            NEW d = User###document##Detail {
                user###document(d) <- user###document(user###document##Detail);
                NS.sku[User###document##Detail](d) <- ware(NS.sku[User###document##Detail](user###document##Detail));
                NS.quantity[User###document##Detail](d) <- NS.quantity[User###document##Detail](user###document##Detail);
                wareUser###document##Detail(user###document##Detail) <- d;
                invoicePrice(d) <- price(NS.sku[User###document##Detail](d), date(d));
                VAT(d) <- VAT(NS.sku[User###document##Detail](d), country(supplierStock(d)));
                valueVAT(d) <- valueRate(VAT(d), date(d));
                price(d) <- round2(invoicePrice(d)*100/(100+calcValueVAT(d)));
            }
    }

    WHEN LOCAL FORMS user###document
         (SETCHANGED(NS.sku[User###document##Detail](User###document##Detail detail)) OR CHANGED(NS.quantity[User###document##Detail](detail))) AND
         ware(NS.sku[User###document##Detail](detail)) AND NOT skipCreateWare[User###document##Detail](detail)
            DO EXEC createWare(detail);

    WHEN LOCAL FORMS user###document
         DROPPED(itemWareUser###document##Detail(User###document##Detail detail)) DO
         DELETE detail;
    
    CONSTRAINT strictWareDocument() AND CHANGED(NS.sku[User###document##Detail](User###document##Detail d)) AND ware(NS.sku[User###document##Detail](d)) AND NOT skipCreateWare[User###document##Detail](d) AND NOT wareUser###document##Detail(d)
        MESSAGE 'Для строки документа должна присутствовать строка посуды';
END

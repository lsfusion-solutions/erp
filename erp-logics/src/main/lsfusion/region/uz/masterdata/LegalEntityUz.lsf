MODULE LegalEntityUz;

REQUIRE LegalEntity, StockUz;

NAMESPACE LegalEntity;

INN 'ИНН' = DATA STRING[10] (LegalEntity) IN doc CHARWIDTH 10 NOFLEX INDEXED;
OKPO 'ОКПО' = DATA STRING[20] (LegalEntity) IN doc CHARWIDTH 15;
OKED 'ОКЭД' = DATA STRING[5] (LegalEntity) IN doc CHARWIDTH 5;
codeVatPayer 'Рег.код плательщика НДС' = DATA STRING[12] (LegalEntity) IN doc CHARWIDTH 12;

taxpayerId(LegalEntity l) += INN(l);

legalEntityINN (inn) = GROUP MAX LegalEntity legalEntity BY INN(legalEntity);
supplierINN = GROUP MAX LegalEntity legalEntity IF isSupplier(legalEntity) BY INN(legalEntity);

META defineDocumentINNLegalEntity(object, contact, caption)
    INN###contact 'ИНН'###caption (###object object) = INN(contact(object)) IN documentPrm;
    OKPO###contact 'ОКПО'###caption (###object object) = OKPO(contact(object)) IN documentPrm;
    OKED###contact 'ОКЭД'###caption (###object object) = OKED(contact(object)) IN documentPrm;
    codeVatPayer###contact 'Рег.код плательщика НДС'###caption (###object object) = codeVatPayer(contact(object)) IN documentPrm;
END

EXTEND FORM legalEntity
    PROPERTIES(l) SHOWIF toShowUNP(l) INN  
    PROPERTIES(l) SHOWIF toShow(l) OKPO, OKED, codeVatPayer
;

DESIGN legalEntity{
    column2 {
        MOVE GROUP(doc,l) {
            lines = 3;
        }
    }
}

EXTEND FORM legalEntities
    PROPERTIES(l) READONLY INN BEFORE shortNameOwnership(l)
;

EXTEND FORM supplierLegalEntities
    PROPERTIES(l) READONLY INN BEFORE shortNameOwnership(l)
;

uniqueINNSupplier 'Запретить повтор ИНН для поставщиков' = DATA BOOLEAN () COMPLEX;
CONSTRAINT uniqueINNSupplier() AND CHANGED (INN(LegalEntity l)) AND [ GROUP SUM 1 IF isSupplier(LegalEntity ll) BY INN(ll)](INN(l)) > 1 MESSAGE 'Запрещен повтор ИНН для поставщиков';

uniqueINNCustomer 'Запретить повтор ИНН для покупателей' = DATA BOOLEAN () COMPLEX;
CONSTRAINT uniqueINNCustomer() AND CHANGED (INN(LegalEntity l)) AND [ GROUP SUM 1 IF isCustomer(LegalEntity ll) BY INN(ll)](INN(l)) > 1 MESSAGE 'Запрещен повтор ИНН для покупателей';

uniqueINN 'Запретить повтор ИНН' = DATA BOOLEAN () COMPLEX;
CONSTRAINT uniqueINN() AND CHANGED (INN(LegalEntity l)) AND [ GROUP SUM 1 BY INN(LegalEntity ll)](INN(l)) > 1 MESSAGE 'Запрещен повтор ИНН';

EXTEND FORM options
    PROPERTIES() uniqueINNSupplier, uniqueINNCustomer, uniqueINN
;

DESIGN options {
    legalEntity {
        MOVE PROPERTY(uniqueINNSupplier());
        MOVE PROPERTY(uniqueINNCustomer());
        MOVE PROPERTY(uniqueINN());
    }
}

region 'Регион' = DATA Region (LegalEntity);
nameRegion 'Регион' (LegalEntity l) = name(region(l));

district 'Район' = DATA District (LegalEntity);
nameDistrict 'Район' (LegalEntity l) = name(district(l));

CONSTRAINT region(LegalEntity l) AND NOT region(district(l)) = region(l)
    CHECKED BY district
    MESSAGE 'Выбранный район не соответствует региону'
;

VATPayer 'Плательщик НДС' = DATA BOOLEAN (LegalEntity);

EXTEND FORM legalEntity
    PROPERTIES (l) nameRegion, nameDistrict, VATPayer
;

DESIGN legalEntity {
    regionContainer {
        MOVE PROPERTY (nameRegion(l));
        MOVE PROPERTY (nameDistrict(l));
        MOVE PROPERTY (VATPayer(l));
    }    
}

EXTEND FORM legalEntities
    PROPERTIES(l) READONLYIF isReadonly() VATPayer BEFORE address(l)
;
MODULE ItemUz;

REQUIRE Item;

NAMESPACE Item;

//ikpu 'ИКПУ' (Item i) = DATA ISTRING[21] (Item);

dataIKPU 'ИКПУ' (Item i) = DATA ISTRING[21] (Item);
overIKPU = ABSTRACT ISTRING[21] (Item);
ikpu 'ИКПУ' (Item i) = OVERRIDE overIKPU(i), dataIKPU(i) IN itemBase;

dialogIKPU 'ИКПУ' (Item i)  { INPUT s = ISTRING[100] DO { dataIKPU(i) <- s;}} ASON CHANGE ikpu[Item];

// Признак маркировки для касс
marked 'Маркированный товар' = DATA BOOLEAN (Item) IN itemBase;

EXTEND FORM item 
    PROPERTIES(i) ikpu, marked
;

EXTEND FORM items 
    PROPERTIES (i) READONLYIF isReadonly() ikpu, marked
;

translate (TEXT s, TEXT ms, TEXT rs) = FORMULA TEXT PG 'translate($1, $2, $3)';
upper (TEXT s) = FORMULA TEXT PG 'upper($1)';

translateUz (TEXT s) = 
    replace(
    replace(
    replace(
    replace(
    replace(
    replace(
    replace(
    replace( 
        translate(upper(s), 'АБВГДЖЗЕИЙКЛМНОПРСТУФХЪЫЭ', 'ABVGDJZEIYKLMNOPRSTUFXʼIE'),
        'Ё', 'YO'),
        'Ц', 'TS'),
        'Ч', 'CH'),
        'Ш', 'SH'),
        'Щ', 'SHCH'),
        'Ь', ''),
        'Ю', 'YU'),
        'Я', 'YA');

nameAttributeUz 'Наименование (уз.)' (Item i) = ISTRING(translateUz(nameAttribute(i)));

EXTEND FORM item
    PROPERTIES (i) nameAttributeUz
;

DESIGN item {
    itemHeader {
        MOVE PROPERTY(nameAttributeUz(i));
    }
}

shortNameUomUZ 'Краткое наим. УЗБ' = DATA ISTRING[100](UOM) CHARWIDTH 3;

EXTEND FORM UOM
    PROPERTIES(u) shortNameUomUZ
;
EXTEND FORM UOMs
    PROPERTIES(u) READONLYIF isReadonly() shortNameUomUZ BEFORE shortNameBase(u)
;
EXTEND FORM dialogUOMs
    PROPERTIES(u) READONLYIF isReadonly() shortNameUomUZ
;
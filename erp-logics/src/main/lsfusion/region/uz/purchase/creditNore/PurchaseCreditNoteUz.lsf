MODULE PurchaseCreditNoteUz;

REQUIRE PurchaseCreditNote;

NAMESPACE Purchase;

selected 'Отм.' = DATA LOCAL NESTED BOOLEAN (CreditNote);
countSelectedCreditNote = GROUP SUM 1 IF selected(CreditNote c);

EXTEND FORM creditNotes
    PROPERTIES(c) BACKGROUND background(c) selected BEFORE isClosed(c)
;

FORM creditNote 'Акт расхождений'
    OBJECTS c = CreditNote 
    PROPERTIES (c) READONLY seriesNumber, date, fullNameSupplier, nameSupplierStock,
        fullNameCustomer, addressCustomer, nameCustomerStock, addressCustomerStock,
        seriesNumberContractSku, quantityCreditNoteDetail, sumCreditNoteDetail,
        VATSumCreditNoteDetail, invoiceSumCreditNoteDetail, note
    FILTERS selected(c)
    
    OBJECTS d = CreditNoteDetail
    PROPERTIES (d) READONLY index, idBarcodeSku, nameSku, shortNameUOMSku, overShortNameUOMSku,
        quantity, price, sum, numberVAT,
        valueVAT, VATSum, invoiceSum,
        documentQuantity, documentVATSum, documentInvoiceSum,
        shipmentQuantity, shipmentVATSum, shipmentInvoiceSum
    ORDERS index(d)
    FILTERS creditNote(d) == c
;

printCreditNote 'Акт расхождения (PDF)' (CreditNote c)  {
    IF NOT countSelectedCreditNote() THEN 
        selected(c) <- TRUE;
        PRINT creditNote ;
        selected(CreditNote cc) <- NULL;
} IMAGE 'print.png' IN print;

printCreditNoteXLSX 'Акт расхождения (XLSX)' (CreditNote c)  {
    IF NOT countSelectedCreditNote() THEN
        selected(c) <- TRUE;
        PRINT creditNote XLSX;
        selected(CreditNote cc) <- NULL;
} IMAGE 'print.png' IN print;

EXTEND FORM creditNotes
    PROPERTIES(c) printCreditNote, printCreditNoteXLSX
;

DESIGN creditNotes {
    printContainer {
        MOVE GROUP(print, c);
    }
}

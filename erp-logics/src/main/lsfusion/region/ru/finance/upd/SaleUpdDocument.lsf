MODULE SaleUpdDocument;

REQUIRE SaleInvoiceConsignmentRu, ConsignmentUpdDocument;

NAMESPACE Sale;

//задание статуса УПД
updDocumentStatus 'Статус УПД (электронный)' = DATA UpdDocumentStatus (Operation);
nameUpdDocumentStatus 'Статус УПД (электронный)' (Operation operation) = name(updDocumentStatus(operation)) CHARWIDTH 10;

EXTEND FORM operation
    PROPERTIES (o) nameUpdDocumentStatus
;

DESIGN operation {
    tabContainer {
        NEW updDocument {
            caption = 'УПД';
            MOVE PROPERTY(nameUpdDocumentStatus(o));
        }
    }
}

updStatus (Operation operation) += updDocumentStatus(operation);

updInvoiceNumber 'Номер СЧФ' (Invoice i) = numberInvoice(updDocument(i));

showUpd 'Просмотреть УПД' (Invoice i) {
    show(updDocument(i));
}

editUpd 'Редактировать УПД' (Invoice i) {
    edit(updDocument(i));
} IMAGE 'edit.png';

createUpdDocuments 'Создать УПД (отм.)' () {
    IF countIncl() THEN {
        NEWSESSION NESTED(incl[Invoice]) {
            LOCAL count = INTEGER ();
            count() <- countIncl();
            FOR incl(Invoice i) DO {
                IF updDocument(i) THEN {
                    recreateUpdDocument(i);
                } ELSE {
                    NEW ei = UpdDocument {
                        fill(i, ei);
                    }
                }
            }
            APPLY NESTED(count);
            IF NOT canceled() THEN MESSAGE CONCAT ' ', 'Обработано', count(), 'документов(-а).';
        }    
    } ELSE {
        MESSAGE 'Проставьте признак "Отм." для обрабатываемых документов.';
    }
}

createUpdDocuments (Consignment invoice) {
     IF countIncl() THEN {
        createUpdDocuments();   
     } ELSE {
        recreateUpd(invoice);
     }
}

EXTEND FORM invoices
    PROPERTIES (i) READONLY updInvoiceNumber AFTER series(i)
    PROPERTIES //createUpd(i) SHOWIF NOT updDocument(i),
               createUpdDocuments(i) HEADER captionCreateUpd(i),
               updNumber = updInvoiceNumber(i) SHOWIF updDocument(i) PANEL READONLY,
               editUpd(i) SHOWIF updDocument(i),
               showUpd(i) SHOWIF updDocument(i)
;

DESIGN invoices {
    documentDetail {
        NEW updDocument {
            type = CONTAINERV;
            caption = 'УПД';
            NEW updNumber {
                type = CONTAINERH;
                MOVE PROPERTY(updNumber) {
                    charWidth = 20;
                };
                MOVE PROPERTY(editUpd(i));
                MOVE PROPERTY(showUpd(i));                
            }
            MOVE PROPERTY(createUpdDocuments(i));        
        }
    }
}
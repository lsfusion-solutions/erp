MODULE UpdDocumentPurchase;

REQUIRE PurchaseInvoice, ConsignmentUpdDocument, ConsignmentUpdDocumentLot, UpdDocument, PurchaseShipmentLot, DiadocUpdDocument;

NAMESPACE Purchase;

operation 'Операция прихода' () = DATA Operation();
nameOperation'Операция прихода' () = name(operation());
stockCustomer 'Склад покупателя' () = DATA Stock();
nameStockCustomer 'Склад покупателя' () = name(stockCustomer());


FORM dialogPurchase 'Параметры Документа закупки'
    PROPERTIES() nameOperation, nameStockCustomer
;

updDocument = DATA UpdDocument(UserInvoice);
updDocumentDetail = DATA UpdDocumentDetail(UserInvoiceDetail);

invoice = 
    GROUP LAST UserInvoice ui
        IF ei IS UserInvoice
        ORDER updDocument(ui), ui 
        BY updDocument(ui);

skipCreateUserInvoiceDetail = ABSTRACT BOOLEAN (UpdDocumentDetail);
overCreateInvoice ABSTRACT (UserInvoice, UpdDocument);
overCreateInvoice ABSTRACT (UserInvoiceDetail, UpdDocumentDetail);

createInvoice 'Создать приходную накладную'(UpdDocument d){
    operation () <- NULL;
    stockCustomer () <- NULL;
    
    
    IF NOT invoice(d) THEN NEWSESSION {
        DIALOG dialogPurchase DO{
            NEW invoice = UserInvoice {
                updDocument(invoice) <- d;
                date(invoice) <- currentDate();
                supplier(invoice) <- OVERRIDE legalEntityEdi(idConsignor(d)), legalEntityInn(innSupplier(d));
                customer(invoice) <- OVERRIDE legalEntityEdi(idConsignee(d)), legalEntityInn(innCustomer(d));
                operation(invoice) <- operation();
                supplierStock(invoice) <- [GROUP LAST Stock s ORDER s BY legalEntity(s)] (supplier(invoice));
                customerStock(invoice) <- stockCustomer();
                overCreateInvoice(invoice, d);
                FOR updDocument(UpdDocumentDetail ed) == d AND NOT skipCreateUserInvoiceDetail(ed) NEW id = UserInvoiceDetail  DO {
                    updDocumentDetail(id) <- ed;
                    userInvoice(id) <- invoice;
                    sku(id) <- skuBarcode(idSku(ed));
                    valueVAT(id) <- NUMERIC[10,5](replace(nameVATStatus(ed), '%',''));
                    quantity(id) <- quantity(ed);
                    price(id) <- price(ed);
                    FOR updDocumentDetail(UpdLotDetail ud) == ed AND NOT lot(id(ud))  NEW l = Lot DO {
                        id(l) <- id(ud);
                        sku(l) <- skuBarcode(idSku(ed));
                    }
                    FOR updDocumentDetail(UpdLotDetail ud) == ed AND l = lot(id(ud))  DO {
                        quantity(id, l) <- 1;
                    }
                overCreateInvoice(id, ed);                    
                }
//            APPLY;
            SHOW userInvoice OBJECTS i = invoice;
            }
        }
    }
}

overShowCreateInvoice = ABSTRACT BOOLEAN (UpdDocument);

showCreateInvoice (UpdDocument d) = (NOT invoice(d) AND primaryStatusTextDocument(d) = 'Подписан') OR overShowCreateInvoice(d);

EXTEND FORM updDocuments
    PROPERTIES(u) createInvoice SHOWIF showCreateInvoice(u)
;

DESIGN updDocuments{
    actionContainer{
        MOVE PROPERTY (createInvoice(u));
    }
}

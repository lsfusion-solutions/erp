MODULE FiscalPirit;

REQUIRE System,
        POS,
        CashRegister;

loadDefaultCashRegisterModels() + {  EXEC loadDefaultCashRegisterModel('Фискальный регистратор Pirit', 'Pirit', NULL, NULL, TRUE, NULL, NULL ); };

isUnix = osVersion(currentComputer()) == 'Linux';

isPirit() = sidModelCurrentCashRegister() == 'Pirit';

overUserName = ABSTRACT ISTRING[100]();
userName 'Кассир' = OVERRIDE overUserName(), currentUserName();

fiscalPiritPrint 'Напечатать фискальный чек'  INTERNAL  'lsfusion.erp.region.ru.machinery.cashregister.fiscalpirit.FiscalPiritPrintReceiptAction' (Receipt);
postPrint(Receipt receipt) + {  IF isPirit() THEN fiscalPiritPrint(receipt); }

fiscalPiritAdvancePaper 'Прогон ленты'  INTERNAL  'lsfusion.erp.region.ru.machinery.cashregister.fiscalpirit.FiscalPiritAdvancePaperAction' ();
fiscalAdvancePaper() + {  IF isPirit() THEN fiscalPiritAdvancePaper(); }

fiscalPiritXReport 'X-отчёт'  INTERNAL  'lsfusion.erp.region.ru.machinery.cashregister.fiscalpirit.FiscalPiritXReportAction' ();
fiscalXReport() + {  IF isPirit() THEN fiscalPiritXReport(); }

fiscalPiritZReport 'Z-отчет'  INTERNAL  'lsfusion.erp.region.ru.machinery.cashregister.fiscalpirit.FiscalPiritZReportAction' ();
fiscalZReport() + {  
    IF isPirit() THEN ASK 'Выполнена ли операция изъятия перед закрытием z-отчёта?' DO {
        fiscalPiritZReport();
    }    
}

fiscalPiritService 'Перемещение денег'  INTERNAL  'lsfusion.erp.region.ru.machinery.cashregister.fiscalpirit.FiscalPiritServiceInOutAction' (CashOperation);
fiscalService(CashOperation cashOperation) + {  IF isPirit() THEN fiscalPiritService(cashOperation); }

fiscalPiritCancel 'Отменить чек'  INTERNAL  'lsfusion.erp.region.ru.machinery.cashregister.fiscalpirit.FiscalPiritCancelReceiptAction' (Receipt);
fiscalCancel(Receipt receipt) + {  IF isPirit() THEN fiscalPiritCancel(receipt); }

fiscalPiritOpenZReport 'Открыть Z-отчет'  INTERNAL  'lsfusion.erp.region.ru.machinery.cashregister.fiscalpirit.FiscalPiritOpenZReportAction' ();
fiscalOpenZReport() + {  IF isPirit() THEN fiscalPiritOpenZReport(); }

saleGiftCardPaymentType 'ТИП ОПЛАТЫ ПРОДАЖИ СЕРТИФИКАТА' = DATA INTEGER (GroupCashRegister) IN base;
saleGiftCardPaymentTypeCurrentCashRegister() = saleGiftCardPaymentType(groupCashRegister(currentCashRegister()));

use1162Tag 'Использовать тег 1162' = DATA BOOLEAN (GroupCashRegister);
use1162TagCurrentCashRegister() = use1162Tag(groupCashRegister(currentCashRegister()));

EXTEND FORM groupCashRegister PROPERTIES(grc) saleGiftCardPaymentType, use1162Tag;

DESIGN groupCashRegister{
    topContainer{
        MOVE PROPERTY(saleGiftCardPaymentType(grc));
        MOVE PROPERTY(use1162Tag(grc));
    }
}

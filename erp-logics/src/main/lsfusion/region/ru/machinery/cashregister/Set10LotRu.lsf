MODULE Set10LotRu;

REQUIRE Set10WebServer, LotTypeRu, EGAISLot, ZReportLot;

NAMESPACE Set10;

productType(Sku s) += WHEN lotType(s) = LotType.tobacco THEN 'ProductCiggyEntity';
productType(Sku s) += WHEN lotType(s) = LotType.alcohol OR lotType(s) = LotType.beer THEN 'ProductSpiritsEntity';
productType(Sku s) += WHEN lotType(s) = LotType.lp THEN 'ProductPieceEntity ';

nameSet 'Наименование SET' = DATA STRING (LotType);
EXTEND FORM dialogLotTypes
    PROPERTIES (t) nameSet READONLY 
;

nameSetMigrate() {
    nameSet(LotType s) <- CASE
                        WHEN s == LotType.tobacco THEN 'TOBACCO'
                        WHEN s == LotType.shoes THEN 'FOOTWEAR'
                        WHEN s == LotType.lp THEN 'LIGHT_INDUSTRY'
                        WHEN s == LotType.milk  THEN 'MILK'
                        WHEN s == LotType.perfumery THEN 'PERFUMES'
                        WHEN s == LotType.photos THEN 'PHOTO'
                        WHEN s == LotType.tyres THEN 'TYRES'
                        WHEN s == LotType.water THEN 'WATER'
                        WHEN s == LotType.beer THEN 'BEER';              
}

@defOnStartMigrate(nameSetMigrate);

markType  = ABSTRACT CASE STRING (Sku);
markType(Sku s) += WHEN nameSet(lotType(s))  THEN nameSet(lotType(s));

//markType(Sku s) += WHEN lotType(s) = LotType.tobacco THEN 'TOBACCO';
//markType(Sku s) += WHEN lotType(s) = LotType.shoes THEN 'FOOTWEAR';
//markType(Sku s) += WHEN lotType(s) = LotType.lp THEN 'LIGHT_INDUSTRY';
//markType(Sku s) += WHEN lotType(s) = LotType.milk  THEN 'MILK';
//markType(Sku s) += WHEN lotType(s) = LotType.perfumery THEN 'PERFUMES';
//markType(Sku s) += WHEN lotType(s) = LotType.photos THEN 'PHOTO';
//markType(Sku s) += WHEN lotType(s) = LotType.tyres THEN 'TYRES';
//markType(Sku s) += WHEN lotType(s) = LotType.water THEN 'WATER';
//markType(Sku s) += WHEN lotType(s) = LotType.beer THEN 'BEER';
skipMarkBarcode = ABSTRACT CASE BOOLEAN (Sku);

skipMarkType = ABSTRACT  BOOLEAN (MachineryPriceTransaction,Barcode.Barcode);
markType (MachineryPriceTransaction t, Barcode.Barcode b) = markType(sku(b)) IF t IS MachineryPriceTransaction AND NOT skipMarkType(t,b);

EXTEND FORM goodsCatalog
    PROPERTIES  = IF lotType(sku(good)) = LotType.alcohol AND NOT skipMarkType(m,good) THEN TRUE EXTID 'excise',  
                = markType(m,good) EXTID 'mark-type'
;

EXTEND FORM barcodesCatalog
    PROPERTIES = IF markType(sku(good)) AND NOT skipMarkBarcode(sku(good)) AND NOT skipMarkType(m,good) THEN TTRUE EXTID 'marked' ATTR
;

@defineOption(createLotZReport, 'Создавать новые марки при приёме чеков', BOOLEAN, zReport);

GROUP exciseBottles;
purchaseBottle = DATA LOCAL INTEGER(INTEGER);
barcode = DATA LOCAL STRING(INTEGER);
exciseBarcode = DATA LOCAL STRING(INTEGER);
volume = DATA LOCAL STRING(INTEGER);
price = DATA LOCAL NUMERIC(INTEGER);

EXTEND FORM purchasesImport
    OBJECTS bottle = INTEGER IN exciseBottles
    FILTERS purchaseBottle(bottle) == purchase
    PROPERTIES(bottle) barcode ATTR, exciseBarcode ATTR, volume ATTR, price ATTR
;
skipSet10Lot = ABSTRACT BOOLEAN ();
importReceiptExtra(STRING purchases)+{
    IF NOT skipSet10Lot() THEN {
        LOCAL lotLoc = Lot (INTEGER);
        LOCAL receiptDetailLot = ReceiptDetail (INTEGER);

        lotLoc(INTEGER i) <- lot(exciseBarcode(i));
        receiptDetailLot(INTEGER ii) <- GROUP MAX ReceiptDetail d IF sku(lotLoc(ii)) = sku(d)
            AND idBarcode(d) = barcode(ii) AND receipt(d) = receipt(idReceipt(purchaseBottle(ii)));

        FOR Lot l = lotLoc(INTEGER ii) AND ReceiptDetail rd = receiptDetailLot(ii) DO{
            lot(rd) <- l;
        }
    }
}

overCreateReceiptDetail(ReceiptDetail d, INTEGER i) + {    
    FOR INTEGER  r == [GROUP MAX INTEGER  rr IF key1(rr) = 'excise-token' BY positionPluginPosition(rr)](i) DO {
        LOCAL lotDetail = Lot();
        lotDetail() <- lot(value1(r));
        IF NOT lotDetail() AND value1(r) AND createLotZReport() THEN {
            NEW l = Lot {
                sku(l) <- sku(d);
                id(l) <- STRING[200](value1(r));
                lotDetail() <- l;
            }
        }
        lot(d) <- lotDetail();        
    }
}

//МРЦ для алкоголя

EXTEND FORM priceEntry
    OBJECTS mrp = Barcode.Barcode EXTID 'min-price-restriction'
    PROPERTIES = idSku(mrp) EXTID 'id' ATTR, = OVERRIDE round2(minPrice(m, mrp)), round2(price(m, mrp)) EXTID 'value' ATTR,
        = IF in(m, mrp) THEN 'GOOD' EXTID 'subject-type' ATTR, = IF in(m, mrp) THEN 'MIN_PRICE' EXTID 'type' ATTR,
        = IF in(m, mrp) THEN idSku(m, mrp) EXTID 'subject-code' ATTR,
        = IF in(m, mrp) THEN idStoreSet(groupMachinery(m)) EXTID 'shop-indices'
    FILTERS minPrice(m, mrp) OR (productType(sku(mrp)) = 'ProductSpiritsEntity' AND in(m, mrp))
;

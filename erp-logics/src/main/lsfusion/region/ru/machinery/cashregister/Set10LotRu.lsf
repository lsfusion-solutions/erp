MODULE Set10LotRu;

REQUIRE Set10WebServer, LotTypeRu, EGAISLot, ZReportLot;

NAMESPACE Set10;

productType(Sku s) += WHEN lotType(s) = LotType.tobacco THEN 'ProductCiggyEntity';
productType(Sku s) += WHEN lotType(s) = LotType.alcohol OR lotType(s) = LotType.beer THEN 'ProductSpiritsEntity';
productType(Sku s) += WHEN lotType(s) = LotType.lp THEN 'ProductPieceEntity ';

markType  = ABSTRACT CASE STRING (Sku);
markType(Sku s) += WHEN lotType(s) = LotType.tobacco THEN 'TOBACCO';
markType(Sku s) += WHEN lotType(s) = LotType.shoes THEN 'FOOTWEAR';
markType(Sku s) += WHEN lotType(s) = LotType.lp THEN 'LIGHT_INDUSTRY';
markType(Sku s) += WHEN lotType(s) = LotType.dairy THEN 'MILK';
markType(Sku s) += WHEN lotType(s) = LotType.perfumery THEN 'PERFUMES';
markType(Sku s) += WHEN lotType(s) = LotType.photos THEN 'PHOTO';
markType(Sku s) += WHEN lotType(s) = LotType.tyres THEN 'TYRES';
markType(Sku s) += WHEN lotType(s) = LotType.water THEN 'WATER';
markType(Sku s) += WHEN lotType(s) = LotType.beer THEN 'BEER';
skipMarkBarcode = ABSTRACT CASE BOOLEAN (Sku);

skipMarkType = ABSTRACT  BOOLEAN (MachineryPriceTransaction,Barcode.Barcode);
markType (MachineryPriceTransaction t, Barcode.Barcode b) = markType(sku(b)) IF t IS MachineryPriceTransaction AND NOT skipMarkType(t,b);

EXTEND FORM goodsCatalog
    PROPERTIES  = IF lotType(sku(good)) = LotType.alcohol AND NOT skipMarkType(m,good) THEN TRUE EXTID 'excise',  
                = markType(m,good) EXTID 'mark-type'
;

EXTEND FORM barcodesCatalog
    PROPERTIES = IF markType(sku(good)) AND NOT skipMarkBarcode(sku(good)) AND NOT skipMarkType(m,good) THEN TTRUE EXTID 'marked' ATTR
;

@defineOption(createLotZReport, 'Создавать новые марки при приёме чеков', BOOLEAN, zReport);

overCreateReceiptDetail(ReceiptDetail d, INTEGER i) + {
    IF key(i) = 'excise-token' THEN {
        LOCAL lotDetail = Lot();
        lotDetail() <- lot(value(i));
        IF NOT lotDetail() AND value(i) AND createLotZReport() THEN {
            NEW l = Lot {
                sku(l) <- sku(d);
                id(l) <- STRING[200](value(i));
                lotDetail() <- l;                
            }            
        }
        lot(d) <- lotDetail();
    }
}

//МРЦ для алкоголя

EXTEND FORM priceEntry
    OBJECTS mrp = Barcode.Barcode EXTID 'min-price-restriction'
    PROPERTIES = idSku(mrp) EXTID 'id' ATTR, = OVERRIDE round2(minPrice(m, mrp)), round2(price(m, mrp)) EXTID 'value' ATTR,
        = IF in(m, mrp) THEN 'GOOD' EXTID 'subject-type' ATTR, = IF in(m, mrp) THEN 'MIN_PRICE' EXTID 'type' ATTR,
        = IF in(m, mrp) THEN idSku(m, mrp) EXTID 'subject-code' ATTR,
        = IF in(m, mrp) THEN idStoreSet(groupMachinery(m)) EXTID 'shop-indices'
    FILTERS minPrice(m, mrp) OR (productType(sku(mrp)) = 'ProductSpiritsEntity' AND in(m, mrp))
;

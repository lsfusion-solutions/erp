MODULE InvoiceVATRu;

REQUIRE Invoice, LegalEntityRu;

NAMESPACE Invoice;

META defineWithVATInvoice (part)
    @defineDocumentInterfaceProperty (invoice, withVATPrice, 'Цена с НДС');
    
    hideWithoutVAT (Invoice i) += withVATPrice(i);  
    hideWithVAT (Invoice i) += i IS Invoice AND NOT withVATPrice(i);
    
    changeReverseNumberVAT (UserInvoiceDetail userInvoiceDetail)  { 
        DIALOG rangeDialog OBJECTS c = countryCustomerStock(userInvoiceDetail), r = VAT(userInvoiceDetail) INPUT NULL DO {
            VAT (userInvoiceDetail) <- r;
            price (userInvoiceDetail) <- round(invoicePrice(userInvoiceDetail) * 100.0 /(100.0 (+) calcValueVAT(userInvoiceDetail)), defaultRound(currency(userInvoiceDetail)));
        }
    }
    
    changeReverseValueVAT (UserInvoiceDetail userInvoiceDetail)  { 
        INPUT n = NUMERIC[10,5] DO {
            valueVAT (userInvoiceDetail) <- n;
            VAT(userInvoiceDetail) <- valueCurrentVAT(country###part##Stock(userInvoiceDetail), n);
            price (userInvoiceDetail) <- round(invoicePrice(userInvoiceDetail) * 100.0 /(100.0 (+) calcValueVAT(userInvoiceDetail)), defaultRound(currency(userInvoiceDetail)));
        }
    }
    
    EXTEND FORM userInvoice
        PROPERTIES (i) withVATPrice
        PROPERTIES  reverseValueVAT 'НДС, %' = valueVAT(d) AFTER invoicePrice(d) ON CHANGE changeReverseValueVAT(d) SHOWIF withVATPrice(d),
                    reverseNumberVAT 'НДС, номер' = numberVAT(d) AFTER invoicePrice(d) ON CHANGE changeReverseNumberVAT(d) SHOWIF withVATPrice(d) 
                   
    ;
    
    DESIGN userInvoice {
        headerExtraParams {
            NEW VATContainer {
                caption = 'НДС';
                MOVE PROPERTY (withVATPrice(i));            
            }
        }
        PROPERTY (invoicePrice(d)){
            background = RGB(212,255,212);
        }
    }
       
    EXTEND FORM invoices
        PROPERTIES  READONLY reverseValueVAT 'НДС, %' = valueVAT(d) AFTER price(d) SHOWIF withVATPrice(d),
                    reverseNumberVAT 'НДС, номер' = numberVAT(d) AFTER price(d) SHOWIF withVATPrice(d) 
                   
    ;
    
    setWithVAT (UserInvoice i) = IF priceListType(i) 
        THEN includeVAT(priceListType(i)) AND isVATPayer(##part(i))
        ELSE isVATPayer(##part(i)); 
    withVATPrice (UserInvoice i) <- setWithVAT(i) 
        WHEN CHANGED(##part(i)) OR CHANGED(priceListType(i));
    
    ON LOCAL {
        FOR (CHANGED(quantity(UserInvoiceDetail d)) OR CHANGED(invoicePrice(d)) OR CHANGED(currency (d))) AND withVATPrice(d) DO
            invoiceSum(d) <- NUMERIC[18,4](round(quantity(d) * invoicePrice(d), currency(d))); 
        FOR (CHANGED(invoiceSum(UserInvoiceDetail d)) OR CHANGED(valueVAT (d)) OR CHANGED(currency (d))) AND withVATPrice(d) DO 
            VATSum(d) <- NUMERIC[18,4](round(invoiceSum(d) * valueVAT(d) / (100 (+) valueVAT (d)), currency(d)));
        FOR (CHANGED(invoiceSum(UserInvoiceDetail d)) OR CHANGED (VATSum(d))) AND withVATPrice(d) DO
            sum(d) <- NUMERIC[18,4](invoiceSum(d) (-) VATSum(d));
    }

END
MODULE WriteOffRu;

REQUIRE WriteOff, LegalEntityRu, PurchaseShipment, Bank;

NAMESPACE WriteOff;

id 'Код' = DATA STRING[100] (Reason) IN id INDEXED CHARWIDTH 10;

EXTEND FORM reason
    PROPERTIES(r) id 
;

EXTEND FORM reasons
    PROPERTIES(r) READONLY BEFORE name(r) id
;

EXTEND FORM userWriteOff
    PROPERTIES(d) nameReason
;

EXTEND FORM writeOffs
    PROPERTIES(d) READONLY nameReason
;

idReason 'Код списания' (WriteOffDetail d) = id(reason(d));

okpo 'ОКПО' (WriteOff writeOff) = okpo(legalEntityStock (writeOff));
okdp 'ОКДП' (WriteOff writeOff) = okpo(legalEntityStock (writeOff));
positionResponsiblePerson 'Материально ответственное лицо' (WriteOff writeOff) = namePosition(responsiblePerson(stock(writeOff)));
shortNameResponsiblePerson 'Материально ответственное лицо' (WriteOff writeOff) = shortName(responsiblePerson(stock(writeOff)));

code 'Код' (WriteOffDetail d)  = OVERRIDE id(sku(d)), idBarcodeSku(d);
idUOM (WriteOffDetail d)  = idUOM(sku(d));
netWeightSku 'Вес нетто' (WriteOffDetail d) = netWeight(sku(d));
netWeight 'Вес нетто' (WriteOffDetail d) = netWeightSku(d) * quantity(d);

invoiceDetail (WriteOffDetail d) = invoiceDetail(batch(d));
invoice(WriteOffDetail d) = invoice(invoiceDetail(batch(d)));
numberInvoice 'Номер накладной' (WriteOffDetail d) = number(invoice(d));
dateInvoice 'Дата накладной' (WriteOffDetail d) = date(invoice(d));

shortNameChief 'Фамилия И.О. '  (WriteOff w) = shortName(chief(legalEntityStock(w),date(w)));
namePositionChief 'Должность'  (WriteOff w) = namePosition(chief(legalEntityStock(w),date(w)));

innLegalEntityStock 'ИНН' (WriteOff writeOff) = inn(legalEntityStock(writeOff));
phoneLegalEntityStock 'Телефон' (WriteOff writeOff) = phone(legalEntityStock(writeOff));
numberAccountLegalEntityStock 'Основной р/сч.' (WriteOff writeOff) = numberAccount(legalEntityStock(writeOff));
nameBankLegalEntityStock 'Банк' (WriteOff writeOff) = nameBank(legalEntityStock(writeOff));
MFOBankLegalEntityStock 'BIC' (WriteOff writeOff) = MFOBank(legalEntityStock(writeOff));
corrAccountLegalEntityStock 'к/сч.' (WriteOff writeOff) = corrAccount(legalEntityStock(writeOff));

FORM writeOffTorg16A 'ТОРГ-16'
    OBJECTS w=WriteOff PANEL 
    PROPERTIES (w) fullNameLegalEntityStock,
                   fullNameStock,
                   number, series, date, 
                   okpo, okdp,
                   nameReason                   
    
    OBJECTS wd=WriteOffDetail SUBREPORT
    PROPERTIES (wd) date, number, idReason, nameReason, dateInvoice, numberInvoice
    FILTERS (wd = GROUP LAST WriteOffDetail d IF invoice(wd) == invoice(d) AND batch(wd) IS Batch AND invoice(wd) AND reason(wd) == reason(d) ORDER d),
            in(w, wd)  
; 
           
FORM writeOffTorg16B 'ТОРГ-16 (оборотная сторона)'  
    OBJECTS w=WriteOff     
    PROPERTIES (w)  namePositionChairmanWriteOffCommittee, shortNameChairmanWriteOffCommittee,
                    sumWriteOffDetail, positionResponsiblePerson, shortNameResponsiblePerson

    OBJECTS d=WriteOffDetail SUBREPORT 
        PROPERTIES (d) READONLY nameSku, code, shortNameUOMSku, idUOM,
                       quantity, netWeight, price, sum 

    OBJECTS e=Employee SUBREPORT 
    PROPERTIES(e) shortName, namePosition
    FILTERS in(w, d),
            inWriteOffCommittee(w, e)
;

FORM writeOffTorg16AB 'ТОРГ-16'
    OBJECTS wA=WriteOff PANEL 
    PROPERTIES (wA) fullNameLegalEntityStock, innLegalEntityStock, addressLegalEntityStock, phoneLegalEntityStock,
                    numberAccountLegalEntityStock, nameBankLegalEntityStock, MFOBankLegalEntityStock, corrAccountLegalEntityStock,
                    fullNameStock,
                    number, series, date, 
                    okpo, okdp,
                    nameReason, 
                    shortNameChief, namePositionChief                   
    
    OBJECTS wdA=WriteOffDetail SUBREPORT
    PROPERTIES (wdA) date, number, idReason, nameReason, dateInvoice, numberInvoice
    FILTERS (wdA = GROUP LAST WriteOffDetail d IF invoice(wdA) == invoice(d) AND batch(wdA) IS Batch AND invoice(wdA) AND reason(wdA) == reason(d) ORDER d),
            in(wA, wdA)  
          
    OBJECTS wB=WriteOff PANEL            
    PROPERTIES (wB)  namePositionChairmanWriteOffCommittee, shortNameChairmanWriteOffCommittee,
                    sumWriteOffDetail, positionResponsiblePerson, shortNameResponsiblePerson

    OBJECTS wdB=WriteOffDetail SUBREPORT 
        PROPERTIES (wdB) READONLY nameSku, code, shortNameUOMSku, idUOM,
                       quantity, netWeight, price, sum 

    OBJECTS e=Employee SUBREPORT 
    PROPERTIES(e) shortName, namePosition
    FILTERS in(wB, wdB),
            inWriteOffCommittee(wB, e)
;

printWriteOffTorg16 'ТОРГ-16' (WriteOff writeOff)  {
    PRINT writeOffTorg16AB OBJECTS wA = writeOff, wB = writeOff;
} IMAGE 'print.png' IN print;

EXTEND FORM writeOffs
    PROPERTIES (w) printWriteOffTorg16
;

DESIGN writeOffs{
    printContainer{
        MOVE PROPERTY(printWriteOffTorg16(w));
    }
}
MODULE VolumeAccountWriteOffLotRu;

REQUIRE VolumeAccountLotRu, WriteOffLot;

NAMESPACE WriteOff;

idVolumeAccountLot (WriteOffDetail detail) = idVolumeAccountLot(WriteOff.sku(detail), quantity(detail));

volumeAccount (WriteOffDetail detail) = volumeAccount(sku(detail));
skipCheckDocumentLot(UserWriteOffDetail detail) += volumeAccount(detail);

setVolumeLot (WriteOffDetail detail)  {
    FOR Lot l = lot(idVolumeAccountLot(detail)) DO {
        IF NOT quantity(detail, l) THEN {
            quantity(detail, Lot l1) <- NULL;
            quantity(detail, l) <- 1.0;
        }
    } ELSE {
        NEW l = Lot {
            id(l) <- idVolumeAccountLot(detail);
            sku(l) <- sku(detail);
            quantity(detail, Lot l1) <- NULL;
            quantity(detail, l) <- 1.0;
        }
    }
}

setVolumeAccountLot 'Заполнить марки по ОСУ' (WriteOffDetail detail) {
    IF volumeAccount(detail) THEN {
        setVolumeLot(detail);
    }
} ASON CONTEXTMENU quantityLot[UserWriteOffDetail];

setVolumeAccountLotUserInvoice 'Заполнить марки по ОСУ (все строки)' (WriteOffDetail detail) {
    FOR writeOff(detail)=writeOff(WriteOffDetail detail1) AND volumeAccount(detail1) DO {
        setVolumeLot(detail1);
    }
} ASON CONTEXTMENU quantityLot[UserWriteOffDetail];

setVolumeAccountLot 'Заполнить марки по ОСУ' (UserWriteOff writeOff) {
    FOR volumeAccount(UserWriteOffDetail detail) AND writeOff(detail)=writeOff DO {
        setVolumeLot(detail);
    }
}

WHEN LOCAL FORMS userWriteOff (SETCHANGED(quantity(UserWriteOffDetail detail)) OR CHANGED(sku(detail)))
    AND volumeAccount(detail) AND quantity(detail)>0.0 DO {
    setVolumeAccountLot(detail);
}

WHEN LOCAL FORMS userWriteOff (DROPPED(quantity(UserWriteOffDetail detail)) OR DROPPED(explicitLot(sku(detail))))
    OR (CHANGED(quantity(detail)) AND quantity(detail)=0.0)
        AND volumeAccount(detail) DO {
    quantity(detail, Lot l) <- NULL;
}
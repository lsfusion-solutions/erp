MODULE InventoryRu;

REQUIRE Inventory, LegalEntityRu;

NAMESPACE Inventory;

okpo 'ОКПО' (CollationSheet cs) = okpo(legalEntityStock(cs));
okdp 'ОКДП' (CollationSheet cs) = okdp(legalEntityStock(cs));
dateFromInventory 'Дата с инвентаризации' (CollationSheet cs) = toDate(timeFrom(inventory(cs)));
dateToInventory 'Дата по инвентаризации' (CollationSheet cs) = toDate(timeTo(inventory(cs)));
headMan  'Зав. складом' (CollationSheet cs) = headMan(inventory(cs));
shortNameHeadMan 'Зав. складом, ФИО' (CollationSheet cs) = shortName[Contact](headMan(cs)) IN documentPrm;
namePositionHeadMan 'Зав. складом, должность' (CollationSheet cs) = shortName[Contact](headMan(cs)) IN documentPrm;
shortNameChairmanCommittee 'Председатель комиссии (ФИО)' (CollationSheet cs) = OVERRIDE shortName(chairman(committee(cs))), shortName(chairman(committee(inventory(cs))));
namePositionBooker 'Главный бухгалтер (должность)' (CollationSheet cs) = namePosition(booker(legalEntityStock(cs)));
shortNameBooker 'Главный бухгалтер (ФИО)' (CollationSheet cs) = shortName(booker(legalEntityStock(cs)));

code 'Код' (Sku sku)  = OVERRIDE id(sku), idBarcode(sku);
countSku (CollationSheet cs) = GROUP SUM 1 IF include(cs, Sku i);

FORM printINV3 'ИНВ-3'
    OBJECTS cs=CollationSheet PANEL SUBREPORT
    PROPERTIES (cs) fullNameLegalEntityStock, fullNameStock, okpo, okdp, name, date, dateFromInventory, dateToInventory, 
        shortNameHeadMan, namePositionHeadMan, shortNameChairmanCommittee, namePositionChairman,
        quantityPageInventoryDetail, quantityBalance,
        sumPageInventoryDetail, sumBalance, countSku,
        namePositionBooker, shortNameBooker
         
    OBJECTS i=Sku    
    PROPERTIES(i) name, code, idUOM, shortNameUOM
     
    PROPERTIES(cs, i) priceBalance, quantityPageInventoryDetail, quantityBalance,
        sumPageInventoryDetail, sumBalance
    FILTERS  include(cs, i)//, quantityPageInventoryDetail(cs, i)  

    OBJECTS e=Employee
    PROPERTIES(e) shortName, namePosition
    FILTERS in(cs, e)
;

printINV3 'Сличительная ведомость' (CollationSheet cs){ 
    PRINT printINV3 OBJECTS cs = cs; 
} IMAGE 'print.png' IN print;
    
EXTEND FORM inventories
    PROPERTIES(cs) printINV3
;

DESIGN inventories {
    GROUP (print, cs) {
        columns = 1;
        type = CONTAINERH;
        
        NEW collation {
            MOVE PROPERTY (printINV3(cs)) {caption = 'ИНВ-3'; }
        }
    }
}

quantityOverage 'Кол-во излишка' (CollationSheet cs, Sku sku) = - quantityShortage(cs, sku) IF quantityShortage(cs, sku) < 0;
quantityDearth 'Кол-во недостачи' (CollationSheet cs, Sku sku) = - quantityShortage(cs, sku) IF quantityShortage(cs, sku) > 0;

sumOverage 'Кол-во излишка' (CollationSheet cs, Sku sku) = - sumShortage(cs, sku) IF sumShortage(cs, sku) < 0;
sumDearth 'Кол-во недостачи' (CollationSheet cs, Sku sku) = - sumShortage(cs, sku) IF sumShortage(cs, sku) > 0;

ruText 'Родительный падеж месяца' = DATA STRING[20] (Month);
onStarted() + {
    ruText(Month d) <- 'января' WHERE d == Month.january; 
    ruText(Month d) <- 'февраля' WHERE d == Month.february; 
    ruText(Month d) <- 'марта' WHERE d == Month.march; 
    ruText(Month d) <- 'апреля' WHERE d == Month.april; 
    ruText(Month d) <- 'мая' WHERE d == Month.may; 
    ruText(Month d) <- 'июня' WHERE d == Month.june; 
    ruText(Month d) <- 'июля' WHERE d == Month.july; 
    ruText(Month d) <- 'августа' WHERE d == Month.august; 
    ruText(Month d) <- 'сентября' WHERE d == Month.september; 
    ruText(Month d) <- 'октября' WHERE d == Month.october; 
    ruText(Month d) <- 'ноября' WHERE d == Month.november; 
    ruText(Month d) <- 'декабря' WHERE d == Month.december; 
}

ruMonth 'Родительный падеж месяца' (CollationSheet c) = ruText(extractMonth(dateToInventory(c)));

FORM printINV19 'ИНВ-19'
    // 1 страница
    OBJECTS cs=CollationSheet PANEL SUBREPORT
    PROPERTIES (cs) fullNameLegalEntityStock, fullNameStock, okpo, okdp, name, date, dateFromInventory, dateToInventory, ruMonth,
        shortNameHeadMan, namePositionHeadMan, shortNameChairmanCommittee, namePositionChairman,
        namePositionBooker, shortNameBooker
         
    // 2 страница     
    OBJECTS i=Sku    
    PROPERTIES(i) name, code, idUOM, shortNameUOM     
    PROPERTIES(cs, i) quantityOverage, quantityDearth, sumOverage, sumDearth
       
    FILTERS  include(cs, i), quantityOverage(cs, i) OR quantityDearth(cs, i)

    // 3 страница
    OBJECTS i2=Sku         
    PROPERTIES(cs, i2) quantityOverage, quantityDearth, sumOverage, sumDearth
       
    FILTERS  include(cs, i2), quantityOverage(cs, i2) OR quantityDearth(cs, i2)

    OBJECTS e=Employee
    PROPERTIES(e) shortName, namePosition
    FILTERS in(cs, e)
;

printINV19 'Сличительная ведомость' (CollationSheet cs){ 
    PRINT printINV19 OBJECTS cs = cs; 
} IMAGE 'print.png' IN print;
    
EXTEND FORM inventories
    PROPERTIES(cs) printINV19
;

DESIGN inventories {
    collation {
        MOVE PROPERTY (printINV19(cs)) {caption = 'ИНВ-19'; }
    }
}

namePositionCheif 'Руководитель (должность)' (CollationSheet cs) = OVERRIDE namePosition(chief(legalEntityStock(cs))),namePositionHeadMan(cs) ;
shortNameCheif 'Руководитель (ФИО)' (CollationSheet cs)= OVERRIDE shortName(chief(legalEntityStock(cs))),shortNameHeadMan(cs);
namePositionChairmanCommittee 'Председатель комиссии (должность)' (CollationSheet cs) = OVERRIDE namePosition(chairman(committee(cs))), namePosition(chairman(committee(inventory(cs))));

fromPeriod 'С' (inventory) = DATA DATE (Inventory) IN documentPrm;
toPeriod 'По' (inventory) = DATA DATE (Inventory) IN documentPrm;
dateFromPeriod 'Отчетный период с' (CollationSheet cs) = fromPeriod(inventory(cs));
dateToPeriod 'Отчетный период по' (CollationSheet cs) = toPeriod(inventory(cs));

overItem 'Излишки'  = GROUP SUM sumShortage(CollationSheet cs, Sku sku) IF include(cs, sku) AND sumShortage(cs, sku)<0 BY cs, skuType(sku);
deficitItem 'Недостача'  = GROUP SUM sumShortage(CollationSheet cs, Sku sku) IF include(cs, sku) AND sumShortage(cs, sku)>0 BY cs,skuType(sku);


FORM printINV26 'ИНВ-26'

    OBJECTS cs=CollationSheet PANEL SUBREPORT
    PROPERTIES (cs) fullNameLegalEntityStock, fullNameStock, okpo, okdp, number, date, dateFromPeriod, dateToPeriod,
        namePositionCheif, shortNameCheif, shortNameBooker, shortNameChairmanCommittee, namePositionChairmanCommittee
    
    OBJECTS st=SkuType
    PROPERTIES (st) staticCaption
    
    PROPERTIES (cs,st) overItem, deficitItem
    FILTERS overItem(cs,st)!=0 OR deficitItem(cs,st)!=0
    
    OBJECTS e=Employee
    PROPERTIES(e) shortName, namePosition
    FILTERS in(cs, e)
;

printINV26 'ИНВ-26' (CollationSheet cs){ 
    PRINT printINV26 OBJECTS cs = cs; 
} IMAGE 'print.png' IN print;

EXTEND FORM inventories
    PROPERTIES(cs) printINV26
;

DESIGN inventories{
    collation{
        MOVE PROPERTY (printINV26(cs));
    }
}

EXTEND FORM inventory
    PROPERTIES (in) fromPeriod, toPeriod
;

DESIGN inventory {
    row4 {
        NEW period {
            caption = 'Отчетный период';
               MOVE PROPERTY(fromPeriod(in));
               MOVE PROPERTY(toPeriod(in));
        }
    }
}

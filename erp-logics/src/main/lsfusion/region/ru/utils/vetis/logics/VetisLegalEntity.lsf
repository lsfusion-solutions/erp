MODULE VetisLegalEntity;

REQUIRE VetisBusinessEntity, LegalEntityRu;

NAMESPACE Vetis;

businessEntity = DATA BusinessEntity (LegalEntity) INDEXED;
guidBusinessEntity 'Код ХС' (LegalEntity st) = guid(businessEntity(st));
nameBusinessEntity 'Наименование ХС' (LegalEntity st) = name(businessEntity(st));

// auto link
WHEN SET(inn(LegalEntity l) = inn(BusinessEntity b)) AND NOT businessEntity(l) DO
    businessEntity(l) <- b; 

// warehouse
EXTEND FORM legalEntity
    PROPERTIES(l) guidBusinessEntity, nameBusinessEntity
;

DESIGN legalEntity {
    column1 {
        NEW vetis {
            type = CONTAINERH;
            caption = 'Ветис';
            MOVE PROPERTY(guidBusinessEntity(l));
            MOVE PROPERTY(nameBusinessEntity(l));
        }
    }
}

nameLegalEntities 'Организации' (BusinessEntity b) = GROUP CONCAT name(LegalEntity l) IF businessEntity(l) == b , ', ' ORDER name(l), l CHARWIDTH 20;

EXTEND FORM businessEntities 
    PROPERTIES(o) READONLY nameLegalEntities BEFORE inn(o)
;

MODULE VetisItem;

REQUIRE Item, VetisProductItem, VetisUnit;

NAMESPACE Vetis;

in = DATA BOOLEAN (Item, ProductItem);

guidProductItem 'Коды номенклатуры (Ветис)' (Item i) = GROUP CONCAT guid(ProductItem pi) IF in(i, pi), ', ' ORDER pi CHARWIDTH 10;
nameProductItem 'Номенклатуры (Ветис)' (Item i) = GROUP CONCAT name(ProductItem pi) IF in(i, pi), ', ' ORDER pi CHARWIDTH 20;

skipCheckProductItem 'Отключить запрет на соответствии Номенклатуры (Ветис) к нескольким товарам' = DATA BOOLEAN ();

EXTEND FORM integrationData
    PROPERTIES() skipCheckProductItem
;

DESIGN integrationData {
    vetis {
        MOVE PROPERTY(skipCheckProductItem());
    }
}

CONSTRAINT CHANGED([GROUP SUM 1 IF in(Item i, ProductItem pi) BY pi] (ProductItem p)) AND  [GROUP SUM 1 IF in(Item i, ProductItem pi) BY pi] (p) > 1 AND NOT skipCheckProductItem() MESSAGE 'Код номенклатуры ветис не может быть привязан более чем к 1 товар';
                                                                                         
productItem = DATA ProductItem (Item) INDEXED;
//guidProductItem 'Код номенклатуры (Ветис)' (Item i) = guid(productItem(i)) IN itemBase;
//nameProductItem 'Номенклатура (Ветис)' (Item i) = name(productItem(i)) IN itemBase;

addProductItem 'Добавить' (Item i){
    DIALOG productItems OBJECTS o INPUT DO {
        in(i, o) <- TRUE;
    }
}
deleteProductItem 'Отключить' (Item i, ProductItem pi) {
    in(i, pi) <- NULL;
}

EXTEND FORM item
    OBJECTS pi = ProductItem
    PROPERTIES(pi) READONLY guid, name
    PROPERTIES addProductItem(i) DRAW pi TOOLBAR, deleteProductItem(i, pi) GRID
    FILTERS in(i, pi)
;

DESIGN item {
    itemDetail {
        MOVE BOX(pi) {
            caption = 'Ветис';
        }
    }
}

EXTEND FORM items
    PROPERTIES(i) nameProductItem
;

DESIGN items {
    PROPERTY(nameProductItem(i)) { hide = TRUE; }
}

// product item
idItem 'Коды товаров' (ProductItem pi) = GROUP CONCAT id(Item i) IF in(i, pi), ',' ORDER i CHARWIDTH 10;
nameItem 'Товары' (ProductItem pi) = GROUP CONCAT name(Item i) IF in(i, pi), ',' ORDER i CHARWIDTH 20;

//idItem 'Коды товаров' (ProductItem pi) = GROUP CONCAT id(Item i) IF productItem(i) = pi, ',' ORDER i CHARWIDTH 10;
//nameItem 'Товары' (ProductItem pi) = GROUP CONCAT name(Item i) IF productItem(i) = pi, ',' ORDER i CHARWIDTH 20;

EXTEND FORM productItems
    PROPERTIES(o) READONLY idItem, nameItem
;

// auto link
//WHEN SET(id(Barcode b) = globalID(ProductItem i) AND active(b)) AND NOT productItem(sku(b)) DO
//    productItem(sku(b)) <- i;

WHEN SET(id(Barcode b) = globalID(ProductItem i) AND active(b)) DO
    in(sku(b), i) <- TRUE;

// factor
factorItem 'Коэффициент в литры/килограммы' = DATA NUMERIC[14,3] (Unit);
volume 'Объем' = DATA BOOLEAN (Unit);
weight 'Вес' = DATA BOOLEAN (Unit);
EXTEND FORM unit PROPERTIES(o) volume, weight, factorItem;
EXTEND FORM units PROPERTIES(o) READONLY volume, weight, factorItem;

volume(Unit u) <- nameCommonUnit(u) = 'мл' WHEN SET(commonUnit(u)); 
weight(Unit u) <- nameCommonUnit(u) = 'мг' OR nameCommonUnit(u) = 'кг' WHEN SET(commonUnit(u)); 
factorItem(Unit u) <- 1000.0 IF (name(u) = 'мл' OR name(u) = 'мг') WHEN SET(u IS Unit); 

factorResult (Unit u) = NUMERIC[17,6]((OVERRIDE factorItem(commonUnit(u)), (1.0 IF u IS Unit)) / factor(u)) MATERIALIZED;

factor 'Коэффициент' (Item i, Unit u) = (OVERRIDE volume(i) IF volume(u), netWeight(i) IF weight(u)) * factorResult(u); 
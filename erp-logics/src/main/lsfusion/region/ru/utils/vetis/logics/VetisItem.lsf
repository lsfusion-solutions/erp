MODULE VetisItem;

REQUIRE Item, VetisProductItem, VetisUnit;

NAMESPACE Vetis;

productItem = DATA ProductItem (Item) INDEXED;
guidProductItem 'Код номенклатуры (Ветис)' (Item i) = guid(productItem(i)) IN itemBase;
nameProductItem 'Номенклатура (Ветис)' (Item i) = name(productItem(i)) IN itemBase;

EXTEND FORM item
    PROPERTIES(i) guidProductItem, nameProductItem
;

EXTEND FORM items
    PROPERTIES(i) nameProductItem
;

DESIGN items {
    PROPERTY(nameProductItem(i)) { hide = TRUE; }
}

// product item
idItem 'Коды товаров' (ProductItem pi) = GROUP CONCAT id(Item i) IF productItem(i) = pi, ',' ORDER i CHARWIDTH 10;
nameItem 'Товары' (ProductItem pi) = GROUP CONCAT name(Item i) IF productItem(i) = pi, ',' ORDER i CHARWIDTH 20;

EXTEND FORM productItems
    PROPERTIES(o) READONLY idItem, nameItem
;

// auto link
WHEN SET(id(Barcode b) = globalID(ProductItem i) AND active(b)) AND NOT productItem(sku(b)) DO
    productItem(sku(b)) <- i; 

// factor
factorItem 'Коэффициент в литры/килограммы' = DATA NUMERIC[14,3] (Unit);
volume 'Объем' = DATA BOOLEAN (Unit);
weight 'Вес' = DATA BOOLEAN (Unit);
EXTEND FORM unit PROPERTIES(o) volume, weight, factorItem;
EXTEND FORM units PROPERTIES(o) READONLY volume, weight, factorItem;

volume(Unit u) <- nameCommonUnit(u) = 'мл' WHEN SET(commonUnit(u)); 
weight(Unit u) <- nameCommonUnit(u) = 'мг' OR nameCommonUnit(u) = 'кг' WHEN SET(commonUnit(u)); 
factorItem(Unit u) <- 1000.0 IF (name(u) = 'мл' OR name(u) = 'мг') WHEN SET(u IS Unit); 

factorResult (Unit u) = NUMERIC[17,6]((OVERRIDE factorItem(commonUnit(u)), (1.0 IF u IS Unit)) / factor(u)) MATERIALIZED;

factor 'Коэффициент' (Item i, Unit u) = (OVERRIDE volume(i) IF volume(u), netWeight(i) IF weight(u)) * factorResult(u); 
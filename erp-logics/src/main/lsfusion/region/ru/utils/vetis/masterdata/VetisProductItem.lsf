MODULE VetisProductItem;

REQUIRE Vetis, VetisEnterprise;

NAMESPACE Vetis;

// product type
CLASS ProductType 'Тип продукции';
TABLE productType (ProductType);

id 'Код' = DATA INTEGER (ProductType);
productType 'Тип продукции' = GROUP AGGR ProductType i BY id(i);
name 'Наименование' = DATA ISTRING (ProductType);

onStarted() + {
    IF NOT productType(1) THEN NEW p = ProductType { id(p) <- 1; name(p) <- 'Мясо и мясопродукты'; }
    IF NOT productType(2) THEN NEW p = ProductType { id(p) <- 2; name(p) <- 'Корма и кормовые добавки'; }
    IF NOT productType(3) THEN NEW p = ProductType { id(p) <- 3; name(p) <- 'Живые животные'; }
    IF NOT productType(4) THEN NEW p = ProductType { id(p) <- 4; name(p) <- 'Лекарственные средства'; }
    IF NOT productType(5) THEN NEW p = ProductType { id(p) <- 5; name(p) <- 'Пищевые продукты'; }
    IF NOT productType(6) THEN NEW p = ProductType { id(p) <- 6; name(p) <- 'Непищевые продукты и другое'; }
    IF NOT productType(7) THEN NEW p = ProductType { id(p) <- 7; name(p) <- 'Рыба и морепродукты'; }
    IF NOT productType(8) THEN NEW p = ProductType { id(p) <- 8; name(p) <- 'Продукция, не требующая разрешения'; }
}

FORM productType 'Тип продукции'
    OBJECTS o = ProductType PANEL
    PROPERTIES(o) id, name
    
    EDIT ProductType OBJECT o
;

FORM productTypes 'Типы продукции'
    OBJECTS o = ProductType
    PROPERTIES(o) READONLY id, name
    PROPERTIES(o) NEWSESSION NEW, EDIT, DELETE
    
    LIST ProductType OBJECT o
;

FORM dialogProductTypes 'Типы продукции'
    OBJECTS o = ProductType
    PROPERTIES(o) READONLY id, name
    
    LIST ProductType OBJECT o
;

// product
CLASS Product 'Продукция';
TABLE product (Product);

guid 'Код' = DATA STRING (Product) CHARWIDTH 36 INDEXED;
product 'Продукция' = GROUP AGGR Product i BY guid(i);

name 'Наименование' = DATA ISTRING (Product) CHARWIDTH 20;

productType = DATA ProductType (Product);
nameProductType 'Тип продукции' (Product p) = name(productType(p));

inactive 'Неактивен' = DATA BOOLEAN (Product);

FORM product 'Продукция'
    OBJECTS o = Product PANEL
    PROPERTIES(o) guid, name, nameProductType, inactive
    
    EDIT Product OBJECT o
;

FORM products 'Продукция'
    OBJECTS o = Product
    PROPERTIES(o) READONLY guid, name, nameProductType, inactive
    PROPERTIES(o) NEWSESSION NEW, EDIT, DELETE
    
    LIST Product OBJECT o
;

EXTEND FORM productType
    OBJECTS p = Product 
    PROPERTIES(p) READONLY guid, name, nameProductType, inactive
    FILTERS productType(p) = o
;

// subProduct
CLASS SubProduct 'Вид продукции';
TABLE subProduct (SubProduct);

guid 'Код' = DATA STRING (SubProduct) CHARWIDTH 36 INDEXED;
subProduct 'Вид продукции' = GROUP AGGR SubProduct i BY guid(i);

name 'Наименование' = DATA ISTRING (SubProduct) CHARWIDTH 20;

product = DATA Product (SubProduct);
nameProduct 'Продукция' (SubProduct s) = name(product(s));

inactive 'Неактивен' = DATA BOOLEAN (SubProduct);

FORM subProduct 'Вид продукции'
    OBJECTS o = SubProduct PANEL
    PROPERTIES(o) guid, name, nameProduct, inactive
    
    EDIT SubProduct OBJECT o
;

FORM subProducts 'Виды продукции'
    OBJECTS o = SubProduct
    PROPERTIES(o) READONLY guid, name, nameProduct, inactive
    PROPERTIES(o) NEWSESSION NEW, EDIT, DELETE
    
    LIST SubProduct OBJECT o
;

EXTEND FORM product
    OBJECTS s = SubProduct 
    PROPERTIES(s) READONLY guid, name, nameProduct, inactive
    FILTERS product(s) = o
;

// product item
CLASS ProductItem 'Номенклатура';
TABLE productItem (ProductItem);

guid 'Код' = DATA STRING (ProductItem) CHARWIDTH 36 INDEXED;
productItem 'Номенклатура' = GROUP AGGR ProductItem i BY guid(i);

globalID 'GTIN' = DATA STRING (ProductItem) CHARWIDTH 14 INDEXED;
productItemGlobalID 'Номенклатура' = GROUP MAX ProductItem i BY globalID(i);

name 'Наименование' = DATA ISTRING (ProductItem) CHARWIDTH 20;
productItemName 'Номенклатура' = GROUP MAX ProductItem i BY name(i);

code 'Артикул' = DATA ISTRING (ProductItem);

subProduct = DATA SubProduct (ProductItem);
nameSubProduct 'Вид продукции' (ProductItem i) = name(subProduct(i));

producer = DATA BusinessEntity (ProductItem);
nameProducer 'Производитель' (ProductItem i) = name(producer(i));

inactive 'Неактивен' = DATA BOOLEAN (ProductItem);

FORM productItem 'Номенклатура'
    OBJECTS o = ProductItem PANEL
    PROPERTIES(o) guid, name, globalID, code, nameSubProduct, nameProducer, inactive
    
    EDIT ProductItem OBJECT o
;

FORM productItems 'Номенклатура'
    
    TREE tree a = BPSTRING[3], pt = ProductType, p = Product, sp = SubProduct
    PROPERTIES READONLY VALUE(a), name(pt), name(p), name(sp)
    
    FILTERS a = 'Все',
            productType(p) = pt,
            product(sp) = p
    
    FILTERGROUP activeTree
        FILTER 'Активные' NOT inactive(p) AND NOT inactive(sp) DEFAULT

    OBJECTS o = ProductItem
    PROPERTIES(o) READONLY guid, name, globalID, code, nameSubProduct, nameProducer, inactive
    PROPERTIES(o) NEWSESSION NEW, EDIT, DELETE

    FILTERGROUP active
        FILTER 'Активная' NOT inactive(o) DEFAULT
    
    FILTERS productType(product(subProduct(o))) = pt OR NOT pt,
            product(subProduct(o)) = p OR NOT p,
            subProduct(o) = sp OR NOT sp

    LIST ProductItem OBJECT o
;

DESIGN productItems {
    OBJECTS {
        NEW pane {
            fill = 1;
            type = SPLITH;
            MOVE BOX(TREE tree) { fill = 0.3; }
            MOVE BOX(o);
        }
    }
}

NAVIGATOR {
    vetisMasterData {
        NEW productTypes;
        NEW products;
        NEW subProducts;
        NEW productItems;
    }
}

MODULE VetisStock;

REQUIRE Stock, VetisEnterprise,
        Warehouse, Store,
        VetisLegalEntity;

NAMESPACE Vetis;

enterprise = DATA Enterprise (Stock) INDEXED;
guidEnterprise 'Код площадки' (Stock st) = guid(enterprise(st));
nameEnterprise 'Наименование площадки' (Stock st) = name(enterprise(st));

CONSTRAINT enterprise(Stock st) AND NOT owner(enterprise(st)) = businessEntity(legalEntity(st))
    CHECKED BY enterprise[Stock]
    MESSAGE 'ХС организации склада должен совпадать с владельцем площадки склада'; 

// warehouse
EXTEND FORM warehouse
    PROPERTIES(w) guidEnterprise, nameEnterprise
;

DESIGN warehouse {
    headerExtraParams {
        MOVE PROPERTY(guidEnterprise(w));
        MOVE PROPERTY(nameEnterprise(w));
    }
}

// store
EXTEND FORM departmentStore
    PROPERTIES(d) guidEnterprise, nameEnterprise
;

DESIGN departmentStore {
    headerExtraParams {
        MOVE PROPERTY(guidEnterprise(d));
        MOVE PROPERTY(nameEnterprise(d));
    }
}

// META 
META defineDocumentVetDocument (doc, consignor, consignee, quantity, obj, container)
    volume 'Объем в документе' = DATA NUMERIC[16,5] (###doc##Detail, VetDocument);
    
    volumeDetail 'Объем в документе' (###doc##Detail vd, VetDocument vtd) = NUMERIC[16,5](quantity(vd) * OVERRIDE factor(sku(vd), unit(vtd)), 1.0); 
    fillVolume 'Выбрать' (###doc##Detail vd, VetDocument vtd) { volume(vd, vtd) <- volumeDetail(vd, vtd); }
    
    matches (###doc##Detail vd, VetDocument vtd) = 
        consignorBusinessEntity(vtd) = businessEntity(consignor(vd)) AND
        consignorEnterprise(vtd) = enterprise(consignor##Stock(vd)) AND
        consigneeEnterprise(vtd) = enterprise(consignee##Stock(vd)) AND
        productItem(vtd) = productItem(sku(vd));
    
    fillVolume 'Заполнить' (###doc d) {
        volume(###doc##Detail vd, VetDocument vtd) <- volumeDetail(vd, vtd) 
                IF seriesNumber(vd) = issueSeriesNumberWaybill(vtd) AND matches(vd, vtd) AND status(vtd) = VetDocumentStatus.CONFIRMED
                   WHERE doc(vd) = d;
    }
    
    uuidVetDocument 'Коды ВСД' (###doc##Detail d) =
        GROUP CONCAT uuid(VetDocument s) IF volume(d, s), ',' ORDER s;
    
    volumeVetDocument 'Объем в документе' (###doc##Detail d) = GROUP SUM volume(d, VetDocument s) MATERIALIZED;
    
    EXTEND FORM doc
        OBJECTS vd = ###doc##Detail
        PROPERTIES(vd) READONLY index, idBarcodeSku, idSku, nameSku, shortNameUOMSku, quantity,
                                uuidVetDocument, volumeVetDocument
        ORDERS index(vd)
        FILTERS doc(vd) = obj,
                productItem(sku(vd)) OR volumeVetDocument(vd)

        PROPERTIES(obj) fillVolume DRAW vd TOOLBAR
        
        OBJECTS vtd = VetDocument
        PROPERTIES(vtd) READONLY nameStatus, uuid, issueDate, issueSeriesNumberWaybill, nameProductItem, volume, expiryDate
        PROPERTIES(vd, vtd)      volume, fillVolume GRID
        FILTERS matches(vd, vtd)
    
        FILTERGROUP active
            FILTER 'Подтвержденные' status(vtd) = VetDocumentStatus.CONFIRMED OR volume(vd, vtd) DEFAULT 
    ;
    
    DESIGN doc {
        container {
            NEW vetis {
                caption = 'ВСД';
                NEW vetisHeader {
                    type = CONTAINERH;
                }
                NEW vetisDetail {
                    fill = 1;
                    type = SPLITH;
                    MOVE BOX(vd);
                    MOVE BOX(vtd);
                }
            }
        }
    }
END

META defineDocumentStockEntry (doc, stock, quantity, obj, container)

    volume 'Объем в документе' = DATA NUMERIC[16,5] (###doc##Detail, StockEntry);

    volumeDetail 'Объем в документе' (###doc##Detail vd, StockEntry s) = NUMERIC[16,5](quantity(vd) * OVERRIDE factor(sku(vd), unit(s)), 1.0); 
    fillVolume 'Выбрать' (###doc##Detail d, StockEntry s) { volume(d, s) <- volumeDetail(d, s); }

    quantity (###doc##Detail vd, StockEntry s) = volume(s) / OVERRIDE factor(sku(vd), unit(s)), 1.0;
    
    matches (###doc##Detail vd, StockEntry s) = enterprise(s) = enterprise(stock(vd)) AND in(s, productItem(sku(vd)));
    
    calcQuantity(###doc##Detail vd, StockEntry s) = 
        PARTITION UNGROUP quantity LIMIT quantity(vd, s) IF matches(vd, s) ORDER expiryDate(s), s BY vd;

    fillVolume 'Заполнить' (###doc d) {
        volume(###doc##Detail vd, StockEntry s) <- NUMERIC[16,5](calcQuantity(vd, s) * OVERRIDE factor(sku(vd), unit(s)), 1.0) WHERE doc(vd) = d;
    }
    
    numberStockEntry 'Номер складского журнала' (###doc##Detail d) =
        GROUP CONCAT number(StockEntry s) IF volume(d, s), ',' ORDER s;
    
    volumeStockEntry 'Объем в документе' (###doc##Detail d) = GROUP SUM volume(d, StockEntry s) MATERIALIZED;
    
    EXTEND FORM doc
        OBJECTS vd = ###doc##Detail
        PROPERTIES(vd) READONLY index, idBarcodeSku, idSku, nameSku, shortNameUOMSku, quantity,
                                numberStockEntry, volumeStockEntry
        ORDERS index(vd)
        FILTERS doc(vd) = obj,
                productItem(sku(vd)) OR volumeStockEntry(vd)
        
        PROPERTIES(obj) fillVolume DRAW vd TOOLBAR
        
        OBJECTS se = StockEntry
        PROPERTIES(se) READONLY guid, number, nameProductItem, volume, expiryDate
        PROPERTIES(vd, se)      volume, fillVolume GRID
        FILTERS matches(vd, se)
    
        FILTERGROUP active
            FILTER 'Активные' active(se) OR volume(vd, se) DEFAULT 
    ;
    
    DESIGN doc {
        specificationBox {
            NEW vetis {
                caption = 'ВСД';
                NEW vetisHeader {
                    type = CONTAINERH;
                }
                NEW vetisDetail {
                    fill = 1;
                    type = SPLITH;
                    MOVE BOX(vd);
                    MOVE BOX(se);
                }
            }
        }
    }

END
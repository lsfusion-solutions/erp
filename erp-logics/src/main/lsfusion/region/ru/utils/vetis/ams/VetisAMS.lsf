MODULE VetisAMS;

REQUIRE Vetis, VetisBusinessEntityOwn;

NAMESPACE Vetis;

urlAMS 'Url к стенду AMS' = DATA STRING ();

initDefaultProd () + { urlAMS() <- 'https://api.vetrf.ru/platform/services/2.1/ApplicationManagementService'; }
initDefaultTest () + { urlAMS() <- 'https://api2.vetrf.ru:8002/platform/services/2.1/ApplicationManagementService'; }

serviceId = 'mercury-g2b.service:2.1';

GROUP initiator EXTID 'merc=http://api.vetrf.ru/schema/cdm/mercury/g2b/applications/v2:initiator';

EXTEND FORM integrationData
    PROPERTIES() vetisUrlAMS = urlAMS
;

DESIGN integrationData {
    vetis {
        MOVE PROPERTY(vetisUrlAMS);
    }
}

// class

CLASS ApplicationRequest 'Заявка в Ветис';
id 'Код' = DATA STRING (ApplicationRequest);

businessEntity = DATA BusinessEntity (ApplicationRequest) INDEXED;
nameBusinessEntity 'Хозяйствующий субъект' (ApplicationRequest r) = name(businessEntity(r));

apiKey (ApplicationRequest r) = apiKey(businessEntity(r));
issuerId (ApplicationRequest r) = guid(businessEntity(r));
loginMercury (ApplicationRequest r) = loginMercury(businessEntity(r));

issueDate 'Время' = DATA DATETIME (ApplicationRequest);
issueDate(ApplicationRequest r) <- currentDateTime() WHEN SET(r IS ApplicationRequest);
formatIssueDate (ApplicationRequest r) = formatT(issueDate(r));

issued 'Дата' (ApplicationRequest r) = DATE(issueDate(r));

request 'Запрос' = DATA TEXT (ApplicationRequest);
error 'Ошибка' = DATA TEXT (ApplicationRequest);

FORM applicationRequests 'Заявки'
    OBJECTS r = ApplicationRequest
    PROPERTIES(r) READONLYIF isReadonly() objectClassName, nameBusinessEntity, id, issueDate, request, error
    PROPERTIES(r) READONLY PANEL prequest = request, perror = error
;

DESIGN applicationRequests {
    OBJECTS {
        NEW pane {
            fill = 1;
            type = SPLITV;
            MOVE BOX(r);
            NEW tabbedPane {
                fill = 0.5;
                type = TABBED;
                NEW prequest {
                    caption = 'Запрос';
                    type = SCROLL;
                    fill = 1;
                    height = 1;
                    MOVE PROPERTY(prequest) { caption = ''; autoSize = TRUE; panelCaptionVertical = TRUE; }
                }
                NEW perror {
                    caption = 'Ошибка';
                    type = SCROLL;
                    fill = 1;
                    height = 1;
                    MOVE PROPERTY(perror) { caption = ''; autoSize = TRUE; panelCaptionVertical = TRUE; }
                }
            }
        }
    }
}


@extendFormEditable(applicationRequests);

NAVIGATOR {
    vetisDocuments {
        NEW applicationRequests;
    }
}

// common tags

GROUP accompanyingForms EXTID 'vd=http://api.vetrf.ru/schema/cdm/mercury/vet-document/v2:accompanyingForms';
GROUP waybill EXTID 'vd:waybill' : accompanyingForms;
GROUP relatedDocument EXTID 'vd:relatedDocument' : accompanyingForms;

GROUP vetCertificate EXTID 'vd:vetCertificate' : accompanyingForms;
GROUP vetCertificateAuthentication EXTID 'vd:authentication' : vetCertificate;
GROUP authenticationPurpose EXTID 'vd:purpose' : vetCertificateAuthentication;

GROUP batch EXTID 'vd=http://api.vetrf.ru/schema/cdm/mercury/vet-document/v2:batch';

volume = DATA LOCAL NUMERIC[16,5]();

unitGuid = DATA LOCAL STRING();

dateOfProductionYear = DATA LOCAL INTEGER ();
dateOfProductionMonth = DATA LOCAL INTEGER ();
dateOfProductionDay = DATA LOCAL INTEGER ();
dateOfProductionHour = DATA LOCAL INTEGER ();

expiryDateYear = DATA LOCAL INTEGER ();
expiryDateMonth = DATA LOCAL INTEGER ();
expiryDateDay = DATA LOCAL INTEGER ();
expiryDateHour = DATA LOCAL INTEGER ();

MODULE UpdDocument;

REQUIRE MasterData, Utils, Numerator,  Contract, Certificate, XMLUtils, VATInvoiceDocument;

NAMESPACE Upd;

@defineStaticObject(updDocumentStatus, 'Функция УПД');

EXTEND CLASS UpdDocumentStatus {
    SHCF 'СЧФ',
    DOP 'ДОП',
    SHCFDOP 'СЧФДОП'
}

@defineStaticObject(updDocumentPaperStatus, 'Статус УПД');

EXTEND CLASS UpdDocumentPaperStatus {
    first '1',
    second '2'
}

//UpdDocument

@defineVATInvoiceDocument('УПД', upd, 'СЧФ', 'Универсальный передаточный документ');

//EXTEND FORM updDocuments
//    OBJECTS v = UpdDocumentInfoDetail
//    PROPERTIES (v) READONLY info, nameInfoStatus                
//    FILTERS updDocument(v) == u
//;
//DESIGN updDocuments {
//    documentDetail{
//         MOVE BOX (v);
//    }
//}

NAVIGATOR {
    customsNavigator {
        NEW FOLDER upd 'УПД' {
            NEW updDocuments;
        }
    }
}

//-ГрузОт (только в УПД)
isConsignerSupplier 'Продавец является отправителем' = DATA BOOLEAN (UpdDocument); //ОнЖе

//-ГрузПолуч (только в УПД)
//--ИдСв
//---СвЮЛУч
nameReceiverCompany 'Грузополучатель' = DATA STRING[1000] (UpdDocument) CHARWIDTH 50; //НаимОрг
innReceiverCompany 'ИНН грузополучателя' = DATA STRING[10] (UpdDocument);//ИННЮЛ

//--ГрузПолуч (только в УПД)
//---Адрес
//----АдрИнф
countryCodeAddressReceiverCompany 'Код страны грузополучателя' = DATA STRING[3] (UpdDocument); //КодСтр
addressReceiverCompany 'Адрес грузополучателя' = DATA STRING[150] (UpdDocument);//АдрТекст

//-ДокПодтвОтгр (только УПД)
shipmentDocumentName 'Наимен. док-та отгрузки' (UpdDocument upd) = 'УПД' IF upd IS UpdDocument; //НаимДокОтгр
shipmentDocumentNumber 'Номер док-та отгрузки' (UpdDocument upd) = numberInvoice(upd) IF upd IS UpdDocument;//НомДокОтгр
shipmentDocumentDate 'Дата док-та отгрузки' (UpdDocument upd) = toDateDDMMYYYY(dateInvoice(upd)); //ДатаДокОтгр

//СвПродПер (только УПД)
//-СвПер
description 'Содержание операции' = DATA STRING[255] (UpdDocument); //СодОпер

//--ОснПер
descriptionContract 'Основание передачи' = DATA STRING[255] (UpdDocument); //НаимОсн
numberContract 'Номер документа основания' = DATA STRING[255] (UpdDocument);//НомОсн
dateContract 'Дата документа основания' = DATA DATE (UpdDocument);//ДатаОсн

//---СвЛицПер
//----РабОргПрод
positionAgentSupplier 'Должность (работник продавца)' = DATA STRING[128](UpdDocument); //Должность

//-----ФИО
lastNameAgentSupplier 'Фамилия' = DATA STRING[60](UpdDocument); //Фамилия
firstNameAgentSupplier 'Имя' = DATA STRING[60](UpdDocument); //Имя
middleNameAgentSupplier 'Отчество' = DATA STRING[60](UpdDocument); //Отчество
isAgentSupplier (UpdDocument d) = lastNameAgentSupplier(d) AND firstNameAgentSupplier(d) AND positionAgentSupplier(d);

//----ИнЛицо
//-----ПредОргПер
positionAgentExecuter 'Должность (представитель передающей орг.)' = DATA STRING[128](UpdDocument); //Должность
nameCompanyAgentExecuter 'Наименование организации (представитель передающей орг.)' = DATA STRING[128](UpdDocument); //НаимОргПер

//------ФИО
lastNameAgentExecuter 'Фамилия' = DATA STRING[60](UpdDocument); //Фамилия
firstNameAgentExecuter 'Имя' = DATA STRING[60](UpdDocument); //Имя
middleNameAgentExecuter 'Отчество' = DATA STRING[60](UpdDocument); //Отчество
isAgentExecuter (UpdDocument d) = positionAgentExecuter(d) AND nameCompanyAgentExecuter(d) AND lastNameAgentExecuter(d) AND firstNameAgentExecuter(d);
//-----ФЛПер
positionExecuter 'Должность (передающее ФЛ)' = DATA STRING[128](UpdDocument); //Должность

//------ФИО
lastNameExecuter 'Фамилия (передающее ФЛ)' = DATA STRING[60](UpdDocument); //Фамилия
firstNameExecuter 'Имя (передающее ФЛ)' = DATA STRING[60](UpdDocument); //Имя
middleNameExecuter 'Отчество (передающее ФЛ)' = DATA STRING[60](UpdDocument); //Отчество

//-СведТов
VATStatus = DATA VATStatus (UpdDocumentDetail);
nameVATStatus 'Ставка НДС' = staticCaption(VATStatus(UpdDocumentDetail d)) CHARWIDTH 7;

//details
quantity 'Кол-во товара' = DATA NUMERIC[26,11] (UpdDocumentDetail); //КолТов
price 'Цена' = DATA NUMERIC[26,11] (UpdDocumentDetail); //ЦенаТов
sum 'Сумма без НДС' = DATA NUMERIC[19,2] (UpdDocumentDetail); //СтТовБезНДС
invoiceSum 'Сумма с НДС' = DATA NUMERIC[19,2] (UpdDocumentDetail); //СтТовУчНал

//--Акциз
dutySum 'Сумма акциза' = DATA NUMERIC[19,2](UpdDocumentDetail);//СумАкциз
withoutDuty 'Без акциза' = DATA BOOLEAN (UpdDocumentDetail);//БезАкциз

//--ДопСведТов
idUOM 'ОКЕИ' = DATA STRING[4] (UpdDocumentDetail) CHARWIDTH 10; //ОКЕИ_Тов
nameUOM 'ЕИ' = DATA STRING[255] (UpdDocumentDetail) CHARWIDTH 10; //НаимЕдИзм
nameCountry 'Страна' = DATA STRING[255] (UpdDocumentDetail) CHARWIDTH 10; //КрНаимСтрПр
idSku 'Код товара' = DATA STRING[100] (UpdDocumentDetail) CHARWIDTH 15; //КодТов

//--СвТД
idCountry 'Код страны' = DATA STRING[3] (UpdDocumentDetail) CHARWIDTH 10; //КодПроисх
numberDeclaration 'Номер ТД' = DATA STRING[29] (UpdDocumentDetail) CHARWIDTH 10;//НомерТД

//--СумНал
VATSum 'Сумма НДС' = DATA NUMERIC[19,2] (UpdDocumentDetail); //СумНал

//-ВсегоОпл
sum 'Сумма без НДС' = DATA NUMERIC[19,2] (UpdDocument); //СтТовБезНДСВсего
invoiceSum 'Сумма с НДС' = DATA NUMERIC[19,2] (UpdDocument); //СтТовУчНалВсего

//--СумНалВсего 
VATSum 'Сумма НДС' = DATA NUMERIC[19,2] (UpdDocument); //СумНал
withoutVAT 'БезНДС' = DATA BOOLEAN (UpdDocument); //БезНДС

//Титул покупателя

//--СвЛицПрин
//---РабОргПок
positionAgentCustomer 'Должность (работник покупателя)' = DATA STRING[128](UpdDocument); //Должность
customerAgentCredentialsBase 'Основание полномочий (работник покупателя)' = DATA STRING[255] (UpdDocument) CHARWIDTH 50; //ОснПолн

//----ФИО
lastNameAgentCustomer 'Фамилия (работник покупателя)' = DATA STRING[60](UpdDocument); //Фамилия
firsNameAgentCustomer 'Имя (работник покупателя)' = DATA STRING[60](UpdDocument); //Имя
middleAgentCustomer 'Отчество (работник покупателя)' = DATA STRING[60](UpdDocument); //Отчество

//---ИнЛицо
//----ПредОргПрин
positionAgentReceiver 'Должность (представитель принимающей орг.)' = DATA STRING[128](UpdDocument); //Должность

//-----ФИО
lastNameAgentReceiver 'Фамилия' = DATA STRING[60](UpdDocument); //Фамилия
firsNametAgentReceiver 'Имя' = DATA STRING[60](UpdDocument); //Имя
middleNameAgentReceiver 'Отчество' = DATA STRING[60](UpdDocument); //Отчество

//----ФЛПрин
receiverAgentCredentialsBase 'Основание полномочий (принимающее ФЛ)' = DATA STRING[120](UpdDocument); //ОснДоверФЛ

//------ФИО
lastNameReceiver 'Фамилия (принимающее ФЛ)' = DATA STRING[60](UpdDocument); //Фамилия
firsNameReceiver 'Имя (принимающее ФЛ)' = DATA STRING[60](UpdDocument); //Имя
middleNameReceiver 'Отчество (принимающее ФЛ)' = DATA STRING[60](UpdDocument); //Отчество

//СодФХЖ4   
//-СвПрин
dateExceptance 'Дата принятия' = DATA DATE (UpdDocument); //ДатаПрин

//маркировка
CLASS UpdTransportPackDetail 'Транспортная упаковка';
TABLE updTransportPackDetail(UpdTransportPackDetail);

updDocumentDetail = DATA UpdDocumentDetail(UpdTransportPackDetail) NONULL DELETE AGGR;
numberDetail 'Номер строки' (UpdTransportPackDetail detail) = number(updDocumentDetail(detail));
countUpdTransportPackDetail 'Кол-во трансп.упаковок' = GROUP SUM 1 BY updDocumentDetail(UpdTransportPackDetail detail);
countUpdTransportPackDetail 'Кол-во трансп.упаковок' = GROUP SUM 1 BY updDocument(updDocumentDetail(UpdTransportPackDetail detail));
updDocument (UpdTransportPackDetail detail) = updDocument(updDocumentDetail(detail));

CLASS UpdPackDetail 'Потребительская упаковка';
TABLE updPackDetail(UpdPackDetail);

updDocumentDetail = DATA UpdDocumentDetail(UpdPackDetail) NONULL DELETE AGGR;
numberDetail 'Номер строки' (UpdPackDetail detail) = number(updDocumentDetail(detail));
countUpdPackDetail 'Кол-во упаковок' = GROUP SUM 1 BY updDocumentDetail(UpdPackDetail detail);
countUpdPackDetail 'Кол-во упаковок' = GROUP SUM 1 BY updDocument(updDocumentDetail(UpdPackDetail detail));
updDocument (UpdPackDetail detail) = updDocument(updDocumentDetail(detail));

CLASS UpdLotDetail 'Марка';
TABLE updLotDetail(UpdLotDetail);

updDocumentDetail = DATA UpdDocumentDetail(UpdLotDetail) NONULL DELETE AGGR;
numberDetail 'Номер строки' (UpdLotDetail detail) = number(updDocumentDetail(detail));
countUpdLotDetail 'Кол-во марок' = GROUP SUM 1 BY updDocumentDetail(UpdLotDetail detail);
countUpdLotDetail 'Кол-во марок' = GROUP SUM 1 BY updDocument(updDocumentDetail(UpdLotDetail detail));
updDocument (UpdLotDetail detail) = updDocument(updDocumentDetail(detail));

//--ДопСведТов
//---НомСредИдентТов
id 'Код' = DATA STRING[255] (UpdTransportPackDetail);//ИдентТрансУпак
id 'Код' = DATA STRING[255] (UpdPackDetail);//КИЗ
id 'Код' = DATA STRING[255] (UpdLotDetail);//НомУпак

fileSupplierPrefix (UpdDocument d) = 
    IF countUpdTransportPackDetail(d) OR countUpdPackDetail(d) OR countUpdLotDetail(d) 
        THEN 'ON_NSCHFDOPPRMARK'
        ELSE 'ON_NSCHFDOPPR';

idFile 'ИдФайл' (UpdDocument d) = 
    STRING[255](CONCAT '_', fileSupplierPrefix(d),  idConsignee(d), [FORMULA STRING[8] 'to_char(($1),\'YYYYMMDD\')'](dateSupplier(d)), idConsignor(d), uuid(d)) CHARWIDTH 25;

EXTEND FORM updDocument
    PROPERTIES (u) idFile
    PROPERTIES (u) isConsignerSupplier
    PROPERTIES (u) nameReceiverCompany, innReceiverCompany
    PROPERTIES (u) countryCodeAddressReceiverCompany, addressReceiverCompany
    PROPERTIES (u) description, descriptionContract, numberContract, dateContract
    PROPERTIES (u) positionAgentSupplier, lastNameAgentSupplier, firstNameAgentSupplier, middleNameAgentSupplier
    PROPERTIES (u) positionAgentExecuter, nameCompanyAgentExecuter, lastNameAgentExecuter, firstNameAgentExecuter, middleNameAgentExecuter
    PROPERTIES (u) positionExecuter, lastNameExecuter, firstNameExecuter, middleNameExecuter
    PROPERTIES (u) sum, invoiceSum, VATSum, withoutVAT
    
    PROPERTIES (u) dateExceptance
    
    PROPERTIES (u) positionAgentCustomer, customerAgentCredentialsBase, lastNameAgentCustomer, firsNameAgentCustomer, middleAgentCustomer
    PROPERTIES (u) positionAgentReceiver, lastNameAgentReceiver, firsNametAgentReceiver, middleNameAgentReceiver
    PROPERTIES (u) receiverAgentCredentialsBase, lastNameReceiver, firsNameReceiver, middleNameReceiver
    
    PROPERTIES(d) idSku BEFORE nameSku(d), quantity, price, sum,  nameVATStatus, invoiceSum, VATSum, dutySum, withoutDuty
    PROPERTIES(d) idUOM, nameUOM, idCountry, nameCountry, numberDeclaration     
;

DESIGN updDocument {
     mainContainer {
        documentContainer {
            mainDocumentAttributes {
                sums {
                    MOVE PROPERTY(sum(u));
                    MOVE PROPERTY(invoiceSum(u));
                    MOVE PROPERTY(VATSum(u));
                    MOVE PROPERTY(withoutVAT(u));                            
                }
            }
            titlesContainer {
                supplierTitle {                   
                    supplierInfo {                       
                        documentInfo {
                            documentDescription {
                                MOVE PROPERTY(description(u));
                            }
                            NEW contractInfo {
                                caption = 'Основание';
                                fill = 1;
                                type = CONTAINERV;
                                MOVE PROPERTY(descriptionContract(u));
                                MOVE PROPERTY(numberContract(u));
                                MOVE PROPERTY(dateContract(u)); 
                            }                                                        
                        }
                        
                        NEW itemTransfer {
                            caption = 'Передача';
                            fill = 1;
                            type = CONTAINERV;
                            MOVE PROPERTY(isConsignerSupplier(u));
                            NEW itemTransferTabbedContainer {
                                 fill = 1;
                                 type = TABBED;
                                 NEW supplierAgent {
                                     caption = 'Работник продавца';
                                     fill = 1;
                                     type = CONTAINERV;
                                     MOVE PROPERTY(positionAgentSupplier(u));
                                     MOVE PROPERTY(lastNameAgentSupplier(u));
                                     MOVE PROPERTY(firstNameAgentSupplier(u));
                                     MOVE PROPERTY(middleNameAgentSupplier(u));                            
                                 }
                                 NEW executerAgent {
                                     caption = 'Представитель передающей организации';
                                     fill = 1;
                                     type = CONTAINERV;
                                     MOVE PROPERTY(positionAgentExecuter(u));
                                     MOVE PROPERTY(nameCompanyAgentExecuter(u));
                                     MOVE PROPERTY(lastNameAgentExecuter(u));
                                     MOVE PROPERTY(firstNameAgentExecuter(u));
                                     MOVE PROPERTY(middleNameAgentExecuter(u));                            
                                 }
                                 NEW executer {
                                     caption = 'Передающее физлицо';
                                     fill = 1;
                                     type = CONTAINERV;
                                     MOVE PROPERTY(positionExecuter(u));
                                     MOVE PROPERTY(lastNameExecuter(u));
                                     MOVE PROPERTY(firstNameExecuter(u));
                                     MOVE PROPERTY(middleNameExecuter(u));                            
                                 }
                            }
                        }                         
                    }
                    
                    customerInfo { 
                        NEW companyReceiver {
                            caption = 'Грузополучатель';
                            fill = 1;
                            type = CONTAINERH;
                            NEW companyReceiverInfo {
                                fill = 1;
                                caption = 'Реквизиты';
                                type = CONTAINERV;
                                MOVE PROPERTY(nameReceiverCompany(u));
                                MOVE PROPERTY(innReceiverCompany(u));
                            }
                            NEW companyReceiverAddress {
                                caption = 'Адрес грузополучателя';
                                fill = 1;
                                type = CONTAINERV;
                                MOVE PROPERTY(countryCodeAddressReceiverCompany(u));
                                MOVE PROPERTY(addressReceiverCompany(u));                            
                            }
                        }
                    }                                                                     
                }
                customerTitle {
                    NEW acceptanceInfo AFTER customerInfoCT {
                        caption = 'Приемка';
                        fill = 1;
                        type = CONTAINERV;
                        MOVE PROPERTY(dateExceptance(u));                       
                    }
                    customerSignerContainer {
                        NEW customerAgent BEFORE customerSignerCredentials {
                            caption = 'Работник покупателя';
                            fill = 1;
                            type = CONTAINERV;
                            MOVE PROPERTY(positionAgentCustomer(u));
                            MOVE PROPERTY(customerAgentCredentialsBase(u));
                            MOVE PROPERTY(lastNameAgentCustomer(u));
                            MOVE PROPERTY(firsNameAgentCustomer(u));
                            MOVE PROPERTY(middleAgentCustomer(u));                            
                        }
                    }                   
                    acceptanceInfo {
                        NEW acceptanceInfoVerticalContainer {
                            alignment = STRETCH;
                            type = CONTAINERH;
                            NEW receiverAgent {
                                caption = 'Представитель принимающей организации';
                                fill = 1;
                                type = CONTAINERV;
                                MOVE PROPERTY(positionAgentReceiver(u));
                                MOVE PROPERTY(lastNameAgentReceiver(u));
                                MOVE PROPERTY(firsNametAgentReceiver(u));
                                MOVE PROPERTY(middleNameAgentReceiver(u));                            
                            }                       
                            NEW receiver {
                                caption = 'Принимающее физлицо';
                                fill = 1;
                                type = CONTAINERV;
                                MOVE PROPERTY(receiverAgentCredentialsBase(u));
                                MOVE PROPERTY(lastNameReceiver(u));
                                MOVE PROPERTY(firsNameReceiver(u));
                                MOVE PROPERTY(middleNameReceiver(u));                            
                            } 
                        }                           
                    }                    
                }
                idsContainer {
                    MOVE PROPERTY(idFile(u)) AFTER PROPERTY(uuid(u));
                }
            }
        }
    }
}

EXTEND FORM updDocuments
    PROPERTIES (u) READONLY AFTER codeCurrency(u) sum, invoiceSum, VATSum, withoutVAT    
    PROPERTIES(d) READONLY idSku BEFORE nameSku(d), quantity, price, sum, nameVATStatus, invoiceSum, VATSum, dutySum, withoutDuty
    PROPERTIES (u) READONLY idFile AFTER codeCurrency(u)
;

//иУПД
correctedUpdDocument 'иУПД' = DATA UpdDocument (UpdDocument);
correction 'иУПД' (UpdDocument upd) = TRUE IF correctedUpdDocument(upd);
correctionDateTime 'Дата/время иУПД' = DATA DATETIME (UpdDocument);
correctionDate 'Дата иУПД' (UpdDocument upd) = toDate(correctionDateTime(upd));

TABLE updDocumentUpdDocument(UpdDocument, UpdDocument);

correctionLevel (UpdDocument child, UpdDocument parent) = RECURSION 1l IF child IS UpdDocument AND parent == child
                                                              STEP 2l IF parent == correctedUpdDocument($parent) MATERIALIZED;
updDocument(UpdDocument child, LONG level)  = GROUP
                                               MAX UpdDocument parent IF level == correctionLevel(child, parent);
levelCorrection 'Номер иУПД' (UpdDocument child)  = GROUP SUM 1 IF correctionLevel(child, UpdDocument parent) MATERIALIZED;

isCorrection (UpdDocument child, UpdDocument parent) = TRUE IF correctionLevel(child, parent);
correctionNumber (updDocument) = GROUP SUM 1 BY correctedUpdDocument(UpdDocument child) MATERIALIZED;
totalCorrectionNumber 'Кол-во иУПД' (UpdDocument parent)  = GROUP SUM 1 IF isCorrection(UpdDocument child, parent) MATERIALIZED;

numberCorrection 'Номер иУПД' (UpdDocument upd) = (levelCorrection(upd) - 1) IF (levelCorrection(upd) - 1) > 0.0;

correctionUpLevel (UpdDocument child, UpdDocument parent) = RECURSION 1l IF parent IS UpdDocument AND parent == child
                                                              STEP 2l IF child == correctedUpdDocument($child) MATERIALIZED;
isUpCorrection (UpdDocument child, UpdDocument parent) = TRUE IF correctionUpLevel(child, parent);

updType 'Тип' (UpdDocument upd) = CASE 
    WHEN correctedUpdDocument(upd) THEN 'иУПД'
    WHEN upd IS UpdDocument THEN 'УПД' CHARWIDTH 5;

backgroundUpdType (UpdDocument upd) = CASE
    WHEN correctionNumber(upd) THEN RGB(255, 224, 255)
    WHEN correctedUpdDocument(upd) AND NOT correctionNumber(upd) THEN RGB(255,238,165)
    WHEN upd IS UpdDocument THEN RGB(212,255,212);

EXTEND FORM updDocuments
    PROPERTIES (u) READONLY BEFORE nameStatus(u) updType BACKGROUND backgroundUpdType(u)
    FILTERGROUP notCorrected
        FILTER 'Без измененных' NOT correctionNumber(u) DEFAULT
;

EXTEND FORM updDocument
    PROPERTIES (u) READONLY SHOWIF correction(u) correction, numberCorrection, correctionDateTime 
;

DESIGN updDocument {
    documentContainer {
        NEW correction FIRST {
            caption = 'иУПД';
            type = CONTAINERH;
            MOVE PROPERTY(correction(u));
            MOVE PROPERTY(numberCorrection(u));
            MOVE PROPERTY(correctionDateTime(u));
        }
    }    
}

GROUP UPDExportA0 EXTID  'СвОЭДОтпр';

GROUP UPDExportA EXTID  'СвСчФакт';
    GROUP UPDExportA01 EXTID 'ИспрСчФ': UPDExportA;
    GROUP UPDExportA2 EXTID  'СвПрод' :UPDExportA;   
        GROUP UPDExportA2_1 EXTID  'ИдСв' :UPDExportA2;        
            GROUP UPDExportA2_1_1 EXTID  'СвЮЛУч' :UPDExportA2_1;            
        GROUP UPDExportA2_2 EXTID  'Адрес' :UPDExportA2;
            GROUP UPDExportA2_2_1 EXTID  'АдрРФ' :UPDExportA2_2;
            GROUP UPDExportA2_2_2 EXTID  'АдрИнф' :UPDExportA2_2;
        GROUP UPDExportA2_4 EXTID  'БанкРекв' :UPDExportA2;            
            GROUP UPDExportA2_4_1 EXTID  'СвБанк' :UPDExportA2_4;     
    GROUP UPDExportA3 EXTID  'ГрузОт' :UPDExportA;
    GROUP UPDExportA4 EXTID  'ГрузПолуч' :UPDExportA;
        GROUP UPDExportA4_1 EXTID  'ИдСв' :UPDExportA4;
            GROUP UPDExportA4_1_1 EXTID  'СвЮЛУч' :UPDExportA4_1;
        GROUP UPDExportA4_2 EXTID  'Адрес' :UPDExportA4;
            GROUP UPDExportA4_2_1 EXTID  'АдрРФ' :UPDExportA4_2;
            GROUP UPDExportA4_2_2 EXTID  'АдрИнф' :UPDExportA4_2;
    GROUP UPDExportA6 EXTID  'СвПокуп' :UPDExportA;
        GROUP UPDExportA6_1 EXTID  'ИдСв' :UPDExportA6;
            GROUP UPDExportA6_1_1 EXTID  'СвЮЛУч' :UPDExportA6_1;
        GROUP UPDExportA6_2 EXTID  'Адрес' :UPDExportA6;
            GROUP UPDExportA6_2_1 EXTID  'АдрРФ' :UPDExportA6_2;
            GROUP UPDExportA6_2_2 EXTID  'АдрИнф' :UPDExportA6_2;
    GROUP UPDExportA7 EXTID 'ДокПодтвОтгр' :UPDExportA;
    GROUP UPDExportA9 EXTID  'ИнфПолФХЖ1' :UPDExportA;
        GROUP UPDExportA9_1 EXTID  'ТекстИнф' :UPDExportA9;
        
GROUP UPDExportB EXTID  'ТаблСчФакт';      
    GROUP UPDExportB1 EXTID  'Акциз';      
    GROUP UPDExportB2 EXTID  'СумНал';     
    GROUP UPDExportB3 EXTID  'СвТД';    
    GROUP UPDExportB4 EXTID  'ДопСведТов';     
GROUP UPDExportBB EXTID  'ВсегоОпл' : UPDExportB;              
    GROUP UPDExportBB_1 EXTID  'СумНалВсего' : UPDExportBB;            
            
GROUP UPDExportC EXTID  'СвПродПер';
    GROUP UPDExportC1 EXTID  'СвПер': UPDExportC;
        GROUP UPDExportC1_1 EXTID  'ОснПер': UPDExportC1;   
        GROUP UPDExportC1_2 EXTID  'СвЛицПер': UPDExportC1;           
            GROUP UPDExportC1_2_1 EXTID  'РабОргПрод': UPDExportC1_2;  
                GROUP UPDExportC1_2_1_1 EXTID  'ФИО': UPDExportC1_2_1;                        
            GROUP UPDExportC1_2_2 EXTID  'ИнЛицо': UPDExportC1_2; 
                GROUP UPDExportC1_2_2_1 EXTID  'ПредОргПер': UPDExportC1_2_2;  
                    GROUP UPDExportC1_2_2_1_1 EXTID  'ФИО': UPDExportC1_2_2_1;             
                GROUP UPDExportC1_2_2_2 EXTID  'ФЛПер': UPDExportC1_2_2;     
                    GROUP UPDExportC1_2_2_2_1 EXTID  'ФИО': UPDExportC1_2_2_2;                      
                
GROUP UPDExportD EXTID  'Подписант';
    GROUP UPDExportD_1 EXTID  'ФЛ':UPDExportD;   
        GROUP UPDExportD_1_1 EXTID  'ФИО':UPDExportD_1;             
    GROUP UPDExportD_2 EXTID  'ИП':UPDExportD; 
        GROUP UPDExportD_2_1 EXTID  'ФИО':UPDExportD_2;  
    GROUP UPDExportD_3 EXTID  'ЮЛ':UPDExportD;          
        GROUP UPDExportD_3_1 EXTID  'ФИО':UPDExportD_3;  


smartToChar(NUMERIC[26,11] nu) = rtrim(rtrim(TEXT(nu),'0'),'.');
                    
FORM UPDPR FORMEXTID '=:Файл'
    PROPERTIES ATTR ='http://www.w3.org/2001/XMLSchema' EXTID 'xmlns:xsd', ='http://www.w3.org/2001/XMLSchema-instance' EXTID 'xmlns:xsi'
    PROPERTIES   = (OVERRIDE idFile(minUpdDocument()), '') EXTID 'ИдФайл' ATTR, 
                 = '3.19.710' EXTID 'ВерсПрог' ATTR, 
                 = '5.01'  EXTID 'ВерсФорм' ATTR
    OBJECTS g = UpdDocument EXTID 'СвУчДокОбор'
    FILTERS inReport(g)

    PROPERTIES = (OVERRIDE idConsignor(g),'') EXTID 'ИдОтпр' ATTR ,
               = (OVERRIDE idConsignee(g),'') EXTID 'ИдПол' ATTR                 
               
    PROPERTIES = nameOperatorConsignor(g) EXTID 'НаимОрг' ATTR IN UPDExportA0,
               = innOperatorConsignor(g) EXTID 'ИННЮЛ' ATTR IN UPDExportA0,
               = codeOperatorConsignor(g) EXTID 'ИдЭДО' ATTR IN UPDExportA0                 
               
    OBJECTS d = UpdDocument EXTID 'Документ'
    FILTERS inReport(d)    
    PROPERTIES = '1115131' IF d IS UpdDocument EXTID 'КНД' ATTR,
               = (OVERRIDE nameStatus(d),'') EXTID 'Функция' ATTR,
               = businessNameDocument(d) EXTID 'ПоФактХЖ' ATTR,
               = supplierNameDocument(d) EXTID 'НаимДокОпр' ATTR,
               = toDateDDMMYYYY(OVERRIDE dateSupplier(d), currentDate()) EXTID 'ДатаИнфПр' ATTR,                     
               = [FORMULA STRING[20] 'to_char(($1),\'HH24.MI.SS\')'](OVERRIDE timeSupplier(d), currentTime()) EXTID 'ВремИнфПр' ATTR,
               = (OVERRIDE nameOriginatorSupplier(d),'')  EXTID 'НаимЭконСубСост' ATTR
               
    PROPERTIES = (OVERRIDE numberInvoice(d),' ') EXTID 'НомерСчФ' ATTR IN UPDExportA,
               = toDateDDMMYYYY(OVERRIDE dateInvoice(d), currentDate()) EXTID 'ДатаСчФ' ATTR IN UPDExportA,
               = (OVERRIDE codeCurrency(d),'') EXTID 'КодОКВ' ATTR IN UPDExportA
    
    PROPERTIES = numberCorrection(d) IF numberCorrection(d)>0.0 EXTID 'НомИспрСчФ' ATTR IN UPDExportA01,
               = toDateDDMMYYYY(correctionDate(d)) IF numberCorrection(d)>0.0 EXTID 'ДатаИспрСчФ' ATTR IN UPDExportA01
               
    PROPERTIES = okpoSupplier(d) EXTID 'ОКПО' ATTR IN UPDExportA2    
    
    PROPERTIES = (OVERRIDE nameSupplier(d),' ') EXTID 'НаимОрг' ATTR IN UPDExportA2_1_1,
               = innSupplier(d) EXTID 'ИННЮЛ' ATTR IN UPDExportA2_1_1,
               = kppSupplier(d) EXTID 'КПП' ATTR IN UPDExportA2_1_1    
               
    //PROPERTIES = (regionCodeSupplier(d) IF regionCodeSupplier(d) AND NOT codeAdrressSupplier(d)) EXTID 'КодРегион' ATTR IN UPDExportA2_2_1            
    PROPERTIES = countryCodeAddressSupplier(d) EXTID 'КодСтр' ATTR IN UPDExportA2_2_2,
               = addressSupplier(d) EXTID 'АдрТекст' ATTR IN UPDExportA2_2_2    
    PROPERTIES = codeAddressSupplier(d) EXTID 'КодГАР' IN UPDExportA2_2  
        
    PROPERTIES = numberAccountSupplier(d) EXTID 'НомерСчета' ATTR IN UPDExportA2_4            
    PROPERTIES = nameBankSupplier(d) EXTID 'НаимБанк' ATTR IN UPDExportA2_4_1,   
               = MFOBankSupplier(d) EXTID 'БИК' ATTR IN UPDExportA2_4_1,
               = corrAccountBankSupplier(d) EXTID 'КорСчет' ATTR IN UPDExportA2_4_1                              
    
    PROPERTIES = 'он же' IF isConsignerSupplier(d) EXTID 'ОнЖе' IN UPDExportA3      

    PROPERTIES = (OVERRIDE nameReceiverCompany(d),'') EXTID 'НаимОрг' ATTR IN UPDExportA4_1_1,
               = innReceiverCompany(d)EXTID 'ИННЮЛ' ATTR IN UPDExportA4_1_1
               
//    PROPERTIES = (regionCodeCustomer(d) IF regionCodeCustomer(d) AND NOT codeAdrressReceiverCompany(d)) EXTID 'КодРегион' ATTR IN UPDExportA4_2_1 
    PROPERTIES = countryCodeAddressReceiverCompany(d) EXTID 'КодСтр' ATTR IN UPDExportA4_2_2,
               = addressReceiverCompany(d)  EXTID 'АдрТекст' ATTR IN UPDExportA4_2_2    
//    PROPERTIES = codeAdrressReceiverCompany(d) EXTID 'КодГАР' IN UPDExportA4_2    
                   
    PROPERTIES = okpoCustomer(d) EXTID 'ОКПО' ATTR IN UPDExportA6   
    
    PROPERTIES = (OVERRIDE nameCustomer(d),'') EXTID 'НаимОрг' ATTR IN UPDExportA6_1_1,
               = innCustomer(d) EXTID 'ИННЮЛ' ATTR IN UPDExportA6_1_1,
               = kppCustomer(d) EXTID 'КПП' ATTR IN UPDExportA6_1_1    
               
    //PROPERTIES = (regionCodeCustomer(d) IF regionCodeCustomer(d) AND NOT codeAdrressCustomer(d)) EXTID 'КодРегион' ATTR IN UPDExportA6_2_1                 
    PROPERTIES = countryCodeAddressCustomer(d) EXTID 'КодСтр' ATTR IN UPDExportA6_2_2,
               = addressCustomer(d) EXTID 'АдрТекст' ATTR IN UPDExportA6_2_2   
    PROPERTIES = codeAddressCustomer(d) EXTID 'КодГАР' IN UPDExportA6_2
    
    PROPERTIES = shipmentDocumentName(d) EXTID 'НаимДокОтгр' ATTR IN UPDExportA7,
               = shipmentDocumentNumber(d) EXTID 'НомДокОтгр' ATTR IN UPDExportA7,
               = shipmentDocumentDate(d) EXTID 'ДатаДокОтгр' ATTR IN UPDExportA7
    
    PROPERTIES = (OVERRIDE codeBusinessEvent(d),'') EXTID 'Идентиф' ATTR IN UPDExportA9_1,
               = (OVERRIDE descriptionBusinessEvent(d),'') EXTID 'Значен' ATTR IN UPDExportA9_1    
        
    OBJECTS v = UpdDocumentInfoDetail EXTID 'ТекстИнф' IN UPDExportA9
    PROPERTIES = (OVERRIDE nameInfoType(v),'') EXTID 'Идентиф' ATTR,
               = (OVERRIDE info(v),'') EXTID 'Значен' ATTR 
    FILTERS updDocument(v)==d 
    //--           
    OBJECTS detail = UpdDocumentDetail EXTID 'СведТов' IN UPDExportB
    FILTERS updDocument(detail)==d   
    
    PROPERTIES = (OVERRIDE INTEGER(number(detail)), defaultIndex(detail)) EXTID 'НомСтр' ATTR,
               = nameSku(detail) EXTID 'НаимТов' ATTR,
               = idUOM(detail) EXTID 'ОКЕИ_Тов' ATTR,
               = smartToChar(quantity(detail))  EXTID 'КолТов' ATTR,               
               = smartToChar(price(detail))  EXTID 'ЦенаТов' ATTR,               
               = sum(detail)  EXTID 'СтТовБезНДС' ATTR,       
               = nameVATStatus(detail)EXTID 'НалСт' ATTR,                       
               = invoiceSum(detail)  EXTID 'СтТовУчНал' ATTR                       
    
    PROPERTIES = dutySum(detail) EXTID 'СумАкциз' IN UPDExportB1           
    PROPERTIES = 'без акциза' IF withoutDuty(detail)  EXTID 'БезАкциз' IN UPDExportB1  
    
    PROPERTIES = VATSum(detail) EXTID 'СумНал' IN UPDExportB2           
    PROPERTIES = 'без НДС' IF NOT VATSum(detail)  EXTID 'БезНДС' IN UPDExportB2      //Указывается, если для суммы налога всего не требуется НДС
    //PROPERTIES = '-' IF NOT VATSum(detail)  EXTID 'ДефНДС' IN UPDExportB2   todo: Знак прочерка. Что это пока непонятно. Указывается, если для товара не требуется НДС, визуализируется как прочерк
    
    PROPERTIES = idCountry(detail)  EXTID 'КодПроисх' ATTR IN UPDExportB3,      
               = numberDeclaration(detail)  EXTID 'НомерТД' ATTR IN UPDExportB3
       
    PROPERTIES = nameUOM(detail)  EXTID 'НаимЕдИзм' ATTR IN UPDExportB4,      
               = nameCountry(detail)  EXTID 'КрНаимСтрПр' ATTR IN UPDExportB4,
               = idSku(detail)  EXTID 'КодТов' ATTR IN UPDExportB4 
    
    OBJECTS utd = UpdTransportPackDetail EXTID 'НомСредИдентТов' IN UPDExportB4
    PROPERTIES = (OVERRIDE id(utd),'') EXTID 'ИдентТрансУпак' ATTR
    FILTERS updDocumentDetail(utd)==detail
    
    OBJECTS upd = UpdPackDetail EXTID 'НомСредИдентТов' IN UPDExportB4
    PROPERTIES = (OVERRIDE id(upd),'') EXTID 'КИЗ'
    FILTERS updDocumentDetail(upd)==detail
        
    OBJECTS uld = UpdLotDetail EXTID 'НомСредИдентТов' IN UPDExportB4
    PROPERTIES = (OVERRIDE id(uld),'') EXTID 'НомУпак'
    FILTERS updDocumentDetail(uld)==detail
        
    PROPERTIES = sum(d) EXTID 'СтТовБезНДСВсего' ATTR IN UPDExportBB          
    PROPERTIES = invoiceSum(d)  EXTID 'СтТовУчНалВсего' ATTR IN UPDExportBB       
        
    PROPERTIES = VATSum(d) EXTID 'СумНал' IN UPDExportBB_1           
    PROPERTIES = 'без НДС' IF withoutVAT(d)  EXTID 'БезНДС' IN UPDExportBB_1      //Указывается, если для суммы налога всего не требуется НДС
    //PROPERTIES = '-' IF NOT VATSum(r)  EXTID 'ДефНДС' IN UPDExportBB   todo: Знак прочерка. Что это пока непонятно. Указывается, если для товара не требуется НДС, визуализируется как прочерк
            
    //--           
    PROPERTIES = (OVERRIDE description(d),'') EXTID 'СодОпер' ATTR IN UPDExportC1
    
    PROPERTIES = (OVERRIDE descriptionContract(d),'') EXTID 'НаимОсн' ATTR IN UPDExportC1_1,
               = numberContract(d) EXTID 'НомОсн' ATTR IN UPDExportC1_1,
               = toDateDDMMYYYY(dateContract(d)) EXTID 'ДатаОсн' ATTR IN UPDExportC1_1      
               
    PROPERTIES = ((OVERRIDE positionAgentSupplier(d),' ') IF isAgentSupplier(d)) EXTID 'Должность' ATTR IN UPDExportC1_2_1,        
               = ((OVERRIDE lastNameAgentSupplier(d),' ') IF isAgentSupplier(d)) EXTID 'Фамилия' ATTR IN UPDExportC1_2_1_1,      
               = ((OVERRIDE firstNameAgentSupplier(d),' ') IF isAgentSupplier(d)) EXTID 'Имя' ATTR IN UPDExportC1_2_1_1,  
               = (middleNameAgentSupplier(d) IF isAgentSupplier(d)) EXTID 'Отчество' ATTR IN UPDExportC1_2_1_1    
               
    PROPERTIES = ((OVERRIDE positionAgentExecuter(d),'') IF isAgentExecuter(d) AND NOT isAgentSupplier(d)) EXTID 'Должность' ATTR IN UPDExportC1_2_2_1,                                                                                 
               = ((OVERRIDE nameCompanyAgentExecuter(d),'') IF isAgentExecuter(d) AND NOT isAgentSupplier(d)) EXTID 'НаимОргПер' ATTR IN UPDExportC1_2_2_1               
    PROPERTIES = ((OVERRIDE lastNameAgentExecuter(d),'') IF isAgentExecuter(d) AND NOT isAgentSupplier(d)) EXTID 'Фамилия' ATTR IN UPDExportC1_2_2_1_1,      
               = ((OVERRIDE firstNameAgentExecuter(d),'') IF isAgentExecuter(d) AND NOT isAgentSupplier(d)) EXTID 'Имя' ATTR IN UPDExportC1_2_2_1_1,  
               = (middleNameAgentExecuter(d) IF isAgentExecuter(d) AND NOT isAgentSupplier(d)) EXTID 'Отчество' ATTR IN UPDExportC1_2_2_1_1 
               
    PROPERTIES = ((OVERRIDE lastNameExecuter(d),' ') IF NOT (isAgentExecuter(d) OR isAgentSupplier(d))) EXTID 'Фамилия' ATTR IN UPDExportC1_2_2_2_1, // todo: Есть еще должность. Что с ней делать непонятно.   
               = ((OVERRIDE firstNameExecuter(d),' ') IF NOT (isAgentExecuter(d) OR isAgentSupplier(d))) EXTID 'Имя' ATTR IN UPDExportC1_2_2_2_1,  
               = (middleNameExecuter(d) IF NOT (isAgentExecuter(d) OR isAgentSupplier(d))) EXTID 'Отчество' ATTR IN UPDExportC1_2_2_2_1   

    //-- Подписант
    PROPERTIES = nameSupplierSignerCredentials(d) EXTID 'ОблПолн' ATTR IN UPDExportD,
               = nameSupplierSignerStatus(d) EXTID 'Статус' ATTR IN UPDExportD,
               = supplierSignerCredentialsBase(d) EXTID 'ОснПолн' ATTR IN UPDExportD   
               
    PROPERTIES = supplierSignerLastName(d) IF isSupplierSigner(d)  EXTID 'Фамилия' ATTR IN UPDExportD_1_1,
               = supplierSignerFirstName(d) IF isSupplierSigner(d) EXTID 'Имя' ATTR IN UPDExportD_1_1,
               = supplierSignerMiddleName(d) IF isSupplierSigner(d) EXTID 'Отчество' ATTR IN UPDExportD_1_1   
               
    PROPERTIES = supplierSignerInn(d) IF NOT isSupplierSigner(d)  EXTID 'ИННЮЛ' ATTR IN UPDExportD_3,
               = supplierSignerPosition(d) IF NOT isSupplierSigner(d) EXTID 'Должн' ATTR IN UPDExportD_3            
    PROPERTIES = supplierSignerAgentLastName(d) IF NOT isSupplierSigner(d)  EXTID 'Фамилия' ATTR IN UPDExportD_3_1,
               = supplierSignerAgentFirstName(d) IF NOT isSupplierSigner(d) EXTID 'Имя' ATTR IN UPDExportD_3_1,
               = supplierSignerAgentMiddleName(d) IF NOT isSupplierSigner(d) EXTID 'Отчество' ATTR IN UPDExportD_3_1                                           
;

@defineInvoiceDocumentXMLExport (upd, 'УПД', PPR, 'ON_NSCHFDOPPR', UPDPR);

MODULE UkdDocumentConsolidated;

REQUIRE UkdDocument;

NAMESPACE Ukd;


CLASS BaseUkd 'Основание УКД';
TABLE basedUkd(BaseUkd);

description = DATA STRING[100](BaseUkd);
number = DATA STRING[100](BaseUkd);
date = DATA DATE (BaseUkd);
ukdDocument = DATA UkdDocument(BaseUkd);

EXTEND FORM ukdDocument
    OBJECTS ub = BaseUkd
    PROPERTIES (ub) description, number, date, DELETE GRID 
    FILTERS ukdDocument(ub)=u
;

DESIGN ukdDocument {
    documentInfo {
        NEW documentConsolidated {
            caption = 'Основание УКД ';
            fill = 1;
            MOVE BOX (ub);
        }
    }
}

EXTEND FORM UKDPR
    OBJECTS b = BaseUkd EXTID 'СчФ' IN UKDExportA
    FILTERS ukdDocument(b) = d
    PROPERTIES  = number(b) EXTID 'НомерСчФ' ATTR,
        = toDateDDMMYYYY(date(b)) EXTID 'ДатаСчФ' ATTR

    OBJECTS b2 = BaseUkd EXTID  'ПередатДокум' IN UKDExportC
    FILTERS ukdDocument(b2) = d
    PROPERTIES = description(b2) EXTID 'НаимОсн' ATTR,
        = number(b2) EXTID 'НомОсн' ATTR,
        = toDateDDMMYYYY(date(b2)) EXTID 'ДатаОсн' ATTR
;

MODULE GISMTAuthentication;

REQUIRE System, Utils, Time,
        CryptoPro;

NAMESPACE GISMT;

url 'Url к стенду ГИС МТ' = DATA STRING ();
useTrueAPI 'Использовать True API' = DATA BOOLEAN ();

EXTEND FORM integrationData
    PROPERTIES() gismtUrl = url, useTrueAPI
;

DESIGN integrationData {
    pane {
        NEW GISMT {
            caption = 'ГИС МТ';
            MOVE PROPERTY(gismtUrl);
            MOVE PROPERTY(useTrueAPI());
        }
    }
}

token = DATA LOCAL STRING();
token = DATA STRING (STRING);
tokenDateTime = DATA DATETIME(STRING);

getToken (STRING keyAlias, STRING keyPassword) {
    IF NOT tokenDateTime(keyAlias) > sumMinutes(currentDateTime(), -480) THEN { 
        NEWSESSION {
        
            LOCAL result = JSONFILE ();
            EXTERNAL HTTP GET url() + '/auth/cert/key' TO result;
            
            LOCAL uuid = STRING();
            LOCAL data = STRING();
            IMPORT JSON FROM result() TO() uuid, data;
            
            stringToFile(data());
            
            sign(RAWFILE(resultFile()), NULL, keyAlias, keyPassword);
        
            EXPORT JSON FROM uuid = uuid(), data = encodedSignature();
            
            EXTERNAL HTTP POST url() + '/auth/cert/' PARAMS exportFile() TO result;
    
            LOCAL token = STRING ();
            IMPORT JSON FROM result() TO() token;
            
            token(keyAlias) <- token();
            tokenDateTime(keyAlias) <- currentDateTime();
            
            APPLY;
        }
    }
    
    token() <- token(keyAlias);
}

getToken() {
    getToken(keyAlias(), keyPassword());
}
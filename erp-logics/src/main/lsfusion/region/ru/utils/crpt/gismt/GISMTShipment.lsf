MODULE GISMTShipment;

REQUIRE EDIGLN, EDIGLNWarehouse, EDIGLNStore, LotAggregate;

META defineDocumentGISMTIn (senderProp, receiverProp)
    needDocument = ABSTRACT VALUE BOOLEAN (Invoice);
    needDocument(Invoice i) += supplier(i) != customer(i);
    createDocument (Invoice i) = isPosted(i) AND createShipment(i) AND needDocument(i);
    
    gismtType = ABSTRACT CASE STRING[30] (Invoice);
    gismtType(Invoice i) += WHEN i IS Invoice THEN 'LP_ACCEPT_GOODS';
    @defineDocumentGISMT(invoice, createDocument, invoices, i, receiverProp, receiverProp##Stock);
    
    participant (InvoiceDocument i) += senderProp(invoice(i));
    participantStock (InvoiceDocument i) += senderProp##Stock(invoice(i));
    
    // release number
    releaseNumber = ABSTRACT STRING[100] (Invoice, LotType);
    
    @defineDocumentIncoming(userInvoice, releaseNumber, headerCreateDetail, receiverProp, senderProp);
    releaseNumber (UserInvoice i, LotType t) += releaseNumber(i, t);
    transferDate = ABSTRACT VALUE DATE (Invoice);
    transferDate(Invoice i) += date(i);
    
    // export
    
    FORM exportInvoice
        OBJECTS i = Invoice, t = LotType PANEL
        
        PROPERTIES acceptance_date = replace(toChar(shipmentDateTime(i), 'yyyy-MM-dd T HH24:MI:SS.MI0Z'),' ',''), 
                   transfer_date = toDateISO(transferDate(i)),
                   document_number = seriesNumber(i),
                   document_date = replace(toChar(dateTime(i), 'yyyy-MM-dd T HH24:MI:SS.MI0Z'),' ',''),
                   request_type = 'ACCEPTANCE', 
                   turnover_type = 'SELLING',
                   trade_sender_inn = (OVERRIDE senderInn(senderProp(i)), inn(senderProp(i))),
                   trade_recipient_inn = inn(receiverProp(i)),
                   release_order_number = releaseNumber(i, t),
                   
                   exporter_taxpayer_id = inn(senderProp(i)) IF senderInn(senderProp(i)),
                   exporter_name = name(senderProp(i)) IF senderInn(senderProp(i)),
                   release_method = 'CROSSBORDER' IF senderInn(senderProp(i))
        
        OBJECTS products = (d = InvoiceDetail, l = Lot)
        PROPERTIES accepted = TRUE IF d IS InvoiceDetail AND l IS Lot, 
                   uit_code = id(l) IF NOT aggregated(l),
                   uitu_code = id(l) IF aggregated(l), 
                   product_description = nameSku(d),
                   
                   tnved_code = tnved(sku(d)) IF senderInn(senderProp(i)),
                   product_cost = sum(d) IF senderInn(senderProp(i)),
                   product_tax = (OVERRIDE VATSum(d), 0.0) IF senderInn(senderProp(i))
                   
        FILTERS invoice(d) = i, quantity(d, l), lotType(sku(d)) = t
    ;

    DESIGN operation {
        paramsContainer {
            NEW inGISMTContainer {
                caption = 'ГИС МТ (приход)';
            }
        }
    }
    
    // -- receipt
    @defineOperationProperty(returnType, 'Вид возврата', STRING, inGISMTContainer);

    gismtType (Invoice i) += WHEN returnType(operation(i)) THEN 'LP_RETURN';
    overTradeParticipantInn = ABSTRACT STRING[15] (Invoice);
    
    FORM exportInvoiceReturn
        OBJECTS i = Invoice, t = LotType PANEL
        
        PROPERTIES return_type = returnType(operation(i)), 
                   trade_participant_inn = (OVERRIDE overTradeParticipantInn(i), inn(senderProp(i)))
        
        OBJECTS products_list = (d = InvoiceDetail, l = Lot)
        PROPERTIES ki = id(l)
                   
        FILTERS invoice(d) = i, quantity(d, l), lotType(sku(d)) = t
    ;
    
    export (InvoiceDocument i) + {
        IF returnType(operation(invoice(i))) THEN
            EXPORT exportInvoiceReturn OBJECTS i = invoice(i), t = lotType(i) JSON;
        ELSE {
            IF NOT releaseNumber(invoice(i), lotType(i)) THEN {
                MESSAGE 'Не задан номер документа отгрузки ГИС МТ';
                stopCreate() <- TRUE;
                RETURN;
            }
            
            EXPORT exportInvoice OBJECTS i = invoice(i), t = lotType(i) JSON;
        }
    }
END

META defineDocumentGISMTOut (senderProp, receiverProp)
    needDocument = ABSTRACT VALUE BOOLEAN (Invoice);
    needDocument(Invoice i) += supplier(i) != customer(i);
    createDocument (Invoice i) = isPosted(i) AND createShipment(i) AND needDocument(i);
    
    gismtType = ABSTRACT CASE STRING[30] (Invoice);
    gismtType(Invoice i) += WHEN i IS Invoice THEN 'LP_SHIP_GOODS';
    @defineDocumentGISMT(invoice, createDocument, invoices, i, senderProp, senderProp##Stock);
    
    participant (InvoiceDocument i) += receiverProp(invoice(i));
    participantStock (InvoiceDocument i) += receiverProp##Stock(invoice(i));

    skipExport = ABSTRACT VALUE BOOLEAN (InvoiceDetail);

    overSenderInn = ABSTRACT STRING[15] (Invoice);
    overReceiverInn = ABSTRACT STRING[15] (Invoice);

    FORM exportInvoice
        OBJECTS i = Invoice, t = LotType PANEL
        
        PROPERTIES transfer_date = toDateISO(date(i)), 
                   document_date = toDateISO(date(i)), 
                   document_num = seriesNumber(i),
                   turnover_type = 'SELLING',
                   sender_inn = (OVERRIDE overSenderInn(i), inn(senderProp(i))), 
                   receiver_inn = (OVERRIDE overReceiverInn(i), inn(receiverProp(i)))
        
        OBJECTS products = (d = InvoiceDetail, l = Lot)
        PROPERTIES uit_code = id(l) IF NOT aggregated(l),
                   uitu_code = id(l) IF aggregated(l),
                   product_description = nameSku(d)
        FILTERS invoice(d) = i, quantity(d, l), NOT skipExport(d), lotType(sku(d)) = t
    ;
    
    DESIGN operation {
        paramsContainer {
            NEW outGISMTContainer {
                caption = 'ГИС МТ (расход)';
            }
        }
    }

    // -- receipt
    @defineOperationProperty(actionReceipt, 'Причина вывода из оборота', STRING, outGISMTContainer);

    @defineOperationProperty(documentType, 'Вид первичного документа', STRING, outGISMTContainer);

    @defineOperationProperty(primaryDocumentCustomName, 'Наименование первичного документа', STRING, outGISMTContainer);

    gismtType(Invoice i) += WHEN actionReceipt(operation(i)) THEN 'LK_RECEIPT';
    
    productCost = ABSTRACT VALUE NUMERIC[16,4] (InvoiceDetail);
    FORM exportInvoiceReceipt
        OBJECTS i = Invoice, t = LotType PANEL
        
        PROPERTIES action = actionReceipt(operation(i)), action_date = toDateISO(date(i)), 
                   document_date = (toDateISO(date(i)) IF documentType(operation(i))), document_number = (seriesNumber(i) IF documentType(operation(i))),
                   document_type = documentType(operation(i)), primary_document_custom_name = primaryDocumentCustomName(operation(i)),
                   inn = inn(senderProp(i))
        
        OBJECTS products = (d = InvoiceDetail, l = Lot)
        PROPERTIES cis = id(l), product_cost = productCost(d)
        FILTERS invoice(d) = i, quantity(d, l), NOT skipExport(d), lotType(sku(d)) = t
    ;
    
    // -- crossborderexport
    crossBorder 'Отгрузка в ЕАЭС с признанием КИ' = DATA BOOLEAN (Operation) PREREAD;
    crossBorderExport 'Экспорт' = DATA BOOLEAN (Operation) PREREAD;

    EXTEND FORM operation
        PROPERTIES(o) crossBorder, crossBorderExport SHOWIF crossBorder(o)
    ;
    DESIGN operation {
        outGISMTContainer {
            NEW crossBorder {
                horizontal = TRUE;
                MOVE PROPERTY(crossBorder(o));
                MOVE PROPERTY(crossBorderExport(o));
            }
        }
    }

    gismtType(Invoice i) += WHEN crossBorder(operation(i)) THEN (IF crossBorderExport(operation(i)) THEN 'EAS_CROSSBORDER_EXPORT' ELSE 'EAS_CROSSBORDER');
    FORM exportInvoiceCrossBorder
        OBJECTS i = Invoice, t = LotType PANEL
        
        PROPERTIES sender_inn = inn(senderProp(i)),
                   receiver_inn = inn(receiverProp(i)), receiver = name(receiverProp(i)), 
                   receiver_country_code = sid(country(receiverProp(i))), receiver_gln = GLN(receiverProp##Stock(i)),
                   document_num = seriesNumber(i), document_date = toDateISO(date(i)), transfer_date = toDateISO(date(i))
        
        OBJECTS products = (d = InvoiceDetail, l = Lot)
        PROPERTIES cis = id(l)
        FILTERS invoice(d) = i, quantity(d, l), NOT skipExport(d), lotType(sku(d)) = t
    ;
    
    export (InvoiceDocument i) + {
        IF crossBorder(operation(invoice(i))) THEN
            EXPORT exportInvoiceCrossBorder OBJECTS i = invoice(i), t = lotType(i) JSON;
        ELSE IF actionReceipt(operation(invoice(i))) THEN
            EXPORT exportInvoiceReceipt OBJECTS i = invoice(i), t = lotType(i) JSON;
        ELSE
            EXPORT exportInvoice OBJECTS i = invoice(i), t = lotType(i) JSON;
    }
END
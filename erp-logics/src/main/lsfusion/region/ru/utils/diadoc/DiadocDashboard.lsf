MODULE DiadocDashboard;

REQUIRE DiadocMessage;

NAMESPACE Diadoc;

in (BPSTRING[20] s, Document d, Box b) = 
    IF s = BPSTRING[20]('Входящие') THEN toBox(message(d)) == b ELSE
        IF s = BPSTRING[20]('Исходящие') THEN fromBox(message(d)) == b;

FORM diadocDashboard 'Ящик Диадок'
    OBJECTS u = User PANEL
    PROPERTIES(u) READONLY login
    FILTERS u == userDiadoc(currentUser())
    
    OBJECTS b = Box PANEL
    PROPERTIES(b) title IN base SELECTOR
    FILTERS isEmployee(organization(b), u)

    OBJECTS s = STRING[20]
    PROPERTIES(s) VALUE
    FILTERS s = BPSTRING[20]('Входящие') OR  s = BPSTRING[20]('Исходящие')
    
    OBJECTS d = Document
    PROPERTIES(d) READONLY counteragentBoxTitle, title, creationTimestamp
    PROPERTIES(b,d) READONLY primaryStatusText, primaryStatusSeverityName, secondaryStatusText, secondaryStatusSeverityName
    PROPERTIES(d) openData, show
    PROPERTIES(d) createDocument SHOWIF showCreateDocument(d)
    PROPERTIES(u,b,d) TOOLBAR messageReceipt SHOWIF showMessageReceipt(b,d)
    PROPERTIES(u,b,d) TOOLBAR messageInvoiceCorrectionRequest SHOWIF showMessageInvoiceCorrectionRequest(b,d)
    PROPERTIES(u,b,d) TOOLBAR messageRevocationRequest SHOWIF showMessageRevocationRequest(b,d)
    PROPERTIES(u,b,d) TOOLBAR messageRevocationSignature SHOWIF showMessageRevocationSignature(b,d)
    PROPERTIES(u,b,d) TOOLBAR messageRevocationRejection SHOWIF showMessageRevocationRejection(b,d)
    PROPERTIES(u,b,d) TOOLBAR messageSignature SHOWIF showMessageSignature(b,d)
    PROPERTIES(u,b,d) TOOLBAR messageRejection SHOWIF showMessageRejection(b,d)
    FILTERS fromBox(message(d)) == b OR toBox(message(d)) == b OR proxyBox(message(d)) == b,
            in(s, d, b)
    ORDERS creationTimestamp(d)
    
    OBJECTS m = Message
    PROPERTIES (m) READONLY messageId, timestamp
    PROPERTIES (m) show
    FILTERS message(d)=m
    
    OBJECTS mp = MessagePatch
    PROPERTIES (mp) READONLY patchId, timestamp
    PROPERTIES (mp) show
    ORDERS timestamp(mp)
    FILTERS message(mp)=m
    
    OBJECTS e = Entity
    PROPERTIES(e) READONLY dateTime, nameOrganization, status, comment
    ORDERS dateTime(e)
    FILTERS messageId(m) == overMessageId(e) AND showStatusLog(e)
;

DESIGN diadocDashboard {
    OBJECTS {
        NEW filters BEFORE BOX(d) {
            horizontal = TRUE;
            MOVE BOX(u);
            MOVE BOX(b);
        }
        NEW main {
            type = SPLITH;
            fill = 1;
            NEW inout {
                fill = 1;
                MOVE BOX(s);
            }
            NEW documents {
                fill = 4;
                NEW doc {
                    horizontal = FALSE;
                    fill = 1;
                    MOVE BOX(d);
                    NEW messages {
                        tabbed = TRUE;
                        fill = 1;
                        MOVE BOX(m);
                        MOVE BOX(mp);
                        MOVE BOX(e) {caption='Лог статуса';}
                        NEW actionContainer {
                            caption = 'Действия';
                            type = CONTAINERV;
                            MOVE PROPERTY(createDocument(d));
                            MOVE PROPERTY(messageReceipt(u,b,d)) {alignment = START;}
                            MOVE PROPERTY(messageInvoiceCorrectionRequest(u,b,d)) {alignment = START;}
                            MOVE PROPERTY(messageRevocationRequest(u,b,d)) {alignment = START;}
                            MOVE PROPERTY(messageRevocationSignature(u,b,d)) {alignment = START;}
                            MOVE PROPERTY(messageRevocationRejection(u,b,d)) {alignment = START;}
                            MOVE PROPERTY(messageSignature(u,b,d)) {alignment = START;}
                            MOVE PROPERTY(messageRejection(u,b,d)) {alignment = START;}
                        }
                    }
                }
            }
        }
    }
}

NAVIGATOR {
    diadocNavigator {
        NEW diadocDashboard;
    }
}

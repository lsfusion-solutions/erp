MODULE DiadocCustomerEOrder;

REQUIRE Bank, Item, EDI, LegalEntityRu, EOrder, CustomerEOrderEDI, DiadocEDIOrganization,
        PurchaseShipment, PurchaseReturnInvoice, PurchaseOrderStatusEOrder, DiadocEDIMessage;

NAMESPACE DiadocEDI;

EXTEND CLASS EDIProvider {
    diadoc 'Diadoc'
}

dateStartEDI 'Дата начала работы EDI' = DATA DATE(LegalEntity);
sendInvoicePrice 'Выгружать цену с НДС вместо цены без НДС(EDI)' = DATA BOOLEAN(LegalEntity); 

EXTEND FORM legalEntity 
    PROPERTIES dateStartEDI(l), sendInvoicePrice(l);

DESIGN legalEntity {
    EDIComm {
        MOVE PROPERTY (dateStartEDI(l));
        MOVE PROPERTY (sendInvoicePrice(l));
    }
}

EXTEND CLASS TypeChange{
    edi 'Отправлен поставщику(EDI)'
}

logSendDocumentOrder (Purchase.Order o) {
    NEW d = DocumentLog {
        documentId(d) <- LONG(o);
        typeDocument(d) <- objectClassName(o);
        numberDocument(d) <- number(o);
        seriesDocument(d) <- series(o);
        dateDocument(d) <- date(o);
        typeChange(d) <- TypeChange.edi;
        hostnameComputer(d) <- hostnameCurrentComputer();
        userLogin(d) <- STRING[100](login(currentUser()));
        nameContact(d) <- STRING[100](name(currentUser()));
        dateTimeSession(d) <- currentDateTime();
        supplierStock(d) <- supplierStock(o);
        customerStock(d) <- customerStock(o);
    }
}

fromBox = DATA LOCAL Box ();
fromBoxId 'Идентификатор ящика отправителя сообщения' () = boxId(fromBox());
tempPath 'Путь для выгрузки файла' = DATA STRING[500]();

EXTEND FORM integrationData
    PROPERTIES tempPath()
;

DESIGN integrationData {
    diadocEDI {
        MOVE PROPERTY(tempPath());
    }
}

overReceiveMessages ABSTRACT LIST ();
afterReceiveMessage ABSTRACT LIST (Message);

importAnswersDiadoc 'Прием ответов' () {
    overReceiveMessages();
}

EXTEND FORM integrationData
    PROPERTIES () importAnswersDiadoc
;

DESIGN integrationData {
    diadocEDI {
        MOVE PROPERTY(importAnswersDiadoc());
    }
}

GROUP interchangeHeader;
GROUP order;
GROUP contractIdentificator1 EXTID 'contractIdentificator' : order;
GROUP blanketOrderIdentificator1 EXTID 'blanketOrderIdentificator' : order;
GROUP proposalOrdersIdentificator1 EXTID 'proposalOrdersIdentificator' : order;
GROUP termsOfPayment : order;
GROUP seller : order;
GROUP organization_ : seller;
GROUP russianAddress_ : seller;
GROUP additionalInfo : seller;
GROUP buyer : order;
GROUP organization1 EXTID 'organization' : buyer;
GROUP russianAddress1 EXTID 'russianAddress' : buyer;
GROUP invoicee : order;
GROUP deliveryInfo_ EXTID 'deliveryInfo' : order;
GROUP shipTo : deliveryInfo_;
GROUP selfEmployed : invoicee;
GROUP fullName : selfEmployed;
GROUP russianAddress2 EXTID 'russianAddress' : invoicee;
GROUP additionalInfo1 EXTID 'additionalInfo' : invoicee;
GROUP productInquiryIdentificator1 EXTID 'productInquiryIdentificator' : order;
GROUP lineItems : order;
GROUP lineItem : lineItems;
GROUP requestedQuantity;

sender = DATA LOCAL STRING();
recipient = DATA LOCAL STRING();
documentType = DATA LOCAL STRING();
creationDateTime = DATA LOCAL STRING();
creationDateTimeBySender = DATA LOCAL STRING();
isTest = DATA LOCAL STRING();
promotionDealNumber = DATA LOCAL STRING();
serviceDateStart = DATA LOCAL STRING();
serviceDateEnd = DATA LOCAL STRING();
contractIdentificator = DATA LOCAL STRING();
number = DATA LOCAL STRING();
date = DATA LOCAL STRING();
blanketOrderIdentificator = DATA LOCAL STRING();
number1 = DATA LOCAL STRING();
proposalOrdersIdentificator = DATA LOCAL STRING();
number2 = DATA LOCAL STRING();
date1 = DATA LOCAL STRING();
gln = DATA LOCAL STRING();
name = DATA LOCAL STRING();
inn_ = DATA LOCAL STRING();
kpp = DATA LOCAL STRING();
regionISOCode = DATA LOCAL STRING();
district = DATA LOCAL STRING();
city = DATA LOCAL STRING();
settlement = DATA LOCAL STRING();
street = DATA LOCAL STRING();
house = DATA LOCAL STRING();
flat = DATA LOCAL STRING();
postalCode = DATA LOCAL STRING();
additionalIdentificator = DATA LOCAL STRING();
taxSystem = DATA LOCAL STRING();
phone = DATA LOCAL STRING();
fax = DATA LOCAL STRING();
bankAccountNumber = DATA LOCAL STRING();
correspondentAccountNumber = DATA LOCAL STRING();
bankName = DATA LOCAL STRING();
BIK = DATA LOCAL STRING();
nameOfCEO = DATA LOCAL STRING();
orderContact = DATA LOCAL STRING();
gln1 = DATA LOCAL STRING();
name1 = DATA LOCAL STRING();
inn1 = DATA LOCAL STRING();
kpp1 = DATA LOCAL STRING();
regionISOCode1 = DATA LOCAL STRING();
district1 = DATA LOCAL STRING();
city1 = DATA LOCAL STRING();
settlement1 = DATA LOCAL STRING();
street1 = DATA LOCAL STRING();
house1 = DATA LOCAL STRING();
flat1 = DATA LOCAL STRING();
postalCode1 = DATA LOCAL STRING();
orderContact1 = DATA LOCAL STRING();
fax1 = DATA LOCAL STRING();
email = DATA LOCAL STRING();
gln3 = DATA LOCAL STRING();
name2 = DATA LOCAL STRING();
inn3 = DATA LOCAL STRING();
kpp2 = DATA LOCAL STRING();
currencyISOCode = DATA LOCAL STRING();
gtin = DATA LOCAL STRING();
internalBuyerCode = DATA LOCAL STRING();
internalSupplierCode = DATA LOCAL STRING();
externalProductId = DATA LOCAL STRING();
typeOfUnit = DATA LOCAL STRING(INTEGER);
lineItemTypeOfUnit = DATA LOCAL INTEGER(INTEGER);
description = DATA LOCAL STRING();
comment1 = DATA LOCAL STRING();
netPrice = DATA LOCAL STRING();
netPriceWithVAT = DATA LOCAL STRING();
netAmount = DATA LOCAL STRING();
exciseDuty = DATA LOCAL STRING();
vATRate = DATA LOCAL STRING();
vATAmount = DATA LOCAL STRING();
amount = DATA LOCAL STRING();
requestedQuantity = DATA LOCAL STRING();
unitOfMeasure = DATA LOCAL STRING();
overRequestedDeliveryDateTime = ABSTRACT DATE(EOrder);

exportOrderDocument = DATA LOCAL EOrder();
nameMessage = DATA LOCAL STRING();
sendingCancelled = DATA LOCAL BOOLEAN ();

GLNSupplierStockDiadoc 'GLN поставщика' (EOrder e) = IF notUseStockGLN(supplier(e)) THEN GLNSupplier(e) ELSE (OVERRIDE GLN(supplierStock(e)), GLNSupplier(e));
GLNSupplierStockDiadoc 'GLN поставщика' (PurchaseReturn.UserInvoice e) = IF notUseStockGLN(supplier(e)) THEN GLN(supplier(e)) ELSE (OVERRIDE GLN(supplierStock(e)), GLN(supplier(e)));

FORM ordersDiadoc FORMEXTID 'eDIMessage'
        PROPERTIES nameMessage() EXTID 'id' ATTR, currentDateTime_ = toChar(currentDateTime(), 'yyyy-MM-ddThh24:mi:ss') EXTID 'creationDateTime' ATTR
        PROPERTIES IN interchangeHeader
            sender = GLNCustomer(exportOrderDocument()), 
            recipient = GLNSupplierStockDiadoc(exportOrderDocument()),
            documentType = 'ORDERS', 
            creationDateTime = toChar(currentDateTime(), 'yyyy-MM-ddThh24:mi:ss'),
            isTest = IF useTest() THEN '1' ELSE '0'
    
        PROPERTIES ATTR IN order 
            number_ = number(exportOrderDocument()) EXTID 'number',
            date_ = toChar(sendDateTime(exportOrderDocument()), 'yyyy-MM-dd') EXTID 'date',
            status_ = (IF sendingCancelled() THEN 'Canceled') EXTID 'status'

        PROPERTIES IN seller 
            gln = GLNSupplierStockDiadoc(exportOrderDocument()) EXTID 'gln' 

        PROPERTIES IN organization_ EXTID 'organization' 
            name = fullName(supplier(exportOrderDocument())) EXTID 'name', 
            inn = inn(supplier(exportOrderDocument())) EXTID 'inn', 
            kpp = kpp[Organization](supplier(exportOrderDocument())) EXTID 'kpp'
    
        PROPERTIES IN buyer
            gln1 = (OVERRIDE GLNCustomer(exportOrderDocument()),'') EXTID 'gln'

        PROPERTIES IN organization1 EXTID 'organization' 
            name1 = fullName(customer(exportOrderDocument())) EXTID 'name',
            inn1 = inn[Organization](customer(exportOrderDocument())) EXTID 'inn',
            kpp1 = kpp[Organization](customer(exportOrderDocument())) EXTID 'kpp'

        PROPERTIES IN invoicee
            gln2 = GLNCustomer(exportOrderDocument()) EXTID 'gln'

        PROPERTIES IN deliveryInfo_
            requestedDeliveryDateTime = (OVERRIDE toChar(overRequestedDeliveryDateTime(exportOrderDocument()), 'yyyy-MM-dd'), toChar(shipmentDateTime(exportOrderDocument()), 'yyyy-MM-ddThh24:mi:ss')) EXTID 'requestedDeliveryDateTime'
        PROPERTIES IN shipTo    
            gln_ = GLNCustomerStock(exportOrderDocument()) EXTID 'gln'
   
        PROPERTIES IN lineItems 
            totalPackageQuantity = packQuantityOrderDetail(exportOrderDocument())

        OBJECTS i = EOrderDetail EXTID 'lineItem' IN lineItems
            PROPERTIES  GTINBarcode(i) EXTID 'gtin',
            idSku(i) EXTID 'internalBuyerCode', 
            internalSupplierCode = idSku(i),   
            externalProductId = idBarcode(i), 
            description = nameSku(i),
            netPrice = (OVERRIDE invoicePrice[Purchase.UserOrderDetail](i) IF sendInvoicePrice(supplier(exportOrderDocument())), price(i)), 
            netPriceWithVAT = invoicePrice[Purchase.UserOrderDetail](i),
            vATRate = (OVERRIDE 'NOT_APPLICABLE' IF valueVAT(i) = 0 OR NOT valueVAT(i), (CONCAT '', round0(valueVAT(i))), ''),
            amount = invoiceSum(i)   // сумма с НДС
        PROPERTIES IN requestedQuantity 
            quantity(i) EXTID 'value', unitOfMeasure = extraCodeUOMSku(i) IN requestedQuantity ATTR
        FILTERS order(i) == exportOrderDocument(), quantity(i) > 0

;

// -------------------- Заказ EDI -------------------- //

sendPurchaseOrderDiadocEDI 'Отправить заказ EDI (Диадок)' (EOrder o, User r) {
    IF url() THEN {
        LOCAL error = TEXT();
        error() <- '';
        IF NOT GLNSupplier(o) THEN error() <- error() + 'ORDERS ' + number(o) + ': Не задан GLN поставщика\n';
        IF NOT GLNCustomer(o) THEN error() <- error() + 'ORDERS ' + number(o) + ': Не задан GLN покупателя\n';
        IF NOT GLNCustomerStock(o) THEN error() <- error() + 'EOrder ' + number(o) + ': Не задан GLN склада покупателя\n';

        IF error() == '' THEN {
            fromBox() <- GROUP LAST Box b IF legalEntity(organization(b)) == customer(o) ORDER b;
            getTokenUser(r);
            exportOrderDocument() <- o;
            generatedUUID() <- NULL;
            generateUUID();
            nameMessage() <- CONCAT '', 'ORDERS_', generatedUUID();
            EXPORT ordersDiadoc XML;
            WRITE exportFile() TO tempPath() + nameMessage();
            LOCAL result = FILE ();
            TRY {
                exported(o) <- TRUE;
                isSent[Purchase.Order](o) <- TRUE;
                isPosted[Purchase.Order](o) <- TRUE;
                IF NOT disableDocumentLog() THEN logSendDocumentOrder[Purchase.Order](o);
                sentDateTime[Purchase.Order](o) <- currentDateTime();
                EXTERNAL HTTP POST url() + '/V1/Messages/SendMessage?boxId='+ boxId(fromBox()) +'&messageFileName=' + nameMessage() HEADERS httpHeadersTokenUser PARAMS exportFile() TO result;
            } CATCH {
                fileToString(result(), 'UTF-8');
                MESSAGE 'Ошибка отправки ' + (OVERRIDE statusHttp() + ' ' + resultString(), messageCaughtException());
            }
        } ELSE {
            logToFile('diadoc', ' SendOrder: Не все поля заполнены. ' + error());
            MESSAGE ' Заказ не выгружен: Не все поля заполнены ' + error() NOWAIT;
        }
    }
    APPLY;
}

sendPurchaseOrderDiadoc 'Отправить заказ EDI (Диадок)' (EOrder o) {
    IF currentUser() IS SystemUser THEN {
        FOR User u == (GROUP MIN User us IF us IS User) DO {
            sendPurchaseOrderDiadocEDI(o, u);
        }
    } ELSE {
        FOR User u == userDiadocEDI(currentUser()) DO {
            sendPurchaseOrderDiadocEDI(o, u);
        }
    }
}

sendPurchase 'Отправить заказ EDI' (Purchase.Order o) {
    IF exported(o) OR isSent(o) THEN {
        MESSAGE 'Заказ уже отправлен ';
    } ELSE {
        sendPurchaseOrderDiadoc(o);
    }
}

hideSendPurchase = ABSTRACT BOOLEAN (Purchase.Order);

EXTEND FORM Purchase.orders
    PROPERTIES sendPurchase(o) SHOWIF (isEOrder(o) AND invoiceSumOrderDetail(o) > 0) AND NOT hideSendPurchase(o)
;

DESIGN Purchase.orders {
    emailContainer {
        MOVE PROPERTY (sendPurchase(o)) BEFORE PROPERTY(cancel(o));
    }
}

send(EOrder o) + {
    IF EDIProvider(supplier(o)) == EDIProvider.diadoc AND NOT isCancel(o) THEN {
        IF NOT toSend(o) THEN {
            MESSAGE 'Заказ уже отправлен';
        } ELSE {
            sendPurchaseOrderDiadoc(o);
        }
    }
}

cancel(EOrder o) + {
    IF EDIProvider(supplier(o)) == EDIProvider.diadoc AND isCancel(o) THEN {
        IF exportedCanceled(o) THEN {
            MESSAGE 'Заказ уже отменен';
        } ELSE {
            sendingCancelled() <- TRUE;
            sendPurchaseOrderDiadoc(o);
            sendingCancelled() <- NULL;
        }
    }
}

// -------------------- ORDRSP -------------------- //

ordrspDocumentNumber = DATA LOCAL STRING[48]();
ordrspDocumentDate = DATA LOCAL DATETIME();
ordrspResponseType = DATA LOCAL STRING ();
ordrspBuyerGLN = DATA LOCAL STRING[13]();
ordrspDestinationGLN = DATA LOCAL STRING[13]();
ordrspSupplierGLN = DATA LOCAL STRING[13]();
ordrspOrderNumber = DATA LOCAL STRING[48]();
ordrspDeliveryDateTimeFirst = DATA LOCAL TEXT();
ordrspDeliveryDateTimeSecond = DATA LOCAL TEXT();
ordrspDeliveryDateTime = DATA LOCAL TEXT();
ordrspComment = DATA LOCAL TEXT();
ordrspGTIN = DATA LOCAL STRING[15](INTEGER);
ordrspAction = DATA LOCAL STRING (INTEGER);
ordrspQuantityOrdered = DATA LOCAL NUMERIC[16,5](INTEGER);
ordrspQuantityAccepted = DATA LOCAL NUMERIC[16,5](INTEGER);
ordrspPriceElement = DATA LOCAL NUMERIC[16,2](INTEGER);
ordrspPriceNoNDS = DATA LOCAL NUMERIC[16,4](INTEGER);
ordrspPriceNDS = DATA LOCAL NUMERIC[16,4](INTEGER);

GROUP interchangeHeader1 EXTID 'interchangeHeader';
GROUP orderResponse;
GROUP originOrder1 EXTID 'originOrder' : orderResponse;
GROUP orderDeliveryInfo EXTID 'deliveryInfo': orderResponse;
GROUP lineItems1 EXTID 'lineItems': orderResponse;
GROUP lineItem1 EXTID 'lineItem': lineItems1;
GROUP orderedQuantity1 EXTID 'orderedQuantity' : lineItem1;
GROUP confirmedQuantity1 EXTID 'confirmedQuantity' : lineItem1;
GROUP onePlaceQuantity1 EXTID 'onePlaceQuantity' : lineItem1;

FORM ordrspDiadoc FORMEXTID 'eDIMessage'
    PROPERTIES() IN interchangeHeader 
        ordrspBuyerGLN EXTID 'recipient',
        ordrspSupplierGLN EXTID 'sender'
    PROPERTIES() IN orderResponse 
        ordrspDocumentNumber EXTID 'number' ATTR ,
        ordrspDocumentDate EXTID 'date' ATTR, 
        ordrspResponseType EXTID 'status' ATTR
    PROPERTIES() IN originOrder1 
        ordrspOrderNumber EXTID 'number' ATTR,
        ordrspDeliveryDateTimeFirst EXTID 'date' ATTR
    PROPERTIES() IN orderDeliveryInfo
        ordrspDeliveryDateTime EXTID 'orderedDeliveryDateTime'
    OBJECTS i = INTEGER EXTID 'lineItem' IN lineItems1
    PROPERTIES(i) ordrspAction EXTID 'status',
        ordrspGTIN EXTID 'gtin',
        ordrspQuantityOrdered EXTID 'orderedQuantity',
        ordrspQuantityAccepted EXTID 'confirmedQuantity',
        ordrspPriceElement EXTID 'netPrice',
        ordrspPriceNoNDS EXTID 'totalSumExcludingTaxes',
        ordrspPriceNDS EXTID 'amount'
;

indexEOrderResponseDetail (INTEGER i) = PARTITION SUM 1 ORDER i BY ordrspGTIN(i),ordrspOrderNumber();

overReceiveMessageOrdrsp ABSTRACT (Message);
canceledReceiveMessageOrdrsp ABSTRACT (Message);

overReceiveMessages () + {
    FOR nameFile(Message m) AND NOT dateTimeProcessed(m) AND NOT denyReceiveMessage(m) DO {
        IF nameFile(m) AND isISubstring(nameFile(m), 'ORDRSP_') AND NOT denyReceiveMessage(m) THEN {
            TRY {
                IMPORT ordrspDiadoc XML FROM rawFile(m);
                ordrspOrderNumber() <- STRING[48](trim(ordrspOrderNumber()));
                overReceiveMessageOrdrsp(m);
                IF eOrder(ordrspOrderNumber()) THEN {
                    NEWSESSION NESTED LOCAL {
                        IF NOT eOrderResponse(ordrspSupplierGLN() + '/' + ordrspDocumentNumber() + '/' + ordrspOrderNumber()) THEN NEW r = EOrderResponse {
                            id(r) <- STRING[100](ordrspSupplierGLN() + '/' + ordrspDocumentNumber() + '/' + ordrspOrderNumber());
                        }
                        LOCAL idDetail = STRING[100] (INTEGER);
                        idDetail(INTEGER l) <- CONCAT '/', ordrspSupplierGLN(), ordrspDocumentNumber(), ordrspOrderNumber(), ordrspGTIN(l), (indexEOrderResponseDetail(l) IF indexEOrderResponseDetail(l) > 1) WHERE ordrspGTIN(l);
                        FOR ordrspGTIN(INTEGER l) AND NOT eOrderResponseDetail(idDetail(l)) DO NEW d = EOrderResponseDetail {
                            orderResponse(d) <- eOrderResponse(ordrspSupplierGLN() + '/' + ordrspDocumentNumber() + '/' + ordrspOrderNumber());
                            id(d) <- idDetail(l);
                        }
                        FOR EOrderResponse r == eOrderResponse(ordrspSupplierGLN() + '/' + ordrspDocumentNumber() + '/' + ordrspOrderNumber()) DO {number(r) <- ordrspDocumentNumber();
                            dateTime(r) <- ordrspDocumentDate();
                            responseType(r) <- CASE
                                WHEN ordrspResponseType() == 'Changed' THEN EOrderResponseType.changed
                                WHEN ordrspResponseType() == 'Cancelled' THEN EOrderResponseType.cancelled
                                WHEN ordrspResponseType() == 'Accepted' THEN EOrderResponseType.accepted;
                            note(r) <- STRING[100](ordrspComment());
                            supplier(r) <- legalEntityStockGLN(ordrspSupplierGLN());
                            customer(r) <- legalEntityGLN(ordrspBuyerGLN());
                            eOrder(r) <- eOrder(ordrspOrderNumber());
                            customerStock(r) <- customerStock(eOrder(ordrspOrderNumber()));
                            deliveryDateTime(r) <- DATETIME(ordrspDeliveryDateTime());
                            deliveryDateTimeSecond(r) <- DATETIME(ordrspDeliveryDateTimeSecond());

                            LOCAL indexSku = INTEGER (INTEGER);
                            LOCAL sku = Sku (INTEGER);

                            sku(INTEGER l) <- OVERRIDE (GROUP MAX sku(EOrderDetail dd) IF GTINBarcode(dd) == ordrspGTIN(l) AND order(dd) == eOrder(r)), skuGTIN(ordrspGTIN(l));
                            indexSku(INTEGER l) <- PARTITION SUM 1 ORDER ordrspQuantityOrdered(l), -ordrspQuantityAccepted(l), l BY sku(l); //сортировка по принятым откланенным, принятые первыми
                            fillIndexSku(eOrder(r));

                            FOR ordrspGTIN(INTEGER l) AND EOrderResponseDetail d == eOrderResponseDetail(idDetail(l)) DO {
                                orderResponse(d) <- r;
                                sku(d) <- sku(l);
                                dataGTIN(d) <- ordrspGTIN(l);
                                action(d) <- CASE
                                    WHEN ordrspAction(l) == 'Added' THEN EOrderResponseDetailAction.added
                                    WHEN ordrspAction(l) == 'Changed' THEN EOrderResponseDetailAction.changed
                                    WHEN ordrspAction(l) == 'Accepted' THEN EOrderResponseDetailAction.accepted
                                    WHEN ordrspAction(l) == 'Cancelled' THEN EOrderResponseDetailAction.cancelled;
                                quantityOrdered(d) <- ordrspQuantityOrdered(l);
                                quantityAccepted(d) <- ordrspQuantityAccepted(l);
                                price(d) <- ordrspPriceElement(l);
                                sumNoNDS(d) <- ordrspPriceNoNDS(l);
                                sumNDS(d) <- ordrspPriceNDS(l);
                            }
                            FOR ordrspGTIN(INTEGER l) INLINE DO {
                                responseDetail(EOrderDetail od) <- eOrderResponseDetail(idDetail(l)) WHERE eOrder(ordrspOrderNumber()) == order(od) AND sku(l) == sku(od) AND indexSku(l) == localIndexSku(od);
                            }
                            IF ordrspResponseType() != 'Cancelled' THEN {
                                isConfirmed(Purchase.userOrder(ordrspOrderNumber())) <- TRUE;
                                isPosted(Purchase.userOrder(ordrspOrderNumber())) <- TRUE;
                            }
                            APPLY NESTED LOCAL;
                            IF canceled() THEN canceledReceiveMessageOrdrsp(m);
                        }
                    }
                }
                dateTimeProcessed(m) <- currentDateTime();
                numberEOrder(m) <- ordrspOrderNumber();
                afterReceiveMessage(m);
                APPLY;
            } CATCH {
                MESSAGE  'Ошибка при импорте ответа по заказу ' + nameFile(m) +'. Обратитесь к администратору' NOWAIT;
                responsesFailed() <- responsesFailed() + 1;
                dateTimeProcessed(m) <- currentDateTime();
                APPLY NESTED LOCAL;
            }
        }
    }
}


// -------------------- DESADV -------------------- //

GROUP interchangeHeader2 EXTID 'interchangeHeader';
GROUP despatchAdvice;
GROUP originOrder2 EXTID 'originOrder' : despatchAdvice;
GROUP lineItems2 EXTID 'lineItems': despatchAdvice;
GROUP lineItem2 EXTID 'lineItem': lineItems2;
GROUP orderedQuantity2 EXTID 'orderedQuantity' : lineItem2;
GROUP despatchedQuantity1 EXTID 'despatchedQuantity' : lineItem2;

desadvDocumentNumber = DATA LOCAL STRING[48]();
desadvDocumentDate = DATA LOCAL DATETIME();
desadvDeliveryNoteNumber = DATA LOCAL STRING[48]();
desadvDeliveryNoteDate = DATA LOCAL DATE();
desadvBuyerGLN = DATA LOCAL STRING[13]();
desadvDestinationGLN = DATA LOCAL STRING[13]();
desadvSupplierGLN = DATA LOCAL STRING[13]();
desadvStGLN = DATA LOCAL STRING[13]();
desadvOrderNumber = DATA LOCAL STRING[48]();
desadvOrderDate = DATA LOCAL DATETIME();
desadvDeliveryDateTimeFirst = DATA LOCAL DATETIME();
desadvComment = DATA LOCAL TEXT();
desadvGTIN = DATA LOCAL STRING[15](INTEGER);
desadvQuantityOrdered = DATA LOCAL NUMERIC[16,5](INTEGER);
desadvQuantityDespatch = DATA LOCAL NUMERIC[16,5](INTEGER);
desadvVAT = DATA LOCAL NUMERIC[16,5](INTEGER);
desadvLineItemPrice = DATA LOCAL NUMERIC[16,2](INTEGER);
desadvLineItemAmountWithoutCharges = DATA LOCAL NUMERIC[16,4](INTEGER);
desadvLineItemAmount = DATA LOCAL NUMERIC[16,4](INTEGER);
desadvLineItemAmountCharges = DATA LOCAL NUMERIC[16,4](INTEGER);

FORM desadvDiadoc FORMEXTID 'eDIMessage'
    PROPERTIES() IN interchangeHeader2
        desadvBuyerGLN EXTID 'recipient', 
        desadvSupplierGLN EXTID 'sender'
    PROPERTIES() IN despatchAdvice
        desadvDocumentNumber EXTID 'number' ATTR ,
        desadvDocumentDate EXTID 'date' ATTR
    PROPERTIES() IN originOrder2
        desadvOrderNumber EXTID 'number' ATTR,
        desadvOrderDate EXTID 'date' ATTR
    OBJECTS i = INTEGER EXTID 'lineItem' IN lineItems2
    PROPERTIES(i) desadvGTIN EXTID 'gtin',
        desadvQuantityOrdered EXTID 'orderedQuantity',
        desadvQuantityDespatch EXTID 'despatchedQuantity',
        desadvLineItemAmount EXTID 'netPrice',
        desadvLineItemAmountWithoutCharges EXTID 'totalSumExcludingTaxes',
        desadvLineItemAmountCharges EXTID 'amount'
;

overReceiveMessageDesadv ABSTRACT (Message);
canceledReceiveMessageDesadv ABSTRACT (Message);

overReceiveMessages() + {
    FOR nameFile(Message m) AND NOT dateTimeProcessed(m) AND NOT denyReceiveMessage(m) DO {
        IF nameFile(m) AND isISubstring(nameFile(m), 'DESADV_') THEN {
            TRY {
                IMPORT desadvDiadoc XML FROM rawFile(m);
                desadvOrderNumber() <- STRING[48](trim(desadvOrderNumber()));
                overReceiveMessageDesadv(m);
                IF eOrder(desadvOrderNumber()) THEN {
                    NEWSESSION NESTED LOCAL {
                        IF NOT eOrderDespatchAdvice(desadvSupplierGLN() + '/' + desadvDocumentNumber() + '/' + desadvOrderNumber()) THEN NEW a = EOrderDespatchAdvice {
                            id(a) <- STRING[100](desadvSupplierGLN() + '/' + desadvDocumentNumber() + '/' + desadvOrderNumber());
                        }
                        FOR desadvGTIN(INTEGER l) AND NOT eOrderDespatchAdviceDetail(desadvSupplierGLN() + '/' + desadvDocumentNumber() + '/' + desadvOrderNumber() + '/' + l) DO NEW d = EOrderDespatchAdviceDetail {
                            orderDespatchAdvice(d) <- eOrderDespatchAdvice(desadvSupplierGLN() + '/' + desadvDocumentNumber() + '/' + desadvOrderNumber());
                            id(d) <- STRING[100](desadvSupplierGLN() + '/' + desadvDocumentNumber() + '/' + desadvOrderNumber() + '/' + l);
                        }
                        FOR EOrderDespatchAdvice a == eOrderDespatchAdvice(desadvSupplierGLN() + '/' + desadvDocumentNumber() + '/' + desadvOrderNumber()) DO {
                            number(a) <- desadvDocumentNumber();
                            dateTime(a) <- desadvDocumentDate();
                            deliveryNoteNumber(a) <- desadvDeliveryNoteNumber();
                            deliveryNoteDateTime(a) <- toDateTime(desadvDeliveryNoteDate());
                            note(a) <- STRING[100](desadvComment());
                            supplier(a) <- legalEntityStockGLN(desadvSupplierGLN());
                            customer(a) <- legalEntityGLN(desadvBuyerGLN());
                            eOrder(a) <- eOrder(desadvOrderNumber());
                            customerStock(a) <- customerStock(eOrder(desadvOrderNumber()));
                            deliveryDateTime(a) <- desadvDeliveryDateTimeFirst();
                            FOR desadvGTIN(INTEGER l) AND EOrderDespatchAdviceDetail d == eOrderDespatchAdviceDetail(desadvSupplierGLN() + '/' + desadvDocumentNumber() + '/' + desadvOrderNumber() + '/' + l) DO {
                                sku(d) <- OVERRIDE (GROUP MAX sku(EOrderDetail dd) IF GTINBarcode(dd) == desadvGTIN(l) AND order(dd) == eOrder(a)), skuGTIN(desadvGTIN(l));
                                dataGTIN(d) <- desadvGTIN(l);
                                quantityOrdered(d) <- desadvQuantityOrdered(l);
                                quantityDespatch(d) <- desadvQuantityDespatch(l);
                                valueVAT(d) <- desadvVAT(l);
                                lineItemPrice(d) <- desadvLineItemPrice(l);
                                lineItemAmountWithoutCharges(d) <- desadvLineItemAmountWithoutCharges(l);
                                lineItemAmount(d) <- desadvLineItemAmount(l);
                                lineItemAmountCharges(d) <- desadvLineItemAmountCharges(l);
                            }
                            APPLY NESTED LOCAL;
                            IF canceled() THEN canceledReceiveMessageDesadv(m);
                        }
                    }
                }
                numberEOrder(m) <- desadvOrderNumber();
                dateTimeProcessed(m) <- currentDateTime();
                afterReceiveMessage(m);
                APPLY;
            } CATCH {
                MESSAGE 'Ошибка при импорте уведомления об отгрузке ' + nameFile(m) + '. Обратитесь к администратору' NOWAIT;
                despatchAdvicesFailed() <- despatchAdvicesFailed() + 1;
                dateTimeProcessed(m) <- currentDateTime();
                APPLY NESTED LOCAL;
            }
        }
    }
}

// -------------------- RECADV -------------------- //

GROUP interchangeHeaderrecadv EXTID 'interchangeHeader';
GROUP receivingAdvice;
GROUP originOrderrecadv EXTID 'originOrder' : receivingAdvice;
GROUP despatchIdentificator1 EXTID 'despatchIdentificator' : receivingAdvice;
GROUP sellerrecadv EXTID 'seller': receivingAdvice;
GROUP buyerrecadv EXTID 'buyer': receivingAdvice;
GROUP invoiceerecadv EXTID 'invoicee': receivingAdvice;
GROUP organization : invoiceerecadv;
GROUP russianAddress : invoiceerecadv;
GROUP deliveryInfo1 EXTID 'deliveryInfo': receivingAdvice;
GROUP ultimateCustomer : deliveryInfo1;
GROUP lineItemsRECADV EXTID 'lineItems' : receivingAdvice;
GROUP orderedQuantity;
GROUP despatchedQuantity;
GROUP acceptedQuantity;

senderrecadv = DATA LOCAL STRING();
recipientrecadv = DATA LOCAL STRING();
documentTyperecadv = DATA LOCAL STRING();
creationDateTimerecadv = DATA LOCAL ZDATETIME();
creationDateTimeBySenderrecadv = DATA LOCAL ZDATETIME();
isTestrecadv = DATA LOCAL STRING();
originOrderrecadv = DATA LOCAL STRING();
numberrecadv = DATA LOCAL STRING();
daterecadv = DATA LOCAL DATE();
numberrecadv2 = DATA LOCAL STRING();
daterecadv2 = DATA LOCAL DATE();
number1recadv = DATA LOCAL STRING();
date1recadv = DATA LOCAL DATE();
number2recadv = DATA LOCAL STRING();
glnrecadv = DATA LOCAL STRING();
glnrecadv1 = DATA LOCAL STRING();
gln1recadv = DATA LOCAL STRING();
gln2recadv = DATA LOCAL STRING();
namerecadv = DATA LOCAL STRING();
inn = DATA LOCAL STRING();
id = DATA LOCAL STRING();
unitOfMeasure1 = DATA LOCAL STRING();
unitOfMeasure2 = DATA LOCAL STRING();
unitOfMeasure3 = DATA LOCAL STRING();

exportDocument = DATA LOCAL EOrder();
invoiceEDI = GROUP MAX Purchase.UserInvoice ii IF include[Purchase.UserOrder,Purchase.UserInvoice](exportDocument(), ii);

FORM recadv FORMEXTID 'eDIMessage'
    PROPERTIES nameMessage() EXTID 'id' ATTR, currentDateTime1 = (toChar(currentDateTime(), 'yyyy-MM-ddThh24:mi:ss')) EXTID 'creationDateTime' ATTR
    PROPERTIES IN interchangeHeaderrecadv
        senderrecadv = GLNCustomer(exportDocument()) EXTID 'sender',
        recipientrecadv = GLNSupplierStockDiadoc(exportDocument()) EXTID 'recipient',
        documentTyperecadv = 'RECADV' EXTID 'documentType',
        creationDateTimerecadv = toChar(currentDateTime(), 'yyyy-MM-ddThh24:mi:ss') EXTID 'creationDateTime',
        isTestrecadv = (IF useTest() THEN '1' ELSE '0') EXTID  'isTest'

    PROPERTIES IN receivingAdvice ATTR
        numberrecadv2 = number(exportDocument()) EXTID 'number',
        daterecadv2 = toChar(currentDateTime(), 'yyyy-MM-dd') EXTID 'date'
    
    PROPERTIES IN originOrderrecadv ATTR
        numberrecadv = number(exportDocument()) EXTID 'number',
        daterecadv = toChar(sendDateTime(exportDocument()), 'yyyy-MM-dd') EXTID 'date'
    
    PROPERTIES IN despatchIdentificator1 ATTR
        number1recadv = number(invoiceEDI()) EXTID 'number',
        date1recadv = toChar(date(invoiceEDI()), 'yyyy-MM-dd') EXTID 'date'

    PROPERTIES IN sellerrecadv
        gln = GLNSupplierStockDiadoc(exportDocument()) EXTID 'gln'

    PROPERTIES IN buyerrecadv
        gln1 = GLNCustomer(exportDocument()) EXTID 'gln'

    PROPERTIES IN invoiceerecadv
        glnrecadv1 = GLNCustomer(exportDocument()) EXTID 'gln'

    PROPERTIES IN organization EXTID  'organization'
        name1 = fullName(customer(exportDocument())) EXTID 'name',
        inn1 = inn[Organization](customer(exportDocument())) EXTID 'inn',
        kpp1 = kpp[Organization](customer(exportDocument())) EXTID 'kpp'

    PROPERTIES IN deliveryInfo1
        requestedDeliveryDateTime = toChar(currentDate(), 'yyyy-MM-dd') EXTID 'receptionDateTime'
    PROPERTIES IN ultimateCustomer
        gln1recadv = GLNCustomerStock(exportDocument()) EXTID 'gln'

    OBJECTS i = EOrderDetail EXTID 'lineItem' IN lineItemsRECADV
        PROPERTIES GTINBarcode(i) EXTID 'gtin',
            idBarcode(i) EXTID 'internalBuyerCode',
            internalSupplierCode = idBarcode(i),
            externalProductId = idBarcode(i),
            description = nameSku(i),
            netPrice = price(i),
            amount = invoiceSum(i)
        PROPERTIES IN orderedQuantity
            quantity(i) EXTID 'value', 
            unitOfMeasure1 = extraCodeUOMSku(i) EXTID 'unitOfMeasure' IN orderedQuantity ATTR
        PROPERTIES IN despatchedQuantity
            quantityDespatch = quantityDespatch(despatchAdviceDetail(i)) EXTID 'value', 
            unitOfMeasure2 = extraCodeUOMSku(i) EXTID 'unitOfMeasure' IN despatchedQuantity ATTR
        PROPERTIES IN acceptedQuantity
            shipped = Purchase.shipped(i) EXTID 'value',
            unitOfMeasure3 = extraCodeUOMSku(i) EXTID 'unitOfMeasure' IN acceptedQuantity ATTR
    FILTERS order(i) == exportDocument()
;

sendRecadvDiadoc 'Отправить RECADV' (EOrder o) {
    FOR User u == userDiadocEDI(currentUser()) DO NEWSESSION {
        IF url() THEN {
            LOCAL error = TEXT();
            error() <- '';
            IF NOT GLNSupplier(o) THEN error() <- error() + 'RECADV ' + number(o) + ': Не задан GLN поставщика\n';
            IF NOT GLNCustomer(o) THEN error() <- error() + 'RECADV ' + number(o) + ': Не задан GLN покупателя\n';

            IF error() == '' THEN {
                fromBox() <- GROUP LAST Box b IF legalEntity(organization(b)) == customer(o) ORDER b;
                getTokenUser(u);
                exportDocument() <- o;
                generatedUUID() <- NULL;
                generateUUID();
                nameMessage() <- CONCAT '', 'RECADV_', generatedUUID();
                EXPORT recadv XML;
                WRITE exportFile() TO tempPath() + nameMessage();
                LOCAL result = FILE ();
                TRY {
                    EXTERNAL HTTP POST url() + '/V1/Messages/SendMessage?boxId='+ boxId(fromBox()) +'&messageFileName=' + nameMessage() HEADERS httpHeadersTokenUser PARAMS exportFile() TO result;
                } CATCH {
                    fileToString(result(), 'UTF-8');
                    MESSAGE 'Ошибка отправки ' + (OVERRIDE statusHttp() + ' ' + resultString(), messageCaughtException());
                }
            } ELSE {
                logToFile('diadoc', ' RECADV: Не все поля заполнены. ' + error());
                MESSAGE 'RECADV не выгружен: Не все поля заполнены ' + error() NOWAIT;
            }
        }
    }
    APPLY;
}

// -------------------- RETANN -------------------- //

exportDocumentReturn() = DATA PurchaseReturn.Invoice();
returnOrderEDI = GROUP MAX Purchase.UserOrder oo IF include[Purchase.UserOrder,Purchase.UserInvoice](oo, exportDocumentReturn());

GROUP interchangeHeaderRetann EXTID 'interchangeHeader';
GROUP announcementForReturns;
GROUP sellerRetann : announcementForReturns;
GROUP organizationRetann : sellerRetann;
GROUP russianAddressRetann : sellerRetann;
GROUP buyerRetann : announcementForReturns;
GROUP organization1Retann EXTID 'organization' : buyerRetann;
GROUP russianAddress1Retann EXTID 'russianAddress' : buyerRetann;
GROUP deliveryInfoRetann : announcementForReturns;
GROUP shipFromRetann : deliveryInfoRetann;
GROUP organization2Retann EXTID 'organization' : shipFromRetann;
GROUP russianAddress2Retann EXTID 'russianAddress' : shipFromRetann;
GROUP shipToRetann : deliveryInfoRetann;
GROUP organization3Retann EXTID 'organization' : shipToRetann;
GROUP russianAddress3Retann EXTID 'russianAddress' : shipToRetann;
GROUP lineItemsRetann EXTID 'lineItems': announcementForReturns;

GROUP returnQuantity;
GROUP contractIdentificator;
GROUP despatchIdentificator;
GROUP deliveryNoteIdentificator;

senderRetann = DATA LOCAL STRING();
recipientRetann = DATA LOCAL STRING();
documentTypeRetann = DATA LOCAL STRING();
creationDateTimeRetann = DATA LOCAL ZDATETIME();
creationDateTimeBySenderRetann = DATA LOCAL ZDATETIME();
glnRetann = DATA LOCAL STRING();
nameRetann = DATA LOCAL STRING();
innRetann = DATA LOCAL STRING();
kppRetann = DATA LOCAL STRING();
OKPOCodeRetann = DATA LOCAL STRING();
regionISOCodeRetann = DATA LOCAL STRING();
cityRetann = DATA LOCAL STRING();
streetRetann = DATA LOCAL STRING();
houseRetann = DATA LOCAL STRING();
flatRetann = DATA LOCAL STRING();
postalCodeRetann = DATA LOCAL STRING();
gln1Retann = DATA LOCAL STRING();
name1Retann = DATA LOCAL STRING();
inn1Retann = DATA LOCAL STRING();
kpp1Retann = DATA LOCAL STRING();
regionISOCode1Retann = DATA LOCAL STRING();
city1Retann = DATA LOCAL STRING();
street1Retann = DATA LOCAL STRING();
house1Retann = DATA LOCAL STRING();
postalCode1Retann = DATA LOCAL STRING();
availabilityDate = DATA LOCAL ZDATETIME();
gln2Retann = DATA LOCAL STRING();
name2Retann = DATA LOCAL STRING();
inn2Retann = DATA LOCAL STRING();
kpp2Retann = DATA LOCAL STRING();
regionISOCode2 = DATA LOCAL STRING();
city2Retann = DATA LOCAL STRING();
street2Retann = DATA LOCAL STRING();
house2Retann = DATA LOCAL STRING();
postalCode2Retann = DATA LOCAL STRING();
gln3Retann = DATA LOCAL STRING();
name3Retann = DATA LOCAL STRING();
inn3Retann = DATA LOCAL STRING();
kpp3Retann = DATA LOCAL STRING();
OKPOCode1 = DATA LOCAL STRING();
regionISOCode3 = DATA LOCAL STRING();
city3 = DATA LOCAL STRING();
street3 = DATA LOCAL STRING();
house3 = DATA LOCAL STRING();
postalCode3 = DATA LOCAL STRING();
currencyISOCodeRetann = DATA LOCAL STRING();
gtinRetann = DATA LOCAL STRING(INTEGER);
internalBuyerCodeRetann = DATA LOCAL STRING(INTEGER);
descriptionRetann = DATA LOCAL STRING(INTEGER);
unitOfMeasureRetann = DATA LOCAL STRING(INTEGER);
netPriceRetann = DATA LOCAL STRING(INTEGER);
netPriceWithVATRetann = DATA LOCAL STRING(INTEGER);
netAmountRetann = DATA LOCAL STRING(INTEGER);
vATAmountRetann = DATA LOCAL STRING(INTEGER);
amountRetann = DATA LOCAL STRING(INTEGER);
numberRetann = DATA LOCAL STRING(INTEGER);
dateRetann = DATA LOCAL DATE(INTEGER);
number1Retann = DATA LOCAL STRING(INTEGER);
date1Retann = DATA LOCAL DATE(INTEGER);
number2Retann = DATA LOCAL STRING(INTEGER);
date2Retann = DATA LOCAL DATE(INTEGER);
number3Retann = DATA LOCAL STRING();
date3Retann = DATA LOCAL DATE();
idRetann = DATA LOCAL STRING();

FORM retann FORMEXTID 'eDIMessage'
    PROPERTIES nameMessage() EXTID 'id' ATTR, currentDateTime1 = (toChar(currentDateTime(), 'yyyy-MM-ddThh24:mi:ss')) EXTID 'creationDateTime' ATTR
    PROPERTIES IN interchangeHeaderRetann 
        senderRetann = GLN(customer(exportDocumentReturn())) EXTID 'sender',
        recipientRetann = GLNSupplierStockDiadoc(exportDocumentReturn()) EXTID 'recipient', 
        documentTypeRetann = 'RETANN' EXTID 'documentType', 
        creationDateTimeRetann = toChar(currentDateTime(), 'yyyy-MM-ddThh24:mi:ss') EXTID 'creationDateTime'
    PROPERTIES IN announcementForReturns
        numberRetann = number(exportDocumentReturn()) EXTID 'number',
        dateRetann= toChar(currentDateTime(), 'yyyy-MM-dd') EXTID 'date'
    PROPERTIES IN sellerRetann
        glnRetann = GLNSupplierStockDiadoc(exportDocumentReturn()) EXTID 'gln'
    PROPERTIES IN organizationRetann
        name1Retann = fullName(supplier(exportDocumentReturn())) EXTID 'name',
        inn1Retann = inn[Organization](supplier(exportDocumentReturn())) EXTID 'inn',
        kpp1Retann = kpp[Organization](supplier(exportDocumentReturn())) EXTID 'kpp'
    PROPERTIES IN buyerRetann
        gln1Retann =  GLN(customer(exportDocumentReturn())) EXTID 'gln'
    PROPERTIES IN organization1Retann
        name2Retann = fullName(customer(exportDocumentReturn())) EXTID 'name',
        inn2Retann = inn[Organization](customer(exportDocumentReturn())) EXTID 'inn',
        kpp3Retann = kpp[Organization](customer(exportDocumentReturn())) EXTID 'kpp'     
    PROPERTIES IN deliveryInfoRetann
        availabilityDateRetann = toChar(currentDate(), 'yyyy-MM-dd') EXTID 'availabilityDate'
    PROPERTIES IN shipFromRetann 
        gln2Retann = GLN(customerStock(exportDocumentReturn())) EXTID 'gln'
    PROPERTIES IN shipToRetann 
        gln3Retann = GLN(supplierStock(exportDocumentReturn())) EXTID 'gln'
    
    OBJECTS i = PurchaseReturn.InvoiceDetail EXTID 'lineItem' IN lineItemsRetann
    PROPERTIES gtinB = GTIN(sku(i)) EXTID 'gtin',
        idBarcodeSku(i) EXTID 'internalBuyerCode',
        internalSupplierCode = idBarcodeSku(i),
        externalProductId = idBarcodeSku(i),
        description = nameSku(i),
        netPrice = price(i),
        amount = invoiceSum(i)
    PROPERTIES IN returnQuantity
        quantity(i),
        unitOfMeasure1 = extraCodeUOM(UOM(sku(i))) EXTID 'unitOfMeasure' IN returnQuantity ATTR

    FILTERS invoice(i) == exportDocumentReturn()
;

sendRetann 'Отправить RETANN' (PurchaseReturn.Invoice o) {
    FOR User u == userDiadocEDI(currentUser()) DO NEWSESSION {
        IF url() THEN {
            LOCAL error = TEXT();
            error() <- '';
            IF NOT GLN(supplier(o)) THEN error() <- error() + 'RETANN ' + number(o) + ': Не задан GLN поставщика\n';
            IF NOT GLN(customer(o)) THEN error() <- error() + 'RETANN ' + number(o) + ': Не задан GLN покупателя\n';

            IF error() == '' THEN {
                fromBox() <- GROUP LAST Box b IF legalEntity(organization(b)) == customer(o) ORDER b;
                getTokenUser(u);
                exportDocumentReturn() <- o;
                generatedUUID() <- NULL;
                generateUUID();
                nameMessage() <- CONCAT '', 'RETANN_', generatedUUID();
                EXPORT retann XML;
                WRITE exportFile() TO tempPath() + nameMessage();
                LOCAL result = FILE ();
                TRY {
                    EXTERNAL HTTP POST url() + '/V1/Messages/SendMessage?boxId='+ boxId(fromBox()) +'&messageFileName=' + nameMessage() HEADERS httpHeadersTokenUser PARAMS exportFile() TO result;
                } CATCH {
                    fileToString(result(), 'UTF-8');
                    MESSAGE 'Ошибка отправки ' + (OVERRIDE statusHttp() + ' ' + resultString(), messageCaughtException());
                }
            } ELSE {
                logToFile('diadoc', ' RETANN: Не все поля заполнены. ' + error());
                MESSAGE 'RETANN не выгружен: Не все поля заполнены ' + error() NOWAIT;
            }
        }
    }
    APPLY;
}

isEOrder(PurchaseReturn.Invoice o) = EDIProvider(supplier(o)) IS EDIProvider AND NOT skipEDI(supplier(o))
    AND NOT skipEDI(supplierStock(o)) AND NOT skipEDI(customerStock(o)) AND NOT isCompany(supplier(o));
isEOrder(PurchaseReturn.UserInvoice o) = EDIProvider(supplier(o)) IS EDIProvider AND NOT skipEDI(supplier(o))
    AND NOT skipEDI(supplierStock(o)) AND NOT skipEDI(customerStock(o)) AND NOT isCompany(supplier(o));

hideSendRetann = ABSTRACT BOOLEAN (PurchaseReturn.Invoice);

EXTEND FORM PurchaseReturn.invoices
    PROPERTIES sendRetann(i) SHOWIF isEOrder(i) AND invoiceSumInvoiceDetail(i) > 0
;

DESIGN PurchaseReturn.invoices {
    actionContainer {
        NEW actionDiadocEDI {
            caption = 'EDI (Diadoc)';
            MOVE PROPERTY (sendRetann(i));
        }
    }
}
MODULE EGAISFRegLedger;

REQUIRE EGAISFReg;

NAMESPACE EGAIS;

CLASS ABSTRACT FRegLedger 'Регистр 1 ЕГАИС';
TABLE fRegLedger(FRegLedger);

posted 'Проведен' = ABSTRACT BOOLEAN (FRegLedger) MATERIALIZED;

date 'Дата' = ABSTRACT DATE (FRegLedger) MATERIALIZED;

number 'Номер' = ABSTRACT STRING (FRegLedger) MATERIALIZED;

f2Reg = ABSTRACT F2Reg (FRegLedger) MATERIALIZED;

quantity 'Кол-во' = ABSTRACT NUMERIC[16,5] (FRegLedger) MATERIALIZED;

INDEX f2Reg(FRegLedger l), date(l), l;

TABLE balanceF2Reg(F2Reg);
currentBalance 'Текущий остаток' (F2Reg r) = GROUP SUM quantity(FRegLedger l) IF posted(l) BY f2Reg(l) MATERIALIZED TABLE balanceF2Reg;
prevCurrentBalance (F2Reg r) = PREV(currentBalance(r));

balanceB 'Остаток на начало дня' (F2Reg r, DATE date) = currentBalance(r)
    (-) [GROUP SUM quantity(FRegLedger l) IF posted(l) AND date(l) >= date BY f2Reg(l)](r);

balanceA 'Остаток на конец дня' (F2Reg r, DATE date) = currentBalance(r)
    (-) [GROUP SUM quantity(FRegLedger l) IF posted(l) AND date(l) > date BY f2Reg(l)](r);

skipCheckCurrentBalanceF2Reg 'Не проверять остатки по справкам Б' = DATA BOOLEAN () PREREAD;
EXTEND FORM integrationData PROPERTIES() skipCheckCurrentBalanceF2Reg;
DESIGN integrationData { EGAISParams { MOVE PROPERTY(skipCheckCurrentBalanceF2Reg()); } }

CONSTRAINT SET(currentBalance(F2Reg r) < 0) AND NOT skipCheckCurrentBalanceF2Reg() MESSAGE 'Остаток по справке Б не может быть отрицательным'; 

EXTEND FORM f2Regs
    PROPERTIES(r) READONLY currentBalance

    FILTERGROUP balance
        FILTER 'С остатком' currentBalance(r) > 0 DEFAULT
;

FORM balanceReg 'Остатки на регистре 1'
    OBJECTS r = F2Reg
    PROPERTIES(r) READONLY id, idF1Reg, idProduct, fullNameProduct, shortNameClient, descriptionClient, currentBalance

    FILTERGROUP balance
        FILTER 'С остатком' currentBalance(r) > 0 DEFAULT

    OBJECTS l = FRegLedger
    PROPERTIES(l) READONLY objectClassName, date, number, quantity
    FILTERS posted(l), f2Reg(l) = r
;

DESIGN balanceReg {
    OBJECTS {
        NEW tabPane {
            tabbed = TRUE;
            fill = 1;
            MOVE BOX(l) { caption = 'Движение'; }
        }
    }
}

NAVIGATOR {
    EGAISReg {
        NEW balanceReg;
    }
}

META defineDocumentOutBalance (doc, obj, supplierStock, quantityProp)

    quantity 'Кол-во (ЕГАИС)' = DATA NUMERIC[16,5] (###doc##Detail, F2Reg);
    fillQuantity 'Выбрать' (###doc##Detail d, F2Reg s) { quantity(d, s) <- quantityProp(d); }
    
    quantityF2Reg 'Кол-во (ЕГАИС)' (###doc##Detail d) = GROUP SUM quantity(d, F2Reg s) MATERIALIZED;
    incorrectQuantityF2Reg (###doc##Detail d) = quantityProp(d) AND NOT quantityProp(d) = quantityF2Reg(d);
    backgroundQuantityF2Reg(###doc##Detail d) = RGB(255,212,212) IF incorrectQuantityF2Reg(d);

    matches (###doc##Detail d, F2Reg r) = client(r) = client(supplierStock(d)) AND in(sku(d), product(r));
    
    calcQuantity(###doc##Detail d, F2Reg r) = 
        PARTITION UNGROUP quantityProp LIMIT prevCurrentBalance(r) IF matches(d, r) ORDER r BY d;

    fillQuantity 'Заполнить' ABSTRACT CASE (###doc);
    
    fillQuantity  (###doc s) + WHEN s IS ###doc THEN {
        quantity(###doc##Detail d, F2Reg r) <- NULL WHERE doc(d) = s;
        quantity(###doc##Detail d, F2Reg r) <- (GROUP SUM quantity(d, Lot l) IF f2Reg(markCode(id(l))) = r) WHERE doc(d) = s;
        quantity(###doc##Detail d, F2Reg r) <- calcQuantity(d, r) WHERE doc(d) = s AND NOT quantityF2Reg(d);
    }
    
    showLots 'Марки' (###doc##Detail d) {
        quantity(Lot l) <- quantity(d, l);
        SHOW dialogInLots;
    }
    
    numberF2Reg 'Справка Б' (###doc##Detail d) =
        GROUP CONCAT id(F2Reg r) IF quantity(d, r), ',' ORDER r;
    
    EXTEND FORM doc
        OBJECTS ed = ###doc##Detail
        PROPERTIES(ed) READONLY index, idBarcodeSku, idSku, nameSku, shortNameUOMSku, quantityProp,
                                numberF2Reg, quantityF2Reg BACKGROUND backgroundQuantityF2Reg(ed)
        ORDERS index(ed)
        FILTERS doc(ed) = obj
        
        PROPERTIES(obj) fillQuantity DRAW ed TOOLBAR
        PROPERTIES (ed) showLots TOOLBAR
        
        OBJECTS re = F2Reg
        PROPERTIES(re) READONLY id, idF1Reg PANEL, fullNameProduct, currentBalance
        PROPERTIES(re) READONLY PANEL idProduct BACKGROUND NOT in(sku(ed), product(re)),
                                      descriptionClient BACKGROUND NOT client(re) = client(supplierStock(ed))
        PROPERTIES(ed, re)      quantity, fillQuantity GRID
        FILTERS matches(ed, re) OR quantity(ed, re)
    
        FILTERGROUP active
            FILTER 'С остатком' currentBalance(re) > 0 OR quantity(ed, re) DEFAULT 
    ;
    
    DESIGN doc {
        specificationBox {
            NEW EGAIS {
                caption = 'ЕГАИС';
                NEW EGAISHeader {
                    horizontal = TRUE;
                }
                NEW EGAISDetail {
                    fill = 1;
                    horizontal = TRUE;
                    MOVE BOX(ed);
                    MOVE BOX(re);
                }
            }
        }
    }

END

// import
restsDate = DATA LOCAL DATETIME();
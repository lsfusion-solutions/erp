MODULE EGAISStock;

REQUIRE Stock, EGAISClient,
        Warehouse, Store;

NAMESPACE EGAIS;

client = DATA Client (Stock) INDEXED;
idClient 'Код организации ЕГАИС' (Stock st) = id(client(st));
shortNameClient 'Организация ЕГАИС' (Stock st) = shortName(client(st));
descriptionClient 'Описание организации ЕГАИС' (Stock st) = description(client(st));

stock 'Склад' = GROUP AGGR Stock s BY client(s) MATERIALIZED INDEXED;

// warehouse
EXTEND FORM warehouse
    PROPERTIES(w) idClient, shortNameClient, descriptionClient
;

DESIGN warehouse {
    headerExtraParams {
        MOVE PROPERTY(idClient(w));
        MOVE PROPERTY(shortNameClient(w));
        MOVE PROPERTY(descriptionClient(w));
    }
}

// store
EXTEND FORM departmentStore
    PROPERTIES(d) idClient, shortNameClient, descriptionClient
;

DESIGN departmentStore {
    headerExtraParams {
        MOVE PROPERTY(idClient(d));
        MOVE PROPERTY(shortNameClient(d));
        MOVE PROPERTY(descriptionClient(d));
    }
}

nameStock 'Склады' (Client c) = GROUP CONCAT name(Stock s) IF client(s) = c, ',' ORDER s CHARWIDTH 20;
EXTEND FORM clients
    PROPERTIES(o) READONLY nameStock
;

// import excel
// В УТ выгружается из Классификатор организаций ЕГАИС (Торговый объект)
importStockProductXls 'Импорт соответствия из Excel' () {
    INPUT f = EXCELFILE DO {
        NEWSESSION {
            LOCAL idStock = STRING[100] (INTEGER);
            LOCAL idClient = STRING (INTEGER);

            IMPORT XLS FROM f TO idStock = A, idClient = B;

            FOR [GROUP SUM 1 BY idClient(INTEGER r)](STRING id) AND NOT client(id) NEW p = Client DO {
                id(p) <- id;
            }
            FOR id(Stock i) = idStock(INTEGER r) AND id(Client p) = idClient(r) DO
                client(i) <- p;

            APPLY;
        }
    }
}
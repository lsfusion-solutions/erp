MODULE EGAISWayBill;

REQUIRE EGAISInDoc, EGAISProduct, EGAISFReg, EGAISMarkCode;

NAMESPACE EGAIS;

CLASS WayBill 'ТТН ЕГАИС';

id 'Код' = DATA STRING (WayBill) INDEXED IN id;

number 'Номер' = DATA STRING (WayBill) IN id;
date 'Дата' = DATA DATE (WayBill) IN id;
numberDate (WayBill w) = CONCAT ' от ', number(w), toDateDDMMYY(date(w));

shippingDate 'Дата поставки' = DATA DATE (WayBill);

key 'Ключ' (WayBill b) = CONCAT '/', id(b), number(b), date(b);

// status

canceled 'Отменена' = DATA BOOLEAN (WayBill);
accepted 'Принята получателем' = DATA BOOLEAN (WayBill);

toAccept 'К приемке' (WayBill w) = w IS WayBill AND NOT accepted(w) AND NOT canceled(w) MATERIALIZED INDEXED;

skipOutLedger 'Не проводить расход по регистру' = DATA BOOLEAN (WayBill);

// properties

shipper = DATA Client (WayBill) INDEXED;
shortNameShipper 'Отправитель' (WayBill w) = shortName(shipper(w)) IN id;
descriptionShipper 'Описание отправителя' (WayBill w) = description(shipper(w)) IN id;

wayBill = GROUP MAX WayBill b BY shipper(b), key(b);

consignee = DATA Client (WayBill) INDEXED;
shortNameConsignee 'Получатель' (WayBill w) = shortName(consignee(w)) IN id;
descriptionConsignee 'Описание получателя' (WayBill w) = description(consignee(w)) IN id;

base 'Основание' = DATA STRING (WayBill);
note 'Примечание' = DATA STRING (WayBill);

regId 'Рег. номер' = DATA STRING (WayBill);
wayBill = GROUP MAX WayBill b BY regId(b);

// transport
tranType 'Тип перевозки' = DATA STRING (WayBill);
changeOwnership 'Смена собственности' = DATA BOOLEAN (WayBill);

tranCompany 'Название компании перевозчика' = DATA STRING (WayBill);

transportType 'Транспортное средство' = DATA STRING (WayBill);
transportRegNumber 'Регистрационный номер транспортного средства' = DATA STRING (WayBill);
tranTrailer 'Прицеп' = DATA STRING (WayBill);

tranCustomer 'Заказчики' = DATA STRING (WayBill);
tranDriver 'Водитель' = DATA STRING (WayBill);   
tranLoadPoint 'Место погрузки' = DATA STRING (WayBill);
tranUnloadPoint 'Место разгрузки' = DATA STRING (WayBill);

tranRedirect 'Перенаправление' = DATA STRING (WayBill);
tranForwarder 'Экспедитор' = DATA STRING (WayBill);

// position

CLASS WayBillPosition 'Строка ТТН ЕГАИС';

wayBill 'ТТН' = DATA WayBill (WayBillPosition) NONULL DELETE;

product 'Продукция' = DATA Product (WayBillPosition) NONULL INDEXED;
idProduct 'Код продукции' (WayBillPosition p) = id(product(p)) IN id;
fullNameProduct 'Продукция' (WayBillPosition p) = fullName(product(p));

identity 'Код' = DATA STRING (WayBillPosition) IN id;
wayBillPosition = GROUP MAX WayBillPosition p BY wayBill(p), identity(p);

INDEX wayBill(WayBillPosition p), identity(p); 

quantity 'Кол-во' = DATA NUMERIC[16,5] (WayBillPosition);
price 'Цена' = DATA NUMERIC[16,4] (WayBillPosition);

f1Reg 'Справка A' = DATA F1Reg (WayBillPosition) NONULL;
idF1Reg 'Справка A' (WayBillPosition p) = id(f1Reg(p));

f2Reg 'Справка Б' = DATA F2Reg (WayBillPosition) NONULL INDEXED;
idF2Reg 'Справка Б' (WayBillPosition p) = id(f2Reg(p));

// header properties

canceled 'Отменена' (WayBillPosition p) = canceled(wayBill(p));
accepted 'Принята получателем' (WayBillPosition p) = accepted(wayBill(p));

skipOutLedger 'Импортирована из ЕГАИС' (WayBillPosition p) = skipOutLedger(wayBill(p));

shipper (WayBillPosition p) = shipper(wayBill(p));
shortNameShipper 'Отправитель' (WayBillPosition p) = shortNameShipper(wayBill(p));
descriptionShipper 'Описание отправителя' (WayBillPosition p) = shortNameShipper(wayBill(p));

consignee (WayBillPosition p) = consignee(wayBill(p));
shortNameConsignee 'Получатель' (WayBillPosition p) = shortNameConsignee(wayBill(p));
descriptionConsignee 'Описание получателя' (WayBillPosition p) = descriptionConsignee(wayBill(p));

number 'Номер' (WayBillPosition p) = number(wayBill(p));
date 'Дата' (WayBillPosition p) = date(wayBill(p));

// accept

realQuantity 'Принятое кол-во' = DATA NUMERIC[16,5] (WayBillPosition);
backgroundRealQuantity (WayBillPosition p) = CASE WHEN realQuantity(p) > (OVERRIDE quantity(p), 0.0) THEN RGB(255,212,212)
                                                           WHEN (OVERRIDE realQuantity(p), 0.0) < quantity(p) THEN RGB(212,212,255);
                                            

countRealQuantity (WayBill w) = GROUP SUM 1 IF wayBill(WayBillPosition p) = w AND realQuantity(p) > 0;
realDifferences (WayBill w) = GROUP SUM 1 IF wayBill(WayBillPosition p) = w AND NOT quantity(p) = realQuantity(p);
isAccept (WayBill w) = CASE WHEN w IS WayBill AND NOT countRealQuantity(w) THEN 'Rejected'
                                          WHEN realDifferences(w) THEN 'Differences'
                                          ELSE 'Accepted';

consigneeF2Reg 'Справка Б получателя' = DATA F2Reg (WayBillPosition);
idConsigneeF2Reg 'Справка Б получателя' (WayBillPosition p) = id(consigneeF2Reg(p));

edit(WayBillPosition p) + { edit(wayBill(p)); }

// box
CLASS Box 'Короб ЕГАИС';

id 'Код' = DATA STRING (Box) INDEXED IN id;
box = GROUP AGGR Box b BY id(b);

// way bill mark
CLASS WayBillMark 'Строка маркировки ТТН ЕГАИС';

wayBillPosition 'Строка ТТН' = DATA WayBillPosition (WayBillMark) NONULL DELETE INDEXED;

markCode 'Код маркировки' = DATA MarkCode (WayBillMark) NONULL INDEXED;
idMarkCode 'Код маркировки' (WayBillMark m) = id(markCode(m));

canceled 'Отменена' (WayBillMark p) = canceled(wayBillPosition(p));

shipper (WayBillMark p) = shipper(wayBillPosition(p));
shortNameShipper 'Отправитель' (WayBillMark p) = shortNameShipper(wayBillPosition(p));
descriptionShipper 'Описание отправителя' (WayBillMark p) = descriptionShipper(wayBillPosition(p));

consignee (WayBillMark p) = consignee(wayBillPosition(p));
shortNameConsignee 'Получатель' (WayBillMark p) = shortNameConsignee(wayBillPosition(p));
descriptionConsignee 'Описание получателя' (WayBillMark p) = descriptionConsignee(wayBillPosition(p));

idConsigneeF2Reg 'Справка Б получателя' (WayBillMark p) = idConsigneeF2Reg(wayBillPosition(p));

number 'Номер' (WayBillMark p) = number(wayBillPosition(p));
date 'Дата' (WayBillMark p) = date(wayBillPosition(p));

INDEX markCode(WayBillMark m), wayBillPosition(m);
wayBillPosition (MarkCode c) = GROUP LAST wayBillPosition(WayBillMark m) ORDER m WHERE accepted(wayBillPosition(m)) BY markCode(m) MATERIALIZED INDEXED;
f2Reg (MarkCode c) += consigneeF2Reg(wayBillPosition(c));

countMarkCodes 'Кол-во марок' (WayBillPosition p) = GROUP SUM 1 IF wayBillPosition(WayBillMark m) = p MATERIALIZED;

//wayBillPosition = GROUP LAST wayBillPosition(WayBillMark m) ORDER m BY markCode(m), wayBill(wayBillPosition(m));

box = DATA Box (WayBillMark) INDEXED;

box (MarkCode c) = GROUP LAST box(WayBillMark m) ORDER m BY markCode(m) MATERIALIZED INDEXED;
prevBox (MarkCode c) = PREV(box(c));

countMarkCodes 'Кол-во марок' (WayBillPosition p, Box b) = GROUP SUM 1 IF wayBillPosition(WayBillMark m) = p AND box(m) = b MATERIALIZED;

accepted 'Принята получателем' = DATA BOOLEAN (WayBillMark);
acceptedMarkCodes (WayBillPosition p) = GROUP SUM 1 IF accepted(WayBillMark m) AND wayBillPosition(m) = p;

CONSTRAINT countMarkCodes(WayBillPosition p) AND (OVERRIDE realQuantity(p), 0.0) != (OVERRIDE acceptedMarkCodes(p), 0.0)
    MESSAGE 'Количество принятого товара не равно количеству принятых марок';

edit(WayBillMark p) + { edit(wayBillPosition(p)); }

bottlingDate 'Дата розлива' = DATA DATE (WayBillPosition);

lastBottlingDate 'Дата розлива' = GROUP LAST bottlingDate(WayBillPosition p) ORDER date(wayBill(p)), p BY f1Reg(p);
lastBottlingDate 'Дата розлива' = GROUP LAST bottlingDate(WayBillPosition p) ORDER date(wayBill(p)), p BY f2Reg(p);

FORM wayBill 'ТТН ЕГАИС'
    OBJECTS w = WayBill PANEL
    PROPERTIES(w) accepted, canceled, skipOutLedger, 
                  id, number, date, shippingDate, regId,
                  shortNameShipper, descriptionShipper, shortNameConsignee, descriptionConsignee, 
                  base, note,
                  
                  tranType, changeOwnership, 
                  tranCompany, tranCustomer,
                  transportType, transportRegNumber, tranTrailer,
                  tranDriver, tranRedirect, tranForwarder,
                  tranLoadPoint, tranUnloadPoint
    
    OBJECTS p = WayBillPosition
    PROPERTIES(p) identity, idProduct, fullNameProduct, quantity, price, idF1Reg, idF2Reg,  
                  idConsigneeF2Reg, realQuantity BACKGROUND backgroundRealQuantity(p), bottlingDate,
                  countMarkCodes READONLY BACKGROUND RGB(212,255,212) IF quantity(p) = countMarkCodes(p),  
                  NEW, DELETE
    FILTERS wayBill(p) = w
    
    OBJECTS b = Box
    PROPERTIES(b) id
    PROPERTIES READONLY countMarkCodes(p, b)
    FILTERS countMarkCodes(p, b)
    
    OBJECTS m = WayBillMark
    PROPERTIES(m) accepted, idMarkCode, NEW, DELETE
    PROPERTIES 'Код короба'=id(box(m))
    FILTERS wayBill(wayBillPosition(m)) = w
    
    FILTERGROUP position
        FILTER 'По позиции' wayBillPosition(m) = p DEFAULT
        
    FILTERGROUP box
        FILTER 'По коробу' box(m) = b
    
    EDIT WayBill OBJECT w
;

DESIGN wayBill {
    size = (1024, 768); 
    OBJECTS {
        NEW header {
            NEW statuses {
                horizontal = TRUE;
                alignment = STRETCH;
                caption = 'Статусы';
                MOVE PROPERTY(accepted(w));
                MOVE PROPERTY(canceled(w));
                MOVE PROPERTY(skipOutLedger(w));
            }
            NEW base {
                horizontal = TRUE;
                alignment = STRETCH;
                caption = 'Основные';
                MOVE PROPERTY(id(w));
                MOVE PROPERTY(number(w));
                MOVE PROPERTY(date(w));
                MOVE PROPERTY(shippingDate(w));
                MOVE PROPERTY(regId(w));
            }
            NEW legalEntities {
                horizontal = TRUE;
                alignment = STRETCH;
                caption = 'Контрагенты';
                MOVE PROPERTY(shortNameShipper(w));
                MOVE PROPERTY(shortNameConsignee(w));
            }
            NEW notes {
                horizontal = TRUE;
                alignment = STRETCH;
                caption = 'Примечания';
                MOVE PROPERTY(base(w));
                MOVE PROPERTY(note(w));
            }
            NEW transport {
                lines = 3;
                alignment = STRETCH;
                caption = 'Транспорт';
                
                MOVE PROPERTY(tranType(w));
                MOVE PROPERTY(changeOwnership(w));
                MOVE PROPERTY(tranCompany(w));
                MOVE PROPERTY(tranCustomer(w));
                MOVE PROPERTY(transportType(w));
                MOVE PROPERTY(transportRegNumber(w));
                MOVE PROPERTY(tranTrailer(w));
                MOVE PROPERTY(tranDriver(w));
                MOVE PROPERTY(tranRedirect(w));
                MOVE PROPERTY(tranForwarder(w));
                MOVE PROPERTY(tranLoadPoint(w));
                MOVE PROPERTY(tranUnloadPoint(w));
            }
        }
        NEW detail {
            horizontal = TRUE;
            fill = 1;
            MOVE BOX(p);
            NEW detailBox {
                fill = 0.5;
                MOVE BOX(b) {
                    fill = 0.3;
                }
                MOVE BOX(m);
            }
        }
    }
}

show 'Просмотреть' (WayBill w) {
    NEWSESSION {
        SHOW wayBill OBJECTS w = w READONLY DOCKED;
    }
}

FORM wayBills 'ТТН'
    OBJECTS w = WayBill
    PROPERTIES(w) READONLY accepted, canceled, id, number, date, shippingDate,
                           shortNameShipper, descriptionShipper, shortNameConsignee, descriptionConsignee, regId,
                           base, note
    PROPERTIES(w) NEWSESSION NEW, EDIT, DELETE, show TOOLBAR

    OBJECTS p = WayBillPosition
    PROPERTIES(p) READONLY identity, idProduct, fullNameProduct, quantity, price, idF1Reg, idF2Reg, 
                           idConsigneeF2Reg, realQuantity, bottlingDate
    FILTERS wayBill(p) = w
    
    LIST WayBill OBJECT w
;

DESIGN wayBills {
    OBJECTS {
        NEW tabbedPane {
            tabbed = TRUE;
            fill = 1;
            MOVE BOX(p) { caption = 'Позиции'; }
        }
    }
}

NAVIGATOR {
    EGAISReg {
        NEW wayBills;
    }
}

EXTEND FORM markCodes
    OBJECTS wbm = WayBillMark
    PROPERTIES(wbm) READONLY accepted, canceled, number, date, 
                             shortNameShipper, descriptionShipper,
                             shortNameConsignee, descriptionConsignee,
                             idConsigneeF2Reg
    PROPERTIES(wbm) EDIT
    FILTERS markCode(wbm) = m
;

DESIGN markCodes {
    pane {
        MOVE BOX(wbm) { caption = 'ТТН'; }
    }
}


META defineDocumentInWayBill (doc, quantityProp, supplierStock, customerStock, prefix, captionDocs)
    quantity 'Кол-во (ЕГАИС)' = DATA NUMERIC[16,5] (###doc##Detail, WayBillPosition);
    
    fillQuantity 'Выбрать' (###doc##Detail d, WayBillPosition p) { 
        quantity(d, p) <- quantityProp(d);
        in(sku(d), product(p)) <- TRUE; 
    }
    
    matches (###doc i, WayBill w) = 
        shipper(w) = client(supplierStock(i)) AND
        consignee(w) = client(customerStock(i));
    
    quantity###doc 'Кол-во (в документе)' (WayBillPosition p) = GROUP SUM quantity(###doc##Detail d, p) MATERIALIZED;
    left###doc (WayBillPosition p) = quantity(p) (-) quantity###doc(p);
    CONSTRAINT left###doc(WayBillPosition p) < 0
        MESSAGE 'По строке документа привязано больше строк ЕГАИС чем в документе';  
    
//    WHEN CHANGED(quantity###doc(WayBillPosition p)) DO 
//        realQuantity(p) <- quantity###doc(p);
    
    wayBill 'ТТН ЕГАИС' = DATA WayBill (###doc) INDEXED;
    numberDateWayBill 'ТТН ЕГАИС' (###doc i) = numberDate(wayBill(i));
    seriesNumber###doc captionDocs (WayBill w) = GROUP CONCAT seriesNumber(###doc d), ', ' ORDER d BY wayBill(d);
    ##doc = GROUP MAX ###doc i BY wayBill(i);
    isExport (WayBill w) = isExport(##doc(w));
    isExport (WayBillPosition p) = isExport(wayBill(p));
    
    changeNumberDateWayBill (###doc d) {
        DIALOG wayBills OBJECTS w = wayBill(d) NULL INPUT FILTERS toAccept(w) AND matches(d, w) DO {
            wayBill(d) <- w;
        }
    }
    
    partitionQuantity (###doc##Detail d, WayBillPosition p) =
        PARTITION UNGROUP quantityProp
            LIMIT STRICT quantity(p) IF in(sku(d), product(p)) AND wayBill(p) = wayBill(doc(d))
            ORDER p
            BY d;
    
    fillQuantity 'Заполнить' (###doc i) {
        quantity(###doc##Detail d, WayBillPosition p) <- partitionQuantity(d, p)
                   WHERE doc(d) = i;
    }
    fillDetailParams(WayBillPosition p, UserInvoiceDetail d) {
        sku(d) <- minItem(product(p));
        quantity(d) <- quantity(p);
        quantity(d, p) <- quantity(p);
        invoicePrice(d) <- price(p);
        price(d) <- round2(price(p) * 100 / (100 + valueVAT(minItem(product(p)))));        
    }
    createDetail 'Создать строки' (###doc i) {
        FOR wayBill(WayBillPosition p) = wayBill(i) ORDER p DO NEW d = UserInvoiceDetail {
            userInvoice(d) <- i;
            fillDetailParams(p,d);
        } 
    }

    quantityWayBill 'Кол-во (ЕГАИС)' (###doc##Detail d) = GROUP SUM quantity(d, WayBillPosition s) IF NOT canceled(s); // проверяем не wayBill(doc(d)) = wayBill(s), чтобы было одинаково с последующим подтверждением
    incorrectQuantityWayBill (###doc##Detail d) = quantityProp(d) AND NOT quantityProp(d) = quantityWayBill(d);
    backgroundQuantityWayBill(###doc##Detail d) = RGB(255,212,212) IF incorrectQuantityWayBill(d);

    EXTEND FORM doc
        OBJECTS ed = ###doc##Detail
        PROPERTIES(ed) READONLY index, idBarcodeSku, idSku, nameSku, shortNameUOMSku, price, quantityProp,
                                quantityWayBill BACKGROUND backgroundQuantityWayBill(ed)
        ORDERS index(ed)
        FILTERS doc(ed) = i
    
        PROPERTIES(i) DRAW ed TOOLBAR createDetail, fillQuantity
        PROPERTIES(i) numberDateWayBill ON CHANGE changeNumberDateWayBill(i)
        
        OBJECTS wp = WayBillPosition
        PROPERTIES(wp) READONLY idProduct PANEL, fullNameProduct, quantity, price, idF1Reg PANEL, idF2Reg PANEL, 
                                idConsigneeF2Reg PANEL, bottlingDate PANEL
        PROPERTIES(ed, wp) quantity, fillQuantity GRID
        FILTERS wayBill(wp) = wayBill(i)
        
        FILTERGROUP sku
            FILTER 'По товару' in(sku(ed), product(wp)) OR quantity(ed, wp) DEFAULT
    
        FILTERGROUP quantity
            FILTER 'По количеству' quantity(ed) = quantity(wp) OR quantity(ed, wp) DEFAULT
    
        FILTERGROUP left
            FILTER 'Непривязанные' (left###doc(wp) > 0) OR quantity(ed, wp) 
    ;
    
    DESIGN doc {
        specificationBox {
            NEW EGAIS {
                caption = 'ЕГАИС';
                NEW EGAISWayBill {
                    fill = 1;
                    showIf = NOT isExport(i);
                    NEW EGAISHeader {
                        horizontal = TRUE;
                        MOVE PROPERTY(numberDateWayBill(i));
                    }
                    NEW EGAISDetail {
                        fill = 1;
                        horizontal = TRUE;
                        MOVE BOX(ed);
                        MOVE BOX(wp);
                    }
                }
            }
        }
    }
    
    EXTEND FORM wayBills
        PROPERTIES(w) READONLY prefix##SeriesNumber###doc = seriesNumber###doc
    ;
END

WBRegId = DATA LOCAL STRING();
note = DATA LOCAL STRING();

shipperClientRegId = DATA LOCAL STRING();
MODULE EGAISLot;

REQUIRE LotAggregate, EGAISMarkCode;

NAMESPACE EGAIS;

lot (MarkCode m) = lot(id(m));

EXTEND CLASS LotType {
    alcohol 'Алкоголь'
}

META defineDocumentInWayBillLot (doc, obj)
    createLots 'Создать марки' (###doc i) {
        FOR WayBillPosition d == [GROUP MIN WayBillPosition dd IF countMarkCodes(dd, Box b) BY wayBill(dd), id(b)](wayBill(i), STRING id) AND NOT lot(id) NEW l = Lot DO {
            id(l) <- id;
            sku(l) <- minItem(product(d)) WHERE (GROUP SUM 1 IF countMarkCodes(WayBillPosition dd, Box b) AND id(b) == id) == 1;
            aggregated(l) <- TRUE;
            mixed(l) <- TRUE WHERE (GROUP SUM 1 IF countMarkCodes(WayBillPosition dd, Box b) AND id(b) == id) > 1;
        }
        
        LOCAL detail = ###doc##Detail (WayBillMark);
        detail (WayBillMark m) <- (GROUP MAX ###doc##Detail d IF quantity(d, wayBillPosition(m))) WHERE wayBill(wayBillPosition(m)) = wayBill(i); 
    
        FOR Sku sku = sku(detail(WayBillMark m)) AND NOT lot(markCode(m)) NEW l = Lot DO {
            id(l) <- idMarkCode(m);
            sku(l) <- sku;
            parent(l) <- lot(id(box(m)));
        }
        
        FOR ###doc##Detail d = detail(WayBillMark m) AND idMarkCode(m) = id(Lot l) DO
            quantity(d, l) <- 1;
            
        MESSAGE 'Марки успешно созданы';
    }
    
    EXTEND FORM doc
        PROPERTIES(obj) createLots DRAW ed TOOLBAR
    ;
END
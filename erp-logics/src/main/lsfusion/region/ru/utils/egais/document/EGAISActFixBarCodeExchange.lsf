MODULE EGAISActFixBarCodeExchange;

REQUIRE EGAISActFixBarCode, EGAISTicket;

NAMESPACE EGAIS;

@defineDocumentOutDoc(actFixBarCode, ActFixBarCode, 'Акт фиксации штрихкодов на балансе');

activeActFixBarCodeOutDoc (ActFixBarCode a) = notRejected(actFixBarCodeOutDoc(a)) AND NOT canceled(a);

GROUP header EXTID 'awr:Header';
GROUP content EXTID 'awr:Content';

GROUP markInfo EXTID 'awr:MarkInfo';

FORM actFixBarCodeOutDoc FORMEXTID 'ns=http://fsrar.ru/WEGAIS/WB_DOC_SINGLE_01:ActFixBarCode'
    PROPERTIES ATTR ='http://fsrar.ru/WEGAIS/ActFixBarCode' EXTID 'xmlns:awr', ='http://fsrar.ru/WEGAIS/CommonV3' EXTID 'xmlns:ce'

    OBJECTS r = ActFixBarCodeOutDoc PANEL

    PROPERTIES =LONG(r) EXTID 'awr:Identity'

    PROPERTIES IN header
        =number(actFixBarCode(r)) EXTID 'awr:Number',
        =toDateISO(date(actFixBarCode(r))) EXTID 'awr:ActDate',
        =note(actFixBarCode(r)) EXTID 'awr:Note'

    OBJECTS p = ActFixBarCodePosition IN content EXTID 'awr:Position'
    PROPERTIES =LONG(p) EXTID 'awr:Identity', idF2Reg(p) EXTID 'awr:Inform2RegId'
    FILTERS actFixBarCode(p) = actFixBarCode(r)
    
    OBJECTS m = ActFixBarCodeMark EXTID 'ce:amc' IN markInfo
    PROPERTIES(m) idMarkCode EXTID 'value'
    FILTERS actFixBarCodePosition(m) = p
;

@defineDocumentOutProcess(actFixBarCode, tabbedPane);

// ticket

@defineOutDocTicket(actFixBarCode, d, tabbedPane);

processActFixBarCodeTicket ABSTRACT (Client, TEXT);
process (Ticket t) + {
    IF docType(t) = 'ActFixBarCode' THEN {
        IF operationName(t) = 'Confirm' AND operationResult(t) = 'Accepted' THEN {
            posted(actFixBarCode(outDoc(t))) <- TRUE;
        }
        IF operationName(t) = 'Confirm' AND operationResult(t) = 'Rejected' THEN {
            IF utm(outDoc(t)) = utm(inDoc(t)) THEN
                canceled(actFixBarCode(outDoc(t))) <- TRUE;
            ELSE { // кто-то списал марки
                processActFixBarCodeTicket(firstClient(utm(inDoc(t))), operationComment(t));
            }
        }
        processed() <- TRUE;
    }
}
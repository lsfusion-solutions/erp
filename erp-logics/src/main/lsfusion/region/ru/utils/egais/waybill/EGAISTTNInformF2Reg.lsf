MODULE EGAISTTNInformF2Reg;

REQUIRE EGAISWayBillDoc;

NAMESPACE EGAIS;

EXTEND CLASS DocType { TTNInformF2Reg 'Информация по справке 2' }
transportId (DocType t) += WHEN t = DocType.TTNInformF2Reg THEN 'Form2RegInfo';

GROUP TTNInformF2Reg EXTID 'ns:TTNInformF2Reg' : document;
GROUP TTNInformF2RegHeader EXTID 'wbr=http://fsrar.ru/WEGAIS/TTNInformF2Reg:Header' : TTNInformF2Reg;
GROUP TTNInform2RegShipper EXTID 'wbr=http://fsrar.ru/WEGAIS/TTNInformF2Reg:Shipper' : TTNInformF2RegHeader;
GROUP TTNInform2RegShipperUL EXTID 'oref=http://fsrar.ru/WEGAIS/ClientRef_v2:UL' : TTNInform2RegShipper;

GROUP TTNInform2RegContent EXTID 'wbr=http://fsrar.ru/WEGAIS/TTNInformF2Reg:Content' : TTNInformF2Reg;

WBNUMBER = DATA LOCAL STRING();
WBDate = DATA LOCAL STRING();

bottlingDate = DATA LOCAL DATE(INTEGER);

FORM TTNInform2Reg FORMEXTID 'ns:Documents'
    PROPERTIES() IN TTNInformF2RegHeader identity EXTID 'wbr=http://fsrar.ru/WEGAIS/TTNInformF2Reg:Identity', 
                                         WBRegId EXTID 'wbr=http://fsrar.ru/WEGAIS/TTNInformF2Reg:WBRegId', 
                                         WBNUMBER EXTID 'wbr=http://fsrar.ru/WEGAIS/TTNInformF2Reg:WBNUMBER', 
                                         WBDate EXTID 'wbr=http://fsrar.ru/WEGAIS/TTNInformF2Reg:WBDate'
    PROPERTIES() IN TTNInform2RegShipperUL shipperClientRegId EXTID 'oref=http://fsrar.ru/WEGAIS/ClientRef_v2:ClientRegId'

    OBJECTS position = INTEGER EXTID 'wbr=http://fsrar.ru/WEGAIS/TTNInformF2Reg:Position' IN TTNInform2RegContent
    PROPERTIES(position) positionIdentity EXTID 'wbr=http://fsrar.ru/WEGAIS/TTNInformF2Reg:Identity', 
                         informF2RegId EXTID 'wbr=http://fsrar.ru/WEGAIS/TTNInformF2Reg:InformF2RegId', 
                         bottlingDate EXTID 'wbr=http://fsrar.ru/WEGAIS/TTNInformF2Reg:BottlingDate'
;

process (InDoc d) + {
    IF docType(d) = DocType.TTNInformF2Reg THEN {
        NEWSESSION {
            stringToFile(document(d));
            IMPORT TTNInform2Reg XML FROM resultFile();
            
            FOR WayBill b = wayBill(client(shipperClientRegId()), (CONCAT '/', identity(), WBNUMBER(), WBDate())) DO {
                
                wayBill(d) <- b;
                regId(b) <- WBRegId();
                
                FOR informF2RegId(INTEGER i) AND NOT f2Reg(informF2RegId(i)) DO NEW r = F2Reg {
                    id(r) <- informF2RegId(i);
                    f1Reg(r) <- f1Reg(wayBillPosition(b, positionIdentity(i)));
                    client(r) <- consignee(b);
                } 

                FOR WayBillPosition p = wayBillPosition(b, positionIdentity(INTEGER i)) DO {
                    consigneeF2Reg(p) <- f2Reg(informF2RegId(i));
                    bottlingDate(p) <- bottlingDate(i);
                }
                                 
                processed(d) <- TRUE;
                APPLY;
            }
        }
    }
}

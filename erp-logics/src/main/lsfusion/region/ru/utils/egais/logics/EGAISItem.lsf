MODULE EGAISItem;

REQUIRE EGAISProduct, Item;

NAMESPACE EGAIS;

in 'Вкл' = DATA BOOLEAN (Item, Product);

fullNameProduct 'Алкогольная продукция' (Item i) = GROUP CONCAT fullName(Product p) IF in(i, p), ',' ORDER p CHARWIDTH 20;

minProduct (Item i) = GROUP MIN Product p IF in(i, p) MATERIALIZED;

capacityMinProduct 'Объем' (Item i) = capacity(minProduct(i));
alcVolumeMinProduct 'Алкоголь, %' (Item i) = alcVolume(minProduct(i));
idType 'Код вида алкогольной продукции' (Item i) = idType(minProduct(i));

// item
add 'Добавить' (Item i){
    DIALOG dialogProducts OBJECTS o INPUT DO {
        in(i, o) <- TRUE;
    }
}
delete 'Отключить' (Item i, Product p) {
    in(i, p) <- NULL;
} 

EXTEND FORM item
    OBJECTS p = Product
    PROPERTIES(p) READONLY id, fullName, capacity, unitTypeId, alcVolume, nameType,
                           idProducer, shortNameProducer
    PROPERTIES add(i) DRAW p TOOLBAR, delete(i, p) GRID
    FILTERS in(i, p)
;

DESIGN item {
    itemDetail {
        MOVE BOX(p) {
            caption = 'ЕГАИС';
        }
    }
}

EXTEND FORM items
    PROPERTIES(i) READONLY fullNameProduct
;

DESIGN items {
    PROPERTY(fullNameProduct(i)) { hide = TRUE; }
}

// product

nameItem 'Товары' (Product p) = GROUP CONCAT name(Item i) IF in(i, p), ',' ORDER i CHARWIDTH 20;

add 'Добавить' (Product p){
    DIALOG items OBJECTS i INPUT DO {
        in(i, p) <- TRUE;
    }
}

EXTEND FORM product
    OBJECTS i = Item
    PROPERTIES(i) READONLY idBarcode, id, name, nameUOM
    PROPERTIES add(o) DRAW i TOOLBAR, delete(i, o) GRID
    FILTERS in(i, o)
;

DESIGN product {
    tabbedPane {
        MOVE BOX(i) { caption = 'Товары'; }
    }
}

EXTEND FORM products
    PROPERTIES(o) READONLY nameItem
;
MODULE EGAISMarkCode;

REQUIRE EGAISFRegLedger;

NAMESPACE EGAIS;

// mark code
CLASS MarkCode 'Код маркировки ЕГАИС';

id 'Код' = DATA STRING[200] (MarkCode) INDEXED IN id;
markCode = GROUP AGGR MarkCode b BY id(b);

skipCheckLengthLot 'Не проверять ограничение "Длина кода маркировки ЕГАИС не равняется 150"' = DATA BOOLEAN () PREREAD;
CONSTRAINT SETCHANGED(id(MarkCode m)) AND length(id(m)) != 150 AND NOT skipCheckLengthLot()
    MESSAGE 'Длина кода маркировки ЕГАИС не равняется 150';

f2Reg = ABSTRACT VALUE F2Reg (MarkCode) MATERIALIZED INDEXED;
prevF2Reg (MarkCode c) = PREV(f2Reg(c));

dataF2Reg = DATA F2Reg (MarkCode) INDEXED;
f2Reg(MarkCode m) += dataF2Reg(m);

idF2Reg 'Справка Б' (MarkCode m) = id(f2Reg(m));

idF1Reg 'Справка А' (MarkCode m) = idF1Reg(f2Reg(m));

product (MarkCode m) = product(f2Reg(m));
idProduct 'Код продукции' (MarkCode m) = idProduct(f2Reg(m));
fullNameProduct 'Продукция' (MarkCode m) = fullNameProduct(f2Reg(m));

shortNameClient 'Организация' (MarkCode m) = shortNameClient(f2Reg(m));
descriptionClient 'Описание организации' (MarkCode m) = descriptionClient(f2Reg(m));

FORM markCodes 'Коды маркировок'
    OBJECTS m = MarkCode
    PROPERTIES(m) READONLY id, idF2Reg, idF1Reg, idProduct, fullNameProduct, shortNameClient, descriptionClient
;

DESIGN markCodes {
    OBJECTS {
        NEW pane {
            tabbed = TRUE;
            fill = 1;
        }
    }
}

NAVIGATOR {
    EGAISReg {
        NEW markCodes;
    }
}

EXTEND FORM balanceReg
    OBJECTS m = MarkCode
    PROPERTIES(m) READONLY id
    FILTERS f2Reg(m) = r
;

DESIGN balanceReg {
    tabPane {
        MOVE BOX(m) { caption = 'Коды маркировки'; }
    }
}

// import
amc = DATA LOCAL STRING[200] (INTEGER);
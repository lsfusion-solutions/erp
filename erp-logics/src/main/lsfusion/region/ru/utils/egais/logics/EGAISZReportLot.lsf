MODULE EGAISZReportLot;

REQUIRE EGAISClient, EGAISActWriteOffExchange, ZReportLot, EGAISStock;

NAMESPACE EGAIS;


sold 'Продано за день' (Client c, Lot l, DATE d) =
    [GROUP SUM quantity(ReceiptSaleDetail sd) IF isPosted(sd) BY client(departmentStore(sd)), lot(sd), date(sd)](c, l, d) (-)
    [GROUP SUM quantity(ReceiptReturnDetail sd) IF isPosted(sd) BY client(departmentStore(sd)), lot(sd), date(sd)](c, l, d);

skipCreateActLot 'Без марок' = ABSTRACT VALUE BOOLEAN (ActWriteOff);

actWriteOff = GROUP MAX ActWriteOff a IF a IS ActWriteOff AND NOT skipCreateActLot(a) BY client(a), date(a), type(a);

createActWriteOffOver ABSTRACT CASE (Client, DATE);

createActWriteOffOver(Client c, DATE d) + WHEN c IS Client AND d IS DATE  THEN {
    quantity(Lot l) <- NULL;
    quantity(Lot l) <- sold(c, l, d) IF sold(c, l, d) > 0 AND f2Reg(markCode(id(l)));
}
createActWriteOff 'Создать списание на основе реализации' (Client c, DATE d) {
    NEWSESSION {        
        createActWriteOffOver(c,d);
        
        IF (GROUP SUM 1 IF quantity(Lot l)) THEN NEW a = ActWriteOff {
            posted(a) <- TRUE;

            client(a) <- c;
            dateTime(a) <- dateTimeToDateTime(d, 23:59);
            type(a) <- writeOffType('Розничная продажа');
            note(a) <- 'Списание на основе реализации';

            FOR [GROUP SUM 1 IF quantity(Lot l) BY f2Reg(markCode(id(l)))](F2Reg f) INLINE NEW ps = ActWriteOffPosition DO {
                actWriteOff(ps) <- a;

                product(ps) <- product(f);
                f2Reg(ps) <- f;

                FOR [GROUP SUM 1 IF quantity(Lot l) BY markCode(id(l))](MarkCode mc) AND f2Reg(mc) = f INLINE NEW m = ActWriteOffMark DO {
                    actWriteOffPosition(m) <- ps;
                    markCode(m) <- mc;
                }
            }

            APPLY;
        }
    }
}

actWriteOffDays 'Кол-во дней проверки списаний на основе реализации' = DATA INTEGER ();

EXTEND FORM integrationData PROPERTIES() actWriteOffDays;

DESIGN integrationData {
    EGAISParams {
        MOVE PROPERTY(actWriteOffDays());
    }
}

createActWriteOff 'Создать списание ЕГАИС на основе реализации' () {
    FOR iterate(DATE d, sum(currentDate(), (OVERRIDE -actWriteOffDays(), -1)), sum(currentDate(), -1)) DO {
        FOR own(Client c) AND NOT actWriteOff(c, d, writeOffType('Розничная продажа')) DO createActWriteOff(c,d);
    }
}
MODULE EGAISPurchaseReturnShipment;

REQUIRE PurchaseReturnShipmentLot, EGAISFRegLedger,
        EGAISItem, EGAISStock,
        EGAISWayBillProcess, EGAISPurchaseReturnInvoice,
        TripShipment;

PRIORITY PurchaseReturn;

NAMESPACE EGAISPurchaseReturn;

@defineDocumentOutBalance(userShipment, s, customerStock, quantity);

quantity = ABSTRACT NUMERIC[16,5] (ShipmentDetail, F2Reg);
quantity(UserShipmentDetail d, F2Reg r) += quantity(d, r);

quantityF2Reg = ABSTRACT NUMERIC[16,5] (ShipmentDetail);
quantityF2Reg(UserShipmentDetail d) += quantityF2Reg(d);

egaisPrice (ShipmentDetail d) = invoicePrice(invoiceDetail(d));
egaisPrice  = GROUP MAX invoicePrice(invoiceDetail(ShipmentDetail d)) IF quantity(d, F2Reg reg) BY shipment(d), reg;

@defineDocumentOutWayBillProcess(shipment, customer, supplier, purchaseReturn, 'Поставки (закупка-возврат)');

@defineDocumentOutWayBillForm(shipment, s);

// invoice

quantity (InvoiceShipmentDetail d, F2Reg r) += quantity(invoiceDetail(d), r);

process 'Отправить в ЕГАИС' (Invoice i) { process(invoiceShipment(i)); }
cancel 'Отменить' (Invoice i) { cancel(invoiceShipment(i)); }
confirm 'Ответить на акт разногласий' (Invoice i) { confirm(invoiceShipment(i)); }
wayBill (Invoice i) = wayBill(invoiceShipment(i));

@defineDocumentOutWayBillForm(invoice, i);

DESIGN invoices { documentDetail { showIf = invoiceShipment(i); }}

quantityF2Reg 'Кол-во (ЕГАИС)' (InvoiceShipmentDetail d) = GROUP SUM quantity(invoiceDetail(d), F2Reg s) MATERIALIZED;
quantityF2Reg(InvoiceShipmentDetail d) += quantityF2Reg(d);

isPurchaseReturnShipment (WayBill w) = GROUP MAX Shipment s IF wayBill(s) == w;
wbType(WayBill w) += WHEN isPurchaseReturnShipment(w) THEN 'WBReturnFromMe';

showFormWaybillShipmentData 'Предпросмотр формы "ТТН ЕГАИС" при нажатии "Отправить в ЕГАИС"' = DATA BOOLEAN ();
showFormWaybillShipment () += showFormWaybillShipmentData();
EXTEND FORM options
    PROPERTIES  showFormWaybillShipmentDataPR =showFormWaybillShipmentData()
;
DESIGN options{
    invoicePurchaseReturn{
        MOVE PROPERTY (showFormWaybillShipmentDataPR);
    }
}
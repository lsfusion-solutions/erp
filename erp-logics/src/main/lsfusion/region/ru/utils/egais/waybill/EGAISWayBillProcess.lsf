MODULE EGAISWayBillProcess;

REQUIRE EGAISWayBillActExchange, EGAISRequestRepealWB, EGAISWayBillConfirmTicket, EGAISLot;

NAMESPACE EGAIS;

// in
META defineDocumentInWayBillActProcess (doc)
    quantity###doc 'Кол-во (в документе)' (WayBillPosition p) = GROUP SUM quantity(###doc##Detail d, p);

    process 'Отправить в ЕГАИС' (###doc d) {
        FOR WayBill w = wayBill(d) DO {
            NEWSESSION {
                realQuantity(WayBillPosition p) <- quantity###doc(p) WHERE wayBill(p) = w;
                accepted(WayBillMark m) <- TRUE IF quantity(d, lot(markCode(m))) WHERE wayBill(wayBillPosition(m)) = w;
                APPLY;
                IF canceled() THEN RETURN;
            }
            
            createAct(w);
        }
    }
    
    cancel 'Отменить' (###doc d) {
        cancel(wayBillAct(wayBill(d)));
    } CONFIRM;
END

META defineDocumentInWayBillActForm (doc, obj)
    EXTEND FORM doc##s
        OBJECTS w = WayBill PANEL
        PROPERTIES(w) READONLY accepted, canceled, number, date, shippingDate, nameStatus
        FILTERS w = wayBill(obj)
    
        OBJECTS wp = WayBillPosition
        PROPERTIES(wp) READONLY idProduct, fullNameProduct, quantity, price, idF1Reg, idF2Reg, 
                                idConsigneeF2Reg, realQuantity BACKGROUND backgroundRealQuantity(wp), bottlingDate
        FILTERS wayBill(wp) = w
    
        PROPERTIES READONLY commentsWayBillAct 'Комментарий' = comments(wayBillActOutDoc(wayBillAct(w))) PANEL     
        PROPERTIES(obj) DRAW wp TOOLBAR 
                        processEGAIS = process SHOWIF isPosted(obj) AND NOT notRejected(wayBillActOutDoc(wayBillAct(w))),
                        cancelEGAIS = cancel SHOWIF accepted(w) AND NOT canceled(w) AND 
                                                    notRejected(wayBillActOutDoc(wayBillAct(w))) AND
                                                    NOT (notRejected(requestRepealWBOutDoc(wayBillAct(w))) AND hasRRWB(wayBillAct(w)))
    ;
    
    DESIGN doc##s {
        documentDetail {
            NEW EGAIS {
                caption = 'ЕГАИС';
                MOVE BOX(w);
                MOVE BOX(wp);
                MOVE PROPERTY(commentsWayBillAct) { panelCaptionVertical = TRUE; fill = 0.2; }
            }
        }
    }
END

// out
META defineDocumentOutWayBillProcess (doc, supplier, customer)
    wayBill = DATA WayBill (###doc) INDEXED;
    
    doc##Detail = DATA ###doc##Detail (WayBillPosition) INDEXED;
    
    process 'Отправить в ЕГАИС' (###doc i) {
        IF GROUP SUM 1 IF doc(###doc##Detail d) = i AND minProduct(sku(d)) AND NOT quantity(d) = quantityF2Reg(d) THEN {
            MESSAGE 'Не по всем строкам накладной выбраны справки Б в ЕГАИС. Отправка в ЕГАИС невозможна';
            RETURN;
        }
        NEWSESSION {
            NEW w = WayBill {
                wayBill(i) <- w;
                
                id(w) <- STRING(w);
                number(w) <- seriesNumber(i);
                
                date(w) <- date(i);
                shippingDate(w) <- date(i);
                
                shipper(w) <- client(supplier##Stock(i));
                consignee(w) <- client(customer##Stock(i));
                
                tranType(w) <- 'Автомобиль';
                changeOwnership(w) <- shipper(w) = consignee(w);
                transportType(w) <- 'car';
                
                tranCompany(w) <- OVERRIDE nameOwner(truck(trip(i))), name(supplier(i));

                transportRegNumber(w) <- sid(truck(trip(i)));
                tranTrailer(w) <- trailer(truck(trip(i)));

                tranCustomer(w) <- name(customer(i));
                tranDriver(w) <- nameDriver(trip(i));

                tranLoadPoint(w) <- address(supplier##Stock(i));
                tranUnloadPoint(w) <- address(customer##Stock(i));

                tranRedirect(w) <- redirect(trip(i));
                tranForwarder(w) <- forwarder(trip(i));

                LOCAL boxId = STRING(); 
                boxId() <- lpad(STRING(w), 18, '0');
                IF NOT box(boxId()) THEN NEW b = Box {
                    id(b) <- boxId();
                }
                
                FOR quantity(###doc##Detail d, Lot l) IF doc(d) = s AND NOT markCode(id(l)) DO NEW m = MarkCode {
                    id(m) <- id(l);
                    dataF2Reg(m) <- GROUP MAX F2Reg r IF quantity(d, r); 
                }
                
                FOR quantity(###doc##Detail d, F2Reg r) IF doc(d) = i INLINE NEW p = WayBillPosition DO {
                    wayBill(p) <- w;
                    
                    doc##Detail(p) <- d;
                    
                    product(p) <- product(r);
                    
                    identity(p) <- STRING(p);
                    
                    quantity(p) <- quantity(d, r);
                    price(p) <- price(d);
                    
                    f1Reg(p) <- f1Reg(r);
                    f2Reg(p) <- r;

                    FOR quantity(d, Lot l) IF MarkCode mc = markCode(id(l)) AND (OVERRIDE prevF2Reg(mc), dataF2Reg(mc)) = r INLINE NEW m = WayBillMark DO {
                        wayBillPosition(m) <- p;
                        markCode(m) <- mc;
                        box(m) <- OVERRIDE prevBox(mc), box(boxId());
                    }
                }
                
                check();
                
                IF NOT canceled() THEN {
                    submitWayBill(wayBill(i));
                    APPLY;
                }
            }
        }
    }

    cancel 'Отменить' (###doc i) {
        createAct(wayBill(i), TRUE);
    } CONFIRM;
    
    confirm 'Ответить на акт разногласий' (###doc i) {
        confirm(wayBillAct(wayBill(i)));
    }
END

META defineDocumentOutWayBillForm (doc, obj)
    EXTEND FORM doc##s
        OBJECTS w = WayBill PANEL
        PROPERTIES(w) READONLY accepted, canceled, id, number, date, shippingDate, nameStatus
        FILTERS w = wayBill(obj)
    
        OBJECTS wp = WayBillPosition
        PROPERTIES(wp) READONLY idProduct, fullNameProduct, quantity, price, idF1Reg, idF2Reg, 
                                idConsigneeF2Reg, realQuantity BACKGROUND backgroundRealQuantity(wp), bottlingDate
        FILTERS wayBill(wp) = w
    
        PROPERTIES READONLY PANEL
                        commentsWayBillAct 'Комментарий' = comments(wayBillOutDoc(w)),
                        commentsWayBillTicket 'Комментарий запроса на подтверждение акта разногласий' = commentsWayBillTicketOutDoc(wayBillAct(w))
                                 
        PROPERTIES(obj) DRAW wp TOOLBAR 
                        processEGAIS = process SHOWIF isPosted(obj) AND (NOT notRejected(wayBillOutDoc(w)) OR canceled(w)),
                        cancelEGAIS = cancel SHOWIF notRejected(wayBillOutDoc(w)) AND NOT notRejected(wayBillActOutDoc(wayBillAct(w))) 
                                                    AND NOT accepted(w) AND NOT canceled(w),
                        confirmEGAIS = confirm SHOWIF differences(wayBillAct(w)) AND NOT accepted(w) AND NOT canceled(w) AND 
                                                      NOT notRejected(wayBillTicketOutDoc(wayBillAct(w)))  
    ;
    
    DESIGN doc##s {
        documentDetail {
            NEW EGAIS {
                caption = 'ЕГАИС';
                MOVE BOX(w);
                MOVE BOX(wp);
                NEW comments {
                    fill = 0.2;
                    type = CONTAINERH;
                    MOVE PROPERTY(commentsWayBillAct) { panelCaptionVertical = TRUE; fill = 1; }
                    MOVE PROPERTY(commentsWayBillTicket) { panelCaptionVertical = TRUE; fill = 1; }
                }
            }
        }
    }
END
MODULE CryptoProLegalEntity;

REQUIRE CryptoPro, LegalEntity;

NAMESPACE CryptoPro;

dataKeyAlias 'Имя ключа УКЭП' = DATA STRING(LegalEntity);
dataKeyPassword 'Пароль ключа УКЭП' = DATA STRING(LegalEntity) ECHO;

EXTEND FORM integrationData
    OBJECTS lcp = LegalEntity
    PROPERTIES(lcp) READONLY id, name
    PROPERTIES(lcp) dataKeyAlias, dataKeyPassword
    FILTERS isCompany(lcp)
;

DESIGN integrationData {
    cryptopro {
        MOVE BOX(lcp);
    }
}
clientKeyAlias = ABSTRACT STRING (LegalEntity);

keyAlias(LegalEntity l) = OVERRIDE clientKeyAlias(l), dataKeyAlias(l), (keyAlias() IF l IS LegalEntity);
keyPassword(LegalEntity l) = OVERRIDE dataKeyPassword(l), (keyPassword() IF l IS LegalEntity);

signCustom = ABSTRACT CASE BOOLEAN (LegalEntity);
signCustom ABSTRACT LIST (FILE, LegalEntity);

sign (FILE f, BOOLEAN detached, LegalEntity l) { 
    IF signCustom(l) THEN signCustom(f, l);
    ELSE sign(RAWFILE(f), detached, NULL, NULL, keyAlias(l), keyPassword(l)); 
}

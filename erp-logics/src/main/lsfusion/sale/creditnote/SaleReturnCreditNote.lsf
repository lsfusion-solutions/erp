MODULE SaleReturnCreditNote;

REQUIRE CreditNote, SaleReturnShipment, SaleCreditNote, SaleReturnInvoice;

PRIORITY Sale, Operation;

NAMESPACE SaleReturn;

//----------------------------------------------- Акт ---------------------------------------------------//

@defineCreditNote(' (продажа-возврат)', supplier, supplier, company, customer, SaleReturn);
@defineCreditNotePaymentCondition(' (продажа-возврат)');
@extendFormFilterAccessStock(CreditNote, c, creditNotes, supplierStock, company);

@defineCreditNoteBatch(supplier, supplier);

@defineCreditNoteBatchDialog();

//------------------------------ Ограничение на выбор контрагентов -----------------------------//

CONSTRAINT supplier(UserCreditNote userCreditNote) AND NOT isCompany(supplier(userCreditNote))
    CHECKED BY supplier[UserCreditNote] MESSAGE 'Для акта расхождения выбрано в качестве поставщика организация, не являющаяся компанией';
CONSTRAINT customer(UserCreditNote userCreditNote) AND NOT isCustomer(customer(userCreditNote))
    CHECKED BY customer[UserCreditNote] MESSAGE 'Для акта расхождения выбрано в качестве покупателя организация, не являющаяся покупателем';

//------------------------------ Автоматическое проставление свойств -----------------------------//

@deriveDocumentLegalEntityDefaultStock(UserCreditNote, customer, userCreditNote);

@defineDocumentLegalEntityStockAccess(UserCreditNote, supplier, company, userCreditNote);

// -- Операция
@defineDocumentOperationContainer(creditNote ,c, overName);
@extendFormFilterRoleAccess(creditNote, c, creditNotes);
@defineDocumentOperationConstraint(creditNote, 'акт расхождения (продажа-возврат)', SaleReturn);
@defineDocumentOperationRole(userCreditNote);
@deriveDocumentOperationProperty(UserInvoice, createCreditNote);

checkDateTimeCreditNote 'Акт расхождения (польз.) должен быть создан позже даты поставки в накладной' (UserCreditNoteDetail d) = checkDateTimeCreditNote(operation(d));
CONSTRAINT (SETCHANGED (invoiceDetail(UserCreditNoteDetail d)) OR SETCHANGED (operation(d)) OR SETCHANGED (dateTime(d)) OR SET(isPosted(d)))
    AND isPosted(d) AND checkDateTimeCreditNote(d) AND dateTime(d) <= shipmentDateTime(invoiceDetail(d))
    MESSAGE 'Акт расхождения (польз.) должен быть создан позже даты поставки в накладной';

@defineDocumentOperationLegalEntity(userCreditNote, supplier, 'Поставщик');
@deriveDocumentOperationLegalEntity(userCreditNote, supplier, userCreditNote);
@defineDocumentOperationLegalEntity(userCreditNote, customer, 'Покупатель');
@deriveDocumentOperationLegalEntity(userCreditNote, customer, userCreditNote);
@defineDocumentOperationStock(userCreditNote, supplier, 'Склад поставщика');
@deriveDocumentOperationStock(userCreditNote, supplier, userCreditNote);
@defineDocumentOperationStock(userCreditNote, customer, 'Склад покупателя');
@deriveDocumentOperationStock(userCreditNote, customer, userCreditNote);

operation(InvoiceCreditNote creditNote) += operation(invoice(creditNote));

//------------------------------ Расширение формы -----------------------------//

// Фильтры
EXTEND FORM userCreditNote

    FILTERGROUP filter
        FILTER 'С остатком ' currentBalance(ks, st) 'F10' 
        FILTER 'В документе ' quantitySupplier(ks, c, st) 'F9'

;

EXTEND FORM userCreditNote

    FILTERGROUP filter3
        FILTER 'С остатком ' prevCurrentBalance(b, sto) 'F10' DEFAULT
        FILTER 'В документе ' quantitySupplier(b, c, sto) 'F9'
;

NAVIGATOR {
    saleReturnNavigator {
        NEW creditNotes;
    }
}

operation[CreditNote.CreditNote] (CreditNote creditNote) += operation(creditNote);

invoice 'Накладная' = DATA Invoice (UserCreditNote);
descriptionInvoice 'Накладная' (UserCreditNote c) = description(invoice(c));

invoice (CreditNote creditNote) = MULTI invoice[UserCreditNote](creditNote), invoice[InvoiceCreditNote](creditNote);
numberInvoice 'Накладная' (CreditNote creditNote) = number(invoice(creditNote));

countUserCreditNote = GROUP SUM 1 BY invoice(UserCreditNote ic);

userCreditNote = GROUP MAX UserCreditNote userCreditNote BY invoice(userCreditNote);
userCreditNote[Invoice.Invoice](SaleReturn.Invoice invoice) += userCreditNote(invoice);

@defineOperationProperty(allowNotOnlyCreditNote, 'Разрешить несколько актов расхождения для накладной', constraintContainer);

CONSTRAINT countUserCreditNote(Invoice invoice) > 1.0 AND NOT allowNotOnlyCreditNote(operation(invoice))
    CHECKED BY invoice[UserCreditNote]
    MESSAGE 'Для накладной уже создан акт расхождения';

CONSTRAINT invoice(userCreditNote(UserCreditNoteDetail detail)) AND invoiceDetail(detail)
    AND NOT invoice(userCreditNote(detail))=invoice(invoiceDetail(detail))
    CHECKED BY invoiceDetail[UserCreditNoteDetail]
    MESSAGE 'Накладная строки акта расхождения отличается от накладной акта расхождения';

CONSTRAINT invoice(UserCreditNote i) AND NOT operation(i)=operation(invoice(i))
    CHECKED BY invoice[UserCreditNote]
    MESSAGE 'Операция акта расхождения отличается от операции накладной';

EXTEND FORM userCreditNote
    PROPERTIES(c) descriptionInvoice AFTER nameOperation(c)
;

DESIGN userCreditNote {
    first {
        MOVE PROPERTY(nameOperation(c));
        MOVE PROPERTY(descriptionInvoice(c));
    }
}    

EXTEND FORM creditNotes
    PROPERTIES(c) READONLY numberInvoice AFTER series(c)
;

WHEN LOCAL CHANGED(invoice(UserCreditNote creditNote)) DO {
    supplier(creditNote) <- supplier(invoice(creditNote));
    supplierStock(creditNote) <- supplierStock(invoice(creditNote));
    customer(creditNote) <- customer(invoice(creditNote));
    customerStock(creditNote) <- customerStock(invoice(creditNote));
    contractSku(creditNote) <- contractSku(invoice(creditNote));
    currency(creditNote) <- currency(invoice(creditNote));
}


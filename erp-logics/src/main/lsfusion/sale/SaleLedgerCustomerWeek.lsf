MODULE SaleLedgerCustomerWeek;

REQUIRE SaleLedger;

NAMESPACE SaleLedger;

quantitySold 'Продано за неделю (кол-во)'  = GROUP SUM
        quantitySold(Sku sku, Stock stock, LegalEntity customer, DATE date) IF date >= DATE dateFrom AND date <= DATE dateTo AND isCompany(stock)
        BY sku, customer, extractWeek(date), dateFrom, dateTo;
quantitySold 'Продано за неделю (кол-во)'  = GROUP SUM
        quantitySold(Sku sku, Stock stock, LegalEntity customer, DATE date) IF date >= DATE dateFrom AND date <= DATE dateTo AND isCompany(stock)
        BY customer, extractWeek(date), dateFrom, dateTo;
quantitySoldTotal 'Продано итого (кол-во)' (Sku sku, LegalEntity customer, DATE dateFrom, DATE dateTo)  = GROUP
        SUM
        quantitySold(sku, Stock stock, customer, DATE date) IF date >= dateFrom AND date <= dateTo AND isCompany(stock);       
 
         
inReport 'Вкл.' = DATA LOCAL BOOLEAN (Stock);

sessionConcatStocks 'Склады' () =
    GROUP CONCAT name(Stock st) IF inReport(st),', ' CHARWIDTH 50;
    

FORM selectStock 'Выбор складов'
    TREE stockTree sg = StockGroup PARENT parent
    PROPERTIES READONLY sgTreeName = name(sg)

    OBJECTS st = Stock GRID
    PROPERTIES (st) READONLY inReport CHANGEABLE, name, id SHOWIF showIDs()
    FILTERS countCompanyStock(sg),
            isCompany(st),
            isParent(sg, st)
    
    FILTERGROUP select FILTER 'Отмеченные' inReport(st) 'F9' 
;
DESIGN selectStock {
    BOX {
        size = (1024, 768);
        NEW top {
            fill = 1;
            type = SPLITH;
            MOVE BOX(TREE stockTree);
            MOVE BOX(st) {fill = 2.5;}
        }
        MOVE TOOLBARBOX;
    }
}

@extendFormFilterStockAccess(st, selectStock, company);

selectStocks '' ()  { SHOW selectStock  ; }



sumSoldSelect 'Продано за неделю (сумма)'  = GROUP SUM
        sumSold(Sku sku, Stock stock, LegalEntity customer, DATE date) IF date >= DATE dateFrom AND date <= DATE dateTo AND inReport(stock)
        BY customer, extractWeek(date), dateFrom, dateTo;
sumSoldSelect 'Продано за неделю (сумма)' (LegalEntity customer, DATE dateFrom, DATE dateTo)  = GROUP
        SUM
        sumSold(Sku sku, Stock stock, customer, DATE date) IF date >= dateFrom AND date <= dateTo AND inReport(stock);
sumSoldSelect 'Продано за неделю (сумма)'  = GROUP SUM
        sumSold(Sku sku, Stock stock, LegalEntity customer, DATE date) IF date >= DATE dateFrom AND date <= DATE dateTo AND inReport(stock)
        BY extractWeek(date), dateFrom, dateTo;
                        
weekString = FORMULA BPSTRING[4] 'CAST($1 AS character(4))' CHARWIDTH 4 NOFLEX;

FORM saleSkuStockCustomer 'Продажи клиентам по неделям'

    OBJECTS dates = (dFrom = DATE, dTo = DATE) PANEL
    PROPERTIES valFrom = VALUE(dFrom), valTo = VALUE(dTo)
    
    OBJECTS cu = LegalEntity PANEL 
    PROPERTIES (cu) SELECTOR name
    FILTERS isCustomer(cu)

    OBJECTS w = INTEGER GRID
    FILTERS quantitySold(cu, w, dFrom, dTo)



    TREE skuTree sk = SkuGroup PARENT parent
    PROPERTIES READONLY order(sk), skuTreeName = name(sk)
    ORDER order(sk), skuTreeName
    FILTERGROUP inactive FILTER 'Активные' active(sk) 'F5' DEFAULT

    OBJECTS           s=Sku
    PROPERTIES        READONLY name(s), id(s)//, idBarcode(s)

    FILTERS           isParent(sk, s),                    
                      quantitySoldTotal(s, cu, dFrom, dTo)
    ORDER          name(s)

    PROPERTIES        READONLY quantitySold(s, cu, w, dFrom, dTo)  COLUMNS (w) HEADER weekString (w),
                      quantitySoldTotal(s, cu, dFrom, dTo)

    PROPERTIES() sessionConcatStocks ON CHANGE selectStocks()
                          
    OBJECTS w1 = INTEGER GRID   
    FILTERS sumSoldSelect(w1, dFrom, dTo)
     
    OBJECTS cu1 = LegalEntity 
    PROPERTIES (cu1) READONLY name, id
    PROPERTIES        READONLY sumSoldSelect(cu1, w1, dFrom, dTo)  COLUMNS (w1) HEADER weekString (w1),
                      sumSoldSelect(cu1, dFrom, dTo)   
                       
    FILTERS isCustomer(cu1),
            sumSoldSelect(cu1, dFrom, dTo)
////            
//    TREE skuTree1 sk1 = SkuGroup PARENT parent
//    PROPERTIES READONLY order(sk1), name(sk1)
//    ORDER order(sk1), name(sk1)
//    FILTERGROUP inactive1 FILTER 'Активные' active(sk1) 'F5' DEFAULT

    OBJECTS           s1=Sku
    PROPERTIES        READONLY name(s1), id(s1)

    FILTERS           isParent(sk, s1),                    
                      quantitySoldTotal(s1, cu1, dFrom, dTo)
    ORDER          name(s1)

    PROPERTIES        READONLY quantitySold(s1, cu1, w1, dFrom, dTo)  COLUMNS (w1) HEADER weekString (w1),
                      quantitySoldTotal(s1, cu1, dFrom, dTo)            
            
                      
;

DESIGN saleSkuStockCustomer {

    NEW topContainer {
        fill = 1;
        type = CONTAINERV;

        NEW top {

            NEW params {
                type = CONTAINERH;
                MOVE BOX(dates) {
                    type = CONTAINERH;
                }
            }
        }
        NEW firstCase {
            fill = 1;
            type = SPLITH;
            MOVE BOX(TREE skuTree) { caption = 'Товарные группы'; }
            
            NEW tab{
                fill = 2.5;
                type = TABBED;
                NEW sku { 
                    type = SPLITH;
                    caption = 'Покупатель';
                    
                    NEW item {
                        fill = 1;
//                        caption = 'Товары';
                        MOVE BOX(cu) {
                            MOVE PROPERTY (name(cu));
                        }
                        MOVE BOX(s);
                    }
                }
                NEW customers {
                    caption = 'Покупатели';
                    fill = 1;
                    NEW stocks {
                        MOVE PROPERTY (sessionConcatStocks());
                    }
                    NEW customers1 {
                        fill = 1;
                        type = SPLITV;
                         MOVE BOX(cu1) {
                            PROPERTY (name(cu1)) {charWidth =30;}
                         }
                         MOVE BOX(s1);
                    }
                   
                }
                
            }

        }
    }
    MOVE TOOLBARBOX;
}

NAVIGATOR {
    salesReports {
        NEW saleSkuStockCustomer;
    }
}


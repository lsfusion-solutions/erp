MODULE SaleStatisticsStore;

REQUIRE SaleStatistics, ZReport;

NAMESPACE Sale;

//---------------------------------------------- Магазин 1 торг стат. отчетов -------------------------------------//

isInternet 'Интернет-магазин' = DATA BOOLEAN (Store);
skipCorporate = ABSTRACT BOOLEAN (DepartmentStore);
isCorporate 'Фирменный магазин' = DATA BOOLEAN (Store);
isCorporate 'Фирменный магазин' (DepartmentStore ds) = isCorporate(store(ds)) AND NOT skipCorporate(ds);
skipInternet = ABSTRACT BOOLEAN (DepartmentStore);
isInternet 'Интернет-магазин' (DepartmentStore ds) = isInternet(store(ds)) AND NOT skipInternet(ds);

EXTEND FORM store PROPERTIES(s) isCorporate, isInternet;

DESIGN store{
    header{
        NEW header1Torg {
            caption = 'Параметры 1-торг';
            type = COLUMNS;
            lines = 4;
            MOVE PROPERTY(isCorporate(s));
            MOVE PROPERTY(isInternet(s));
        }
    }
}

totalSumCorporateSoldBy 'В фирменных магазинах, руб.' (StatisticGroup group, legalEntity, region, DATE dateFrom, DATE dateTo)=
    GROUP SUM sumSoldStatistic (Sku sku, Stock stock, dateFrom, dateTo) IF isCorporate(stock) AND isParent(group, sku) AND NOT skipSale(stock)
    BY legalEntity(stock), region(stock);

totalSumInternetSoldBy 'В интернет-магазинах, руб.' (StatisticGroup group, legalEntity, region, DATE dateFrom, DATE dateTo)=
    GROUP SUM sumSoldStatistic (Sku sku, Stock stock, dateFrom, dateTo) IF isInternet (stock) AND isParent(group, sku) AND NOT skipSale(stock)
    BY legalEntity(stock), region(stock);

totalSumCorporateSoldBy 'В фирменных магазинах, руб.' (StatisticGroup group, Stock stock, DATE dateFrom, DATE dateTo)=
    GROUP
        SUM sumSoldStatistic (Sku sku, stock, dateFrom, dateTo) IF isCorporate(stock) AND isParent(group, sku);

totalSumInternetSoldBy 'В интернет-магазинах' (StatisticGroup group, Stock stock, DATE dateFrom, DATE dateTo)=
    GROUP
        SUM sumSoldStatistic (Sku sku, stock, dateFrom, dateTo) IF isInternet(stock) AND isParent(group, sku);

// было вот так - переделал на Z-отчет
//sumCard 'Продано с использованием банк. карточек' (departmentStore, dateFrom, dateTo) = GROUP SUM sum(Payment payment) IF paymentMeans(payment)==PaymentMeans.paymentMeansCard
//    AND date(receipt(payment)) >= DATE dateFrom AND date(receipt(payment)) <= DATE dateTo AND isPosted(receipt(payment))
//        BY departmentStore(receipt(payment)), dateFrom, dateTo;
//     
//sumSale 'Продано по кассе' (departmentStore, dateFrom, dateTo) = GROUP SUM sumReceiptDetail(Receipt receipt)
//    IF date(receipt) >= DATE dateFrom AND date(receipt) <= DATE dateTo AND isPosted(receipt)
//        BY departmentStore(receipt), dateFrom, dateTo;

sumCard 'Продано с использованием банк. карточек' (DATE dateFrom, DATE dateTo) =
    GROUP 
    SUM sumCard (ZReport z) IF inStat(departmentStore(z)) AND dateFrom <= date(z) AND dateTo >= date(z);

extraSumSale = ABSTRACT NUMERIC[18,4](DATE, DATE);

sumSale 'Продано по кассе' (DATE dateFrom, DATE dateTo) =
    (GROUP 
    SUM sumReceiptDetail (ZReport z) IF inStat(departmentStore(z)) AND dateFrom <= date(z) AND dateTo >= date(z)) // вся сумма, с учетом проданных сертификатов
    (+) extraSumSale(dateFrom, dateTo);

EXTEND FORM statisticalReportStockSelect
    PROPERTIES   sumCard(dFrom, dTo), 
                 sumSale(dFrom, dTo) // вся сумма, с учетом проданных сертификатов

;

DESIGN statisticalReportStockSelect {
    printContainer {
        NEW summa {
            caption = 'По выбранным складам';
            MOVE PROPERTY(sumCard(dFrom,dTo));
            MOVE PROPERTY(sumSale(dFrom,dTo));  
        } 
    }
}
MODULE SaleInvoiceTracking;

REQUIRE SaleInvoice, ItemTracking, BatchInventoryTracking;

NAMESPACE Sale;

isTracking (Invoice i) = GROUP SUM 1 IF isTrackingOver(sku(UserInvoiceDetail d)) AND invoice(d)==i;
showSPT (Invoice i)= TRUE IF isTracking(i);
isTracking 'Подлежит прослеживанию' (UserInvoiceDetail d) = TRUE IF isTrackingOver(sku(d));

quantityDespatchedSPT 'Кол-во в ед.СПТ ' (UserInvoiceDetail d) =  NUMERIC[12,5] (CASE
    WHEN trackingUOMCustomsGroup(sku(d))=='112'  THEN (OVERRIDE quantity(d), 1.0)* volume(sku(d))
    WHEN trackingUOMCustomsGroup(sku(d))=='166'  THEN (OVERRIDE quantity(d), 1.0)* netWeight(sku(d))
    ELSE (OVERRIDE quantity(d), 1.0));
lineItemPriceSPT'Цена за единицу товара для прослеживаемости' (UserInvoiceDetail d) = NUMERIC[12,2] (OVERRIDE lineItemPriceSPT(batch(d), supplierStock(d)), (CASE
    WHEN trackingUOMCustomsGroup(sku(d))=='112'  THEN (OVERRIDE price(d), 1.0)/ volume(sku(d))
    WHEN trackingUOMCustomsGroup(sku(d))=='166'  THEN (OVERRIDE price(d), 1.0)/ netWeight(sku(d))
    ELSE (OVERRIDE price(d), 1.0)));

itemCustomCode 'Код ТНВЭД' (UserInvoiceDetail d) = OVERRIDE itemCustomCode(batch(d), supplierStock(d)), itemCustomCode(batch(d)), codeCustomsGroupDefaultCountry(sku(d));
itemCustomCodeOther 'Дополнительный таможенный код'(UserInvoiceDetail d) = OVERRIDE itemCustomCodeOther(batch(d), supplierStock(d)), itemCustomCodeOther(batch(d)), idExtraCode(sku(d));
deliveryTypePrev 'Тип приходного документа'(UserInvoiceDetail d) = OVERRIDE deliveryTypePrev(batch(d), supplierStock(d)), deliveryTypePrev(batch(d));
deliveryNotePrev 'Номер приходного документа' (UserInvoiceDetail d) = OVERRIDE deliveryNotePrev(batch(d), supplierStock(d)), deliveryNotePrev(batch(d));
deliveryNoteDate 'Дата создания приходного документа' (UserInvoiceDetail d) = OVERRIDE deliveryNoteDate(batch(d), supplierStock(d)), deliveryNoteDate(batch(d));
deliveryNotePrevLineID 'Номер строки в приходном документе' (UserInvoiceDetail d) = OVERRIDE deliveryNotePrevLineID(batch(d), supplierStock(d)), deliveryNotePrevLineID(batch(d));
lineItemQuantitySPT 'Ед. изм.СПТ' (UserInvoiceDetail d) = OVERRIDE lineItemQuantitySPT(batch(d), supplierStock(d)), lineItemQuantitySPT(batch(d)), trackingUOMCustomsGroup(sku(d));

EXTEND FORM userInvoice
    OBJECTS d6 = UserInvoiceDetail GRID 
    PROPERTIES(d6) SHOWIF showSPT(i) READONLY index, idBarcodeSku, nameSku, isTracking BACKGROUND backgroundEC(extraCode(sku(d6))), itemCustomCode, itemCustomCodeOther,
        deliveryTypePrev, deliveryNotePrev, deliveryNoteDate, deliveryNotePrevLineID, lineItemQuantitySPT, quantityDespatchedSPT, lineItemPriceSPT
    FILTERS userInvoice(d6) == i, isTracking(d6)

;

DESIGN userInvoice{
    specificationBox {
        NEW sptContainer {
            caption = 'Прослеживание';
            MOVE BOX(d6);
        }
    }
}

skipCheckItemSize = DATA BOOLEAN ();

CONSTRAINT (SET(isPosted(UserInvoiceDetail id)) OR SETCHANGED(sku(id))) AND isPosted(id) AND trackingUOMCustomsGroup(sku(id))=='112' AND isTrackingOver(sku(id)) AND NOT volume(sku(id)) AND NOT skipCheckItemSize()
    MESSAGE 'Товару нужно задать параметр "Объем, л" ' ;

CONSTRAINT (SET(isPosted(UserInvoiceDetail id)) OR SETCHANGED(sku(id))) AND isPosted(id) AND trackingUOMCustomsGroup(sku(id))=='166' AND isTrackingOver(sku(id)) AND NOT netWeight(sku(id)) AND NOT skipCheckItemSize()
    MESSAGE 'Товару нужно задать параметр "Вес нетто, кг" ' ;

skipCheckBatch = DATA BOOLEAN ();

CONSTRAINT (SET(isPosted(UserInvoiceDetail id)) OR SETCHANGED(sku(id)) OR CHANGED(batch(id))) AND isPosted(id) AND isTrackingOver(sku(id)) AND NOT batch(id) AND NOT skipCheckBatch()
    MESSAGE 'Укажите партию для прослеживаемого товара' ;
MODULE SaleInvoiceRRP;

REQUIRE SaleInvoice, AgreementRRP;

NAMESPACE Sale;

//------------------------- Рекомендуемая розничная цена-------------------------//

@defineDocumentInterfaceHeaderProperty (invoice, showRRP, 'RRP');

// -- Операция
@defineOperationProperty(showRRP, 'RRP', showContainer);
@deriveDocumentOperationProperty(UserInvoice, showRRP);

@defineDocumentInterfaceDetailPriceListType (invoice, priceListType, RRP, ' (RRP)');

RRPPriceListType(UserInvoiceDetail userInvoiceDetail) <- RRPPriceListType(agreement(userInvoice(userInvoiceDetail)))
    WHEN CHANGED (agreement(userInvoice(userInvoiceDetail)));

currencyRRP (UserInvoiceDetail userInvoiceDetail) = currency(RRPPriceListType(userInvoiceDetail));
nameCurrencyRRP 'Валюта (RRP)' (UserInvoiceDetail userInvoiceDetail) = name(currencyRRP(userInvoiceDetail));

currencyRRP (InvoiceDetail invoiceDetail) = currency(RRPPriceListType(invoiceDetail));
nameCurrencyRRP 'Валюта (RRP)' (InvoiceDetail invoiceDetail) = name(currencyRRP(invoiceDetail));

@defineDocumentInterfaceDetailPriceCustomPrefix (invoiceDetail, RRP, ' (RRP)');
WHEN LOCAL (CHANGED(RRPPriceListType(UserInvoiceDetail detail)) OR
             CHANGED(sku(detail)) OR
             CHANGED(supplierStock(detail)) OR
             CHANGED(dateTime(detail))) AND PriceListType pt == RRPPriceListType(detail) NOINLINE (pt) DO
    RRPPrice (detail)  <-  prevPriceB(pt,
                                                                               sku(detail),
                                                                               supplierStock(detail),
                                                                               dateTime(detail))
                                                    ;

RRPSum 'Сумма (RRP)' (UserInvoiceDetail detail) = round(RRPPrice (detail)*quantity(detail), currency(detail));
RRPSum 'Сумма (RRP)' (InvoiceDetail detail) = round(RRPPrice(detail)*quantity(detail), currency(detail));

EXTEND FORM userInvoice
    PROPERTIES(i) showRRP
    PROPERTIES(d) SHOWIF showRRP(i) BEFORE deleteid nameRRPPriceListType, nameCurrencyRRP READONLY, RRPPrice
;
DESIGN userInvoice {
    headerExtraParams {
        NEW headerRRP {
            caption = 'RRP';
            MOVE PROPERTY(showRRP(i));
        }
    }
}

EXTEND FORM invoices
    PROPERTIES(d) READONLY SHOWIF showRRP(i) nameRRPPriceListType, nameCurrencyRRP, RRPPrice
;

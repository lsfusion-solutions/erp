MODULE SaleShipment;

REQUIRE Shipment, SaleInvoice, SaleOperation, OrderShipment, InvoiceShipment, PriceListLedger, StockDocumentSkuLedger;

NAMESPACE Sale;


//----------------------------------------------- Поставка ---------------------------------------------------//

@defineShipment('Поставки', ' (продажа)', supplier, company, customer, company, Sale, s);
@extendFormFilterAccessStock(Shipment, s, shipments, supplierStock, company);

@defineShipmentBatch(supplierStock);
@implementDocumentBatch(shipment);

@implementDocumentBatchForm(ss);

@defineShipmentBatchDialogStock(supplierStock, Out);

expiryDate(UserShipmentDetail detail) <- prevExpiryDate(batch(detail)) WHEN CHANGED (batch(detail));

@defineShipmentStockDestination(supplierStock, customerStock);

@defineOrderShipment(' (продажа)', supplierStock);
@defineInvoiceShipment(' (продажа)', supplierStock, company, Sale);
@defineInvoiceShipmentAction(' (продажа)');

@defineInvoiceShipmentBatch();

@defineDocumentSkuStockSupplier (userShipment, userShipment, s);
@defineDocumentBatchStockSupplier(userShipment, userShipment, s);

@defineOrderShipmentExecution(); 
@defineOrderInvoiceSupplierExecution(sale, ' (продажа)', supplier);
fillSelectOrder(DATE df, DATE dt) + { 
    IF df IS DATE AND dt IS DATE THEN {
        selectOrder(OrderDetail d) <- NULL;
        IF NOT (GROUP SUM 1 IF selectOrder(LegalEntity l))  THEN {
            selectOrder(LegalEntity l) <- TRUE WHERE active(l) AND isCustomer(l);    
        }
        IF NOT (GROUP SUM 1 IF selectOrder(Stock st))  THEN {
            selectOrder(Stock st) <- TRUE WHERE active(st) AND isCompany(st) AND accessCompany(currentUser(), st); 
        }
        IF NOT (GROUP SUM 1 IF selectOrder(Sku s))  THEN {
            selectOrder(Sku s) <- TRUE WHERE active(s) AND name(s); 
        } 
        selectOrder(OrderDetail d) <- TRUE WHERE isPosted(d) AND date(d) >= df AND date(d) <= dt AND selectOrder(customer(d)) AND selectOrder(supplierStock(d)) AND selectOrder(sku(d));          
    }
    formRefresh(); 
}
EXTEND FORM executionSupplierOrder
    FILTERS isCustomer(l)
;

//------------------------------------------------- Операции ------------------------------------------------------//

@defineDocumentOperationContainer(shipment, s);
operation[Shipment.Shipment](Shipment s)+=operation(s);
@extendFormFilterRoleAccess(shipment, s, shipments);
@defineDocumentOperationConstraint(shipment, 'поставка (продажа)', Sale);

@defineDocumentOperationLegalEntity(userShipment, supplier, 'Поставщик');
@deriveDocumentOperationLegalEntity(userShipment, supplier, userShipment);
@defineDocumentOperationLegalEntity(userShipment, customer, 'Покупатель');
@deriveDocumentOperationLegalEntity(userShipment, customer, userShipment);
@defineDocumentOperationStock(userShipment, supplier, 'Склад поставщика');
@deriveDocumentOperationStock(userShipment, supplier, userShipment);
@defineDocumentOperationStock(userShipment, customer, 'Склад покупателя');
@deriveDocumentOperationStock(userShipment, customer, userShipment);

@defineDocumentOperationRole(userShipment);

@defineOperationProperty(createShipment, 'Поставка', createContainer);
@deriveDocumentOperationProperty(UserInvoice, createShipment);

overFillInvoice(UserShipment s, Invoice i) + { 
    operation(s) <- operation(i);
}
operation(InvoiceShipment shipment) += operation(invoice(shipment));

// Добавляем в копирование поставок
overCopy(Shipment s, UserShipment d) + { 
    operation(d) <- operation(s);
}
@defineOperationFilterProperty(shipment, s, shipments, nameFilterShipmentSupplier);
//------------------------------ Ограничение на выбор контрагентов -----------------------------//

CONSTRAINT supplier(UserShipment userShipment) AND NOT isCompany(supplier(userShipment))
    CHECKED BY supplier[UserShipment] MESSAGE 'Для поставки выбрано в качестве поставщика организация, не являющаяся компанией';
CONSTRAINT customer(UserShipment userShipment) AND NOT isCustomer(customer(userShipment))
    CHECKED BY customer[UserShipment] MESSAGE 'Для поставки выбрано в качестве покупателя организация, не являющаяся покупателем';
    
CONSTRAINT filterShipmentSupplier() AND NOT isCompany(filterShipmentSupplier())
    CHECKED BY filterShipmentSupplier[] MESSAGE 'Для фильтра в поставке выбрана в качестве поставщика организация, не являющаяся компанией';
CONSTRAINT filterShipmentSupplierStock() AND NOT isCompany(filterShipmentSupplierStock())
    CHECKED BY filterShipmentSupplierStock[] MESSAGE 'Для фильтра в поставке выбран в качестве склада поставщика склад, который не принадлежит компании';              
CONSTRAINT filterShipmentCustomer() AND NOT isCustomer(filterShipmentCustomer())
    CHECKED BY filterShipmentCustomer[] MESSAGE 'Для фильтра в поставке выбрана в качестве покупателя организация, не являющаяся покупателем';       

CONSTRAINT filterShipmentSupplier() AND filterShipmentSupplierStock() AND NOT in(filterShipmentSupplier(), filterShipmentSupplierStock())
    CHECKED BY filterShipmentSupplierStock[] MESSAGE 'Поставщик и склад поставщика в фильтре для поставки не имеют связи';
CONSTRAINT filterShipmentCustomer() AND filterShipmentCustomerStock() AND NOT in(filterShipmentCustomer(), filterShipmentCustomerStock())
    CHECKED BY filterShipmentCustomerStock[] MESSAGE 'Покупатель и склад покупателя в фильтре для поставки не имеют связи';  
//------------------------------ Ввод в упаковках -----------------------------//

@defineDocumentPack(shipment, s);
packQuantity[Shipment.ShipmentDetail](ShipmentDetail detail) += packQuantity(detail);
DESIGN userShipment {
    headerExtraParams {
        NEW headerPack {
            caption = 'Упаковка';
            MOVE PROPERTY(showPack(s));
        }
    }
}

@deriveDocumentOperationProperty(UserShipment, showPack);

@defineOrderShipmentPack();
@defineInvoiceShipmentPack();

//------------------------------ Автоматическое проставление свойств -----------------------------//

@deriveDocumentLegalEntityDefaultStock(UserShipment, customer, userShipment);

@defineDocumentLegalEntityStockAccess(UserShipment, supplier, company, userShipment);

//------------------------------ Расширение формы -----------------------------//

// Фильтры
in (UserShipment userShipment, Sku sku)= inCustomer(customer(userShipment), sku);
EXTEND FORM userShipment

    FILTERGROUP filter
        FILTER 'С остатком ' prevCurrentBalance(ks, s) 'F10' DEFAULT
        FILTER 'В документе ' quantityUserShipmentDetail(ks, s) 'F9'

    FILTERGROUP filter2
        FILTER 'С продажей ' in(s, ks) 'F8'
//        FILTER 'В прайсе' 'F7' ledgerPriceSkuUserShipment(ks, s)

;
in (UserShipment userShipment, Batch batch)= inCustomer(customer(userShipment), sku(batch));
EXTEND FORM userShipment

    FILTERGROUP filter3
        FILTER 'С остатком ' prevCurrentBalance(b,s) 'F10' DEFAULT
        FILTER 'В документе ' quantityUserShipmentDetail(b,s) 'F9'

    FILTERGROUP filter4
        FILTER 'С продажей ' in(s,b) 'F8'
//        FILTER 'В прайсе' 'F7' ledgerPriceBatchUserShipment(b, s)

;

// Резервы
@extendFormDocumentSkuOrderLedger(shipment, userShipment, s, supplierStock);
@extendFormDocumentSkuOrderLedgerAll(userShipment, userShipment, s);

@extendFormDocumentBatchOrderLedger(userShipment, userShipment, s, supplierStock);
@extendFormDocumentBatchOrderLedgerAll(userShipment, userShipment, s);

// ------------------------------- Расчет учетной цены для поставки ------------------------ //

@deriveDocumentDetailPriceAccountBatchDisable (userInvoice, shipment, sku, supplierStock);
@deriveDocumentDetailPriceAccountBatchDisable (userShipment, sku, supplierStock);

// ------------------------------- Проведение по товарному отчету ------------------------ //
@implementStockDocumentLedgerOut(Shipment, supplierStock);

overSumItem = ABSTRACT  NUMERIC[18,4](Shipment);
overSumContainer = ABSTRACT NUMERIC[18,4](Shipment);

type(Shipment l) += STRING[50]('Продажа') IF l IS Shipment;
sumItem (Shipment ledger) += OVERRIDE overSumItem(ledger), sumItemShipmentDetail(ledger);
sumContainer (Shipment ledger) += OVERRIDE overSumContainer(ledger), sumContainerShipmentDetail(ledger);

legalEntity(Shipment ledger) += customer(ledger);  
legalEntityStock(Shipment ledger) += customerStock(ledger);  

operation[StockDocumentLedger](Shipment ledger) += operation(ledger);

close[StockDocumentLedger](Shipment l) + {  close(l); }

// ------------------------------- Проведение по регистру остатков ------------------------ //
@implementSkuLedgerOutFIFO(ShipmentDetail, sku, supplierStock);
quantity[OutFIFOSkuLedger] (ShipmentDetail ledger) += quantity (ledger);
batch[SkuLedger](ShipmentDetail ledger) += batch(ledger);
@implementSkuLedgerOutFIFOBatchBalance(shipmentDetail, supplierStock);

overSum = ABSTRACT NUMERIC[18,4] (ShipmentDetail);
sum[OutSkuLedger](ShipmentDetail ledger) += OVERRIDE overSum(ledger), sum(ledger);

stockDocumentLedger(ShipmentDetail ledger) += shipment(ledger); 

cost(ShipmentDetail detail, Batch batch) += cost[SkuLedger,Batch](detail, batch) IF detail IS ShipmentDetail;

show[SkuLedger](ShipmentDetail d) + {  show(d); }

// ------------------------------- Себестоимость продажи ------------------------ //
costSale (invoiceDetail) = GROUP SUM costSum[SkuLedger](ShipmentDetail shipmentDetail) BY invoiceDetail(shipmentDetail);
costSum(InvoiceDetail ledger) += WHEN ledger IS Sale.InvoiceDetail THEN costSale(ledger);
cost(InvoiceDetail ledger, Batch batch) += cost (ledger, batch);

NAVIGATOR {
    saleStockNavigator {
        NEW shipments;
    }
}

sumWholesaleMarkup(InvoiceDetail detail) = sum(detail) - shipmentSum(detail);
sumWholesaleMarkupInvoiceDetail(invoice) = GROUP SUM sumWholesaleMarkup(InvoiceDetail detail) BY invoice(detail);

//-- Запрет на изменение документа другим пользователем
@defineOperationChangesDocument(shipment, UserShipment, preventChangesDocument, created);

overShowEdit (Shipment i) += showEditDeleteUser(i) AND isOpened(i) AND NOT skipShowEdit(i);
overShowDelete (UserShipment i) += showEditDeleteUser(i) AND isUserOpened(i) AND NOT skipShowDelete(i); 

overFillShipmentPriceAll  ABSTRACT LIST ( );

fillShipmentPriceAll 'Переписать учетную цену в накладных (продажа)'()   { 
    shipmentPrice (UserInvoiceDetail detail)  <- prevAccountPriceB(batch(detail), supplierStock(detail), dateTime(detail)) WHERE isOpened(detail) AND NOT disableUpdateShipmentPrice(detail) AND batch(detail);
    shipmentPrice (UserInvoiceDetail detail)  <- prevAccountPriceB(sku(detail), supplierStock(detail), dateTime(detail)) WHERE isOpened(detail) AND NOT disableUpdateShipmentPrice(detail) AND NOT batch(detail); 
    overFillShipmentPriceAll();
}

EXTEND FORM options
    PROPERTIES() SaleFillShipmentPriceAll = fillShipmentPriceAll
;

DESIGN options {
    invoices {
        invoiceSale {
            MOVE PROPERTY(SaleFillShipmentPriceAll);
        }            
    }
}

skipCheckShipmentPrice = ABSTRACT BOOLEAN (ShipmentDetail);
CONSTRAINT isPosted(ShipmentDetail d) AND isShipmentPrice(operation(d)) AND NOT price(d) AND NOT skipCheckShipmentPrice(d)
    MESSAGE 'Для документа (продажа) должна быть задана учетная цена';


EXTEND FORM invoices
    OBJECTS dsLog = DocumentDetailLog GRID
    PROPERTIES (dsLog) SHOWIF enableDocumentLog() READONLY index, idSku, nameSku, quantity, price, nameUser, date, time, note
    FILTERS documentId(dsLog) == LONG(invoiceShipment(i))
;

DESIGN invoices {
    historyTabs {
         NEW detailShipmentLog{
             caption = 'По строкам (поставка)';
             MOVE BOX(dsLog);
         }
    }
}
 
EXTEND FORM userInvoice
    OBJECTS dsLog = DocumentDetailLog GRID
    PROPERTIES (dsLog) SHOWIF enableDocumentLog() READONLY index, idSku, nameSku, quantity, price, nameUser, date, time, note
    FILTERS documentId(dsLog) == LONG(invoiceShipment(i))
;

DESIGN userInvoice {
    historyTabs {
         NEW detailShipmentLog{
             caption = 'По строкам (поставка)';
             MOVE BOX(dsLog);
         }
    }
} 
MODULE SaleShipmentCost;

REQUIRE SaleShipment, SkuLedgerCost;

NAMESPACE Sale;

recalculateCostCalculated(DATE dFrom, DATE dTo) +{
    logToFile('cost', 'Начат пересчет цен в поставках (продажа)');
    APPLY NESTED (calculatedCost[Sku, Stock]) {            
        price(UserShipmentDetail d) <- calculatedCost(sku(d), supplierStock(d)) 
                WHERE date(d) >= dFrom AND date(d) <= dTo AND NOT date(d) < documentsClosedDate(supplierStock(d)) AND 
                      calculatedCost(sku(d), supplierStock(d)) AND NOT calculatedCost(sku(d), supplierStock(d)) = price(d);
    }
    IF canceled() THEN CANCEL;
    
    logToFile('cost', 'Начат пересчет цен в накладных (продажа)');
    APPLY NESTED (calculatedCost[Sku, Stock]) {            
        shipmentPrice(UserInvoiceDetail d) <- calculatedCost(sku(d), supplierStock(d)) 
                WHERE date(d) >= dFrom AND date(d) <= dTo AND NOT date(d) < documentsClosedDate(supplierStock(d)) AND
                      calculatedCost(sku(d), supplierStock(d)) AND NOT calculatedCost(sku(d), supplierStock(d)) = shipmentPrice(d);
    }
    IF canceled() THEN CANCEL;
    logToFile('cost', 'Закончен пересчет цен в поставках и накладных (продажа)');
}

recalculateCostAccount(DATE dFrom, DATE dTo) +{
    logToFile('cost', 'Начат пересчет цен в поставках (продажа)');
    APPLY NESTED (calculatedCost[Sku, Stock]) {
        price(UserShipmentDetail d) <- prevAccountPriceB(sku(d), supplierStock(d), dateTime(d))
            WHERE date(d) >= dFrom AND date(d) <= dTo AND calcRecalculateCost(sku(d)) AND NOT date(d) < documentsClosedDate(supplierStock(d));
    }
    IF canceled() THEN CANCEL;

    logToFile('cost', 'Начат пересчет цен в накладных (продажа)');
    APPLY NESTED (calculatedCost[Sku, Stock]) {
        shipmentPrice(UserInvoiceDetail d) <- prevAccountPriceB(sku(d), supplierStock(d), dateTime(d))
            WHERE date(d) >= dFrom AND date(d) <= dTo AND calcRecalculateCost(sku(d)) AND NOT date(d) < documentsClosedDate(supplierStock(d));
    }
    IF canceled() THEN CANCEL;
    logToFile('cost', 'Закончен пересчет цен в поставках и накладных (продажа)');
}

MODULE SaleInternetOrderCourier;

REQUIRE SaleInternetPickingDetail, ItemInternet;

dateTimeFrom 'Доставка' (InternetOrder o) = dateTimeToDateTime(deliveryDate(o), deliveryTimeFromInternetOrder(o));

FORM setDeliveryTime 'Установка времени доставки'
    OBJECTS o = InternetOrder PANEL
    PROPERTIES (o) deliveryDate, deliveryTimeFromInternetOrder, deliveryTimeToInternetOrder
    EVENTS ON OK{ APPLY; }
;
setDeliveryTime 'Выбрать время доставки' (InternetOrder o){
    SHOW setDeliveryTime OBJECTS o = o;
} ASON CONTEXTMENU nameStatus[InternetOrder];


captionDelivery 'Описание' (InternetOrder o) = CONCAT ' ', 'Заказ', '№' + number(o), deliveryDate(o), 'c ' + deliveryTimeFromInternetOrder(o),
    'по ' + deliveryTimeToInternetOrder(o) IN id;

filterCourier = ABSTRACT CASE BOOLEAN (InternetOrder);

FORM printInternetOrderDelivery
    OBJECTS o = InternetOrder
    PROPERTIES (o) number, pickingSumInternetOrderDetail, nameInternetCustomer, phoneInternetCustomer, customerComment,
        addressInternetCustomer, captionDelivery

    OBJECTS d = InternetOrderDetail
    PROPERTIES (d) nameSku, pickingQuantity, price, discountPrice, pickingSum
    FILTERS internetOrder(d) = o

    FILTERS filterCourier(o)
;

FORM dialogDates 'Печать ведомости'
    OBJECTS dates = (dateFrom = DATE, dateTo = DATE) PANEL
    PROPERTIES df = VALUE(dateFrom), dt = VALUE(dateTo)
;

DESIGN dialogDates {
    NEW date BEFORE TOOLBARBOX {
        fill = 1;
        horizontal = TRUE;
        MOVE PROPERTY (df) {
            fill = 1;
            caption = 'Введите дату c';
            font = 'bold 64';
            panelCaptionVertical = TRUE;
        }
        MOVE PROPERTY (dt) {
            fill = 1;
            caption = 'Введите дату на';
            font = 'bold 64';
            panelCaptionVertical = TRUE;
        }
    }
}

useDeliveryCalendar 'Календарь доставки' = DATA BOOLEAN (Carrier);

showDeliveryCalendar 'Отображать в календаре' = DATA BOOLEAN (Carrier, StatusInternetOrder);

EXTEND FORM internetCatalogs
    PROPERTIES (carrier) useDeliveryCalendar
;
EXTEND FORM carrier
    OBJECTS statusDelivery = StatusInternetOrder
    PROPERTIES SHOWIF useDeliveryCalendar(c) showDeliveryCalendar(c, statusDelivery), name(statusDelivery) READONLY
;

printInternetOrderDelivery 'Печать ведомости'(){
    DIALOG dialogDates OBJECTS dateFrom INPUT, dateTo INPUT  DO {
        IF NOT dateFrom OR NOT dateTo THEN {
            MESSAGE 'Не задан период';
            RETURN;
        }
        PRINT printInternetOrderDelivery FILTERS deliveryDate(o) >= dateFrom AND deliveryDate(o) <= dateTo AND filterCourier(o)  PREVIEW;
           // isMinsk(o) AND useDeliveryCalendar(carrier(o)) AND showDeliveryCalendar(carrier(o), status(o));
    }
}

FORM deliveryCalendar 'Календарь доставки'
    OBJECTS o = InternetOrder CALENDAR
    PROPERTIES (o) dateTime = dateTimeFrom, captionDelivery, number = number
    PROPERTIES (o) PANEL READONLY number, nameStatus, addressInternetCustomer, deliveryDate, deliveryTimeFromInternetOrder,
        deliveryTimeToInternetOrder
    PROPERTIES setDeliveryTime(o), edit(o), printInternetOrderDelivery() TOOLBAR

    FILTERS filterCourier(o)
;   

DESIGN deliveryCalendar{
    GRID (o){
        NEW popup{
            MOVE PROPERTY (number(o));
            MOVE PROPERTY (nameStatus(o));
            MOVE PROPERTY (addressInternetCustomer(o));
            MOVE PROPERTY (deliveryDate(o));
            MOVE PROPERTY (deliveryDate(o));
            MOVE PROPERTY (deliveryTimeFromInternetOrder(o));
            MOVE PROPERTY (deliveryTimeToInternetOrder(o));
            NEW action{
                horizontal = TRUE;
                MOVE PROPERTY (setDeliveryTime(o));
                MOVE PROPERTY (edit(o));
            }
        }
    }
}

CLASS Courier 'Курьер' : Employee;
TABLE courier (Courier);

phone 'Телефон' = DATA STRING[100] (Courier) CHARWIDTH 10;
created 'Создан' = DATA DATETIME (Courier);

FORM couriers 'Курьеры'
    OBJECTS c = Courier
    PROPERTIES (c) READONLY name, phone, login, inactive
    PROPERTIES (c) NEWSESSION NEW, EDIT, DELETE

    LIST Courier OBJECT c
;
FORM courier 'Курьер'
    OBJECTS c = Courier PANEL
    PROPERTIES(c) objectClassName, lastName, firstName, initialsEmployee, id SHOWIF showIDs(), namePosition, login,
        sha256Password ON CHANGE changeSHA256Password(c),
        email, nameMainRole, limitAccess, isLocked, inactive,
        useClientLocale, userLanguage SHOWIF NOT useClientLocale(c), userCountry SHOWIF NOT useClientLocale(c),
        userTimeZone, userTwoDigitYearStart
    
    EDIT Courier OBJECT  c
;


courier = DATA Courier (InternetOrder);
fullNameCourier 'Курьер' (InternetOrder o) = name(courier(o));
courierStatus 'Статус доставки' = ABSTRACT STRING (InternetOrder);
backgroundCourier = ABSTRACT CASE COLOR (InternetOrder);

EXTEND FORM internetOrders
    PROPERTIES (o) READONLY BACKGROUND backgroundCourier(o) fullNameCourier, courierStatus
;

assignCourier 'Назначить курьера' (InternetOrder o){
    DIALOG couriers OBJECTS c INPUT c DO NEWSESSION {
        courier(o) <- c;
        APPLY;
    }
}

EXTEND FORM deliveryCalendar
    PROPERTIES (o) PANEL fullNameCourier SHOWIF courier(o) READONLY, assignCourier
;
DESIGN deliveryCalendar{
    popup{
        MOVE PROPERTY (fullNameCourier(o)) BEFORE action;
        action{
            MOVE PROPERTY (assignCourier(o));
        }
    }
}




NAVIGATOR {
    eShop{
        NEW couriers;
    }
}



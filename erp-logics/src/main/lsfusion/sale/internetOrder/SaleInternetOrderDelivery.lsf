MODULE SaleInternetOrderDelivery;

REQUIRE SaleInternetPicking, Label;

NAMESPACE Sale;
// Отчет курьера

CLASS DeliveryReport 'Отчет курьера';
TABLE deliveryReport(DeliveryReport);

@defineDocumentHeaderTimePrefix(DeliveryReport, , '');

@deriveDocumentHeaderTimePrefix(DeliveryReport, );
@defineCreated(DeliveryReport);
@deriveDocumentHeaderTimePrefix(DeliveryReport, );

@defineDocumentHeaderNumber(DeliveryReport, BPSTRING[2]);

@defineNumeratedDefault(DeliveryReport, 'Отчет курьера', 'ОК');

deliveryReport = DATA DeliveryReport (InternetOrderDetail) INDEXED;
in 'Вкл' (DeliveryReport o, InternetOrderDetail d) = deliveryReport(d) == o;

@defineDocumentHeaderCount(deliveryReport, InternetOrderDetail);

relation = GROUP SUM 1 IF in(DeliveryReport o, InternetOrderDetail d) BY o, internetOrder(d) MATERIALIZED INDEXED ;

seriesNumberInternetOrders 'Интернет-заказы' (DeliveryReport deliveryReport) = GROUP
                                                 CONCAT seriesNumber(InternetOrder order) IF relation(deliveryReport, order) , ', '
                                                 ORDER order  CHARWIDTH 30;   

dataDeliveryReportIndex 'Номер строки' = DATA INTEGER (InternetOrderDetail) INDEXED; 
defaultDeliveryReportIndex 'Номер строки (авт.)' (InternetOrderDetail d) =
    PARTITION SUM 1
    ORDER internetOrder(d),d BY deliveryReport(d) CHARWIDTH 4;
indexDeliveryReport 'Номер строки' (InternetOrderDetail d) = OVERRIDE dataDeliveryReportIndex(d), defaultDeliveryReportIndex(d) IN id;

pickingQuantityInternetOrderDetail 'Кол-во (всего) собрано' = GROUP SUM pickingQuantity(InternetOrderDetail d) 
    BY deliveryReport(d) IN documentSum;

pickingSumInternetOrderDetail'Сумма (всего) собрано' = GROUP SUM pickingSum(InternetOrderDetail d) 
    BY deliveryReport(d) IN documentSum;

// Возврат
returnQuantity 'Возвращено' = DATA NUMERIC[16,5](InternetOrderDetail);
returnQuantityInternetOrderDetail'Кол-во (всего) возвращено' = GROUP SUM returnQuantity(InternetOrderDetail d) 
    BY deliveryReport(d) IN documentSum;


CLASS StatusDeliveryReport 'Статус' {
    collected 'Собран',
    delivery 'Доставка',
    delivered 'Доставлен',
    completed 'Завершен'
}    
    
FORM statusDeliveryReport 'Статус'
    OBJECTS o = StatusDeliveryReport
    PROPERTIES(o) READONLY staticCaption
    
    LIST StatusDeliveryReport OBJECT o 
;       
    
status = DATA StatusDeliveryReport (DeliveryReport);
nameStatus 'Статус' (DeliveryReport c) = staticCaption(status(c)) IN documentPrm;

status(DeliveryReport r) <- StatusDeliveryReport.collected WHEN SET(r IS DeliveryReport);    

seriesNumberPicking 'Номер комплектации' (InternetOrderDetail d) = seriesNumber(picking(d)) CHARWIDTH 10;



countInternetOrderDetail 'Кол-во строк товара в документе'  = 
    GROUP SUM 1 IF sku(InternetOrderDetail idetail)
          BY  sku(idetail),
              deliveryReport(idetail);   
editReturnQuantity  = ABSTRACT BOOLEAN (InternetOrderDetail);

FORM deliveryReport 'Отчет курьера'
    OBJECTS r = DeliveryReport PANEL
    PROPERTIES(r) nameStatus, nameNumerator, number, series, date, time, countInternetOrderDetail, pickingQuantityInternetOrderDetail, pickingSumInternetOrderDetail
    
    OBJECTS o = InternetOrder
    FILTERS relation(r,o)
    PROPERTIES (o) READONLY isClosed, isPosted, nameStatus, number, series, date, time

    PROPERTIES (o) READONLY nameCurrency, nameCarrier, nameInternetCustomer, addressInternetCustomer, phoneInternetCustomer
    PROPERTIES (o) READONLY countInternetOrderDetail, quantityInternetOrderDetail, sumInternetOrderDetail,
                            note
    PROPERTIES (o) deliveryDate, namePaymentTypeInternetOrder, paymentCashSumInternetOrder, paymentCardSumInternetOrder, paymentERIPSumInternetOrder, paymentDateInternetOrder,
                            numberPaymentDocumentInternetOrder, deliverySumInternetOrder, deliveryTimeFromInternetOrder, deliveryTimeToInternetOrder   
         
    OBJECTS d = InternetOrderDetail
    PROPERTIES in(r, d)
    PROPERTIES(d) indexDeliveryReport
    PROPERTIES (d) READONLY seriesNumberPicking, idBarcodeSku, idSku, nameSku, shortNameUOMSku
    PROPERTIES (d) READONLY idBatch, nameBatch, pickingQuantity
    PROPERTIES (d) returnQuantity READONLYIF editReturnQuantity(d)
    PROPERTIES (d) READONLY price, discountPrice, pickingSum, nameSupplierStock, seriesNumber, date, time, nameInternetCustomer, addressInternetCustomer, phoneInternetCustomer
    FILTERS deliveryReport(d) == r                                                    
    ORDERS indexDeliveryReport(d)  
    FILTERGROUP filters1 FILTER 'По заказу' internetOrder(d)<=o 'F10'  
            
    EDIT DeliveryReport OBJECT r     
;

DESIGN deliveryReport {

    BOX {
        size = (1024, 768);
        OBJECTS {
            NEW specificationBox{
                fill = 1;
                type = SPLITV;
                
                MOVE BOX(o);
                MOVE BOX(d) {
                    caption = 'Спецификация';
                    fill = 2;
                    PANEL(d) {
                        type = CONTAINERV;
                    }
                }
            }
    
            NEW headerBox BEFORE specificationBox {
                type = CONTAINERH;
    
                NEW headerRow1 {
                    fill = 1;
    
                    MOVE GROUP(documentHeader,r) {
                        type = CONTAINERH;
    
                        MOVE PROPERTY(nameNumerator(r));
                        MOVE PROPERTY(number(r));
                        MOVE PROPERTY(series(r));
                        MOVE PROPERTY(date(r));
                        MOVE PROPERTY(time(r));
                    }
                    MOVE GROUP(documentPrm,r);
                }
                NEW sumContainer {
                    MOVE GROUP(documentSum,r) {
                        columns = 1;
                    }
                }
            }
        }
    }
}

FORM deliveryReports 'Отчеты курьера'
    OBJECTS r = DeliveryReport LAST
    PROPERTIES(r) READONLY nameStatus, date, time, countInternetOrderDetail, pickingQuantityInternetOrderDetail, pickingSumInternetOrderDetail,
                  seriesNumberInternetOrders
    PROPERTIES(r) NEWSESSION NEW, EDIT, DELETE GRID            
    
    OBJECTS d = InternetOrderDetail
    PROPERTIES(d) READONLY indexDeliveryReport, seriesNumberPicking
    PROPERTIES (d) READONLY BACKGROUND background(d) idBarcodeSku, idSku, nameSku, shortNameUOMSku
    PROPERTIES (d) READONLY BACKGROUND background(d) pickingQuantity, price, discountPrice, pickingSum, 
                                                     nameSupplierStock
    FILTERS deliveryReport(d) == r      
    ORDERS indexDeliveryReport(d)
;

carrier = DATA Carrier (DeliveryReport); 
nameCarrier 'Перевозчик' (DeliveryReport r) = name(carrier(r));

createDeliveryReport 'Создать отчет курьера' (InternetOrder order)  { 
    IF NOT countInternetOrders() THEN {
        select(order) <- TRUE;
    } 
    IF countInternetOrders() THEN  {       
            
        FOR [GROUP SUM 1 IF select(internetOrder(InternetOrderDetail d)) AND NOT deliveryReport(d) BY Sale.supplierStock[InternetOrderDetail](d), carrier(internetOrder(d))](Stock stock, Carrier carrier) DO {
            NEWSESSION {
                NEW r = DeliveryReport {
                    carrier(r) <- carrier;
                    FOR Sale.supplierStock[InternetOrderDetail](InternetOrderDetail detail) == stock AND carrier(internetOrder(detail)) == carrier AND select(internetOrder(detail)) AND NOT deliveryReport(detail) DO {
                        deliveryReport(detail) <- r;                        
                    }
                    SHOW deliveryReport OBJECTS r = r DOCKED NOCANCEL;
                }    
            }
        }        
        select(InternetOrder o) <- NULL;
    } ELSE {
        MESSAGE 'Вначале необходимо отметить интернет заказы.';
    }
}


filterDashboardDelivery = GROUP SUM 1 IF InternetOrderDetail detail IS InternetOrderDetail AND NOT deliveryReport(detail) 
        BY internetOrder(detail);


inStockDeliveryReport = GROUP SUM 1 BY Sale.supplierStock[InternetOrderDetail](InternetOrderDetail detail), deliveryReport(detail); 
supplierStocks 'Склады поставщика' (DeliveryReport deliveryReport) = GROUP  
                                                                        CONCAT name(Stock stock) IF inStockDeliveryReport(stock, deliveryReport), ', ' 
                                                                        ORDER name(stock) CHARWIDTH 30;
filterDeliveryDashboard = ABSTRACT CASE BOOLEAN (InternetOrder);
useFilterDeliveryDashboard = ABSTRACT BOOLEAN ();

filterDelivery (InternetOrder o) = IF useFilterDeliveryDashboard() THEN filterDeliveryDashboard(o) ELSE status(o) == StatusInternetOrder.picked;

countDaysFilterInternetOrder 'Количество дней для фильтра на форме "Отчет курьера"' = DATA INTEGER ();

EXTEND FORM options PROPERTIES countDaysFilterInternetOrder();

DESIGN options {
    sale {
        MOVE PROPERTY(countDaysFilterInternetOrder()); 
    }
}

FORM deliveryDashboard 'Отчеты курьера'
    OBJECTS df = DATE PANEL
    PROPERTIES dateFrom 'Дата с' = VALUE(df)
    
    OBJECTS o = InternetOrder 
    PROPERTIES (o) select
    PROPERTIES (o) READONLY isPosted, nameStatus, 
                   nameNumerator, number, series, date, time, nameCurrency, note,
                   deliveryDate, nameInternetCustomer, addressInternetCustomer, phoneInternetCustomer
    PROPERTIES (o) READONLY countInternetOrderDetail, quantityInternetOrderDetail, sumInternetOrderDetail
                  
    PROPERTIES (o) READONLY namePaymentTypeInternetOrder, paymentCashSumInternetOrder, paymentCardSumInternetOrder, paymentERIPSumInternetOrder, paymentDateInternetOrder,
                   numberPaymentDocumentInternetOrder, deliverySumInternetOrder, deliveryTimeFromInternetOrder, deliveryTimeToInternetOrder 
                                                                                                     
    FILTERS filterDashboardDelivery(o),
            filterDelivery(o),
            isOpened(o),
            date(o) >= df
    PROPERTIES createDeliveryReport(o) TOOLBAR           
                   
    OBJECTS d = InternetOrderDetail
    PROPERTIES (d) READONLY index
    PROPERTIES (d) READONLY seriesNumberPicking, idBarcodeSku, idSku, nameSku, shortNameUOMSku, idBatch, nameBatch, quantity, pickingQuantity, returnQuantity,
                   price, discountPrice, sum, nameSupplierStock,
                   seriesNumber, date, time, nameInternetCustomer, addressInternetCustomer, phoneInternetCustomer         
    ORDERS index(d)
    FILTERS internetOrder(d) == o 

    OBJECTS r = DeliveryReport LAST
    PROPERTIES(r) READONLY nameStatus, number, series,date, time, nameCarrier, supplierStocks, seriesNumberInternetOrders, countInternetOrderDetail, 
                  pickingQuantityInternetOrderDetail, pickingSumInternetOrderDetail
    PROPERTIES(r) NEWSESSION NEW, EDIT, DELETE  
    PROPERTIES (r) PANEL READONLY createdTime, createdNameUser, createdHostnameComputer  
    
    OBJECTS pd = InternetOrderDetail
    PROPERTIES (pd) READONLY indexDeliveryReport 
    PROPERTIES (pd) READONLY seriesNumberPicking, idBarcodeSku, idSku, nameSku, shortNameUOMSku, idBatch, nameBatch
    PROPERTIES (pd) READONLY pickingQuantity, returnQuantity, price, pickingSum, discountPrice, discountSumPicking
    PROPERTIES (pd) READONLY seriesNumber, date, time, nameInternetCustomer, addressInternetCustomer, phoneInternetCustomer, nameSupplierStock
    PROPERTIES (pd) NEWSESSION EDIT TOOLBAR           
    ORDERS indexDeliveryReport(pd) 
    FILTERS deliveryReport(pd)==r,
            date(r) >= df
    
    EVENTS ON INIT {
        SEEK deliveryDashboard.df = OVERRIDE subtract(currentDate(), countDaysFilterInternetOrder()), subtract(currentDate(), 30);
    }           
    
;

DESIGN deliveryDashboard {
    BOX {
        NEW top {
            fill = 1;
            MOVE BOX(df);
            NEW tabContainer{
                fill =1;
                type = TABBED;
                NEW body {
                    caption = 'Отчеты курьера';
                    fill = 1;
                    type = SPLITV;
                    MOVE BOX(o);
                    NEW specification {
                        type = TABBED ;
                        fill = 2;
                        NEW specPick{
                            caption = 'Отчеты курьера';
                            fill = 1;
                            type = SPLITH;
                            MOVE BOX(r);
                            NEW tab {
                                fill = 1;
                                type = TABBED ;
                                MOVE BOX(pd);
                            }
                        }
                        MOVE BOX(d){ caption ='Спецификация интернет заказа';}
                    }
                }                          
            }
        }
        MOVE TOOLBARBOX;
    }    
}

NAVIGATOR {
    eShop {
        NEW deliveryDashboard;
    }
}

filterInternetOrderDateFrom 'Дата с' = DATA LOCAL DATE ();
filterInternetOrderDateFrom (InternetOrderDetail detail) = date(detail) >= filterInternetOrderDateFrom() OR (detail IS InternetOrderDetail AND NOT filterInternetOrderDateFrom());      

filterInternetOrderDateTo 'Дата по' = DATA LOCAL DATE ();
filterInternetOrderDateTo (InternetOrderDetail detail) = date(detail) <= filterInternetOrderDateTo() OR (detail IS InternetOrderDetail AND NOT filterInternetOrderDateTo());    

FORM internetOrderLedger 'Регистр интернет заказов'
    PROPERTIES filterInternetOrderDateFrom(), filterInternetOrderDateTo()
    OBJECTS d = InternetOrderDetail
    PROPERTIES(d) READONLY   indexDeliveryReport, seriesNumber, nameInternetCustomer, 
                             phoneInternetCustomer, addressInternetCustomer, cityInternetCustomer, 
                             seriesNumberPicking, idBarcodeSku, idSku, nameSku, shortNameUOMSku, idBatch, nameBatch, quantity, pickingQuantity,
                             returnQuantity, price, discountPrice, sum, discountSum, date, deliveryTimeFromInternetOrder, 
                             deliveryTimeToInternetOrder //bulky, guarantee, 
                             
    ORDERS seriesNumber(d) 
    FILTERS deliveryReport(d),
            filterInternetOrderDateFrom(d),
            filterInternetOrderDateTo(d)
;

DESIGN internetOrderLedger {
    NEW topFilters {
        caption = 'Фильтры';
        type = CONTAINERH; 
        MOVE PROPERTY(filterInternetOrderDateFrom());                                               
        MOVE PROPERTY(filterInternetOrderDateTo());                                               
    }   
    MOVE BOX(d){
        PROPERTY(seriesNumber(d)){
            caption = 'Номер интернет заказа';
        }    
        PROPERTY(date(d)){
            caption = 'Дата заказа';
        }     
    }  
    MOVE TOOLBARBOX;
}

NAVIGATOR {
    eShop  {
        NEW internetOrderLedger;
    }
}

WHEN CHANGED (status(DeliveryReport r)) DO {
    IF status(r) == StatusDeliveryReport.delivery THEN {
        status(InternetOrder o) <- StatusInternetOrder.delivery WHERE relation(r, o);
    }

    IF status(r) == StatusDeliveryReport.delivered THEN {
        status(InternetOrder o) <- StatusInternetOrder.delivered WHERE relation(r, o);
    }
}

WHEN CHANGED (status(DeliveryReport r)) DO {
    IF status(r) == StatusDeliveryReport.completed THEN {
        status(InternetOrder o) <- StatusInternetOrder.completed WHERE relation(r, o);
    }
}

internetOrderPickingPrint 'Счет-заказ' (DeliveryReport r)  { 
    in(InternetOrder order) <- NULL;
    skip(InternetOrderDetail d) <- NULL;   
     
    full() <- NULL ;   
    in(InternetOrder o) <- TRUE WHERE relation(r, o);    
    skip(InternetOrderDetail d) <- TRUE WHERE NOT pickingQuantity(d) AND in(internetOrder(d));
     
    PRINT internetOrderPrint;
} IMAGE 'print.png' IN print;

EXTEND FORM deliveryDashboard
    PROPERTIES (r) internetOrderPickingPrint TOOLBAR 
;
    
CONSTRAINT deliveryReport(InternetOrderDetail detail) AND NOT picking(detail)
    MESSAGE 'Нельзя удалять комплектацию до удаления отчета курьера или создавать отчет курьера без комплектации';

MODULE  FoodSupplement;

REQUIRE BOMNutrition, PurchaseShipment, ProductionConsumption;

NAMESPACE Production;

supplementQuantity = GROUP SUM consumedQuantity(ComponentDetail cd) * overNetWeight(sku(cd))*1000.0 IF isSupplement(sku(cd)) BY order(cd), sku(cd); 
supplementQuantity  = GROUP SUM consumedQuantity(ComponentDetail cd) * overNetWeight(sku(cd))*1000.0 
    IF isSupplement(sku(cd)) AND order(cd) == order(ProductDetail pd) AND BOM(cd) == BOM(pd) 
        BY pd, sku(cd); 
supplementQuantity (ProductDetail pd)  = GROUP 
        SUM consumedQuantity(ComponentDetail cd) * overNetWeight(sku(cd))*1000.0 
    IF isSupplement(sku(cd)) AND order(cd) == order(pd) AND BOM(cd) == BOM(pd); 
        
supplementQuantityPlan = GROUP SUM bruttoQuantity(ComponentDetail cd) * overNetWeight(sku(cd))*1000.0 IF isSupplement(sku(cd)) BY order(cd), sku(cd); 
supplementQuantityPlan  = GROUP SUM bruttoQuantity(ComponentDetail cd) * overNetWeight(sku(cd))*1000.0 
    IF isSupplement(sku(cd)) AND order(cd) == order(ProductDetail pd) AND BOM(cd) == BOM(pd) 
        BY pd, sku(cd); 
supplementQuantityPlan (ProductDetail pd)  = GROUP 
        SUM bruttoQuantity(ComponentDetail cd) * overNetWeight(sku(cd))*1000.0 
    IF isSupplement(sku(cd)) AND order(cd) == order(pd) AND BOM(cd) == BOM(pd);   
             
isPlan 'План'  = DATA LOCAL BOOLEAN (Order);       
        
FORM  foodSupplement 'Расход пищевых добавок'
    OBJECTS o =Order PANEL SUBREPORT 
    PROPERTIES(o) date, nameProductsStock, nameComponentsStock, isPlan 
    
    OBJECTS sk = Sku SUBREPORT
    FILTERS (supplementQuantity(o,sk) IF NOT isPlan(o)) OR (supplementQuantityPlan(o,sk) IF isPlan(o))

    OBJECTS pd = ProductDetail SUBREPORT
    PROPERTIES nameSku(pd), outputQuantity(pd), supplementQuantity(pd,sk) COLUMNS (sk) HEADER name(sk) FOOTER supplementQuantity(o,sk)
    PROPERTIES quantity(pd), supplementQuantityPlan(pd,sk) COLUMNS (sk) HEADER name(sk) FOOTER supplementQuantityPlan(o,sk)
    FILTERS order(pd) == o,
            (supplementQuantity(pd) IF NOT isPlan(o)) OR  (supplementQuantityPlan(pd) IF isPlan(o))           
;

foodSupplement 'Расход пищевых добавок (факт)' (Order o)  { 
    isPlan(o) <- NULL;
    PRINT foodSupplement OBJECTS o = o ;
}IMAGE 'print.png' IN print;

foodSupplementPlan 'Расход пищевых добавок (план)' (Order o)  { 
    isPlan(o) <- TRUE;    
    PRINT foodSupplement OBJECTS o = o ; 
} IMAGE 'print.png' IN print;
EXTEND FORM orders
    PROPERTIES (o)  foodSupplement, foodSupplementPlan
;
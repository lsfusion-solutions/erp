MODULE BOMOperation;

REQUIRE BOM, ProductionOperation, ProductionOrder;

NAMESPACE Production;

operation = DATA Operation (BOM) NONULL;
nameOperation 'Операция' = name(operation(BOM bom)) IN documentPrm NONULL;

overCopy (BOM BOM, BOM b) + { 
    operation(b) <- operation(BOM);
}

EXTEND FORM BOM 
    PROPERTIES (b) nameOperation
;

DESIGN BOM {
    GROUP(documentPrm,b) {
        type = COLUMNS;
        columns = 3;
    }
}

EXTEND FORM BOMs 
    PROPERTIES (b) READONLYIF isReadonly() nameOperation AFTER name(b)
;

CONSTRAINT equalsBOMNettoNetWeight(operation(BOM b)) AND overNettoNetWeightProduct(b) != overNettoNetWeightComponent(b)
    MESSAGE 'Вес нетто компонентов не равен весу нетто изделий в спецификации';
    
EXTEND FORM BOMs FILTERS in(currentUser(), operation(b));

filterBOMOperation = DATA LOCAL Operation ();
nameFilterBOMOperation 'Операция' = name(filterBOMOperation()) CHARWIDTH 15;          
filterOperation (BOM i) = operation(i) == filterBOMOperation() OR (i IS BOM AND NOT filterBOMOperation());

    
EXTEND FORM BOMs
    PROPERTIES () nameFilterBOMOperation
    FILTERS filterOperation(b), filterCustomUser(b),  filterComputer(b)
;
DESIGN BOMs{
    filterBOM{
        MOVE PROPERTY (nameFilterBOMOperation ()) BEFORE PROPERTY (nameFilterBOMStock());
    }                              
}
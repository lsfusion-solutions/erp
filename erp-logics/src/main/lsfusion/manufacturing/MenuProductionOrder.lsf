MODULE  MenuProductionOrder;

REQUIRE Menu,
        ProductionOrder,
        BOMNutrition;

// -- Заполнение на основе производственных заказов ---- //

FORM menuOrders 'Производственные заказы'

    OBJECTS o = Order
    PROPERTIES(o) READONLY isPosted, number, series, date, time,
                  nameComponentsStock, nameProductsStock, fromDate, toDate, quantityProductDetail,
                  sumProductDetail, 
                  nameCalcPriceListType, namePriceListType, note
    FILTERS isPosted(o)

    OBJECTS pd=ProductDetail
    PROPERTIES(pd) READONLY index, nameSku, nameProduct, shortNameUOM, quantity,
                   price, sum
    FILTERS order(pd)==o
;

DESIGN menuOrders {
    BOX {
        size = (1024, 768);
        NEW documentContainer BEFORE TOOLBARBOX {
            fill = 1;
            MOVE BOX(o);
            MOVE BOX(pd) {
                caption = 'Изделия';
            }
        }
    }
}
compositionOver = ABSTRACT STRING (Product);
fillOrder 'Заполнить на основе производственного заказа'(UserMenu userMenu)   { 
    DIALOG menuOrders OBJECTS o INPUT DO {
        note(userMenu) <- 'Основание: ' + description(o);
        FOR order(ProductDetail productDetail) == o NEW d = UserMenuDetail DO {
            userMenu(d) <- userMenu;
            price(d) <- price(productDetail);
            sku(d) <- sku(productDetail);
            productYield(d) <- productYield(product(productDetail));
            composition(d) <- OVERRIDE compositionOver(product(productDetail)), composition(product(productDetail));
        }
    }
}

EXTEND FORM userMenu
    PROPERTIES(m) fillOrder
;
DESIGN userMenu {
    headerRow1 {
        NEW row2 BEFORE GROUP(documentPrm,m) {
            caption = 'Производственный заказ';
            horizontal = TRUE;

            MOVE PROPERTY(fillOrder(m));
        }
    }
}

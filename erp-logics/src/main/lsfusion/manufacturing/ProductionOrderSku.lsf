MODULE ProductionOrderSku;

REQUIRE ProductionOrder, Warehouse, Store;

NAMESPACE Production;

skuGroup = DATA ItemGroup(Stock);
//nameSkuGroup 'Группа товаров' (Stock st) = name(skuGroup(st));

inWorkshop 'Вкл' = DATA BOOLEAN (ItemGroup, Stock);
levelParentinWorkshop (ItemGroup g, Stock st) = GROUP MIN level(g, ItemGroup parent) IF inWorkshop(parent, st) MATERIALIZED;
inWorkshopOver 'Вкл' (ItemGroup g, Stock st) = OVERRIDE 
    inWorkshop(g, st),
    TRUE IF levelParentinWorkshop(g,st);
inWorkshop 'Вкл'(Sku s,Stock st) = inWorkshopOver(skuGroup(s),st);
        
nameInWorkshopGroups 'Группы товаров' (Stock st) = GROUP CONCAT name(ItemGroup g) IF inWorkshop(g,st),', ' ORDER name(g),g CHARWIDTH 25;
countInWorkshopGroups 'Вкл' (Stock st) = GROUP SUM 1 IF inWorkshop(ItemGroup g, st);       

resetInWorkshopGroups  'Сбросить отмеченные'(Stock st)  { 
    inWorkshop(ItemGroup g, st) <- NULL WHERE g IS ItemGroup ;    
}

FORM selectInWorkshopGroups 'Выбор групп'

    OBJECTS st = Stock PANEL 

    TREE skuTree2 sk2 = SkuGroup PARENT parent(sk2)
    PROPERTIES READONLY order(sk2), id(sk2), name(sk2)
    PROPERTIES inWorkshop(sk2,st), inWorkshopOver(sk2,st)
    ORDERS order(sk2), name(sk2)
    PROPERTIES (st) resetInWorkshopGroups TOOLBAR DRAW sk2 SHOWIF countInWorkshopGroups(st)
    FILTERGROUP inactive2 FILTER 'Активные' active(sk2) 'F5' DEFAULT           
;

DESIGN selectInWorkshopGroups {
    BOX {
        size = (1024, 768);
    }
}

changeInWorkshopGroups (Stock st)  { 
        SHOW selectInWorkshopGroups OBJECTS st = st ; 
}

EXTEND FORM warehouse PROPERTIES nameInWorkshopGroups(w) ON CHANGE changeInWorkshopGroups(w);

DESIGN warehouse {
    headerExtraParams {
        MOVE PROPERTY (nameInWorkshopGroups(w));
    }
}

EXTEND FORM departmentStore PROPERTIES nameInWorkshopGroups(d) ON CHANGE changeInWorkshopGroups(d);

DESIGN departmentStore {
    headerExtraParams {
        MOVE PROPERTY (nameInWorkshopGroups(d));
    }
}

MODULE OrderRamp;

REQUIRE Ramp, Order, Time;

NAMESPACE Order;

TABLE orderRampInterval(Order, Ramp, Interval);
in 'Вкл.' = DATA BOOLEAN (Order, Ramp, Interval);

CONSTRAINT in(Order o,Ramp r,Interval i) AND NOT in(r,i)
    MESSAGE 'В заказ включены рампа и интервал, у которых нет связи';    
    
//orderRampIntervalDate (r,i,d)= GROUP NAGGR o BY r,i,shipmentDateOrder(o) WHERE inOrderRampInterval(o,r,i);   
order (Ramp r,Interval i,d)= GROUP MAX Order o IF in(o,r,i) BY shipmentDate(o);   
countSupplier (Ramp r,Interval i,d,s)= GROUP SUM 1 IF in(Order o,r,i) BY shipmentDate(o), supplier(o); 

countSuppliers (Ramp r,Interval i,DATE d)= GROUP SUM 1 IF countSupplier (r,i,d,LegalEntity s); 
  
banDiffSupplier'Запрет резервирования рамы на одно время для разных поставщиков (только заказы)'= DATA BOOLEAN (Ramp);
EXTEND FORM ramp
    PROPERTIES (r) banDiffSupplier
;
  
CONSTRAINT countSuppliers (Ramp r,Interval i,DATE d) >1 AND banDiffSupplier(r)
    MESSAGE 'Рампа не может быть зарезервирована на одно и то же время для разных поставщиков.';    

//descriptionOrdersRampIntervalDate 'Заказ' (r,i,d)= descriptionOrder(orderRampIntervalDate(r,i,d));   
descriptionOrders 'Заказы' (Ramp r,Interval i,d)= 
    GROUP CONCAT description(Order o) IF in(o,r,i), ', '
    ORDER o 
    BY shipmentDate(o) 
    CHARWIDTH 30;   

//nameSupplierOrdersRampIntervalDate 'Поставщик' (r,i,d) = nameSupplierOrder(orderRampIntervalDate(r,i,d));
nameSupplierOrders 'Поставщики' (Ramp r,Interval i,DATE d)= 
    GROUP 
    CONCAT name(LegalEntity s) IF countSupplier (r,i,d,s), ', '
    ORDER s 
    CHARWIDTH 30;   
    
//nameCustomerOrdersRampIntervalDate 'Покупатель' (r,i,d) = nameCustomerOrder(orderRampIntervalDate(r,i,d)); 
countCustomer (Ramp r,Interval i,d,c)= GROUP SUM 1 IF in(Order o,r,i) BY shipmentDate(o), customer(o); 
nameCustomerOrders 'Покупатели' (Ramp r,Interval i,DATE d)= 
    GROUP 
    CONCAT name(LegalEntity c) IF countCustomer (r,i,d,c), ', '
    ORDER c 
    CHARWIDTH 30;   
descriptionOrders'Заказы' (Ramp r,Interval i,Order o) =  descriptionOrders(r,i,shipmentDate(o));    
nameSupplierOrders 'Поставщики' (Ramp r,Interval i,Order o) = nameSupplierOrders(r,i,shipmentDate(o));
nameCustomerOrders 'Покупатели' (Ramp r,Interval i,Order o) = nameCustomerOrders(r,i,shipmentDate(o));

descriptionRamps 'Рампы' (Order o)= 
    GROUP 
    CONCAT (CONCAT ' ', [ FORMULA BPSTRING[10] 'to_char($1,\'DD.MM.YYYY\')'](shipmentDate(o)), time(Interval i), '№ '+name(Ramp r)) IF in(o,r,i), ', '
    ORDER shipmentDate(o), name(r), time(i) 
    CHARWIDTH 20;            

defaultShipmentTimeDate (Order o)= GROUP MIN time(Interval i) IF in(o,Ramp r,i);

backgroundOrders 'Цвет (свой заказ)' (r,i,o) = ABSTRACT CASE COLOR(Ramp,Interval,Order);

backgroundOrders(Ramp r,Interval i,Order o) +=  WHEN in(o,r,i) THEN  RGB(204,255,204);

in (Ramp ramp, order) = GROUP SUM 1 IF (in(ramp, sku(OrderDetail detail)) OR (ramp IS Ramp AND NOT countSkuGroups(ramp) AND sku(detail))) BY order(detail);

FORM orderRamp 'Рампы'
    OBJECTS o=Order PANEL 
    OBJECTS st=Stock PANEL 
            
    OBJECTS r=Ramp 
    PROPERTIES(r) READONLY name
    ORDER      name(r)
    FILTERS stock(r) == st,
            in(r,o)
    
    OBJECTS i= Interval
    PROPERTIES(i) READONLY BACKGROUND backgroundOrders(r,i,o) time
    ORDER time(i)
    FILTERS in(r,i) 
       
    PROPERTIES(r,i,o) BACKGROUND backgroundOrders(r,i,o) READONLY descriptionOrders,
                      nameSupplierOrders, nameCustomerOrders 
    PROPERTIES    BACKGROUND backgroundOrders(r,i,o) in(o,r,i)           
                                                        
    FILTERGROUP filter
        FILTER 'Только свободное время' o IS Order AND NOT order(r,i,shipmentDate(o)) 'F10'   
                                                             
    FILTERGROUP filter2
        FILTER 'По заказу' in(o,r,i) 'F9'                                                                      
;
DESIGN orderRamp {
    BOX { 
        size = (800, 800); 
        MOVE BOX(o);
        NEW row {
            type = SPLITV;
            fill = 1;
            MOVE BOX(r) { fill = 1;}
            MOVE BOX(i) { fill = 2;}
        }
        MOVE TOOLBARBOX;
    } 
}

META  defineOrderRamp(stockProp)       
    
    toFillRamp 'Зарезервировать рампу'(Order order)  { 
        DIALOG orderRamp OBJECTS o = order, st = stockProp(order) DO
            shipmentTime(order) <- defaultShipmentTimeDate(order);         
    } 
    
    EXTEND FORM userOrder
        PROPERTIES (o) descriptionRamps READONLY, toFillRamp 
    ;
    DESIGN userOrder {
        headerExtraParams {                                 
            NEW documentRamp AFTER documentShipmentGroup {
                fill = 1;
                caption = 'Погрузка/отгрузка';
                MOVE PROPERTY(descriptionRamps(o));
                MOVE PROPERTY(toFillRamp(o));
            }
        }    
    }    
    

END 

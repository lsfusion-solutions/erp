MODULE PurchaseScheduleOrderRamp;

REQUIRE OrderRamp, PurchaseOrder, PurchaseScheduleOrder;

NAMESPACE Purchase;


//rampScheduleOrderTimeStock = DATA Ramp (ScheduleOrderTime, Stock);
//nameRampScheduleOrderTimeStock 'Рампа' (scheduleOrderTime,stock) = nameRamp(rampScheduleOrderTimeStock(scheduleOrderTime,stock));
//
//inScheduleOrderTimeStockInterval 'Вкл.' = DATA BOOLEAN (ScheduleOrderTime, Stock, Interval);
//
//CONSTRAINT rampScheduleOrderTimeStock(scheduleOrderTime,stock) AND NOT stockRamp(rampScheduleOrderTimeStock(scheduleOrderTime,stock))==stock
//    CHECKED BY rampScheduleOrderTimeStock MESSAGE 'Для дня недели и склада выбрана рампа, которая не относится к складу';  
//
//CONSTRAINT rampScheduleOrderTimeStock(d,tts) AND inScheduleOrderTimeStockInterval(d,tts,in) AND NOT inRampInterval(rampScheduleOrderTimeStock(d,tts),in)
//    MESSAGE 'Для дня недели, склада и рампы выбраны интервалы, которае не относятся к рампе'; 
//            
//// todo: когда сработает CONSTRAINT не понятно где искать ошибку. Надо придумать понятное поведение (возможно сессионное событие на сброс inScheduleOrderTimeStockInterval )            
//  
//  
//rampScheduleOrderDetail = DATA Ramp (ScheduleOrderDetail); 
//nameRampScheduleOrderDetail 'Рампа' (scheduleOrderDetail) = nameRamp(rampScheduleOrderDetail(scheduleOrderDetail));
//inScheduleOrderDetailInterval 'Вкл.' = DATA BOOLEAN (ScheduleOrderDetail, Interval);
//  
//CONSTRAINT rampScheduleOrderDetail(scheduleOrderdetail) AND NOT stockRamp(rampScheduleOrderDetail(scheduleOrderdetail))==customerStockScheduleOrderDetail(scheduleOrderdetail)
//    CHECKED BY rampScheduleOrderDetail MESSAGE 'Для предварительного заказа выбрана рампа, которая не относится к складу покупателя';  
//      
//CONSTRAINT rampScheduleOrderDetail(scheduleOrderdetail) AND inScheduleOrderDetailInterval(scheduleOrderdetail, interval) AND NOT inRampInterval(rampScheduleOrderDetail(scheduleOrderdetail),interval)
//    MESSAGE 'Для предварительного заказа и рампы выбраны интервалы, которые не относятся к рампе'; 
//
//EXTEND FORM scheduleOrder
//    OBJECTS tts=Stock GRID       
//    PROPERTIES (tts) SHOWIF toShowGenDayMonth(s) READONLY nameStock
//    PROPERTIES SHOWIF toShowGenDayMonth(s) nameRampScheduleOrderTimeStock(d,tts)
//    FILTERS inScheduleOrderStock(s,tts)
//    
//    OBJECTS in=Interval GRID
//    PROPERTIES (in) SHOWIF toShowGenDayMonth(s) READONLY timeInterval            
//    PROPERTIES SHOWIF toShowGenDayMonth(s) inScheduleOrderTimeStockInterval(d,tts,in)
//    FILTERS inRampInterval(rampScheduleOrderTimeStock(d,tts),in)
//    FILTERGROUP filter
//        FILTER 'Отм.' 'F10' inScheduleOrderTimeStockInterval(d,tts,in)
//    
//;
//DESIGN scheduleOrder {
//    days1 {
//        type = CONTAINERH;
//        NEW days11 {
//            fill = 1;
//            type = CONTAINERV;
//            
//            NEW BOX(d);
//            NEW days111 {
//                fill = 1;
//                type = CONTAINERH;
//                NEW BOX(tts);
//                NEW BOX(in);
//            } 
//        }        
//        NEW BOX(m);
//    }
//}      

//-- По дням недели
TABLE stockScheduleOrderTimeRampInterval(Stock, ScheduleOrderTime, Ramp, Interval);
in 'Вкл.' = DATA BOOLEAN (Stock, ScheduleOrderTime, Ramp, Interval);

CONSTRAINT in(Stock st,ScheduleOrderTime o,Ramp r,Interval i) AND NOT in(r,i)
    MESSAGE 'Для дня недели и склады включены рампа и интервал, у которых нет связи';  

CONSTRAINT in(Stock st,ScheduleOrderTime o,Ramp r,Interval i) AND NOT stock(r)==st
    MESSAGE 'Для дня недели и склада включена рампа, у которой нет связи со складом';   
 
//-- По предварительным заказам
TABLE scheduleOrderDetailRampInterval(ScheduleOrderDetail, Ramp, Interval);
in 'Вкл.' = DATA BOOLEAN (ScheduleOrderDetail, Ramp, Interval);
countIntervals (ScheduleOrderDetail o,Ramp r) = GROUP SUM 1 IF in(o,r,Interval i);

countScheduleOrderDetails (date,Ramp r,Interval i) = GROUP SUM 1 IF in(ScheduleOrderDetail o,r,i) AND isOpened(o) BY dateSupply(o);
  
CONSTRAINT in(ScheduleOrderDetail o,Ramp r,Interval i) AND NOT in(r,i)
    MESSAGE 'Для предварительного заказа и склады включены рампа и интервал, у которых нет связи';   
     
CONSTRAINT in(ScheduleOrderDetail o,Ramp r,Interval i) AND NOT stock(r)==customerStock(o)
    MESSAGE 'Для предварительного заказа и склада включена рампа, у которой нет связи со складом';                      
      
overCreate(ScheduleOrderDetail o) + { 
    in(o,Ramp ramp,Interval interval) <- in(
                                                            customerStock(o),
                                                            shipmentScheduleOrderTime(scheduleOrder(o),extractDOW(dateSupply(o))),
                                                            ramp,
                                                            interval);                                                             
}  

descriptionRamps 'Рампы' (ScheduleOrderDetail o)= 
    GROUP 
    CONCAT (CONCAT ' ', time(Interval i), name(Ramp r)) IF in(o,r,i), ', '
    ORDER name(r), time(i) 
    CHARWIDTH 20; 
      
overCreate(UserOrder o) + { 
    IF o IS UserOrder THEN {
        in(o,Ramp r,Interval i) <-  in(scheduleOrderDetail(o),r,i); 
        shipmentTime(o) <- OVERRIDE defaultShipmentTimeDate(o), currentTime();
    }      
} 
 
backgroundIn = RGB(0,255,0) IF [GROUP SUM 1 IF in(Stock tts,ScheduleOrderTime d,Ramp r,Interval in) BY tts,d,r](Stock tts,ScheduleOrderTime d,Ramp r);      
EXTEND FORM scheduleOrder
    OBJECTS tts=Stock GRID       
    PROPERTIES (tts) SHOWIF toShowGenDayMonth(s) READONLY name    
    FILTERS in(s,tts)
    
    OBJECTS r=Ramp GRID
    PROPERTIES(r) SHOWIF toShowGenDayMonth(s) READONLY  name BACKGROUND backgroundIn(tts,d,r)
    FILTERS stock(r) == tts 
    
    OBJECTS in=Interval GRID
    PROPERTIES (in) SHOWIF toShowGenDayMonth(s) READONLY time            
    PROPERTIES SHOWIF toShowGenDayMonth(s) in(tts,d,r,in)
    FILTERS in(r,in)
    
    FILTERGROUP filter
        FILTER 'Отм.' in(tts,d,r,in) 'F10'
        
        
    PROPERTIES(sd) SHOWIF toShowGenDayMonth(s) READONLY descriptionRamps BEFORE deletesd
    
;
DESIGN scheduleOrder {
    days1 {
        type = SPLITH;
        
        NEW days11 {
            fill = 1;
// временно убираем SPLIT, так как тормозит в вебе
//            type = SPLITV;  
            NEW days111 {
                fill = 2;
//                type = SPLITV;    
                MOVE BOX(dow);
                MOVE BOX(d); 
            }            
            MOVE BOX(m);
        }     
        NEW days12 {
            caption = 'Выбор рамп';
            fill = 2;
            type = CONTAINERH; // SPLITH
            MOVE BOX(tts);
            NEW reight {
                fill = 2;
                type = SPLITV;
                MOVE BOX(r);
                MOVE BOX(in) {
                    fill = 3;
                }
            }                
        }      
    
    }
}

includeShipmentDowSupplierCustomer '' = GROUP SUM 1 IF isOpened(ScheduleOrderDetail d) AND dateSupply(d) >= DATE date AND
    in(d,Ramp r,Interval i) 
        BY extractDOW(dateSupply(d)), supplier(d), customer(d), r, i, date;
        
descriptionSuppliersCustomer 'Поставщики' (DOW dow, LegalEntity c, Ramp r, Interval i, DATE date)= 
    GROUP 
    CONCAT name(LegalEntity s) IF includeShipmentDowSupplierCustomer(dow, s, c, r, i,date), ', '
    ORDER name(s), s
    CHARWIDTH 20; 
prevDescriptionSuppliersCustomer 'Поставщики' (DOW dow, LegalEntity c, Ramp r, Interval i, DATE date) = PREV(descriptionSuppliersCustomer(dow, c, r, i, date)) CHARWIDTH 20;
  
descriptionSuppliers 'Поставщики' (ScheduleOrderTime d,Ramp r,Interval i) = descriptionSuppliersCustomer(shipmentDOW(d), customer(scheduleOrder(d)), r, i, startDate(scheduleOrder(d)));

    
includeShipmentDowScheduleOrderCustomer '' = GROUP SUM 1 IF isOpened(ScheduleOrderDetail d) AND dateSupply(d) >= DATE date AND
    in(d,Ramp r,Interval i) 
        BY extractDOW(dateSupply(d)), scheduleOrder(d), r, i, date;
countInclude = GROUP SUM 1 IF isOpened(ScheduleOrderDetail d) AND dateSupply(d) >= DATE date AND
    in(d,Ramp r,Interval i) 
        BY extractDOW(dateSupply(d)), r, i, date;
               
editScheduleOrder 'Редактировать' (ScheduleOrderTime d, Ramp r, Interval in, Stock tts)  { 
    FOR includeShipmentDowScheduleOrderCustomer(shipmentDOW(d), ScheduleOrder o,  r, in, startDate(scheduleOrder(d))) AND o != scheduleOrder(d) DO {
        SHOW  scheduleOrder OBJECTS s = o, d = shipmentScheduleOrderTime(o,shipmentDOW(d)), tts = tts, r = r, in = in DOCKED;
    }    
} IMAGE 'edit.png' TOOLBAR;
showInclude(ScheduleOrderTime d, Ramp r, Interval in) = countInclude(shipmentDOW(d),r, in, startDate(scheduleOrder(d)));    
EXTEND FORM scheduleOrder
    PROPERTIES (d,r,in) READONLY descriptionSuppliers SHOWIF toShowGenDayMonth(s) 
    PROPERTIES editScheduleOrder(d,r,in, tts) SHOWIF showInclude(d,r,in)
;

FORM listScheduleOrderDetail 'Список предварительных заказов'
    OBJECTS dt = DATE PANEL
    OBJECTS dow = DOW PANEL
    OBJECTS c = LegalEntity PANEL
    OBJECTS r = Ramp PANEL
    OBJECTS i = Interval PANEL
    
    OBJECTS d=ScheduleOrderDetail
    PROPERTIES (d) READONLY isClosed, dateOrder, dateSupply
    PROPERTIES (d) READONLY monthInDate, DOWInDate,
                    nameSupplier, nameSupplierStock,
                    nameCustomer, nameCustomerStock,
                    nameOperation

    ORDERS dateSupply(d)
    FILTERS dateSupply(d) >= dt,
            customer(d) ==c,
            in(d,r,i),
            extractDOW(dateSupply(d)) == dow
    FILTERGROUP curDate
        FILTER 'Больше текущей даты' dateSupply(d) > currentDate() 'F9'
;
showListDetail 'Показать список предварительных заказов' (ScheduleOrderTime d,Ramp r,Interval in)  { 
    SHOW listScheduleOrderDetail OBJECTS dt = startDate(scheduleOrder(d)), 
                                         dow = shipmentDOW(d),
                                         c = customer(scheduleOrder(d)),
                                         r = r,
                                         i = in ; 
} ASON CONTEXTMENU descriptionSuppliers[ScheduleOrderTime,Ramp,Interval];


//----------------- Логирование времени поставок ------------------- //

CLASS ScheduleOrderTimeLog 'Запись лога времени поставок';
TABLE scheduleOrderTimeLog(ScheduleOrderTimeLog);

scheduleOrder = DATA ScheduleOrder (ScheduleOrderTimeLog) INDEXED;
//in 'Вкл.' = DATA BOOLEAN (Stock, ScheduleOrderTime, Ramp, Interval);
in 'Вкл.' = DATA BOOLEAN (ScheduleOrderTimeLog);
time 'Время' = DATA TIME (ScheduleOrderTimeLog);
shipmentNameDOW 'День недели поставки' = DATA BPISTRING[100] (ScheduleOrderTimeLog);
nameRamp 'Рампа' = DATA ISTRING[100](ScheduleOrderTimeLog);
nameStock 'Склад' = DATA ISTRING[150](ScheduleOrderTimeLog);

nameUser 'Имя пользователя' = DATA STRING[100] (ScheduleOrderTimeLog);
dateTime 'Время изменения' = DATA DATETIME (ScheduleOrderTimeLog);

WHEN (CHANGED(in(Stock s, ScheduleOrderTime t, Ramp r, Interval i)) AND s IS Stock AND t IS ScheduleOrderTime AND r IS Ramp AND i IS Interval) DO {
    NEW l = ScheduleOrderTimeLog {
        scheduleOrder(l) <- scheduleOrder(t);
        in(l) <- in(s, t, r, i);
        time(l) <- time(i);
        shipmentNameDOW(l) <- shipmentNameDOW(t);
        nameRamp(l) <- name(r);
        nameStock(l) <- name(s);
        nameUser(l) <- ISTRING[30](name(currentUser()));
        dateTime(l) <- currentDateTime();
    }
}

EXTEND FORM scheduleOrder
    OBJECTS logT = ScheduleOrderTimeLog
    PROPERTIES(logT) READONLY in, time, shipmentNameDOW, nameRamp, nameStock, nameUser, dateTime
    FILTERS s == scheduleOrder(logT)
;

DESIGN scheduleOrder {
    days {
        MOVE BOX(logT) {
            caption = 'История';
        }
    }
}
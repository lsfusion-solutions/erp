MODULE Box;

REQUIRE Stock, Barcode, WMS;

//------------------------- Короб -----------------------------//

CLASS Box 'Короб' ;
TABLE box (Box);

@defineExternalizable(box, STRING[100]);
name 'Наименование' = DATA ISTRING[20](Box);


FORM box 'Короб'
    OBJECTS s = Box PANEL
    PROPERTIES(s) name, id SHOWIF showIDs()
    EDIT Box OBJECT s
;

FORM boxes 'Короба'

    OBJECTS s = Box
    PROPERTIES(s) READONLY name, id SHOWIF showIDs()
    PROPERTIES(s) NEWSESSION NEW, EDIT, DELETE 

    ORDER name(s)

    LIST Box OBJECT s
;

DESIGN boxes { size = (1024, 768); }

                                                               
//--------------------------------------------------Короб для Detail--------------------------------------//

META defineDocumentDetailBoxCustom(detail, prefix, caption)
    prefix###box = DATA Box (###detail);
    name###prefix##Box 'Короб'###caption (###detail detail) = Box.name(prefix###box(detail)) CHARWIDTH 10;
END
META defineDocumentAbstractDetailBoxCustom(detail, prefix, caption)
    prefix###box = ABSTRACT Box (###detail) MATERIALIZED;
    name###prefix##Box 'Короб'###caption (###detail detail) = Box.name(prefix###box(detail)) CHARWIDTH 10;
END
META defineDocumentInterfaceDetailBoxCustom(detail, prefix, caption)
    @defineDocumentAbstractDetailBoxCustom(detail, prefix, caption);
    @defineDocumentDetailBoxCustom(user###detail, prefix, caption);
    prefix###box(User###detail detail) += prefix###box(detail);
END
META defineDocumentInterfaceDetailBoxCustom(detail)
    @defineDocumentInterfaceDetailBoxCustom(detail, , );
END

META defineDocumentAbstractDetailBox(object)
    @defineDocumentAbstractDetailBoxCustom(object###detail, , );
END

META defineDocumentInterfaceDetailBox(object, caption)
    @defineDocumentInterfaceDetailBoxCustom(object###detail, , caption);
END
META defineDocumentInterfaceDetailBox(object)
    @defineDocumentInterfaceDetailBoxCustom(object###detail, , );
END

META extendFormInterfaceDetailBox(object, concrete, prefix, befProp)
    @defineDocumentInterfaceProperty (object, showBox, 'Короб');

    EXTEND FORM user###object
        PROPERTIES(concrete) concrete##howBox
        PROPERTIES (d) SHOWIF concrete##howBox(concrete) nameBox BEFORE befProp(d)
    ;
    DESIGN user###object {
        headerExtraParams {
            NEW headerBox {
                caption = 'Короб';
                type = CONTAINERH;
                MOVE PROPERTY(concrete##howBox(concrete));
            }
        }
    }
    EXTEND FORM object##s
    PROPERTIES (d) READONLY SHOWIF concrete##howBox(concrete) nameBox BEFORE befProp(d)
    ;
END

//---------------- invoiceShipment --------------------//

META defineInvoiceShipmentBox(stockProp)

    @defineDocumentInterfaceDetailBox(invoice);
    box (InvoiceShipmentDetail detail) += box(invoiceDetail(detail));

    @defineDocumentInterfaceProperty (invoice, showBox, 'Короб');

    EXTEND FORM userInvoice
        PROPERTIES(i) showBox
        PROPERTIES(d) AFTER invoiceSum(d) SHOWIF showBox(i) BACKGROUND backgroundShipped(i)
                      nameBox
    ;
    DESIGN userInvoice {
        headerExtraParams {
            NEW headerExtraBox {
                caption = 'Короб';
                MOVE PROPERTY(showBox(i));
            }
        }
    }

    EXTEND FORM invoices
        PROPERTIES(d) READONLY AFTER invoiceSum(d) SHOWIF showBox(i) BACKGROUND backgroundShipped(i)
                      nameBox
    ;
    EXTEND FORM invoiceDetails
        PROPERTIES(d) READONLY AFTER invoiceSum(d) SHOWIF showBox(d) BACKGROUND backgroundShipped(d)
                      nameBox
    ;
    
    overCopy[UserInvoiceDetail,UserInvoiceDetail](UserInvoiceDetail d, UserInvoiceDetail detail) + {       
        box(d) <- box(detail);
    }     

END

NAVIGATOR {
    WMSNavigator {
        NEW FOLDER boxNavigator 'Короба' {
            NEW boxes;
        }
    }
}




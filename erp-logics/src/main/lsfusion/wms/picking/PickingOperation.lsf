MODULE PickingOperation;

REQUIRE Picking;

NAMESPACE Picking;


@defineOperation(' (комплектация)');

@defineOperationRole();

DESIGN operation {
    tabContainer {
        NEW constraintContainer {
            caption = 'Ограничения';
        }
        NEW paramsContainer {
            caption = 'Параметры';
        }
    }
}

operation = ABSTRACT Operation (PickingOrder) MATERIALIZED;
idOperation 'Код операции'(PickingOrder b) = id(operation(b));
nameOperation 'Операция'(PickingOrder b) = name(operation(b));

operation = DATA Operation (UserPickingOrder);
operation(UserPickingOrder o) += operation(o);

WHEN LOCAL FORMS userPickingOrder SET (UserPickingOrder o IS UserPickingOrder) AND NOT CHANGED(operation(o)) AND countOperations(currentUser()) == 1 DO
    operation(o) <- defaultOperation(currentUser());

EXTEND FORM userPickingOrder   
    PROPERTIES (o) nameOperation
;

DESIGN userPickingOrder {
    row1 {
        MOVE PROPERTY(nameOperation(o)) BEFORE PROPERTY(nameStock(o));
    }
}

EXTEND FORM pickingOrders
    PROPERTIES (o) READONLY nameOperation BEFORE nameStock(o)
;

operation = ABSTRACT Operation (Picking) MATERIALIZED;
idOperation 'Код операции'(Picking b) = id(operation(b));
nameOperation 'Операция'(Picking b) = name(operation(b));

operation = DATA Operation (UserPicking);
operation(UserPicking o) += operation(o);

WHEN LOCAL FORMS userPicking SET (UserPicking o IS UserPicking) AND NOT CHANGED(operation(o)) AND countOperations(currentUser()) == 1 DO
    operation(o) <- defaultOperation(currentUser());

EXTEND FORM userPicking
    PROPERTIES (o) nameOperation
;

DESIGN userPicking {
    GROUP(documentPrm,o) {
        MOVE PROPERTY(nameOperation(o));
    }
}

EXTEND FORM pickings
    PROPERTIES (o) READONLY nameOperation BEFORE nameStock(o)
;

NAVIGATOR {
    pickingNavigator {
        NEW operations FIRST;
    }
}

@defineOperationProperty(skipCreateOrderPickingDetails, 'Не создавать строки Комплектации при создании из Заказа на комплектацию', paramsContainer);

skipCreateOrderPickingDetails(UserPicking p) += skipCreateOrderPickingDetails(operation(p));
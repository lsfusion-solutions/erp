MODULE OrderPickingOrderBin;

REQUIRE OrderPickingOrder, PickingOrderBin;

NAMESPACE Order;

@defineDocumentAbstractDetailBin(order);
bin(OrderPickingOrderDetail detail) += bin(orderDetail(detail));

META defineOrderPickingOrderBin(stockProp)
    //-------------------- Ячейки -----------------------//

    @defineDocumentInterfaceDetailBin(order);
    bin[Order.OrderDetail](OrderDetail d) += bin(d);
    
    @extendFormInterfaceDetailBin(order, o, , quantity);

    @deriveDocumentOperationProperty(UserOrder, showBin);

    countUserOrderDetail 'Кол-во строк' = GROUP SUM 1 BY userOrder(UserOrderDetail d), bin(d);
    quantity 'Кол-во' = GROUP SUM quantity(UserOrderDetail d) BY userOrder(d), bin(d);

    pickingNumber 'Номер комплектации' = GROUP MAX pickingNumber(UserOrderDetail d) BY userOrder(d), bin(d);
    changePickingNumber(UserOrder o, Bin b)  { 
        INPUT i = INTEGER DO
            pickingNumber(UserOrderDetail d) <- i WHERE userOrder(d) == o AND bin(d) == b;
    }

    calculatePickingNumber(UserOrder o)  { 
        LOCAL countDetail = INTEGER();
        countDetail() <- (countUserOrderDetail(o) + pickingOrderCount(o) - 1) / pickingOrderCount(o);

        LOCAL pickingNumber = INTEGER();
        pickingNumber() <- 1;
        
        LOCAL quantity = INTEGER();
        quantity() <- 0;
        
        FOR countUserOrderDetail(o, Bin b) ORDER name(b) DO {
            pickingNumber(UserOrderDetail d) <- pickingNumber() WHERE userOrder(d) == o AND bin(d) == b;
            quantity() <- quantity() + countUserOrderDetail(o, b);
            IF (quantity() >= countDetail()) THEN {
                quantity() <- 0;
                pickingNumber() <- pickingNumber() + 1;
            }
        }   
    }

    changePickingOrderCount(UserOrder o)  { 
        INPUT i = INTEGER DO {
            pickingOrderCount(o) <- MIN (MAX i, 1), 5;
            calculatePickingNumber(o);
        }
    }
    
    EXTEND FORM userOrder
        PROPERTIES(o) SHOWIF createPickingOrder(o) pickingOrderCount ON CHANGE changePickingOrderCount(o) 
        
        PROPERTIES(d) SHOWIF createPickingOrder(o) pickingNumber AFTER nameBin(d)
         
        OBJECTS bn = Bin GRID
        PROPERTIES(bn) READONLY SHOWIF createPickingOrder(o)    name 
        PROPERTIES(o, bn) SHOWIF createPickingOrder(o)          countUserOrderDetail READONLY, quantity READONLY, 
                                                                         pickingNumber ON CHANGE changePickingNumber(o, bn)
        FILTERS quantity(o, bn)
        ORDERS name(bn)
    ;
    
    DESIGN userOrder {
        headerPicking {
            MOVE PROPERTY(pickingOrderCount(o));
        }
        specificationBox {
            MOVE BOX(bn) {
                caption = 'Комплектация';
            }
        }
    }
END
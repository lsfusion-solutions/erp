MODULE WriteOffOperationBinLedger;

REQUIRE WriteOffOperation, BinLedger;

NAMESPACE WriteOff;

//операция списания
@defineOperationProperty(useBins, 'Ячейки', showContainer);

@defineOperationProperty(needOut, '"Ячейка (из)" обязательна', constraintContainer);

@defineOperationProperty(needIn, '"Ячейка (в)" обязательна', constraintContainer);

TABLE stockOperation(Stock, Operation);
TABLE stockGroupOperation(StockGroup, Operation);
@defineOperationStock(, stc, 'Склады');

inBin 'Ячейка (в)' = DATA Bin (Operation, Stock);
nameInBin 'Ячейка (в)' (Operation operation, Stock stock) = name(inBin(operation, stock)) CHARWIDTH 10;

setInBin (Operation operation, Stock stock)  {
    DIALOG dialogBinStock OBJECTS s = stock, b = inBin(operation, stock) INPUT NULL DO
        inBin(operation, stock) <- b;
}

CONSTRAINT inBin(Operation o, Stock st) AND NOT stock(inBin(o, st)) == st
    CHECKED BY inBin[Operation, Stock] MESSAGE 'Ячейка недоступна для склада';

outBin 'Ячейка (из)' = DATA Bin (Operation, Stock);
nameOutBin 'Ячейка (из)' (Operation operation, Stock stock) = name(outBin(operation, stock)) CHARWIDTH 10;;

setOutBin (Operation operation, Stock stock)  {
    DIALOG dialogBinStock OBJECTS s = stock, b = outBin(operation, stock) INPUT NULL DO
        outBin(operation, stock) <- b;
}

CONSTRAINT outBin(Operation o, Stock st) AND NOT stock(outBin(o, st)) == st
    CHECKED BY outBin[Operation, Stock] MESSAGE 'Ячейка недоступна для склада';

EXTEND FORM operation
    PROPERTIES(o, stc) nameInBin ON CHANGE setInBin(o, stc), nameOutBin ON CHANGE setOutBin(o, stc)
;
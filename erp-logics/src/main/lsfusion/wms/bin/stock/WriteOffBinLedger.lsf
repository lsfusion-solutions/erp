MODULE WriteOffBinLedger;

REQUIRE WriteOff, BinLedger;

NAMESPACE WriteOff;

@defineDocumentBins(writeOff);

@implementAggregationBinLedger(writeOffDetail, writeOff, WriteOff, sku, quantity, stock);
@implementBinLedger(writeOffBinLedger, writeOffDetail, sku, quantity, stock, inBin);
@implementBinLedger(writeOffBinLedger, writeOffDetail, sku, quantity, stock, outBin);
needToCreate(WriteOffDetail detail) += inBin(detail) OR outBin(detail);

@setBinDocument(userWriteOff);
@defineBinDocumentConstraints(userWriteOff);

backgroundBins 'Цвет' (WriteOff writeOff) = RGB(255, 224, 255) IF writeOff IS WriteOff;
backgroundBins (WriteOffDetail detail) = backgroundBins(writeOff(detail));

@extendObjectFormsBins(userWriteOff, writeOffs, w, stock, backgroundBins);

//операция списания
@defineOperationProperty(useBins, 'Ячейки', showContainer);

TABLE stockOperation(Stock, Operation);
TABLE stockGroupOperation(StockGroup, Operation);
@defineOperationStock(, stc, 'Склады');

inBin 'Ячейка (в)' = DATA Bin (Operation, Stock);
nameInBin 'Ячейка (в)' (Operation operation, Stock stock) = name(inBin(operation, stock)) CHARWIDTH 10;

setInBin (Operation operation, Stock stock)  { 
    DIALOG dialogBinStock OBJECTS s = stock, b = inBin(operation, stock) INPUT NULL DO
        inBin(operation, stock) <- b;
}

CONSTRAINT inBin(Operation o, Stock st) AND NOT stock(inBin(o, st)) == st
    CHECKED BY inBin[Operation, Stock] MESSAGE 'Ячейка недоступна для склада';

outBin 'Ячейка (из)' = DATA Bin (Operation, Stock);
nameOutBin 'Ячейка (из)' (Operation operation, Stock stock) = name(outBin(operation, stock)) CHARWIDTH 10;;

setOutBin (Operation operation, Stock stock)  { 
    DIALOG dialogBinStock OBJECTS s = stock, b = outBin(operation, stock) INPUT NULL DO
        outBin(operation, stock) <- b;
}

CONSTRAINT outBin(Operation o, Stock st) AND NOT stock(outBin(o, st)) == st
    CHECKED BY outBin[Operation, Stock] MESSAGE 'Ячейка недоступна для склада';

EXTEND FORM operation
    PROPERTIES(o, stc) nameInBin ON CHANGE setInBin(o, stc), nameOutBin ON CHANGE setOutBin(o, stc)
;

@deriveDocumentOperationProperty(UserWriteOff, useBins);

@deriveDocumentBins (userWriteOff, userWriteOff, inBin, inBin, outBin, outBin, stock);
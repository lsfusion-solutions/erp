MODULE BinTransferBinLedger;

REQUIRE BinLedger, BinOrderBinLedger;

NAMESPACE Bin;

CLASS ABSTRACT BinTransfer 'Перемещение по ячейкам';
CLASS ABSTRACT BinTransferDetail 'Строка перемещения по ячейкам';

//--- !Необходимо для каждой реализации BinTransfer и BinTransferDetail делать зависимость на Document и DocumentDetail! --// 
//Если наследовать в BinTransfer и BinTransferDetail, то невозможно от них наследовать те классы, что уже наследованы от Document и DocumentDetail

CLASS UserBinTransfer 'Перемещение по ячейкам (польз.)': BinTransfer, Document;
CLASS UserBinTransferDetail 'Строка перемещения по ячейкам': BinTransferDetail, DocumentDetail;

@defineDocumentInterface(binTransfer);

GROUP binTransfer 'Информация о перемещении' : base;

@defineDocumentInterfaceDetailPrice(binTransfer);

@deriveDocumentHeaderTimePrefix(UserBinTransfer, );

@defineDocumentInterfaceNumber(binTransfer, BPSTRING[2]);

@defineDocumentAbstractHeaderPosted(BinTransfer);
@defineDocumentDetailPosted(binTransfer, BinTransferDetail);

isPosted[BinTransfer](UserBinTransfer o) += o IS UserBinTransfer; //Пользовательский документ всегда считаем проведеным

@defineDocumentInterfaceClosed(binTransfer);

@defineDocumentInterfaceDataStock(binTransfer, stock, 'Склад');

@defineDocumentInterfaceDescription(binTransfer, 'Перемещение по ячейкам');

@defineDocumentInterfaceDetailSku(binTransfer, sku);
@defineDocumentInterfaceDetailQuantity(binTransfer);

@defineAddDetailDialogBarcode(userBinTransfer, sku);

@defineDocumentDetailGrossWeight(binTransfer, sku);

@defineDocumentHeaderSkuQuantity(binTransfer, sku);

@defineDocumentHeaderQuantity(binTransfer);

@defineDocumentBins(binTransfer);

disallowSameInOutBins 'Запрет на совпадение ячеек "из" и "в" в одной строке перемещения' = DATA BOOLEAN () PREREAD;

EXTEND FORM options
    PROPERTIES disallowSameInOutBins()
;

DESIGN options {
    WMS {
        MOVE PROPERTY(disallowSameInOutBins());
    }
}

CONSTRAINT SET(outBin(BinTransferDetail detail)==inBin(detail)) AND disallowSameInOutBins()
    MESSAGE 'Ячейки "из" и "в" не могут совпадать в строке перемещения';

useBins(UserBinTransfer transfer) <- TRUE WHEN SET(transfer IS UserBinTransfer);

//todo: вернуть метакод, когда починят баг с тремя ###
//@implementDocumentPrefix(userBinTransfer);

isClosed[Document](UserBinTransfer userBinTransfer) += isClosed(userBinTransfer);
isPosted[Document](UserBinTransfer userBinTransfer) += userBinTransfer IS UserBinTransfer;
number[Document](UserBinTransfer userBinTransfer) += number(userBinTransfer);
series[Document](UserBinTransfer userBinTransfer) += series(userBinTransfer);
date[Document](UserBinTransfer userBinTransfer) += date(userBinTransfer);

index[DocumentDetail](UserBinTransferDetail detail) += index(detail);
sku[DocumentDetail](UserBinTransferDetail detail) += sku(detail);
quantity[DocumentDetail](UserBinTransferDetail detail) += quantity(detail);
price[DocumentDetail](UserBinTransferDetail detail) += price(detail);

document[DocumentDetail](UserBinTransferDetail detail) += userBinTransfer(detail);

overFillDocument  ABSTRACT LIST (UserBinTransferDetail, DocumentDetail);
fillDocument 'Подбор документа'(UserBinTransfer userBinTransfer)  {
    DIALOG documents DO {
        FOR inSession(document(DocumentDetail documentDetail)) NEW i = UserBinTransferDetail DO {
            userBinTransfer(i) <- userBinTransfer;
            sku(i) <- sku(documentDetail);
            quantity(i) <- quantity(documentDetail);
            price(i) <- price(documentDetail) WHERE fillPriceDocuments();
            overFillDocument(i, documentDetail);
        }
    }
} TOOLBAR;

supplierStock[Document](UserBinTransfer binTransfer) += stock(binTransfer);

@implementDocumentForm(binTransfer, bt, 'Перемещение по ячейкам');

@defineAddDetailDialogSkuStock(userBinTransfer, sku, stock, dialogSku);

//пользовательский заказ на перемещение
@defineExternalizable(userBinTransfer, STRING[100]);

@defineExternalizable(userBinTransferDetail, STRING[100]);

@defineNumeratedDefault(UserBinTransfer, 'Перемещения по ячейкам ', 'ПП');

//вес трансфера
sumGrossWeight 'Вес брутто, кг' = ABSTRACT NUMERIC[14,3] (BinTransferDetail);        
sumGrossWeight 'Вес брутто, кг' = DATA NUMERIC[14,3] (UserBinTransferDetail);
sumGrossWeight(UserBinTransferDetail d) += sumGrossWeight(d);
   
sumGrossWeight(UserBinTransferDetail d) <- grossWeight(sku(d)) * quantity(d)
    WHEN CHANGED(sku(d)) OR CHANGED(quantity(d));
              
@defineDocumentHeaderGrossWeightCustomPrefix (binTransfer, binTransferDetail, sum, ', кг');
@defineDocumentHeaderGrossWeightCustomPrefix (userBinTransfer, userBinTransferDetail, sum, ', кг'); 

@defineDocumentDialogStocks(userBinTransfer, stock, company, , );

FORM userBinTransfer 'Перемещение по ячейкам (польз.)'
    OBJECTS o = UserBinTransfer PANEL
    PROPERTIES (o) nameStock ON CHANGE changeStockCompany(o), nameNumerator, number, series, date, time, note
    PROPERTIES (o) READONLY countUserBinTransferDetail, quantityBinTransferDetail, sumGrossWeightUserBinTransferDetail 
    
    OBJECTS d = UserBinTransferDetail
    PROPERTIES (d) index, idBarcodeSku, idSku, nameSku, shortNameUOMSku, quantity, sumGrossWeight
    PROPERTIES (d) NEW, deleteod = DELETE GRID
    PROPERTIES(o) DRAW d fillDocument, addDetailDialogSkuStockUserBinTransferDetail,
                           addDetailInputBarcodeUserBinTransferDetail, deleteUserBinTransferDetail
    FILTERS binTransfer(d)==o
    ORDERS index(d)
    EDIT UserBinTransfer OBJECT o
;

DESIGN userBinTransfer {
    BOX {
        size = (1024, 768);
        NEW mainTabbed {
            tabbed = TRUE;
            fill = 1;
            NEW documentBox{
                fill = 1;
                caption = 'Перемещение';
                NEW headerBox {
                    horizontal = TRUE;
    
                    NEW headerRow1 {
                        fill = 1;
                        MOVE GROUP(documentHeader,o) {
                            lines = 1;
                            NEW first {
                                horizontal = TRUE;
                            }
                            NEW second { 
                                horizontal = TRUE;
                                MOVE PROPERTY(nameNumerator(o));
                                MOVE PROPERTY(number(o));
                                MOVE PROPERTY(series(o));
                                MOVE PROPERTY(date(o));
                                MOVE PROPERTY(time(o));
                                MOVE PROPERTY(nameStock(o));
                            }    
                        }                    
                        
                        NEW headerRow11 {
                            horizontal = TRUE;
                        }
                        NEW headerTabbedPane {
                            alignment = STRETCH;
                            tabbed = TRUE;
                            NEW headerMainParams {
                                caption = 'Основные параметры';
                                MOVE GROUP(documentPrm,o) {
                                    lines = 4;
                                }
                            }
                            NEW headerCreateDocuments {
                                caption = 'Производные документы';
                                horizontal = FALSE;
                            }                            
                        }
                    }
    
                    MOVE GROUP(documentSum,o) {
                        lines = 1;
                    }
                }
                NEW specificationBox {
                    fill = 1;
                    tabbed = TRUE;
                    MOVE BOX(d) {
                        caption = 'Спецификация';
                    }
                }
            }
        }
        MOVE TOOLBARBOX;

        PROPERTY(formOk()) {
            caption = 'Провести';
        }                               
    }
}


overShowEdit = ABSTRACT BOOLEAN (BinTransfer);
overShowDelete = ABSTRACT BOOLEAN (BinTransfer); 

skipToShowEdit = ABSTRACT BOOLEAN (BinTransfer);
skipToShowDelete = ABSTRACT BOOLEAN (BinTransfer);
overShowEdit (BinTransfer o) += isOpened(o) AND NOT skipToShowEdit(o);

isUserOpened(UserBinTransfer o)=  isOpened[BinTransfer](o) AND is(o);
overShowDelete (UserBinTransfer o) += isUserOpened(o) AND NOT skipToShowDelete(o); 

overCopy  ABSTRACT LIST ( UserBinTransferDetail, BinTransferDetail);
overCopy  ABSTRACT LIST ( BinTransfer, UserBinTransfer);
copy 'Копировать'(BinTransfer transfer)  { 
	NEWSESSION {
	    NEW o = UserBinTransfer {
	    	stock(o) <- stock(transfer);
	        inBin(o) <- inBin(transfer);
	        outBin(o) <- outBin(transfer);
	        note(o) <- note(transfer);
	
	        EXEC overCopy(transfer, o);
	
	        FOR binTransfer(BinTransferDetail transferDetail) == transfer DO {
	            NEW d=UserBinTransferDetail {
	                userBinTransfer(d) <- o;
	                sku(d) <- sku(transferDetail);
	                quantity(d) <- quantity(transferDetail);
                    inBin(d) <- inBin(transferDetail);
                    outBin(d) <- outBin(transferDetail);
	                
	                EXEC overCopy(d, transferDetail);
	            }
	        }
	
	        SHOW userBinTransfer OBJECTS o = o DOCKED;
	    }
	}
} TOOLBAR;
    
//-- Копирование строк     
overCopy  ABSTRACT LIST ( UserBinTransferDetail, UserBinTransferDetail);

copy 'Копировать'(UserBinTransferDetail detail)  { 
    NEW d = UserBinTransferDetail {          

        userBinTransfer(d) <- userBinTransfer(detail);
        sku(d) <- sku(detail);
        quantity(d) <- quantity(detail);
        outBin(d) <- outBin(detail);
        inBin(d) <- inBin(detail);
        
        EXEC overCopy(d, detail);                              
    }
} CHANGEKEY 'F6' TOOLBAR;

filterBinTransferDateFrom 'Дата с' = DATA LOCAL DATE ();
filterDateFrom (BinTransfer o) = date(o) >= filterBinTransferDateFrom() OR (o IS BinTransfer AND NOT filterBinTransferDateFrom());      

filterBinTransferDateTo 'Дата по' = DATA LOCAL DATE ();
filterDateTo (BinTransfer o) = date(o) <= filterBinTransferDateTo() OR (o IS BinTransfer AND NOT filterBinTransferDateTo());      

filterBinTransferStock  = DATA LOCAL Stock ();
nameFilterBinTransferStock 'Склад' = name(filterBinTransferStock()) CHARWIDTH 15;          
filterStock (BinTransfer o) = stock(o) == filterBinTransferStock() OR (o IS BinTransfer AND NOT filterBinTransferStock());

filterBinTransferCustomUser  = DATA LOCAL CustomUser ();
nameFilterBinTransferCustomUser 'Создан пользователем' = name[Contact](filterBinTransferCustomUser()) CHARWIDTH 15;          
filterBinCustomUser (BinTransfer i) = createdUser[UserBinTransfer](i) == filterBinTransferCustomUser() OR (i IS BinTransfer AND NOT filterBinTransferCustomUser());  

filterBinTransferComputer = DATA LOCAL Computer ();
nameFilterBinTransferComputer 'Создан на компьютере' = hostname(filterBinTransferComputer()) CHARWIDTH 15;          
filterComputer (BinTransfer i) = createdComputer(i) == filterBinTransferComputer() OR (i IS BinTransfer AND NOT filterBinTransferComputer());
    
show 'Просмотреть' (UserBinTransfer o)   { 
    NEWSESSION{
        SHOW userBinTransfer OBJECTS o = o DOCKED READONLY;
    }
    
}TOOLBAR;
    
showToShow (UserBinTransfer i) = is(i) AND NOT overShowEdit(i);
 
FORM binTransfers 'Перемещения по ячейкам'
    PROPERTIES() filterBinTransferDateFrom, filterBinTransferDateTo, nameFilterBinTransferStock, nameFilterBinTransferCustomUser, nameFilterBinTransferComputer
    
    OBJECTS o = BinTransfer LAST
    PROPERTIES (o) READONLYIF isReadonly() isClosed, isPosted, number, series, date, time, nameStock, nameOutBin, nameInBin, note,
                            countBinTransferDetail, quantityBinTransferDetail, sumGrossWeightUserBinTransferDetail GRID    
    
    FILTERS filterDateFrom(o), filterDateTo(o), filterStock(o)
       
    OBJECTS d = BinTransferDetail
    PROPERTIES (d) READONLY index, idBarcodeSku, nameSku, shortNameUOMSku, nameOutBin, nameInBin, quantity, sumGrossWeight
    
    PROPERTIES (o) READONLY PANEL createdNameUser[UserBinTransfer], createdTime, createdHostnameComputer

    PROPERTIES (o) NEWSESSION addUserBinTransfer = NEW[UserBinTransfer], EDIT SHOWIF overShowEdit(o) 
    PROPERTIES (o) copy
    PROPERTIES (o) close[UserBinTransfer] SHOWIF isOpened[UserBinTransfer](o), open SHOWIF isClosed[UserBinTransfer](o)
    PROPERTIES     NEWSESSION deleteo=DELETE(o) SHOWIF overShowDelete(o) //isUserOpenedTransfer(o)
            
    FILTERS binTransfer(d)==o
    ORDERS index(d)
    LIST BinTransfer OBJECT o
;

DESIGN binTransfers 'Перемещения по ячейкам' {
    NEW documentContainer BEFORE TOOLBARBOX {
        fill = 1;

        NEW filtersBox {                       
            fill = 1;
            NEW topFilters {  
                caption = 'Фильтры';
                NEW topFilters1 {
                    horizontal = TRUE; 
                    MOVE PROPERTY(filterBinTransferDateFrom());
                    MOVE PROPERTY(filterBinTransferDateTo());                            
                    MOVE PROPERTY(nameFilterBinTransferStock());
                    MOVE PROPERTY(nameFilterBinTransferCustomUser());
                    MOVE PROPERTY(nameFilterBinTransferComputer());
                }
            }
            MOVE BOX(o) {
                fill = 1;
            }
        }

        NEW documentDetail {
            fill = 1;
            tabbed = TRUE;

            MOVE BOX(d) {
                fill = 1;
                caption = 'Спецификация';
            }
            NEW documentHistory {
                caption = 'История';
                MOVE GROUP(created,o);
            }
            NEW printTab {
                caption = 'Печатные формы';
                NEW printContainer {
                    caption = 'Печать';
                }
            }
            NEW actionContainer {
                caption = 'Действия';
                horizontal = TRUE;
                NEW createdContainer {
                    caption = 'Создание на основе';
                    MOVE PROPERTY(copy(o)) { caption = 'Перемещение';}
                }
            }
        }
    }
}
@extendFormFilterAccessStock(o, binTransfers, stock, company);
@extendFormEditable(binTransfers);

@defineFilterIsOpened (binTransfer, binTransfers, o);

@defineDocumentLogForm(binTransfers, o);

//Операция
@defineDocumentOperationContainer(binTransfer, o, isPostedBintransfer, nameStock, first, name, );

overCopy(BinTransfer transfer, UserBinTransfer userTransfer) + { 
    operation(userTransfer) <- operation(transfer);    
}

@defineDocumentOperationRoleOver(UserBinTransfer, bin);

@defineOperationFilterProperty(binTransfer, o, binTransfers, nameFilterBinTransferStock);

@extendFormFilterAccessStock(BinTransfer, o, binTransfers, stock, company);
@defineDocumentStockAccess(UserBinTransfer, stock, company);

@defineDocumentOperationConstraint(binTransfer, 'перемещение по ячейкам', Bin);

CONSTRAINT operation(BinTransfer binTransfer) AND stock(binTransfer) AND NOT in(stock(binTransfer), operation(binTransfer))
    CHECKED BY stock[UserBinTransfer]
    MESSAGE 'Склад должен совпадать с отмеченными в операции';
CONSTRAINT operation(BinTransfer binTransfer) AND isPosted(binTransfer) AND NOT stock(binTransfer) AND needStock(operation(binTransfer))
    MESSAGE 'Склад должен быть задан для документа';

@deriveDocumentBins (userBinTransfer, userBinTransfer, inBin, inBin, outBin, outBin, stock);

@defineBinDocumentConstraints(userBinTransfer);

@defineDocumentSkuStockPriceListTypeSystemLedger(userBinTransfer, );

@extendFormDocumentSkuStock(userBinTransfer, userBinTransfer, o, , );

bins 'Ячейки с остатком' (Sku sku, BinTransfer transfer) = bins(sku, stock(transfer));
prevCurrentBalanceOutBin 'Текущий остаток (ячейка из)' (Sku sku, BinTransfer transfer) = prevCurrentBalance(sku, outBin(transfer));
prevCurrentBalanceInBin 'Текущий остаток (ячейка в)' (Sku sku, BinTransfer transfer) = prevCurrentBalance(sku, inBin(transfer));

EXTEND FORM userBinTransfer
    PROPERTIES (ks, o) READONLY AFTER prevCurrentBalance(ks, st) bins, prevCurrentBalanceOutBin, prevCurrentBalanceInBin
    FILTERGROUP filter
        FILTER 'С остатком (ячейка из)' prevCurrentBalanceOutBin(ks, o) 'F7' 
        FILTER 'С остатком ' currentBalance(ks,st) 'F10' DEFAULT
        FILTER 'В документе ' quantity(ks, o, st) 'F9'
;

//упаковка
@defineDocumentPack(binTransfer, o);

packQuantity 'Кол-во упаковок в документе' (sku, userBinTransfer, stock) = GROUP SUM packQuantity(UserBinTransferDetail userBinTransferDetail)
    BY sku(userBinTransferDetail), userBinTransfer(userBinTransferDetail), stock(userBinTransferDetail);

changePackQuantity(Sku sku, UserBinTransfer userBinTransfer, Stock stock)  { 
    INPUT n = NUMERIC[16,5] DO
        IF userBinTransferDetail(sku, userBinTransfer, stock) THEN {
            IF n THEN {
                packQuantity(UserBinTransferDetail userBinTransferDetail) <- n WHERE userBinTransferDetail == userBinTransferDetail(sku, userBinTransfer, stock);
                quantity(UserBinTransferDetail userBinTransferDetail) <- NUMERIC[16,5](packQuantity(userBinTransferDetail)*(OVERRIDE amountPack(userBinTransferDetail), 1.0)) 
                                        WHERE userBinTransferDetail == userBinTransferDetail(sku, userBinTransfer, stock) AND NOT skipChangeQuantity(userBinTransferDetail(sku, userBinTransfer, stock));
            } ELSE {
                FOR userBinTransferDetail(sku, userBinTransfer, stock) == UserBinTransferDetail d DO
                    DELETE d;
            }
        } ELSE {
            IF n THEN  {
                NEW d = UserBinTransferDetail {
                   userBinTransfer(d) <- userBinTransfer;
                   sku(d) <- sku;
                   packQuantity (d) <- n;
                   quantity(d) <- NUMERIC[16,5](packQuantity(d) * (OVERRIDE amountPack(d), 1.0));
                }
            }
        }
}

changeAmountPack(Sku sku, UserBinTransfer userBinTransfer, Stock stock)  { 
    INPUT n = NUMERIC[14,3] DO {
        IF userBinTransferDetail(sku, userBinTransfer, stock) THEN {
            IF n THEN {
                amountPack(UserBinTransferDetail userBinTransferDetail) <- n WHERE userBinTransferDetail == userBinTransferDetail(sku, userBinTransfer, stock);
                quantity(UserBinTransferDetail userBinTransferDetail) <- NUMERIC[16,5](packQuantity(userBinTransferDetail)*(OVERRIDE amountPack(userBinTransferDetail), 1.0)) 
                                        WHERE userBinTransferDetail == userBinTransferDetail(sku, userBinTransfer, stock) AND packQuantity(userBinTransferDetail) AND NOT skipChangeQuantity(userBinTransferDetail(sku, userBinTransfer, stock));
            } ELSE {
                amountPack(UserBinTransferDetail userBinTransferDetail) <- NULL WHERE userBinTransferDetail == userBinTransferDetail(sku, userBinTransfer, stock);
            }
    
        }
        changeValuePack(sku, n);
    }
}

@extendFormDocumentPackSkuStock(userBinTransfer, userBinTransfer, o, );

@deriveDocumentOperationProperty(UserBinTransfer, showPack);

//проведение по регистру
EXTEND CLASS BinTransferDetail: BinLedger;
    
@implementBinLedger(binTransferDetail, stock);

NAVIGATOR {
    WMSNavigator {
         WMSDocument {
            NEW binTransfers;
         }
    }
}

CONSTRAINT (CHANGED(operation(BinTransferDetail d)) OR CHANGED(inBin(d)) OR CHANGED(outBin(d))) AND levelBinGroup(operation(d)) AND NOT 
    binGroupRoot(binGroup(inBin(d)), levelBinGroup(operation(d))) == binGroupRoot(binGroup(outBin(d)), levelBinGroup(operation(d))) AND
    inBin(d) AND outBin(d)
    MESSAGE 'Несовпадение общего уровня группы ячеек заданного для операции';

CONSTRAINT (CHANGED(operation(BinTransfer t)) OR CHANGED(inBin(t)) OR CHANGED(outBin(t))) AND levelBinGroup(operation(t)) AND NOT 
    binGroupRoot(binGroup(inBin(t)), levelBinGroup(operation(t))) == binGroupRoot(binGroup(outBin(t)), levelBinGroup(operation(t))) AND
    inBin(t) AND outBin(t)
    MESSAGE 'Несовпадение общего уровня группы ячеек заданного для операции';

CONSTRAINT (CHANGED(operation(BinTransferDetail d)) OR CHANGED(inBin(d))) AND inBin(d) AND NOT inBin(inBin(d), operation(d))
    MESSAGE '"Ячейка (в)" строки перемещения по ячейкам  должна совпадать с отмеченными в операции';

CONSTRAINT (CHANGED(operation(BinTransfer t)) OR CHANGED(inBin(t))) AND inBin(t) AND NOT inBin(inBin(t), operation(t))
    MESSAGE '"Ячейка (в)" документа перемещение по ячейкам  должна совпадать с отмеченными в операции';

CONSTRAINT (CHANGED(operation(BinTransferDetail d)) OR CHANGED(outBin(d))) AND outBin(d) AND NOT outBin(outBin(d), operation(d))
    MESSAGE '"Ячейка (из)" строки перемещения по ячейкам должна совпадать с отмеченными в операции';

CONSTRAINT (CHANGED(operation(BinTransfer t)) OR CHANGED(outBin(t))) AND outBin(t) AND NOT outBin(outBin(t), operation(t))
    MESSAGE '"Ячейка (из)" документа перемещения по ячейкам должна совпадать с отмеченными в операции';

CONSTRAINT (CHANGED(operation(BinTransferDetail d)) OR DROPPED(outBin(d)) OR SET(d IS BinTransferDetail)) AND NOT outBin(d) AND needOut(operation(d))
    MESSAGE '"Ячейка (из)" обязательна для заданной операции перемещения между ячейками';

CONSTRAINT (CHANGED(operation(BinTransferDetail d)) OR DROPPED(inBin(d)) OR SET(d IS BinTransferDetail)) AND NOT inBin(d) AND needIn(operation(d))
    MESSAGE '"Ячейка (в)" обязательна для заданной операции перемещения между ячейками';

extraBinFilter (Bin b, UserBinTransfer t) = 
    (levelBinGroup(operation(t)) AND inBin(t) AND binGroupRoot(binGroup(inBin(t)), levelBinGroup(operation(t))) = binGroupRoot(binGroup(b), levelBinGroup(operation(t))) OR
    levelBinGroup(operation(t)) AND outBin(t) AND binGroupRoot(binGroup(b), levelBinGroup(operation(t))) == binGroupRoot(binGroup(outBin(t)), levelBinGroup(operation(t))) OR 
    t IS UserBinTransfer AND b IS Bin AND (NOT levelBinGroup(operation(t)) OR (NOT inBin(t) AND NOT outBin(t))));

overExtraInBinFilter = ABSTRACT BOOLEAN (Bin, UserBinTransfer);
needExtraInBinFilter = ABSTRACT BOOLEAN (Bin, UserBinTransfer);
extraInBinFilter (Bin b, UserBinTransfer t) =
    needExtraInBinFilter(b, t) AND overExtraInBinFilter(b, t) OR t IS UserBinTransfer AND b IS Bin AND NOT needExtraInBinFilter(b, t);

overExtraOutBinFilter = ABSTRACT BOOLEAN (Bin, UserBinTransfer);
needExtraOutBinFilter = ABSTRACT BOOLEAN (Bin, UserBinTransfer);
extraOutBinFilter (Bin b, UserBinTransfer t) =
    needExtraOutBinFilter(b, t) OR overExtraOutBinFilter(b, t) OR t IS UserBinTransfer AND b IS Bin AND NOT needExtraOutBinFilter(b, t);

extraBinFilter (Bin b, UserBinTransferDetail d) = 
    (levelBinGroup(operation(d)) AND inBin(d) AND binGroupRoot(binGroup(inBin(d)), levelBinGroup(operation(d))) = binGroupRoot(binGroup(b), levelBinGroup(operation(d))) OR
    levelBinGroup(operation(d)) AND outBin(d) AND binGroupRoot(binGroup(b), levelBinGroup(operation(d))) == binGroupRoot(binGroup(outBin(d)), levelBinGroup(operation(d))) OR
    d IS UserBinTransferDetail AND b IS Bin AND (NOT levelBinGroup(operation(d)) OR (NOT inBin(d) AND NOT outBin(d))));

overExtraInBinFilter = ABSTRACT BOOLEAN (Bin, UserBinTransferDetail);
needExtraInBinFilter = ABSTRACT BOOLEAN (Bin, UserBinTransferDetail);
extraInBinFilter (Bin b, UserBinTransferDetail d) =
    needExtraInBinFilter(b, d) AND overExtraInBinFilter(b, d) OR d IS UserBinTransferDetail AND b IS Bin AND NOT needExtraInBinFilter(b, d);

overExtraOutBinFilter = ABSTRACT BOOLEAN (Bin, UserBinTransferDetail);
needExtraOutBinFilter = ABSTRACT BOOLEAN (Bin, UserBinTransferDetail);
extraOutBinFilter (Bin b, UserBinTransferDetail d) =
    needExtraOutBinFilter(b, d) AND overExtraOutBinFilter(b, d) OR d IS UserBinTransferDetail AND b IS Bin AND NOT needExtraOutBinFilter(b, d);

@setBinDocumentWithfilter(UserBinTransfer, stock);

EXTEND FORM userBinTransfer
    PROPERTIES(o) AFTER note(o) nameInBin ON CHANGE setInBin(o), nameOutBin ON CHANGE setOutBin(o)
    PROPERTIES(d) AFTER shortNameUOMSku(d) nameOutBin ON CHANGE setOutBin(d), nameInBin ON CHANGE setInBin(d)
;

userBinTransferDetail (sku, userBinTransfer, bin, stock) =
    GROUP MAX UserBinTransferDetail detail BY sku(detail), userBinTransfer(detail), outBin(detail), stock(detail);

userBinTransferDetail (sku, userBinTransfer, bin) =
    GROUP MAX UserBinTransferDetail detail BY sku(detail), userBinTransfer(detail), outBin(detail);

quantity 'Кол-во товара в документе' (sku, userBinTransfer, bin, stock) =
    GROUP SUM quantity(UserBinTransferDetail detail) BY sku(detail), userBinTransfer(detail), outBin(detail), stock(detail);

backgroundQuantity 'Цвет' (sku, userBinTransfer, bin, stock) = ABSTRACT CASE COLOR (Sku, UserBinTransfer, Bin, Stock);

changeQuantityValue(Sku sku, UserBinTransfer userBinTransfer, Bin bin, Stock stock, NUMERIC[16,5] n)  {
    IF userBinTransferDetail(sku, userBinTransfer, bin, stock) THEN {
        IF (n AND NOT deleteNullDetail()) OR (deleteNullDetail() AND n != 0) THEN {
            quantity(UserBinTransferDetail userBinTransferDetail) <- n WHERE userBinTransferDetail == userBinTransferDetail(sku, userBinTransfer, bin, stock);
            overChangeQuantityValueSku(userBinTransferDetail(sku, userBinTransfer, bin, stock));
        } ELSE {
            FOR userBinTransferDetail(sku, userBinTransfer, bin, stock) == UserBinTransferDetail d DO
                DELETE d;
        }
    } ELSE {
        IF (n AND NOT deleteNullDetail()) OR (deleteNullDetail() AND n != 0) THEN {
            NEW d = UserBinTransferDetail {
                userBinTransfer(d) <- userBinTransfer;
                sku(d) <- sku;
                outBin(d) <- bin;
                quantity(d) <- n;
                overChangeQuantityValueSku(d);
            }
        }
    }
}

changeQuantity(Sku sku, UserBinTransfer userBinTransfer, Bin bin, Stock stock)  {
    INPUT n = NUMERIC[16,5] DO
        changeQuantityValue(sku, userBinTransfer, bin, stock, n);
}

allQuantity 'Весь остаток' = DATA LOCAL BOOLEAN (Sku, UserBinTransfer, Bin, Stock);
changeAllQuantity(Sku sku, UserBinTransfer userBinTransfer, Bin bin, Stock stock) {
    INPUT b = BOOLEAN DO {
        allQuantity (sku, userBinTransfer, bin, stock) <- b;
        changeQuantityValue(sku, userBinTransfer, bin, stock, prevCurrentBalance(sku, bin) IF b);
    }
}

EXTEND FORM userBinTransfer
    OBJECTS sbin = Bin GRID
    PROPERTIES(sbin) order, canonicalId
    PROPERTIES quantity(ks, o, sbin, st) ON CHANGE changeQuantity(ks, o, sbin, st) BACKGROUND backgroundQuantity(ks, o, sbin, st)
    PROPERTIES prevCurrentBalance(ks, sbin)
    ORDERS order(sbin), canonicalId(sbin)

    FILTERS prevCurrentBalance(ks, sbin),
        stock(sbin) == st
;

DESIGN userBinTransfer {
    BOX(sts) {
        GRID(sts) {
            fill = 3;
        }
        MOVE BOX(sbin) {
            fill = 1;
            caption = 'Ячейки';
            PROPERTY(prevCurrentBalance(ks, sbin)) { background = #FFEEEE; }
            PROPERTY(quantity(ks, o, sbin, st)) { background = #D4FFD4; }
        }
    }
}

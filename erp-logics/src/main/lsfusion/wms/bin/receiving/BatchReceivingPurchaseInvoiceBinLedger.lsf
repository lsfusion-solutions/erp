MODULE BatchReceivingPurchaseInvoiceBinLedger;

REQUIRE ReceivingPurchaseInvoiceBinLedger, BatchReceivingInvoiceBinLedger, PurchaseCreditNoteSkuLedger;

NAMESPACE Purchase;

overCreateDetail(ReceivingDetail d, InvoiceDetail detail) + {
    expiryDate(d) <- expiryDate(detail);
    consolidationKey(d) <- consolidationKey(detail);
}

skipReceiving(Invoice r) += NOT createShipment(r);

CONSTRAINT userReceiving(Invoice invoice) IS UserReceiving AND (NOT createShipment(invoice))
    MESSAGE 'Для накладной приемки не задана поставка';

quantityCreditNote 'Кол-во в актах расхождений' (Sku s, STRING str, Receiving r) =
    [GROUP SUM quantity(CreditNoteDetail d) BY sku(d), overConsolidationKey(d), receiving(invoice(invoiceDetail(d)))](s, str, r);

createCreditNoteQuantityVariance(Invoice i) + {
    IF NOT userCreditNote(i) THEN NEW c = UserCreditNote {
        invoice(c) <- i;
        operation(c) <- operation(i);
        executeLocalEvents('Purchase.userCreditNote');
        supplier(c) <- supplier(i);
        supplierStock(c) <- supplierStock(i);
        customer(c) <- customer(i);
        customerStock(c) <- customerStock(i);
        date(c) <- currentDate();
        time(c) <- currentTime();
        isPosted(c) <- TRUE;
    }

    FOR quantityVariance(InvoiceDetail d) DO NEW dc = UserCreditNoteDetail {
        userCreditNote(dc) <- userCreditNote(invoice(d));
        invoiceDetail(dc) <- d;
        batch(dc) <- overBatch(d);
        sku(dc) <- sku(d);
        quantity (dc) <- quantityVariance(d);
        VAT(dc) <- VAT(d);
        valueVAT(dc) <- valueVAT(d);
        price(dc) <- price(d);
        invoicePrice(dc) <- invoicePrice(d);
    }
    executeLocalEvents('Purchase.userCreditNote');
}

createCreditNoteQualityVariance(Invoice i) + {
    IF NOT userCreditNote(i) THEN NEW c = UserCreditNote {
        invoice(c) <- i;
        operation(c) <- operation(i);
        executeLocalEvents('Purchase.userCreditNote');
        supplier(c) <- supplier(i);
        supplierStock(c) <- supplierStock(i);
        customer(c) <- customer(i);
        customerStock(c) <- customerStock(i);
        date(c) <- currentDate();
        time(c) <- currentTime();
        isPosted(c) <- TRUE;
    }

    FOR qualityVariance(InvoiceDetail d) DO NEW dc = UserCreditNoteDetail {
        userCreditNote(dc) <- userCreditNote(invoice(d));
        invoiceDetail(dc) <- d;
        batch(dc) <- overBatch(d);
        sku(dc) <- sku(d);
        quantity (dc) <- qualityVariance(d);
        VAT(dc) <- VAT(d);
        valueVAT(dc) <- valueVAT(d);
        price(dc) <- price(d);
        invoicePrice(dc) <- invoicePrice(d);
    }
    executeLocalEvents('Purchase.userCreditNote');
}

hasInvoice = GROUP SUM 1 BY invoice(invoiceDetail(CreditNoteDetail d)), creditNote(d);

descriptionCreditNotes 'Акты расхождения' =
    GROUP CONCAT seriesNumber(CreditNote creditNote) IF hasInvoice(Invoice invoice, creditNote), '; ' ORDER dateTime(creditNote), creditNote BY invoice;

EXTEND FORM invoices
    PROPERTIES READONLY BACKGROUND RGB(255, 224, 255) descriptionCreditNotes(i) AFTER nameStatusReceiving(i)
;
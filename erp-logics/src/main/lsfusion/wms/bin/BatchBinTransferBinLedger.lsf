MODULE BatchBinTransferBinLedger;

REQUIRE BatchBinSkuLedger, BinTransferBinLedger;

NAMESPACE Bin;

@defineDocumentInterfaceDetailBatch(binTransfer, batch);

@implementDocumentBatch(binTransfer);

@implementDocumentBatchForm(bt);

EXTEND FORM userBinTransfer
    PROPERTIES(d) AFTER shortNameUOMSku(d) idBatch, nameBatch
;

overCopy(UserBinTransferDetail d, BinTransferDetail transferDetail) + {
    batch(d) <- batch(transferDetail);
}
overCopy[UserBinTransferDetail,UserBinTransferDetail](UserBinTransferDetail d, UserBinTransferDetail detail) + {
    batch(d) <- batch(detail);
}

EXTEND FORM binTransfers
    PROPERTIES(d) READONLY AFTER shortNameUOMSku(d) idBatch, nameBatch
;

@defineOperationProperty(isBatch, 'Должна быть задана партия', constraintContainer);

CONSTRAINT isPosted(BinTransferDetail d) AND isBatch(operation(d)) AND sku(d) AND NOT skipFillBatch(d) AND NOT batch(d)
    MESSAGE 'Для строки строка перемещения по ячейкам должна быть задана партия';


@defineDocumentBatchStockPriceListTypeSystemLedger(userBinTransfer, );

@showDocumentInterfaceDetailBatch(binTransfer);

@extendFormDocumentBatchStock(userBinTransfer, userBinTransfer, o, , );

bins 'Ячейки с остатком' (Batch batch, BinTransfer transfer) = bins(batch, stock(transfer));
prevCurrentBalanceOutBin 'Текущий остаток (ячейка из)' (Batch batch, BinTransfer transfer) = prevCurrentBalance(batch, outBin(transfer));
prevCurrentBalanceInBin 'Текущий остаток (ячейка в)' (Batch batch, BinTransfer transfer) = prevCurrentBalance(batch, inBin(transfer));

EXTEND FORM userBinTransfer
    PROPERTIES (b, o) READONLY AFTER prevCurrentBalance(b, sto) bins, prevCurrentBalanceOutBin, prevCurrentBalanceInBin
    FILTERGROUP filterBatch
        FILTER 'С остатком (ячейка из)' prevCurrentBalanceOutBin(b, o) 'F7' 
        FILTER 'С остатком ' currentBalance(b, sto) 'F10' DEFAULT
        FILTER 'В документе ' quantity(b, o, sto) 'F9'
;
    
@implementBatchBinLedger(binTransferDetail, stock);

skipSetInBin(UserBinTransferDetail d) += TRUE IF inBin(d);
skipSetOutBin(UserBinTransferDetail d) += TRUE IF outBin(d);
MODULE ShippingBinLedger;

REQUIRE EmployeeBin, SaleBinLedger, BinTransferBinLedger, WMSDashboard, OrderPickingOrder, PickingBinLedger;

NAMESPACE Sale;

defaultShippingBin = DATA Bin (Employee, Stock);
nameDefaultShippingBin 'Ячейка для отгрузки' (Employee employee, Stock stock) = canonicalId(defaultShippingBin(employee, stock)) CHARWIDTH 10;

CONSTRAINT defaultShippingBin(Employee employee, Stock stock) AND NOT stock(defaultShippingBin(employee, stock)) == stock
    CHECKED BY defaultShippingBin[Employee, Stock] MESSAGE 'Ячейка задана для другого склада';

CONSTRAINT defaultShippingBin(Employee employee, Stock stock) AND NOT accessCompany(employee, stock)
    CHECKED BY defaultShippingBin[Employee, Stock] MESSAGE 'Нет доступа к складу ячейки';

CONSTRAINT defaultShippingBin(Employee employee, Stock stock) AND NOT shipping(defaultShippingBin(employee, stock))
    CHECKED BY defaultShippingBin[Employee, Stock] MESSAGE 'Выбранная ячейка не предназначена для отгрузки';

EXTEND FORM employee
    PROPERTIES(e, s) nameDefaultShippingBin
;

dataShippingBin = DATA LOCAL NESTED Bin (CustomUser);
shippingBin (CustomUser u) = OVERRIDE dataShippingBin(u), (GROUP MAX defaultShippingBin(u, Stock stock));
nameShippingBin 'Ячейка(отгрузка)' (CustomUser u) = canonicalId(shippingBin(u)) CHARWIDTH 10;

nameBinGroup2ShippingBin 'Зона ячейки' (CustomUser u) = nameBinGroup2(shippingBin(u)) CHARWIDTH 10;

CONSTRAINT LOCAL dataShippingBin(CustomUser u) AND NOT accessCompany(u, stock(dataShippingBin(u)))
    CHECKED BY dataShippingBin[CustomUser] MESSAGE 'Нет доступа к складу ячейки';

CONSTRAINT LOCAL dataShippingBin(CustomUser u) AND NOT shipping(dataShippingBin(u))
    CHECKED BY dataShippingBin[CustomUser] MESSAGE 'Выбранная ячейка не предназначена для приемки';

FORM shipping 'Отгрузка'
    OBJECTS u=CustomUser PANEL
    PROPERTIES(u) READONLY name
    PROPERTIES(u) nameShippingBin, nameBinGroup2ShippingBin READONLY
    FILTERS u=currentUser()

    OBJECTS s = Sku
    PROPERTIES(s) select
    PROPERTIES(s) READONLY name, idBarcode
    PROPERTIES READONLY currentBalance 'Текущий остаток в ячейке' = currentBalance(s, shippingBin(u))
    FILTERS currentBalance(s, shippingBin(u))
;

DESIGN shipping {
    OBJECTS {
        MOVE BOX(u) {
            PROPERTY(name(u)) {
                font = 'bold 24';
                panelCaptionVertical = TRUE;
                focusable = FALSE;
                charWidth = 30;
            }
            PROPERTY(nameShippingBin(u)) {
                font = 'bold 24';
                panelCaptionVertical = TRUE;
                charWidth = 30;
            }
            PROPERTY(nameBinGroup2ShippingBin(u)) {
                font = 'bold 24';
                panelCaptionVertical = TRUE;
                charWidth = 30;
            }
        }
        NEW tabContainer {
            tabbed = TRUE;
            fill = 1;
            NEW balance {
                caption = 'Остаток';
                tabbed = TRUE;
                fill = 1;
                MOVE BOX(s) { caption = 'По товарам'; }
            }
        }
    }
}

NAVIGATOR {
    WMSDashboardNavigator {
        NEW shipping;
    }
}
MODULE BinTransferStockDocumentBinLedger;

REQUIRE BinTransferBinLedger, StockDocumentBinLedger;

NAMESPACE Bin;

shipmentPrice '{document.price} (учетная)' = ABSTRACT NUMERIC[16,4] (BinTransferDetail) MATERIALIZED PATTERN '#,##0.00##';
shipmentPrice '{document.price} (учетная)' = DATA NUMERIC[16,4] (UserBinTransferDetail);
shipmentPrice(UserBinTransferDetail userBinTransferDetail) += shipmentPrice(userBinTransferDetail);

shipmentSum '{document.sum.of} (учетная)'  = ABSTRACT NUMERIC[16,4] (BinTransferDetail) MATERIALIZED PATTERN '#,##0.00##';
shipmentSum '{document.sum.of} (учетная)'  = DATA NUMERIC[16,4] (UserBinTransferDetail);
shipmentSum(UserBinTransferDetail userBinTransferDetail) += shipmentSum(userBinTransferDetail);

shipmentSumBinTransferDetail '{document.sum.of} (учетная)' (invoice) = 
    GROUP SUM shipmentSum(BinTransferDetail idetail) BY binTransfer(idetail) CHARWIDTH 10 IN documentSum PATTERN '#,##0.00##';
shipmentSumUserBinTransferDetail '{document.sum.of} (учетная)' (userInvoice) = 
    GROUP SUM shipmentSum(UserBinTransferDetail idetail) BY userBinTransfer(idetail) CHARWIDTH 10 IN documentSum PATTERN '#,##0.00##';

WHEN LOCAL (CHANGED(stock(UserBinTransferDetail detail)) OR CHANGED(dateTime(detail)) OR SETCHANGED(batch(detail))) AND 
    batch(detail) AND createStockDocumentBinLedger(operation(detail)) AND NOT CHANGED(shipmentPrice(detail)) AND NOT skipUpdatePriceAccount() DO {
    shipmentPrice (detail) <- prevAccountPriceB(batch(detail), stock(detail), dateTime(detail));
}

calcShipmentSum = ABSTRACT VALUE NUMERIC[18,4] (UserBinTransferDetail);
calcShipmentSum(UserBinTransferDetail detail) += NUMERIC[18,4](round2(quantity(detail) * shipmentPrice(detail))); //округление делаем до двух знаков, если надо, то переопределиться потом
shipmentSum (UserBinTransferDetail detail) <- calcShipmentSum(detail)
    WHEN DO (CHANGED(quantity(detail)) OR CHANGED (shipmentPrice(detail))) AND createStockDocumentBinLedger(operation(detail));

EXTEND FORM userBinTransfer
    PROPERTIES(o) READONLY shipmentSumBinTransferDetail
    PROPERTIES(d) BACKGROUND RGB(255, 224, 255) AFTER quantity(d) SHOWIF createStockDocumentBinLedger(operation(d))
        shipmentPrice, shipmentSum
;

EXTEND FORM binTransfers
    PROPERTIES(o) READONLY AFTER quantityBinTransferDetail(o) shipmentSumUserBinTransferDetail  
    PROPERTIES(d) READONLY BACKGROUND RGB(255, 224, 255) AFTER quantity(d) SHOWIF createStockDocumentBinLedger(operation(d))
        shipmentPrice, shipmentSum
;

CLASS IncStockDocumentBinTransferLedger 'Перемещение по ячейкам (приход)' : IncStockDocumentBinLedger;

needToCreateIncStockDocumentBinLedger (BinTransfer t, BinGroup g) = createStockDocumentBinLedger(operation(t)) AND
    (GROUP SUM quantity(BinTransferDetail d) IF binTransfer(d) == t AND binGroup2(inBin(d)) == g);
incStockDocumentBinTransferLedger = AGGR IncStockDocumentBinTransferLedger WHERE needToCreateIncStockDocumentBinLedger(BinTransfer binTransfer, BinGroup binGroup) MATERIALIZED INDEXED;

dateTime[StockDocumentBinLedger] (IncStockDocumentBinTransferLedger ledger) += dateTime(binTransfer(ledger));
isPosted[StockDocumentBinLedger] (IncStockDocumentBinTransferLedger ledger) += isPosted(binTransfer(ledger));
isClosed[StockDocumentBinLedger] (IncStockDocumentBinTransferLedger ledger) += isClosed(binTransfer(ledger));
number[StockDocumentBinLedger] (IncStockDocumentBinTransferLedger ledger) += number(binTransfer(ledger));
series[StockDocumentBinLedger] (IncStockDocumentBinTransferLedger ledger) += series(binTransfer(ledger));
operation[StockDocumentBinLedger] (IncStockDocumentBinTransferLedger ledger) += operation(binTransfer(ledger));
stock[StockDocumentBinLedger] (IncStockDocumentBinTransferLedger ledger) += stock(binTransfer(ledger));
binGroup[StockDocumentBinLedger] (IncStockDocumentBinTransferLedger ledger) += binGroup(ledger);
description[StockDocumentBinLedger] (IncStockDocumentBinTransferLedger ledger) += description(binTransfer(ledger));

sumItem[IncStockDocumentBinLedger] (IncStockDocumentBinTransferLedger ledger) +=
    NUMERIC[18,4](GROUP SUM quantity(BinTransferDetail d) IF isItem(sku(d)) AND binTransfer(d) == binTransfer(ledger) AND binGroup2(inBin(d)) == binGroup(ledger));
sumContainer[IncStockDocumentBinLedger] (IncStockDocumentBinTransferLedger ledger) +=
    NUMERIC[18,4](GROUP SUM quantity(BinTransferDetail d) IF isContainer(sku(d)) AND binTransfer(d) == binTransfer(ledger) AND binGroup2(inBin(d)) == binGroup(ledger));

edit(IncStockDocumentBinLedger ledger) + {edit[UserBinTransfer](binTransfer(ledger));}

CLASS OutStockDocumentBinTransferLedger 'Перемещение по ячейкам (расход)' : OutStockDocumentBinLedger;

needToCreateOutStockDocumentBinLedger (BinTransfer t, BinGroup g) = createStockDocumentBinLedger(operation(t)) AND
    (GROUP SUM quantity(BinTransferDetail d) IF binTransfer(d) == t AND binGroup2(outBin(d)) == g);
outStockDocumentBinTransferLedger = AGGR OutStockDocumentBinTransferLedger WHERE needToCreateOutStockDocumentBinLedger(BinTransfer binTransfer, BinGroup binGroup) MATERIALIZED INDEXED;

dateTime[StockDocumentBinLedger] (OutStockDocumentBinTransferLedger ledger) += dateTime(binTransfer(ledger));
isPosted[StockDocumentBinLedger] (OutStockDocumentBinTransferLedger ledger) += isPosted(binTransfer(ledger));
isClosed[StockDocumentBinLedger] (OutStockDocumentBinTransferLedger ledger) += isClosed(binTransfer(ledger));
number[StockDocumentBinLedger] (OutStockDocumentBinTransferLedger ledger) += number(binTransfer(ledger));
series[StockDocumentBinLedger] (OutStockDocumentBinTransferLedger ledger) += series(binTransfer(ledger));
operation[StockDocumentBinLedger] (OutStockDocumentBinTransferLedger ledger) += operation(binTransfer(ledger));
stock[StockDocumentBinLedger] (OutStockDocumentBinTransferLedger ledger) += stock(binTransfer(ledger));
binGroup[StockDocumentBinLedger] (OutStockDocumentBinTransferLedger ledger) += binGroup(ledger);
description[StockDocumentBinLedger] (OutStockDocumentBinTransferLedger ledger) += description(binTransfer(ledger));

sumItem[OutStockDocumentBinLedger] (OutStockDocumentBinTransferLedger ledger) +=
    NUMERIC[18,4](GROUP SUM quantity(BinTransferDetail d) IF isItem(sku(d)) AND binTransfer(d) == binTransfer(ledger) AND binGroup2(outBin(d)) == binGroup(ledger));
sumContainer[OutStockDocumentBinLedger] (OutStockDocumentBinTransferLedger ledger) +=
    NUMERIC[18,4](GROUP SUM quantity(BinTransferDetail d) IF isContainer(sku(d)) AND binTransfer(d) == binTransfer(ledger) AND binGroup2(outBin(d)) == binGroup(ledger));

edit(OutStockDocumentBinTransferLedger ledger) + {edit[UserBinTransfer](binTransfer(ledger));}
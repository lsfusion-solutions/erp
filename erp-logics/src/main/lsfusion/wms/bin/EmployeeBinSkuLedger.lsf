MODULE EmployeeBinSkuLedger;

REQUIRE EmployeeBin, BinSkuLedger;

NAMESPACE BinLedger;

FORM dialogZoneBinGroups 'Зоны ячеек'
    OBJECTS g = BinGroup
    PROPERTIES READONLY name(g), canonicalName(g), nameStock(g)
    ORDERS canonicalName(g)
    FILTERS level(g) = 2, 
        accessCompany(currentUser(), g)
    FILTERGROUP active FILTER 'Активный' active(g) DEFAULT
;

countAccessCompanyZoneBinGroups 'Кол-во доступных зон ячеек (компания)' (Employee employee) =
    GROUP
        SUM 1 IF accessCompany(employee, BinGroup bg) IF level(bg) = 2;
countAccessCompanyZoneBinGroups 'Кол-во доступных зон ячеек (компания)' (Employee employee, Stock st) =
    GROUP
        SUM 1 IF accessCompany(employee, BinGroup bg) IF level(bg) = 2 AND stock(bg) = st;
defaultCompanyZoneBinGroup (Employee employee) =
    GROUP
        MIN  BinGroup bg IF accessCompany(employee, bg) AND level(bg) = 2;
defaultCompanyZoneBinGroup (Employee employee, Stock st) =
    GROUP
        MIN  BinGroup bg IF accessCompany(employee, bg) AND level(bg) = 2 AND stock(bg) = st;

companyBins  'Зоны ячеек (компания)' (Employee employee) = ISTRING[2000](GROUP
    CONCAT name(BinGroup bg) IF accessCompany(employee, bg) AND level(bg) = 2, ', '
    ORDER name(bg)) CHARWIDTH 20;
companyBins  'Зоны ячеек (компания)' (Employee employee, Stock st) = ISTRING[2000](GROUP
    CONCAT name(BinGroup bg) IF accessCompany(employee, bg) AND level(bg) = 2 AND stock(bg) = st, ', '
    ORDER name(bg)) CHARWIDTH 20;


filterZoneBinGroup  = DATA LOCAL NESTED BinGroup ();
nameFilterZoneBinGroup 'Зоны ячеек' = name(filterZoneBinGroup()) CHARWIDTH 15;
filterZoneBinGroup (Sku s) = [GROUP SUM 1 IF currentBalance(Sku s1, Bin b) BY s1, binGroup(b)](s, filterZoneBinGroup()) 
    OR (s IS Stock AND NOT filterZoneBinGroup());   

changeFilterZoneBinGroup(Stock st) {
    DIALOG dialogZoneBinGroups OBJECTS g INPUT NULL FILTERS stock(g) = st DO  {
        filterZoneBinGroup() <- g;
    }
}

EXTEND FORM currentBalanceSkuStock
    PROPERTIES SHOWIF countAccessCompanyZoneBinGroups(currentUser(), ss) AND singleStore() 
        READONLYIF countAccessCompanyZoneBinGroups(currentUser(), ss) = 1 nameFilterZoneBinGroup() ON CHANGE changeFilterZoneBinGroup(ss)
    FILTERS (GROUP SUM 1 IF currentBalance(s, Bin bb) AND isParent(filterZoneBinGroup(), bb)) 
        OR NOT filterZoneBinGroup() OR NOT countAccessCompanyZoneBinGroups(currentUser(), ss) OR multipleStores() 
    OBJECTS bg = BinGroup PANEL 
    PROPERTIES currentBalance(s, bg) DRAW sts SHOWIF filterZoneBinGroup() AFTER currentBalance(s, st)
    FILTERS bg = filterZoneBinGroup()
    
    EVENTS ON CHANGE ss {
        IF countAccessCompanyZoneBinGroups(currentUser(), ss) = 1 AND singleStore() THEN {
            filterZoneBinGroup() <- defaultCompanyZoneBinGroup(currentUser(), ss);
        } ELSE {
            filterZoneBinGroup() <- NULL;
        }
    }
;

DESIGN currentBalanceSkuStock {
    filter {
        MOVE PROPERTY(nameFilterZoneBinGroup()) AFTER PROPERTY(name(ss));
    }
}

EXTEND FORM balanceSkuStock
    PROPERTIES SHOWIF countAccessCompanyZoneBinGroups(currentUser(), ss) AND singleStore()
        READONLYIF countAccessCompanyZoneBinGroups(currentUser(), ss) = 1 nameFilterZoneBinGroup() ON CHANGE changeFilterZoneBinGroup(ss)
    FILTERS (GROUP SUM 1 IF currentBalance(s, Bin bb) AND isParent(filterZoneBinGroup(), bb))
        OR NOT filterZoneBinGroup() OR NOT countAccessCompanyZoneBinGroups(currentUser(), ss) OR multipleStores()
    OBJECTS bg = BinGroup PANEL
    PROPERTIES balanceB(s, bg, t) DRAW sts SHOWIF filterZoneBinGroup() AFTER balanceB(s, st,t)
    FILTERS bg = filterZoneBinGroup()

    EVENTS ON CHANGE ss {
        IF countAccessCompanyZoneBinGroups(currentUser(), ss) = 1 AND singleStore() THEN {
            filterZoneBinGroup() <- defaultCompanyZoneBinGroup(currentUser(), ss);  
        } ELSE {
            filterZoneBinGroup() <- NULL;
        }
    }
;

DESIGN balanceSkuStock {
    filter {
        MOVE PROPERTY(nameFilterZoneBinGroup()) AFTER PROPERTY(name(ss));
    }
}
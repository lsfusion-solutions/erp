MODULE Declaration;

REQUIRE Certificate, Shipment, CustomsGroup;

CLASS Declaration 'Таможенная декларация' : Certificate;
TABLE declaration(Declaration);

@defineExternalizable(declaration, STRING[100]);

// Номер
number 'Номер' = DATA ISTRING[100] (Declaration) IN numbered CHARWIDTH 14;
series 'Серия' = DATA BPSTRING[2] (Declaration) IN numbered CHARWIDTH 3 NOFLEX; 

seriesNumber 'Серия/Номер' (Declaration o) = 
    CONCAT '', series(o), number(o) 
    CHARWIDTH 7 MATERIALIZED;
    
series(Declaration declaration) += series(declaration);
number(Declaration declaration) += number(declaration);

// Валюта
@defineDocumentHeaderTimePrefix(Declaration, ,' документа');
@deriveDocumentHeaderTimePrefix(Declaration, );
date(Declaration declaration) += date(declaration);
time(Declaration declaration) += time(declaration);

customsHouse = DATA LegalEntity (Declaration);
nameCustomsHouse 'Таможня' (Declaration declaration) = name(customsHouse(declaration));

description 'Таможенная декларация' (Declaration declaration) = CONCAT ' ', seriesNumber(declaration), 'от ' + date(declaration) CHARWIDTH 20;

@defineBatchCertificate(declaration, Declaration, 'Таможенная декларация');

FORM declaration 'Таможенная декларация'
    OBJECTS d = Declaration PANEL
    PROPERTIES(d) number, series, date, time, nameCustomsHouse
    EDIT Declaration OBJECT d
;
DESIGN declaration {
    BOX {
        NEW header FIRST {
            alignment = STRETCH;
            type = CONTAINERH;
            caption = 'Шапка документа';
            MOVE PROPERTY(number(d));
            MOVE PROPERTY(series(d));
            MOVE PROPERTY(date(d));
            MOVE PROPERTY(time(d));
            MOVE PROPERTY(nameCustomsHouse(d));
        }
        MOVE TOOLBARBOX;
    }
}

skipShowNew = ABSTRACT BOOLEAN (); 

FORM declarations 'Таможенные декларации'
    OBJECTS d = Declaration LAST
    PROPERTIES(d) READONLY number, series, date, time, nameCustomsHouse
    PROPERTIES(d) NEWSESSION NEW SHOWIF (NOT skipShowNew())
    PROPERTIES(d) NEWSESSION EDIT, DELETE 

    LIST Declaration OBJECT d
;

NAVIGATOR {
    customsDocuments {
        NEW declarations;
    }
}
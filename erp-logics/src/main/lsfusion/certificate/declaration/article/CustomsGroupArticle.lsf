MODULE CustomsGroupArticle;

REQUIRE CustomsGroupItem, ItemArticle;

NAMESPACE CustomsGroup;

//-- ТН ВЭД
TABLE countryArticle(Country, Article);
customsGroup = DATA CustomsGroup(Country, Article);
codeCustomsGroup 'ТН ВЭД' (Country country, Article article) = code(customsGroup(country, article)) CHARWIDTH 10;
nameCustomsGroup 'Позиция ТН ВЭД' (Country country, Article article) = name(customsGroup(country, article)) CHARWIDTH 30;
codeCustomsGroupDefaultCountry 'ТН ВЭД' (Article article) = code(customsGroup(defaultCountry(), article)) CHARWIDTH 10;

originalCustomsGroup 'Код ТНВЭД (ориг.)' = DATA STRING[12] (Article) IN base CHARWIDTH 10 NOFLEX;

// Действие по изменению кода ТНВЭД через ручной ввод
changeCodeCustomsGroup(Country country, Article article)  { 
    IF requestInputChangeCustomsCode() THEN {
        INPUT s = STRING[10] DO {
            IF customsGroup(rpad(s,10,'0')) OR NOT s THEN {
                customsGroup(country, article) <- customsGroup(rpad(s,10,'0')); 
            } ELSE {
                caption() <- caption(article);
                article() <- id(article);
                originalCustomsGroup() <- originalCustomsGroup(article);
                composition() <- composition(article);
                gender() <- nameGender(article);
                DIALOG customsGroups OBJECTS cz = customsZone(country), cg = nearestCustomsGroup(s) INPUT DO
                    customsGroup(country, article) <- cg;
            }
        }
    } ELSE {
        DIALOG customsGroups OBJECTS cz = customsZone(country), cg = customsGroup(country, article) CHANGE;
    }
}

changeWYSCodeCustomsGroup(Country country, Article article)  { 
    INPUT s = STRING[10] DO
        IF customsGroup(rpad(s, 10, '0')) OR NOT s THEN {
            customsGroup(country, article) <- customsGroup(rpad(s, 10, '0')); 
        } ELSE {
            caption() <- caption(article);
            article() <- id(article);
            originalCustomsGroup() <- originalCustomsGroup(article);
            composition() <- composition(article);
            gender() <- nameGender(article);
            DIALOG customsGroups OBJECTS cz = customsZone(country), cg = nearestCustomsGroup(s) INPUT DO
                customsGroup(country, article) <- cg;
        }
}

changeCustomsGroupDefaultCountry(Article article)  { 
    DIALOG customsGroups OBJECTS cz = customsZoneDefaultCountry(), cg = customsGroup(defaultCountry(), article) INPUT NULL DO
        customsGroup(Country country, article) <- cg WHERE country == defaultCountry();
}

CONSTRAINT customsZone(customsGroup(Country country, Article article)) != customsZone(country) CHECKED BY customsGroup[Country,Article]
    MESSAGE 'Таможенная зона должна соответствовать стране';

EXTEND FORM article
    PROPERTIES(a)    originalCustomsGroup,
                     codeCustomsGroupDefaultCountry  ON CHANGE changeCustomsGroupDefaultCountry(a)
    PROPERTIES(c, a) codeCustomsGroup
    
    PROPERTIES(i) BEFORE deletei codeCustomsGroupDefaultCountry ON CHANGE changeCustomsGroupDefaultCountry(i) 
;
DESIGN article  {
    regionPrm {
        MOVE PROPERTY(originalCustomsGroup(a));
        MOVE PROPERTY(codeCustomsGroupDefaultCountry(a));
    }
}

EXTEND FORM articles
    PROPERTIES(a) originalCustomsGroup
;

WHEN LOCAL FORMS article CHANGED(customsGroup(Country country, Article article)) DO         
    customsGroup(country, Item i) <- customsGroup(country, article) WHERE article(i) == article;      

WHEN LOCAL FORMS article SET(Item i IS Item) DO
    customsGroup(Country country, i) <- customsGroup(country, article(i));      
    

EXTEND FORM articles
    PROPERTIES(a)  READONLYIF isReadonly() BEFORE deletea  codeCustomsGroupDefaultCountry
;

MODULE Veterinary;

REQUIRE Certificate;

CLASS Veterinary 'Ветеринарное свидетельство' : Certificate;
TABLE veterinary(Veterinary);

// ----------------- Даты / время ------------------ //

@defineDocumentHeaderTimePrefix(Veterinary, ,' документа');
@deriveDocumentHeaderTimePrefix(Veterinary, );
date(Veterinary certificate) += date(certificate);
time(Veterinary certificate) += time(certificate);

fromDate 'Дата с' = DATA DATE (Veterinary) IN documentHeader;
fromDate(Veterinary certificate) += fromDate(certificate);

toDate 'Дата по' = DATA DATE (Veterinary) IN documentHeader;
toDate(Veterinary certificate) += toDate(certificate);

// Файл
image 'Файл сертификата'  = DATA IMAGEFILE (Veterinary);
saveImage 'Загрузить сертификат' (Veterinary veterinary)  { INPUT =image(veterinary) CHANGE; }
openImage 'Просмотреть сертификат' (Veterinary veterinary)  { open(image(veterinary)); }

CLASS ExtraPageVeterinaryDetail 'Дополнительная страница сертификата';
TABLE extraPageVeterinaryDetail (ExtraPageVeterinaryDetail);

veterinary = DATA Veterinary (ExtraPageVeterinaryDetail);

image 'Файл сертификата'  = DATA IMAGEFILE (ExtraPageVeterinaryDetail);
saveImage 'Загрузить сертификат' (ExtraPageVeterinaryDetail extraPageVeterinaryDetail)  { INPUT =image(extraPageVeterinaryDetail) CHANGE; }
openImage 'Просмотреть сертификат' (ExtraPageVeterinaryDetail extraPageVeterinaryDetail)  { open(image(extraPageVeterinaryDetail)); }

@defineDocumentDetailIndex(veterinary, ExtraPageVeterinaryDetail);

extraPageVeterinaryDetail (index, veterinary) = GROUP AGGR ExtraPageVeterinaryDetail extraPageVeterinaryDetail WHERE extraPageVeterinaryDetail IS ExtraPageVeterinaryDetail BY index (extraPageVeterinaryDetail), veterinary(extraPageVeterinaryDetail);

@defineDocumentDetailNote(ExtraPageVeterinaryDetail);

addExtraPageDetail 'Добавить'(Veterinary veterinary)  { 
    NEW d = ExtraPageVeterinaryDetail {
        veterinary(d) <- veterinary;
        saveImage(d);
        apply();
    }    
} IMAGE 'add.png';

// ------ Номер ----------- //

number 'Номер' = DATA ISTRING[100] (Veterinary) NONULL IN numbered CHARWIDTH 10;
series 'Серия' = DATA BPSTRING[2] (Veterinary) IN numbered CHARWIDTH 3 NOFLEX; 

seriesNumber 'Серия/Номер' (Veterinary o) = 
    CONCAT '', series(o), number(o) 
    CHARWIDTH 7 MATERIALIZED;

series(Veterinary certificate) += series(certificate);
number(Veterinary certificate) += number(certificate);

veterinary = GROUP NAGGR Veterinary s BY number (s);

// Описание
description 'Ветеринарное свидетельство' (Veterinary veterinary) = CONCAT ' ', seriesNumber(veterinary),
                                                 'от ' + fromDate(veterinary), 'по ' + toDate(veterinary) CHARWIDTH 20;
// История                                                 
@defineDocumentHeaderCreated(Veterinary);                                                 


@defineBatchCertificate(veterinary, Veterinary, 'Ветеринарное свидетельство');
fromDateVeterinary 'Дата с' (Batch batch) = fromDate(veterinary(batch));
toDateVeterinary 'Дата по' (Batch batch) = toDate(veterinary(batch));

FORM veterinary 'Ветеринарное свидетельство'
    OBJECTS c = Veterinary PANEL
    PROPERTIES(c) number, series, date, time,
                  fromDate, toDate, saveImage, openImage       
                  
    OBJECTS d = ExtraPageVeterinaryDetail
    PROPERTIES(d) index, note
    PROPERTIES(c) addExtraPageDetail DRAW d TOOLBAR
    PROPERTIES(d) NEWSESSION TOOLBAR openImage, saveImage, DELETE 
    FILTERS veterinary(d) == c                              

    EDIT Veterinary OBJECT c
;

DESIGN veterinary {
    BOX {
        NEW pane {
            fill = 1;
            NEW header {
                caption = 'Шапка документа';
                type = CONTAINERH;
                MOVE PROPERTY (number(c)) { charWidth = 70;};
                MOVE PROPERTY (series(c));
                MOVE PROPERTY (date(c));
                MOVE PROPERTY (time(c));

            }
            NEW params {
                NEW timeContainer{
                    type = CONTAINERH;
                    caption = 'Период действия';
                    MOVE PROPERTY (fromDate(c));
                    MOVE PROPERTY (toDate(c));
                }
                NEW documentParams {
                    type = COLUMNS;
                    columns = 3;
                    caption = 'Параметры документа';
                }
            }
            NEW fileContainer{
                type = CONTAINERH;
                caption = 'Файл свидетельства';
                MOVE PROPERTY(openImage(c));
                MOVE PROPERTY(saveImage(c));
            }    
            NEW tabContainer{
                fill = 1;
                type = TABBED;
                NEW firstTab{
                    caption = 'Дополнительная информация';
                    fill = 1;
                }
                MOVE BOX(d){
                    caption = 'Дополнительные страницы свидетельства';
                    PROPERTY(index(d)){
                        caption = 'Номер файла';
                    }
                }
            }                    
        }    
        MOVE TOOLBARBOX;
    }
}

overCopy  ABSTRACT LIST ( Veterinary, Veterinary);
    
copy 'Копировать'(Veterinary veterinary)  { 
	NEWSESSION {
	    NEW c = Veterinary {
	        fromDate(c) <- fromDate(veterinary);
	        toDate(c) <- toDate(veterinary);
	
	        overCopy(c, veterinary);
	        
	        SHOW veterinary OBJECTS c = c DOCKED;
	    }
	}
} TOOLBAR;

skipShowEdit = ABSTRACT BOOLEAN (Veterinary);
showEdit = Veterinary c IS Veterinary AND NOT skipShowEdit(c);

FORM veterinaries 'Ветеринарные свидетельства'
    OBJECTS c = Veterinary LAST
    PROPERTIES(c) READONLY number, series, date, time, fromDate, toDate
    PROPERTIES(c) copy

    PROPERTIES (c) READONLY PANEL createdNameUser, createdTime, createdHostnameComputer
    PROPERTIES(c) NEWSESSION NEW, EDIT SHOWIF showEdit(c), DELETE SHOWIF showEdit(c) 
    
    LIST Veterinary OBJECT c
;

DESIGN veterinaries {
    BOX {
        size = (1024, 768);
        NEW header {
            fill = 1;
            type = SPLITV;
            MOVE BOX(c) { fill = 2; }
            NEW tabContainer {
                fill = 1;
                type=TABBED;
                NEW documentHistory {
                    caption = 'История';
                    fill = 1; 
                    type = CONTAINERV;
        
                    MOVE GROUP(created,c);
                }
            }
        }
        MOVE TOOLBARBOX;
    }
}

NAVIGATOR {
    customsDocuments {
        NEW veterinaries;
    }
}
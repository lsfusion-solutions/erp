MODULE Registration;

REQUIRE Certificate;

CLASS Registration 'Свидетельство государственной регистрации' : Certificate;
TABLE registration(Registration);

// ----------------- Даты / время ------------------ //

@defineDocumentHeaderTimePrefix(Registration, ,' документа');
@deriveDocumentHeaderTimePrefix(Registration, );
date(Registration certificate) += date(certificate);
time(Registration certificate) += time(certificate);

fromDate 'Дата с' = DATA DATE (Registration) IN documentHeader;
fromDate(Registration certificate) += fromDate(certificate);

toDate 'Дата по' = DATA DATE (Registration) IN documentHeader;
toDate(Registration certificate) += toDate(certificate);

// ------ Номер ----------- //

number 'Номер' = DATA ISTRING[100] (Registration) NONULL IN numbered CHARWIDTH 30;
series 'Серия' = DATA BPSTRING[2] (Registration) IN numbered CHARWIDTH 3 NOFLEX; 

seriesNumber 'Серия/Номер' (Registration o) = 
    CONCAT '', series(o), number(o) 
    CHARWIDTH 7 MATERIALIZED;

series(Registration certificate) += series(certificate);
number(Registration certificate) += number(certificate);

registration = GROUP AGGR Registration s BY number (s);

// Описание
description 'Свидетельство государственной регистрации' (Registration registration) = CONCAT ' ', seriesNumber(registration),
                                                 'от ' + fromDate(registration), 'по ' + toDate(registration) CHARWIDTH 20;
// История                                                 
@defineDocumentHeaderCreated(Registration);                                                 


@defineBatchCertificate(registration, Registration, 'Свидетельство государственной регистрации');
fromDateRegistration 'Дата с' (Batch batch) = fromDate(registration(batch));
toDateRegistration 'Дата по' (Batch batch) = toDate(registration(batch));

legalEntity = DATA LegalEntity(Registration);
nameLegalEntity 'Держатель' (Registration r) = name(legalEntity(r));

FORM registration 'Свидетельство государственной регистрации'
    OBJECTS c = Registration PANEL
    PROPERTIES(c) number, series, date, time, nameLegalEntity,
                  fromDate, toDate               

    EDIT Registration OBJECT c
;

DESIGN registration {
    BOX {
        NEW pane {
            fill = 1;
            NEW header {
                caption = 'Шапка документа';
                horizontal = TRUE;
                MOVE PROPERTY (number(c)) { charWidth = 70;};
                MOVE PROPERTY (series(c));
                MOVE PROPERTY (date(c));
                MOVE PROPERTY (time(c));

            }
            NEW params {
                NEW timeContainer{
                    horizontal = TRUE;
                    caption = 'Период действия';
                    MOVE PROPERTY (fromDate(c));
                    MOVE PROPERTY (toDate(c));
                }
                NEW documentParams {
                    lines = 3;
                    caption = 'Параметры документа';
                    MOVE PROPERTY(nameLegalEntity(c));
                }
            }
        }    
        MOVE TOOLBARBOX;
    }
}
    
overCopy  ABSTRACT LIST ( Registration, Registration);
    
copy 'Копировать'(Registration registration)  { 
	NEWSESSION {
	    NEW c = Registration {
	        fromDate(c) <- fromDate(registration);
	        toDate(c) <- toDate(registration);

            overCopy(c, registration);
            	
	        SHOW registration OBJECTS c = c DOCKED;
	    }
	}
} TOOLBAR;

skipShowEdit = ABSTRACT BOOLEAN (Registration);
showEdit = Registration c IS Registration AND NOT skipShowEdit(c);

FORM registrations 'Свидетельства государственной регистрации'
    OBJECTS c = Registration
    PROPERTIES(c) READONLY number, series, date, time, fromDate, toDate, nameLegalEntity
    PROPERTIES(c) copy

    PROPERTIES (c) READONLY PANEL createdNameUser, createdTime, createdHostnameComputer
    PROPERTIES(c) NEWSESSION NEW, EDIT SHOWIF showEdit(c), DELETE SHOWIF showEdit(c) 
    
    LIST Registration OBJECT c
;

DESIGN registrations {
    BOX {
        size = (1024, 768);
        NEW header {
            fill = 1;
            MOVE BOX(c) { fill = 2; }
            NEW documentDetail {
                fill = 1; 
                NEW documentHistory {
                    caption = 'История';
                    fill = 1;
                    MOVE GROUP(created,c);
                }
            }
        }
        MOVE TOOLBARBOX;
    }
}

NAVIGATOR {
    customsDocuments {
        NEW registrations;
    }
}
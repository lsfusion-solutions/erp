MODULE ComplianceType;

REQUIRE Compliance;

NAMESPACE Compliance;

// ------ Тип сертификатов ----------- //

CLASS ComplianceType 'Тип сертификатов';
TABLE complianceType(ComplianceType);

name 'Тип сертификатов' = DATA STRING[100](ComplianceType) IN base;

EXTEND CLASS ComplianceType {
    forBatch 'На партию',
    forSku 'На товар',
    forManufacturing 'На производство',
    temporary 'Временный'
}

FORM complianceType 'Тип сертификатов'
    OBJECTS ct = ComplianceType PANEL
    PROPERTIES(ct) staticCaption
    EDIT ComplianceType OBJECT ct
;

FORM complianceTypes 'Типы сертификатов'
    OBJECTS ct = ComplianceType
    PROPERTIES(ct) READONLY staticCaption
    PROPERTIES(ct) NEWSESSION NEW, EDIT, DELETE 

    LIST ComplianceType OBJECT ct
;

complianceType = DATA ComplianceType(Compliance);
nameComplianceType 'Тип' (Compliance compliance) = staticCaption(complianceType(compliance));

EXTEND FORM compliance
    PROPERTIES(c) nameComplianceType
;
DESIGN compliance {
    documentParams {
        MOVE PROPERTY(nameComplianceType(c));
    }
}

EXTEND FORM compliances
    PROPERTIES(c) READONLY nameComplianceType AFTER nameLegalEntity(c)
;
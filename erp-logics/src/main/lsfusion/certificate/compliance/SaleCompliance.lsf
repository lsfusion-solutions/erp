MODULE SaleCompliance;

REQUIRE Printer, Compliance, SaleCertificate;

NAMESPACE Sale;

CLASS ABSTRACT PrintPageCompliance 'Страница сертификата соответствия';
compliance = ABSTRACT Compliance (PrintPageCompliance);
image = ABSTRACT IMAGEFILE (PrintPageCompliance);
file = ABSTRACT FILE (PrintPageCompliance);

EXTEND CLASS Compliance : PrintPageCompliance;
compliance(Compliance compliance) += compliance IF compliance IS Compliance;
image(Compliance compliance) += Compliance.image(compliance);
file(Compliance compliance) += Compliance.file(compliance);

EXTEND CLASS ExtraPageComplianceDetail : PrintPageCompliance;
compliance(ExtraPageComplianceDetail detail) += Compliance.compliance(detail);
image(ExtraPageComplianceDetail detail) += Compliance.image(detail);
file(ExtraPageComplianceDetail detail) += Compliance.file(detail);

@defineDocumentCertificate(compliance, invoice, i, 'Сертификат соответствия');
@defineDocumentCertificateConstraint(compliance, invoice, 'Сертификат соответствия');
@deriveDocumentSaleCertificate(compliance, invoice, supplierStock, data);

count (compliance, invoice) =
    GROUP SUM 1
    BY compliance(InvoiceDetail detail), invoice(detail);
    
countComplianceFiles 'Кол-во файлов' = 
    GROUP SUM 1 IF file(PrintPageCompliance pc) AND count(compliance(pc), Invoice i) BY i;     

seriesCompliance(UserInvoiceDetail d) = series(compliance(d));
numberCompliance(UserInvoiceDetail d) = number(compliance(d));

file 'Файл сертификата' (InvoiceDetail invoiceDetail) = Compliance.file(compliance(invoiceDetail));
openFile 'Просмотреть сертификат' (InvoiceDetail invoiceDetail)  { open(file(invoiceDetail)); }

// todo : при попытке распечатать выдет сообщение "Форма не подходит для данных параметров"
FORM imageComplianceInvoice 'Файл сертификата'
    OBJECTS i = Invoice PANEL SUBREPORT

    OBJECTS c = Compliance
    FILTERS count(c, i)
    
    OBJECTS p = PrintPageCompliance
    PROPERTIES(p) image
    FILTERS compliance(p) == c    
;
//printAllImage 'Печать сертификатов'(Invoice invoice)  { 
//    PRINT imageComplianceInvoice OBJECTS i = invoice ;
//}
//printAllImageAuto 'Печать сертификатов'(Invoice invoice)  { 
//    PRINT imageComplianceInvoice OBJECTS i = invoice NOPREVIEW;
//}

FORM fileCompliance 'Сертификат'
    OBJECTS o = PrintPageCompliance PANEL SUBREPORT 
    PROPERTIES file = IMAGEFILE(file(o))
;

printAllFile 'Печать сертификатов'(Invoice invoice)  { 
    IF countComplianceFiles(invoice) THEN {
        IF (GROUP SUM 1 IF file(PrintPageCompliance p) AND count(compliance(p), invoice)) THEN {
            FOR lower(extension(file(PrintPageCompliance p))) == 'pdf' AND count(compliance(p), invoice) AND file(p) DO 
                open(PDFFILE(file(p)));
                
            FOR count(compliance(PrintPageCompliance p), invoice) AND NOT lower(extension(file(p))) == 'pdf' AND file(p) DO
                PRINT fileCompliance OBJECTS o = p;
        } ELSE PRINT imageComplianceInvoice OBJECTS i = invoice;
    } ELSE {
        MESSAGE 'К сертификатам не прикреплён ни один файл' NOWAIT;
    }    
}

printAllFileAuto 'Печать сертификатов'(Invoice invoice)  { 
    IF countComplianceFiles(invoice) THEN {
        IF (GROUP SUM 1 IF file(PrintPageCompliance p) AND count(compliance(p), invoice)) THEN {
            FOR lower(extension(file(PrintPageCompliance p))) == 'pdf' AND count(compliance(p), invoice) AND file(p) DO 
                printFile(PDFFILE(file(p)));
                
            FOR count(compliance(PrintPageCompliance p), invoice) AND NOT lower(extension(file(p))) == 'pdf' AND file(p) DO
                PRINT fileCompliance OBJECTS o = p NOPREVIEW;
        } ELSE PRINT imageComplianceInvoice OBJECTS i = invoice NOPREVIEW;
    } ELSE {
        MESSAGE 'К сертификатам не прикреплён ни один файл' NOWAIT;
    }          
}

EXTEND FORM userInvoice
    PROPERTIES(i) DRAW dc TOOLBAR printAllFileAuto
    PROPERTIES(dc) TOOLBAR openFile
;

EXTEND FORM invoices
    PROPERTIES(i) DRAW dc TOOLBAR printAllFileAuto
    PROPERTIES(dc) TOOLBAR openFile
;

@defineDocumentCertificate(compliance, shipment, s, 'сертификат соответствия');
compliance(InvoiceShipmentDetail detail) += compliance(invoiceDetail(detail));

overFillInvoice(UserShipmentDetail s, InvoiceDetail i) + { 
    dataCompliance(s) <- compliance(i);
}
@defineDocumentCertificateConstraint(compliance, shipment, 'сертификат соответствия');
@deriveDocumentSaleCertificate(compliance, shipment, supplierStock, data);
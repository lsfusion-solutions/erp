MODULE Compliance;

REQUIRE Certificate;

CLASS Compliance 'Сертификат соответствия' : Certificate;
TABLE compliance(Compliance);

// ----------------- Даты / время ------------------ //

@defineDocumentHeaderTimePrefix(Compliance, ,' документа');
@deriveDocumentHeaderTimePrefix(Compliance, );
date(Compliance certificate) += date(certificate);
time(Compliance certificate) += time(certificate);

fromDate 'Дата с' = DATA DATE (Compliance) IN documentHeader;
fromDate(Compliance certificate) += fromDate(certificate);

toDate 'Дата по' = DATA DATE (Compliance) IN documentHeader;
toDate(Compliance certificate) += toDate(certificate);

// ------ Номер ----------- //

number 'Номер' = DATA VARISTRING[100] (Compliance) NONULL IN numbered CHARWIDTH 10;
series 'Серия' = DATA BPSTRING[2] (Compliance) IN numbered CHARWIDTH 3 NOFLEX; 

seriesNumber 'Серия/Номер' (Compliance o) = 
    CONCAT '', series(o), number(o) 
    CHARWIDTH 7 MATERIALIZED;

series(Compliance compliance) += series(compliance);
number(Compliance compliance) += number(compliance);

compliance (number) = GROUP NAGGR Compliance compliance BY number (compliance);

// Описание
description 'Сертификат соответствия' (Compliance compliance) = CONCAT ' ', seriesNumber(compliance),
                                                 'от ' + fromDate(compliance), 'по ' + toDate(compliance) CHARWIDTH 20;
// История                                                 
@defineDocumentHeaderCreated(Compliance);                                                 

// Текст
text 'Текст сертификата' = DATA TEXT (Compliance);

@defineBatchCertificate(compliance, Compliance, 'Сертификат соответствия');
fromDateCompliance 'Дата с' (Batch batch) = fromDate(compliance(batch));
toDateCompliance 'Дата по' (Batch batch) = toDate(compliance(batch));

legalEntity = DATA LegalEntity(Compliance);
nameLegalEntity 'Держатель' (Compliance compliance) = name(legalEntity(compliance));

// Файл сертификата
image 'Файл сертификата'  = DATA IMAGEFILE (Compliance);
saveImage 'Загрузить сертификат' (Compliance compliance)  { INPUT =image(compliance) CHANGE; }
openImage 'Просмотреть сертификат' (Compliance compliance)  { open(image(compliance)); }

CLASS ExtraPageComplianceDetail 'Дополнительная страница сертификата';
TABLE extraPageComplianceDetail (ExtraPageComplianceDetail);

compliance = DATA Compliance (ExtraPageComplianceDetail);

image 'Файл сертификата'  = DATA IMAGEFILE (ExtraPageComplianceDetail);
saveImage 'Загрузить сертификат' (ExtraPageComplianceDetail extraPageComplianceDetail)  { INPUT =image(extraPageComplianceDetail) CHANGE; }
openImage 'Просмотреть сертификат' (ExtraPageComplianceDetail extraPageComplianceDetail)  { open(image(extraPageComplianceDetail)); }

@defineDocumentDetailIndex(compliance, ExtraPageComplianceDetail);

extraPageComplianceDetail (index, compliance) = GROUP AGGR ExtraPageComplianceDetail extraPageComplianceDetail WHERE extraPageComplianceDetail IS ExtraPageComplianceDetail BY index (extraPageComplianceDetail), compliance(extraPageComplianceDetail);

@defineDocumentDetailNote(ExtraPageComplianceDetail);

addExtraPageDetail 'Добавить'(Compliance compliance)  { 
    NEW d = ExtraPageComplianceDetail {
        compliance(d) <- compliance;
        EXEC saveImage(d);
        EXEC apply();
    }    
} IMAGE 'add.png';

FORM compliance 'Сертификат соответствия'
    OBJECTS c = Compliance PANEL
    PROPERTIES(c) number, series, date, time,
                  fromDate, toDate, nameLegalEntity, saveImage, openImage
                      
    OBJECTS d = ExtraPageComplianceDetail
    PROPERTIES(d) index, note
    PROPERTIES(c) addExtraPageDetail DRAW d TOOLBAR
    PROPERTIES(d) NEWSESSION TOOLBAR openImage, saveImage, DELETE 
    FILTERS compliance(d) == c                   

    EDIT Compliance OBJECT c
;

DESIGN compliance {
    BOX {
        NEW pane {
            fill = 1;
            NEW header {
                caption = 'Шапка документа';
                type = CONTAINERH;
                MOVE PROPERTY (number(c)) { charWidth = 45;};
                MOVE PROPERTY (series(c));
                MOVE PROPERTY (date(c));
                MOVE PROPERTY (time(c));
            }
            NEW params {
                NEW timeContainer{
                    type = CONTAINERH;
                    caption = 'Период действия';
                    MOVE PROPERTY (fromDate(c));
                    MOVE PROPERTY (toDate(c));
                }
                NEW documentParams {
                    type = COLUMNS;
                    columns = 3;
                    caption = 'Параметры документа';
                    MOVE PROPERTY(nameLegalEntity(c));
                }
            }
            NEW fileContainer{
                type = CONTAINERH;
                caption = 'Файл сертификата';
                MOVE PROPERTY(openImage(c));
                MOVE PROPERTY(saveImage(c));
            }
            NEW tabContainer{
                fill = 1;
                type = TABBED;
                NEW firstTab{
                    caption = 'Дополнительная информация';
                    fill = 1;
                }
                MOVE BOX(d){
                    caption = 'Дополнительные страницы сертификата';
                    PROPERTY(index(d)){
                        caption = 'Номер файла';
                    }
                }
            }
        }    
        MOVE TOOLBARBOX;
    }
}

over  ABSTRACT LIST ( Compliance, Compliance);
overCopy  ABSTRACT LIST ( ExtraPageComplianceDetail, ExtraPageComplianceDetail);
    
copy 'Копировать'(Compliance compliance)  { 
	NEWSESSION {
	    NEW c = Compliance {
	        fromDate(c) <- fromDate(compliance);
	        toDate(c) <- toDate(compliance);
	        legalEntity(c) <- legalEntity(compliance);
	        text(c) <- text(compliance);
	        image(c) <- image(compliance);
	        
	        over(compliance, c);
	
	        FOR compliance(ExtraPageComplianceDetail detail) == compliance DO {
	            NEW d=ExtraPageComplianceDetail {
	                compliance(d) <- c;
	                image(d) <- image(detail);
	
	                
	                overCopy(d, detail);
	            }
	        }
	
	        SHOW compliance OBJECTS c = c DOCKED;
	    }
	}
} TOOLBAR;

skipShowEdit = ABSTRACT BOOLEAN (Compliance);
showEdit = Compliance c IS Compliance AND NOT skipShowEdit(c);

FORM compliances 'Сертификаты соответствия'
    OBJECTS c = Compliance
    PROPERTIES(c) READONLY number, series, date, time, fromDate, toDate,
                           nameLegalEntity
    PROPERTIES(c) copy

    PROPERTIES (c) READONLY PANEL createdNameUser, createdTime, createdHostnameComputer
    PROPERTIES(c) NEWSESSION NEW, EDIT SHOWIF showEdit(c), DELETE SHOWIF showEdit(c) 
    
    LIST Compliance OBJECT c
;

DESIGN compliances {
    size = (1024, 768);
    NEW header {
        fill = 1;
        type = SPLITV;
        MOVE BOX(c) { fill = 2; }
        NEW documentDetail {
            fill = 1;
            type = TABBED;
            NEW documentHistory {
                caption = 'История';
                fill = 1; 
                type = CONTAINERV;
    
                MOVE GROUP(created,c);
            }
        }
    }
    MOVE TOOLBARBOX;
}

NAVIGATOR {
    customsDocuments {
        NEW compliances;
    }
}
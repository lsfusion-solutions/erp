MODULE Compliance;

REQUIRE Certificate;

CLASS Compliance 'Сертификат соответствия' : Certificate;
TABLE compliance(Compliance);

// ----------------- Даты / время ------------------ //

@defineDocumentHeaderTimePrefix(Compliance, ,' документа');
@deriveDocumentHeaderTimePrefix(Compliance, );
date(Compliance certificate) += date(certificate);
time(Compliance certificate) += time(certificate);

fromDate 'Дата с' = DATA DATE (Compliance) IN documentHeader;
fromDate(Compliance certificate) += fromDate(certificate);

toDate 'Дата по' = DATA DATE (Compliance) IN documentHeader;
toDate(Compliance certificate) += toDate(certificate);

// ------ Номер ----------- //

number 'Номер' = DATA ISTRING[100] (Compliance) NONULL IN numbered CHARWIDTH 30;
series 'Серия' = DATA BPSTRING[2] (Compliance) IN numbered CHARWIDTH 3 NOFLEX; 

seriesNumber 'Серия/Номер' (Compliance o) = 
    CONCAT '', series(o), number(o) 
    CHARWIDTH 7 MATERIALIZED;

overNumber = ABSTRACT ISTRING[100] (Compliance);
overDescription = ABSTRACT ISTRING[200] (Compliance);

seriesNumberCompliance(Compliance o) = OVERRIDE overNumber(o), seriesNumber(o);

series(Compliance compliance) += series(compliance);
number(Compliance compliance) += number(compliance);

compliance (number) = GROUP NAGGR Compliance compliance BY number (compliance);

numberDeclaration 'Номер декларации' = DATA ISTRING[100] (Compliance) CHARWIDTH 15;
dateDeclaration 'Дата декларации' = DATA DATE (Compliance);

overSeriesNumber (Compliance o) = OVERRIDE numberDeclaration(o), seriesNumber(o);
overDate (Compliance o) = OVERRIDE dateDeclaration(o), date(o);

// Описание
description 'Сертификат соответствия' (Compliance compliance) = 
    OVERRIDE overDescription(compliance), 
             CONCAT ' ', seriesNumber(compliance), 'от ' + fromDate(compliance), 'по ' + toDate(compliance) CHARWIDTH 20;
// История                                                 
@defineDocumentHeaderCreated(Compliance);                                                 

// Текст
text 'Текст сертификата' = DATA TEXT (Compliance);

@defineBatchCertificate(compliance, Compliance, 'Сертификат соответствия');
fromDateCompliance 'Дата с' (Batch batch) = fromDate(compliance(batch));
toDateCompliance 'Дата по' (Batch batch) = toDate(compliance(batch));

legalEntity = DATA LegalEntity(Compliance);
nameLegalEntity 'Держатель' (Compliance compliance) = name(legalEntity(compliance));

// Файл сертификата
TABLE fileCompliance (Compliance);

image 'Файл сертификата'  = DATA IMAGEFILE (Compliance);
file 'Файл сертификата'  = DATA FILE (Compliance) TABLE fileCompliance;
saveFile 'Загрузить сертификат' (Compliance compliance)  {
    INPUT = file(compliance) CHANGE; 
    IF NOT (lower(extension(file(compliance))) = 'pdf' OR 
        lower(extension(file(compliance))) = 'jpg' OR 
        lower(extension(file(compliance))) = 'jpeg') THEN {
            MESSAGE 'Некорректный формат файла';
            file(compliance) <- NULL;
        }
}
openFile 'Просмотреть сертификат' (Compliance compliance)  { open(file(compliance)); }
hasFile 'Файл загружен' (Compliance d) = TRUE IF file(d);

CLASS ExtraPageComplianceDetail 'Дополнительная страница сертификата';
TABLE extraPageComplianceDetail (ExtraPageComplianceDetail);

compliance = DATA Compliance (ExtraPageComplianceDetail);

TABLE fileComplianceDetail (ExtraPageComplianceDetail);
image 'Файл сертификата'  = DATA IMAGEFILE (ExtraPageComplianceDetail);
file 'Файл сертификата'  = DATA FILE (ExtraPageComplianceDetail) TABLE fileComplianceDetail;
saveFile 'Загрузить сертификат' (ExtraPageComplianceDetail extraPageComplianceDetail)  {
    INPUT = file(extraPageComplianceDetail) CHANGE; 
    IF NOT (lower(extension(file(extraPageComplianceDetail))) = 'pdf' OR 
        lower(extension(file(extraPageComplianceDetail))) = 'jpg' OR 
        lower(extension(file(extraPageComplianceDetail))) = 'jpeg') THEN {
            MESSAGE 'Некорректный формат файла';
            file(extraPageComplianceDetail) <- NULL;
        }
}
openFile 'Просмотреть сертификат' (ExtraPageComplianceDetail extraPageComplianceDetail)  { open(file(extraPageComplianceDetail)); }

fileComplianceMigrate = DATA BOOLEAN ();
onStarted() + {
    file(Compliance c) <- FILE(image(c)) WHERE image(c) AND NOT file(c) AND NOT fileComplianceMigrate(); 
    file(ExtraPageComplianceDetail d) <- FILE(image(d)) WHERE image(d) AND NOT file(d) AND NOT fileComplianceMigrate(); 
    fileComplianceMigrate() <- TRUE;
}

@defineDocumentDetailIndex(compliance, ExtraPageComplianceDetail);

extraPageComplianceDetail (index, compliance) = GROUP AGGR ExtraPageComplianceDetail extraPageComplianceDetail WHERE extraPageComplianceDetail IS ExtraPageComplianceDetail BY index (extraPageComplianceDetail), compliance(extraPageComplianceDetail);

@defineDocumentDetailNote(ExtraPageComplianceDetail);

addExtraPageDetail 'Добавить'(Compliance compliance)  { 
    NEW d = ExtraPageComplianceDetail {
        compliance(d) <- compliance;
        EXEC saveFile(d);
        EXEC apply();
    }    
} IMAGE 'add.png';

FORM compliance 'Сертификат соответствия'
    OBJECTS c = Compliance PANEL
    PROPERTIES(c) number, series, date, time,
                  fromDate, toDate, nameLegalEntity, saveFile, openFile
                      
    OBJECTS d = ExtraPageComplianceDetail
    PROPERTIES(d) index, note
    PROPERTIES(c) addExtraPageDetail DRAW d TOOLBAR
    PROPERTIES(d) TOOLBAR openFile, saveFile, DELETE 
    FILTERS compliance(d) == c                   

    EDIT Compliance OBJECT c
;

DESIGN compliance {
    BOX {
        NEW pane {
            fill = 1;
            NEW header {
                caption = 'Шапка документа';
                horizontal = TRUE;
                MOVE PROPERTY (number(c)) { charWidth = 45;};
                MOVE PROPERTY (series(c));
                MOVE PROPERTY (date(c));
                MOVE PROPERTY (time(c));
            }
            NEW params {
                NEW timeContainer{
                    horizontal = TRUE;
                    caption = 'Период действия';
                    MOVE PROPERTY (fromDate(c));
                    MOVE PROPERTY (toDate(c));
                }
                NEW documentParams {
                    lines = 3;
                    caption = 'Параметры документа';
                    MOVE PROPERTY(nameLegalEntity(c));
                }
            }
            NEW fileContainer{
                horizontal = TRUE;
                caption = 'Файл сертификата';
                MOVE PROPERTY(openFile(c));
                MOVE PROPERTY(saveFile(c));
            }
            NEW tabContainer{
                fill = 1;
                tabbed = TRUE;
                NEW firstTab{
                    caption = 'Дополнительная информация';
                    fill = 1;
                }
                MOVE BOX(d){
                    caption = 'Дополнительные страницы сертификата';
                    PROPERTY(index(d)){
                        caption = 'Номер файла';
                    }
                }
            }
        }    
        MOVE TOOLBARBOX;
    }
}

over  ABSTRACT LIST ( Compliance, Compliance);
overCopy  ABSTRACT LIST ( ExtraPageComplianceDetail, ExtraPageComplianceDetail);
    
copy 'Копировать'(Compliance compliance)  { 
	NEWSESSION {
	    NEW c = Compliance {
	        fromDate(c) <- fromDate(compliance);
	        toDate(c) <- toDate(compliance);
	        legalEntity(c) <- legalEntity(compliance);
	        text(c) <- text(compliance);
	        file(c) <- file(compliance);
	        
	        over(compliance, c);
	
	        FOR compliance(ExtraPageComplianceDetail detail) == compliance DO {
	            NEW d=ExtraPageComplianceDetail {
	                compliance(d) <- c;
	                file(d) <- file(detail);
	
	                
	                overCopy(d, detail);
	            }
	        }
	
	        SHOW compliance OBJECTS c = c DOCKED;
	    }
	}
} TOOLBAR;

skipShowEdit = ABSTRACT BOOLEAN (Compliance);
showEdit = Compliance c IS Compliance AND NOT skipShowEdit(c);

FORM compliances 'Сертификаты соответствия'
    OBJECTS c = Compliance
    PROPERTIES(c) READONLY number, series, date, time, fromDate, toDate,
                           nameLegalEntity, hasFile
    PROPERTIES(c) copy

    PROPERTIES (c) READONLY PANEL createdNameUser, createdTime, createdHostnameComputer
    PROPERTIES(c) NEWSESSION NEW, EDIT SHOWIF showEdit(c), DELETE SHOWIF showEdit(c) 
    
    LIST Compliance OBJECT c
;

DESIGN compliances {
    size = (1024, 768);
    NEW header {
        fill = 1;
        MOVE BOX(c) { fill = 2; }
        NEW documentDetail {
            fill = 1;
            tabbed = TRUE;
            NEW documentHistory {
                caption = 'История';
                fill = 1;
                MOVE GROUP(created,c);
            }
        }
    }
    MOVE TOOLBARBOX;
}

NAVIGATOR {
    customsDocuments {
        NEW compliances;
    }
}
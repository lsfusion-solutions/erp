MODULE ComplianceEmail;

REQUIRE Compliance;


countDaysToEndCompliance 'За сколько дней уведомлять об истечении сертификата' = DATA INTEGER ();
emailToNotification 'E-mail через точку с запятой' = DATA STRING[300] () CHARWIDTH 30;

EXTEND FORM options PROPERTIES countDaysToEndCompliance(), emailToNotification();
DESIGN options { 
    pane { 
        certificate {
            MOVE PROPERTY(countDaysToEndCompliance()); 
            MOVE PROPERTY(emailToNotification());        
        } 
    } 
}



countToNotification 'Сертификаты, которым нужно сообщение' (compliance, DATE date) = GROUP SUM 1 
    IF currentBalance(Batch b) AND subtract(toDateCompliance(b), countDaysToEndCompliance()) <= date
        BY compliance(b);
    
countToNotification 'Кол-во сертификатов' (DATE date) = GROUP SUM 1 IF countToNotification(Compliance compliance, date);   
    
compliancesToNotification 'Сертификаты' (DATE date) = GROUP
           CONCAT description(Compliance c) IF countToNotification(c, date), ' , '
           ORDER description(c)
;  
FORM emailCompliance 'Сертификаты'
 
    OBJECTS dt = DATE PANEL
    PROPERTIES  val = VALUE(dt)
    PROPERTIES int = countDaysToEndCompliance() DRAW dt

    OBJECTS c = Compliance
    PROPERTIES(c) READONLY number, series, date, time,
                  fromDate, toDate, nameLegalEntity
    ORDER toDate(c)                  

    FILTERS countToNotification(c,dt) 
;

emailCompliance 'Выслать уведомление об окончании срока действия сертификатов' (DATE date)  { 
    LOCAL bodyFile = FILE (); 
    PRINT emailCompliance OBJECTS dt = date AS DATE HTML TO bodyFile;
    EMAIL
    SUBJECT 'Уведомление об окончании срока действия сертификатов на ' + countToNotification(date) + ' шт.'
    TO emailToNotification()
    BODY bodyFile();
; }
  
  
WHEN CHANGED (currentDate()) AND countToNotification(currentDate()) DO {
    emailCompliance(currentDate());        
}




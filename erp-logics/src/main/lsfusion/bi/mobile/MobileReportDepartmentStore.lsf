MODULE MobileReportDepartmentStore;

REQUIRE MobileReport, Store;

NAMESPACE MobileReport;

in = DATA LOCAL NESTED BOOLEAN (DepartmentStore);
dataMobileName = DATA STRING[20] (Stock);
overMobileName 'Наименование mobile' (Stock s)=  OVERRIDE dataMobileName(s), STRING[20](name(s));

index (Stock s) = PARTITION SUM 1 IF in(s) ORDER s BY overMobileName(s);

mobileName 'Наименование mobile' (Stock s) = CONCAT '-', overMobileName(s);

EXTEND FORM departmentStore
    PROPERTIES (d) overMobileName
;

DESIGN departmentStore{
    headerExtraParams{
        MOVE PROPERTY (overMobileName(d));
    }
}

switchOff (DepartmentStore ds) {
    in(ds) <- NULL;
} 

fill 'Выбрать все' (){
    in(DepartmentStore ds) <- accessCompany(ds) AND active(ds);
}

clear 'Очистить' (){
    in(DepartmentStore ds) <- NULL;
}

FORM departmentStores 'Отделы магазинов'
    OBJECTS sds = DepartmentStore
    PROPERTIES (sds) overMobileName BACKGROUND #A0A0FE IF in(sds) ON CHANGE { in(sds) <- NOT in(sds); }
    PROPERTIES fill(), clear()
    
    FILTERS active(sds), accessCompany(sds)
;

DESIGN departmentStores {
    GRID(sds) {
        headerHeight = 1;
        REMOVE TOOLBARBOX (sds);
        PROPERTY (overMobileName(sds)) { changeOnSingleClick = TRUE; }
        PROPERTY (fill()) {alignment = STRETCH ;};
        PROPERTY (clear()) {alignment = STRETCH ;};
    }
}
customSelectDepartmentStores = ABSTRACT BOOLEAN ();
customSelectDepartmentStores ABSTRACT LIST ();

departmentStores '' ()  {
    IF customSelectDepartmentStores() THEN customSelectDepartmentStores();
    ELSE SHOW departmentStores WAIT NOMANAGESESSION NOCANCEL;
} IMAGE 'retail.png';



EXTEND FORM mobileReport
    OBJECTS sds = DepartmentStore
    FILTERS in(sds)
    
    PROPERTIES departmentStores() FIRST GRID  
;

DESIGN mobileReport {
    PANEL {
        NEW mobileDepartmentStore {
            type = CONTAINERH;
            MOVE GROUP ();
        }
    }
    BOX (intervalDate) {
        MOVE PROPERTY (departmentStores());
    }
}
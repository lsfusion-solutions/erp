MODULE CreditNote;

REQUIRE Shipment, Invoice, Item;

//----------------------------------------------- Акт расхождения ---------------------------------------------------//

DESIGN options {
    pane {
        NEW creditNotes  {
            caption = 'Акт расхождения';            
        }
    }
}

CLASS ABSTRACT CreditNote 'Акт расхождения';
CLASS ABSTRACT CreditNoteDetail 'Строка акта расхождения';

creditNote = ABSTRACT CreditNote (CreditNoteDetail);
in(CreditNote o, CreditNoteDetail d) = creditNote(d) == o;
countCreditNoteDetail '{document.count.detail.of}' (o) =
    GROUP SUM 1 BY creditNote(CreditNoteDetail d) IN documentSum;
index '{document.index.detail.class}' (d) = ABSTRACT INTEGER (CreditNoteDetail) IN base;
date '{document.date.of} {document.of.document}' = ABSTRACT DATE (CreditNote) IN documentHeader;
time '{document.time.of} {document.of.document}' = ABSTRACT TIME (CreditNote) IN documentHeader;
dateTime '{document.date.time.of} {document.of.document}' (CreditNote o) = dateTimeToDateTime(date(o), time(o));
dateTime '{document.date.time.of} {document.of.document}' (CreditNoteDetail d) = dateTime(creditNote(d)) CHARWIDTH 10;
date '{document.date.of} {document.of.document}' (CreditNoteDetail d) = date(creditNote(d));
time '{document.time.of} {document.of.document}' (CreditNoteDetail d) = time(creditNote(d));
note '{document.note}' = ABSTRACT ISTRING[500] (CreditNote) CHARWIDTH 30 IN documentPrm;
is (CreditNote o) = o IS CreditNote;
is (CreditNoteDetail i) = i IS CreditNoteDetail;
number '{numerator.number.numerator}' = ABSTRACT STRING[48] (CreditNote) IN numbered CHARWIDTH 8;
series '{numerator.series.numerator}' = ABSTRACT BPSTRING[2] (CreditNote) IN numbered CHARWIDTH 3 NOFLEX;

seriesNumber '{numerator.series.number.numerator}' (CreditNote o) =
    CONCAT '', series(o), number(o)
    CHARWIDTH 8;

number '{document.number.detail.class}' (CreditNoteDetail d) = number(creditNote(d)) IN id CHARWIDTH 7;
series '{document.series.detail.class}' (CreditNoteDetail d) = series(creditNote(d)) IN id CHARWIDTH 3 NOFLEX;
seriesNumber '{document.series.number.detail.class}' (CreditNoteDetail d) = seriesNumber(creditNote(d));

supplier (creditNote) = ABSTRACT LegalEntity (CreditNote);
nameSupplier 'Поставщик' (CreditNote creditNote)= name(supplier(creditNote)) IN documentPrm CHARWIDTH 20;
fullNameSupplier 'Поставщик (полное наименование)' (CreditNote creditNote)= fullName(supplier(creditNote)) IN documentPrm CHARWIDTH 20;
addressSupplier 'Поставщик (адрес)' (CreditNote creditNote)= address(supplier(creditNote)) IN documentPrm CHARWIDTH 20;
phoneSupplier 'Поставщик (телефон)' (CreditNote creditNote)= phone(supplier(creditNote)) IN documentPrm CHARWIDTH 20;
supplier (CreditNoteDetail idetail) = supplier(creditNote(idetail));
nameSupplier 'Поставщик' (CreditNoteDetail idetail) = name(supplier(idetail));
fullNameSupplier 'Поставщик (полное наименование)' (CreditNoteDetail idetail) = fullName(supplier(idetail));
addressSupplier 'Поставщик (адрес)' (CreditNoteDetail idetail)= address(supplier(idetail));

customer (creditNote) = ABSTRACT LegalEntity (CreditNote);
nameCustomer 'Покупатель' (CreditNote creditNote)= name(customer(creditNote)) IN documentPrm CHARWIDTH 20;
fullNameCustomer 'Покупатель (полное наименование)' (CreditNote creditNote)= fullName(customer(creditNote)) IN documentPrm CHARWIDTH 20;
addressCustomer 'Покупатель (адрес)' (CreditNote creditNote)= address(customer(creditNote)) IN documentPrm CHARWIDTH 20;
phoneCustomer 'Покупатель (телефон)' (CreditNote creditNote)= phone(customer(creditNote)) IN documentPrm CHARWIDTH 20;
customer (CreditNoteDetail idetail) = customer(creditNote(idetail));
nameCustomer 'Покупатель' (CreditNoteDetail idetail) = name(customer(idetail));
fullNameCustomer 'Покупатель (полное наименование)' (CreditNoteDetail idetail) = fullName(customer(idetail));
addressCustomer 'Покупатель (адрес)' (CreditNoteDetail idetail)= address(customer(idetail));

supplierStock = ABSTRACT Stock(CreditNote);
nameSupplierStock 'Склад поставщика' (CreditNote creditNote) = name(supplierStock(creditNote)) IN documentHeader
    CHARWIDTH 20;
fullNameSupplierStock 'Склад поставщика' (CreditNote creditNote) = fullName(supplierStock(creditNote)) IN documentHeader
    CHARWIDTH 20;

supplierLegalEntityStock (CreditNote creditNote) = legalEntity(supplierStock(creditNote));
nameSupplierLegalEntityStock 'Компания (Склад поставщика)' (CreditNote creditNote) = name(supplierLegalEntityStock (creditNote));
fullNameSupplierLegalEntityStock 'Компания (Склад поставщика) полное наим-ие' (CreditNote creditNote) = fullName(supplierLegalEntityStock (creditNote));
addressSupplierLegalEntityStock 'Адрес компании (Склад поставщика)' (CreditNote creditNote) = address(supplierLegalEntityStock (creditNote));
addressSupplierStock 'Адрес (Склад поставщика)' (CreditNote creditNote) = address(supplierStock(creditNote));

supplierCountry (CreditNote creditNote) = country(supplierStock(creditNote));

supplierStock (CreditNoteDetail idetail) = supplierStock(creditNote(idetail));
nameSupplierStock 'Склад поставщика' (CreditNoteDetail idetail) = name(supplierStock(idetail)) CHARWIDTH 10;
fullNameSupplierStock 'Склад поставщика' (CreditNoteDetail idetail) = fullName(supplierStock(idetail)) CHARWIDTH 10;
countrySupplierStock (CreditNoteDetail idetail) = country(supplierStock(idetail));
legalEntitySupplierStock (CreditNoteDetail idetail) = legalEntity(supplierStock(idetail));
nameLegalEntitySupplierStock 'Компания (Склад поставщика)' (CreditNoteDetail idetail) = name(legalEntitySupplierStock (idetail));
fullNameLegalEntitySupplierStock 'Компания (Склад поставщика)' (CreditNoteDetail idetail) = fullName(legalEntitySupplierStock (idetail));

customerStock = ABSTRACT Stock(CreditNote);
nameCustomerStock 'Склад покупателя' (CreditNote creditNote) = name(customerStock(creditNote)) IN documentHeader
    CHARWIDTH 20;
fullNameCustomerStock 'Склад покупателя' (CreditNote creditNote) = fullName(customerStock(creditNote)) IN documentHeader
    CHARWIDTH 20;
customerLegalEntityStock (CreditNote creditNote) = legalEntity(customerStock(creditNote));
nameCustomerLegalEntityStock 'Компания (Склад покупателя)' (CreditNote creditNote) = name(customerLegalEntityStock (creditNote));
fullNameCustomerLegalEntityStock 'Компания (Склад покупателя) полное наим-ие' (CreditNote creditNote) = fullName(customerLegalEntityStock (creditNote));
addressCustomerLegalEntityStock 'Адрес компании (Склад покупателя)' (CreditNote creditNote) = address(customerLegalEntityStock (creditNote));
addressCustomerStock 'Адрес (Склад покупателя)' (CreditNote creditNote) = address(customerStock(creditNote));

customerCountry (CreditNote creditNote) = country(customerStock(creditNote));

customerStock (CreditNoteDetail idetail) = customerStock(creditNote(idetail));
nameCustomerStock 'Склад покупателя' (CreditNoteDetail idetail) = name(customerStock(idetail)) CHARWIDTH 10;
fullNameCustomerStock 'Склад покупателя' (CreditNoteDetail idetail) = fullName(customerStock(idetail)) CHARWIDTH 10;
countryCustomerStock (CreditNoteDetail idetail) = country(customerStock(idetail));
legalEntityCustomerStock (CreditNoteDetail idetail) = legalEntity(customerStock(idetail));
nameLegalEntityCustomerStock 'Компания (Склад покупателя)' (CreditNoteDetail idetail) = name(legalEntityCustomerStock (idetail));
fullNameLegalEntityCustomerStock 'Компания (Склад покупателя)' (CreditNoteDetail idetail) = fullName(legalEntityCustomerStock (idetail));

isPosted '{document.posted}' (o) = ABSTRACT BOOLEAN(CreditNote);
isDraft '{document.draft}' (CreditNote o) = o IS CreditNote AND NOT isPosted(o);

isPosted '{document.posted}' (CreditNoteDetail d) = isPosted(creditNote(d));
isNotPosted '{document.draft}' (CreditNoteDetail d) = d IS CreditNoteDetail AND NOT isPosted(d);

isClosed '{documents.is.closed}' = ABSTRACT BOOLEAN(CreditNote);
isOpened '{documents.is.opened}' (CreditNote o) = o IS CreditNote AND NOT isClosed(o);
close '{documents.close}' (o)  ABSTRACT LIST ( CreditNote);

isClosed '{documents.is.closed}' (CreditNoteDetail d) = isClosed(creditNote(d));
isOpened '{documents.is.opened}' (CreditNoteDetail d) = isPosted(d) AND isOpened(creditNote(d));

description '{document.description.object}' = ABSTRACT STRING[200] (CreditNote) CHARWIDTH 3;
description '{document.description.object}' (CreditNoteDetail d) = description(creditNote(d)) CHARWIDTH 30;
descriptionIndex '{document.description.object}' (CreditNoteDetail d) = STRING[200](description(d) + ', {document.description.position} ' + index(d)) CHARWIDTH 30 IN base;

currency (creditNote) = ABSTRACT Currency (CreditNote);
nameCurrency 'Валюта' (CreditNote creditNote)= name(currency(creditNote)) IN documentPrm CHARWIDTH 10;
shortNameCurrency 'Валюта (сокр.)' (CreditNote creditNote) = shortName(currency(creditNote));


currency (CreditNoteDetail idetail) = currency(creditNote(idetail));
nameCurrency 'Валюта' (CreditNoteDetail idetail) = nameCurrency(creditNote(idetail));
shortNameCurrency 'Валюта (сокр.)' (CreditNoteDetail idetail) = shortName(currency(idetail));

contractSku = ABSTRACT ContractSku (CreditNote);
seriesNumberContractSku 'Номер договора' (CreditNote o) = seriesNumber[Contract](contractSku(o)) IN documentPrm;
dateFromContractSku 'Дата договора' (CreditNote o) = dateFrom(contractSku(o)) IN documentPrm;

descriptionContractSku 'Описание договора' (CreditNote o)= CONCAT ' ', seriesNumberContractSku(o), ' от ' + toDateDDMMYYYY(dateFromContractSku(o));

isCommission 'Продажа на комиссию' = ABSTRACT BOOLEAN (CreditNote) IN documentPrm;

sku = ABSTRACT Sku (CreditNoteDetail);
prevSku (CreditNoteDetail creditNoteDetail) = PREV(sku(creditNoteDetail));
idSku 'Код' (CreditNoteDetail creditNoteDetail) = id(sku(creditNoteDetail));
nameSku 'Наименование' (CreditNoteDetail creditNoteDetail) = name(sku(creditNoteDetail));
prevNameSku 'Наименование' (CreditNoteDetail creditNoteDetail) = prevName(sku(creditNoteDetail)) IN id;
shortNameUOMSku 'Ед. изм.' = shortNameUOM(sku(CreditNoteDetail creditNoteDetail)) IN base;
idBarcodeSku 'Штрихкод' (CreditNoteDetail creditNoteDetail) = idBarcode(sku(creditNoteDetail)) IN id;
allBarcodesSku 'Все штрихкоды' (CreditNoteDetail creditNoteDetail) = allBarcodes(sku(creditNoteDetail));
nameCountrySku 'Страна' (CreditNoteDetail creditNoteDetail) = nameCountry(sku(creditNoteDetail));

quantity '{document.quantity.of}' = ABSTRACT NUMERIC[16,5] (CreditNoteDetail) CHARWIDTH 7;

price 'Цена' = ABSTRACT NUMERIC[16,4] (CreditNoteDetail);

sum '{document.sum.of}' = ABSTRACT NUMERIC[18,4] (CreditNoteDetail) CHARWIDTH 9 PATTERN '#,##0.00##';

VAT(creditNoteDetail) = ABSTRACT Range (CreditNoteDetail);
numberVAT 'НДС, номер' (CreditNoteDetail creditNoteDetail) = number(VAT(creditNoteDetail));
valueVAT 'НДС, %' (creditNoteDetail) = ABSTRACT NUMERIC[10,5] (CreditNoteDetail) CHARWIDTH 7;
calcValueVAT 'НДС (перегруженный), %' (creditNoteDetail) = ABSTRACT NUMERIC[10,5] (CreditNoteDetail);

invoicePrice 'Цена с НДС' = ABSTRACT NUMERIC[16,4] (CreditNoteDetail);

VATSum 'Сумма НДС' (creditNoteDetail) = ABSTRACT NUMERIC[18,4] (CreditNoteDetail) CHARWIDTH 8 PATTERN '#,##0.00##';
invoiceSum 'Сумма с НДС' (creditNoteDetail) = ABSTRACT NUMERIC[18,4] (CreditNoteDetail) CHARWIDTH 9 PATTERN '#,##0.00##';

quantityCreditNoteDetail '{document.quantity.total.of}' (creditNote) = GROUP SUM quantity(CreditNoteDetail idetail) BY creditNote(idetail) IN documentSum CHARWIDTH 8;

@defineDocumentHeaderSkuQuantity(creditNote, creditNoteDetail, sku);

@defineDocumentHeaderSum(creditNote);

@defineDocumentHeaderVATSum(creditNote, creditNoteDetail, invoice);

@defineDocumentHeaderItemSum (creditNote, VAT, VAT);

@defineDocumentHeaderItemSum (creditNote, , );

@defineDocumentHeaderItemSum (creditNote, invoice, invoice);

@defineDocumentHeaderItemSumDouble (creditNote, range, VAT, VAT, , VAT);

@defineDocumentHeaderItemSumDouble (creditNote, range, , , , VAT);

@defineDocumentHeaderItemSumDouble (creditNote, range, invoice, invoice, , VAT);


documentQuantity '{document.quantity.of} по накладной' = ABSTRACT NUMERIC[16,5] (CreditNoteDetail) CHARWIDTH 7;

documentSum '{document.sum.of} по накладной' = ABSTRACT NUMERIC[18,4] (CreditNoteDetail) CHARWIDTH 9 PATTERN '#,##0.00##';


documentVATSum '{document.sum.of} НДС по накладной' = ABSTRACT NUMERIC[18,4] (CreditNoteDetail) CHARWIDTH 9 PATTERN '#,##0.00##';

documentInvoiceSum '{document.sum.of} с НДС по накладной' = ABSTRACT NUMERIC[18,4] (CreditNoteDetail) CHARWIDTH 9 PATTERN '#,##0.00##';

shipmentQuantity '{document.quantity.of} по факту' = ABSTRACT NUMERIC[16,5] (CreditNoteDetail) CHARWIDTH 7;

shipmentSum '{document.sum.of} по факту' = ABSTRACT NUMERIC[18,4] (CreditNoteDetail) CHARWIDTH 9 PATTERN '#,##0.00##';

shipmentVATSum '{document.sum.of} НДС по факту' = ABSTRACT NUMERIC[18,4] (CreditNoteDetail) CHARWIDTH 9 PATTERN '#,##0.00##';

shipmentInvoiceSum '{document.sum.of} с НДС по факту' = ABSTRACT NUMERIC[18,4] (CreditNoteDetail) CHARWIDTH 9 PATTERN '#,##0.00##';

userCreditNote = ABSTRACT CreditNote (Invoice);

invoiceDetail = ABSTRACT InvoiceDetail (CreditNoteDetail);
descriptionIndexInvoiceInvoice 'Накладная' (CreditNoteDetail creditNoteDetail) =  descriptionIndex(invoiceDetail(creditNoteDetail));
quantity (invoice, creditNote) = GROUP SUM quantity(CreditNoteDetail creditNoteDetail) BY invoice(invoiceDetail(creditNoteDetail)), creditNote(creditNoteDetail);
invoices 'Накладные'(CreditNote creditNote)  = GROUP
    CONCAT STRING[255](description(Invoice invoice)) IF quantity(invoice, creditNote) , ', '
    ORDER invoice CHARWIDTH 30;

disableShowBatch = ABSTRACT BOOLEAN (CreditNote);
batch = ABSTRACT Batch (CreditNoteDetail);
nameBatch 'Партия' (CreditNoteDetail creditNoteDetail) = name(batch(creditNoteDetail)) CHARWIDTH 20;
documentNameBatch 'Наименование (для документов)' (CreditNoteDetail creditNoteDetail) = documentNameSku(batch(creditNoteDetail)) CHARWIDTH 20;

paymentCondition 'Условия оплаты' = ABSTRACT PaymentCondition (CreditNote);

operation = ABSTRACT Operation.Operation(CreditNote);
nameOperation 'Операция' (CreditNote creditNote) = name(operation(creditNote));
nameReturnOperation 'Операция' (CreditNote creditNote) = nameReturn(operation(creditNote));

operation (CreditNoteDetail idetail) = operation(creditNote(idetail));
nameOperation 'Склад покупателя' (CreditNoteDetail idetail) = name(operation(idetail)) CHARWIDTH 10;
nameReturnOperation 'Склад покупателя' (CreditNoteDetail idetail) = nameReturn(operation(idetail)) CHARWIDTH 10;

background 'Цвет' = ABSTRACT COLOR (CreditNote);

FORM creditNotePrintBase 'Акт расхождений'
    OBJECTS c = CreditNote PANEL SUBREPORT
    PROPERTIES (c) READONLY seriesNumber, date, fullNameSupplier, nameSupplierStock,
        fullNameCustomer, addressCustomer, nameCustomerStock, addressCustomerStock,
        seriesNumberContractSku, quantityCreditNoteDetail, sumCreditNoteDetail,
        VATSumCreditNoteDetail, invoiceSumCreditNoteDetail, note

    OBJECTS d = CreditNoteDetail
    PROPERTIES (d) READONLY index, idBarcodeSku, nameSku, shortNameUOMSku,
        quantity, price, sum, numberVAT,
        valueVAT, VATSum, invoiceSum,
        documentQuantity, documentVATSum, documentInvoiceSum,
        shipmentQuantity, shipmentVATSum, shipmentInvoiceSum
    ORDERS index(d)
    FILTERS creditNote(d) == c
;

print 'Акт расхождения' ABSTRACT CASE OVERRIDE FIRST (CreditNote) TOOLBAR IMAGE 'print.png';
print(CreditNote c) + WHEN c IS CreditNote THEN { PRINT creditNotePrintBase OBJECTS c = c; }

META defineCreditNote(sign, legalEntityProp, stockProp, supplierFilter, customerFilter, NS)//contact, contactCaption)

    CLASS ABSTRACT CreditNote 'Акт расхождения'###sign : CreditNote.CreditNote;
    CLASS ABSTRACT CreditNoteDetail 'Строка акта расхождения'###sign : CreditNote.CreditNoteDetail;

    CLASS UserCreditNote 'Акт расхождения (польз.)'###sign : CreditNote;
    CLASS UserCreditNoteDetail 'Строка акта расхождения (польз.)'###sign : CreditNoteDetail;

    GROUP creditNote 'Информация об акте расхождения' : base;
    
    @defineExternalizable(userCreditNote, STRING[100]);
    @defineExternalizable(userCreditNoteDetail, STRING[100]);

    @defineDocumentInterface(creditNote);

    creditNote[CreditNote.CreditNoteDetail](CreditNoteDetail creditNoteDetail) += creditNote(creditNoteDetail);
    index[CreditNote.CreditNoteDetail](CreditNoteDetail creditNoteDetail) += index(creditNoteDetail);
    
    date[CreditNote.CreditNote] (CreditNote creditNote) += date(creditNote);
    time[CreditNote.CreditNote] (CreditNote creditNote) += time(creditNote);
    note[CreditNote.CreditNote] (CreditNote creditNote) += note(creditNote);
        
    @deriveDocumentHeaderTimePrefix(UserCreditNote, );

    @defineDocumentInterfaceNumber(creditNote, BPSTRING[2]);

    number[CreditNote.CreditNote] (CreditNote creditNote) += number(creditNote);
    series[CreditNote.CreditNote] (CreditNote creditNote) += series(creditNote);

    @defineNumeratedDefault(UserCreditNote, 'Акты расхождений'###sign, 'АР', NS);

    @defineDocumentInterfaceLegalEntity (creditNote, supplier, 'Поставщик');

    supplier[CreditNote.CreditNote] (CreditNote creditNote) += supplier(creditNote);
        
    @defineDocumentInterfaceLegalEntity (creditNote, customer, 'Покупатель');

    customer[CreditNote.CreditNote] (CreditNote creditNote) += customer(creditNote);
        
    @defineDocumentInterfaceDataStock(creditNote, stock, 'Склад поставщика', supplier);
    supplierStock[CreditNote.CreditNote] (CreditNote creditNote) += supplierStock(creditNote);
        
    @defineDocumentInterfaceDataStock(creditNote, stock, 'Склад покупателя', customer);

    customerStock[CreditNote.CreditNote] (CreditNote creditNote) += customerStock(creditNote);
     
    CONSTRAINT supplier(UserCreditNote userCreditNote) AND supplierStock(userCreditNote) AND NOT
               in(supplier(userCreditNote), supplierStock(userCreditNote))
        CHECKED BY supplierStock[UserCreditNote]
            MESSAGE 'Поставщик и склад поставщика для акта расхождений не имеют связи';
    CONSTRAINT customer(UserCreditNote userCreditNote) AND customerStock(userCreditNote) AND NOT
               in(customer(userCreditNote), customerStock(userCreditNote))
        CHECKED BY customerStock[UserCreditNote]
            MESSAGE 'Покупатель и склад покупателя для акта расхождений не имеют связи';

    @defineDocumentInterfacePosted(creditNote);

    isPosted[CreditNote.CreditNote] (CreditNote creditNote) += isPosted(creditNote);

    @defineDocumentInterfaceClosed(creditNote);
    isClosed '{documents.is.closed}' (UserCreditNoteDetail d) = isClosed(userCreditNote(d));
    
    isClosed[CreditNote.CreditNote] (CreditNote creditNote) += isClosed(creditNote);     
        
    @defineDocumentClosedConstraint(UserCreditNote);
    @defineDocumentInterfaceDescription(creditNote, 'Акт расхождения'###sign);

    description[CreditNote.CreditNote] (CreditNote creditNote) += description(creditNote);

    @defineDocumentInterfaceCurrency(creditNote);

    currency[CreditNote.CreditNote] (CreditNote creditNote) += currency(creditNote);
        
    @deriveDocumentCurrency(userCreditNote, stockProp###stock);

    @defineDocumentInterfaceContract(CreditNote, contractSku, supplier, customer,
                                     'Организация (поставщик) договора акта расхождений не соответствует организация (поставщик) акта расхождений',
                                     'Организация (покупатель) договора акта расхождений не соответствует организация (покупатель) акта расхождений',
                                     'акт расхождения'###sign, NS);

    contractSku[CreditNote.CreditNote] (CreditNote creditNote) += contractSku(creditNote);
    isCommission[CreditNote.CreditNote] (CreditNote creditNote) += isCommission(creditNote);
        
    @defineDocumentInterfaceDetailSku(creditNote, sku);

    sku[CreditNote.CreditNoteDetail] (CreditNoteDetail creditNoteDetail) += sku(creditNoteDetail);
        
    INDEX sku(CreditNoteDetail d), customerStock(d);

    @defineDocumentInterfaceDetailQuantity(creditNote);

    quantity[CreditNote.CreditNoteDetail] (CreditNoteDetail creditNoteDetail) += quantity(creditNoteDetail);
        
    price 'Цена' = ABSTRACT NUMERIC[16,4] (CreditNoteDetail) MATERIALIZED;
    price 'Цена' = DATA NUMERIC[16,4] (UserCreditNoteDetail);
    price (UserCreditNoteDetail creditNoteDetail) += price(creditNoteDetail);
    price[CreditNote.CreditNoteDetail] (CreditNoteDetail creditNoteDetail) += price(creditNoteDetail);

    homePriceRound = ABSTRACT CASE INTEGER (CreditNoteDetail);
    homePriceRound (CreditNoteDetail d) += WHEN currency(d) THEN defaultRound(currency(d));

    @defineDocumentInterfaceDetailDataSum(creditNote);

    sum[CreditNote.CreditNoteDetail] (CreditNoteDetail creditNoteDetail) += sum(creditNoteDetail);
        
    @deriveDocumentDetailSum(userCreditNote, quantity);

    @defineDocumentInterfaceDetailVAT(creditNote, country###stockProp###stock);

    VAT[CreditNote.CreditNoteDetail] (CreditNoteDetail creditNoteDetail) += VAT(creditNoteDetail);
    valueVAT[CreditNote.CreditNoteDetail] (CreditNoteDetail creditNoteDetail) += valueVAT(creditNoteDetail);
    calcValueVAT[CreditNote.CreditNoteDetail] (CreditNoteDetail creditNoteDetail) += calcValueVAT(creditNoteDetail);
        
    invoicePrice 'Цена с НДС' = ABSTRACT NUMERIC[16,4] (CreditNoteDetail) MATERIALIZED;
    invoicePrice 'Цена с НДС' = DATA NUMERIC[16,4] (UserCreditNoteDetail);
    invoicePrice (UserCreditNoteDetail creditNoteDetail) += invoicePrice(creditNoteDetail);
    invoicePrice[CreditNote.CreditNoteDetail] (CreditNoteDetail creditNoteDetail) += invoicePrice(creditNoteDetail);
    
    //  derive см. ниже в "Связь акта и накладной"
    @defineDocumentInterfaceDetailVATDataSumCustom(creditNoteDetail, invoice);

    VATSum[CreditNote.CreditNoteDetail] (CreditNoteDetail creditNoteDetail) += VATSum(creditNoteDetail);
    invoiceSum[CreditNote.CreditNoteDetail] (CreditNoteDetail creditNoteDetail) += invoiceSum(creditNoteDetail);

    @defineDocumentInterfaceHeaderQuantity(creditNote);
    @defineDocumentHeaderSkuQuantity(creditNote, sku);
    @defineDocumentInterfaceHeaderSum(creditNote);
    @defineDocumentInterfaceHeaderVATSum(creditNote, creditNoteDetail, invoice);
    
    @defineDocumentInterfaceHeaderItemSum (creditNote, VAT, VAT); //-- сумма НДС поставщика (с разбивкой по таре, товару...)
    @defineDocumentInterfaceHeaderItemSum (creditNote, , ); //-- сумма поставщика (с разбивкой по таре, товару...)
    @defineDocumentInterfaceHeaderItemSum(creditNote, invoice, invoice); //-- сумма поставщика с НДС (с разбивкой по таре, товару...)

    @defineDocumentInterfaceHeaderItemSumDouble (creditNote, range, VAT, VAT, , VAT); //-- сумма НДС поставщика (с разбивкой по таре, товару...) по документу и по шкале
    @defineDocumentInterfaceHeaderItemSumDouble (creditNote, range, , , , VAT);  //сумма без НДС поставщика (с разбивкой по таре, товару...) по документу и по шкале
    @defineDocumentInterfaceHeaderItemSumDouble (creditNote, range, invoice, invoice, , VAT);  //сумма с НДС (с разбивкой по таре, товару...) по документу и по шкале

    @defineAddDetailDialogSkuStock(userCreditNote, sku, stockProp###stock, dialogSku);
    @defineAddDetailDialogBarcode(userCreditNote, sku);
        
    @defineMovementSku(creditNoteDetail, stockProp###stock); //-- показываем по нажатию правой клавиши движение товара
    @defineMovementSku(userCreditNoteDetail, stockProp###stock); //-- показываем по нажатию правой клавиши движение товара
    @defineBalancesSku(creditNoteDetail); //-- показываем по нажатию правой клавиши остатки товара
    @defineBalancesSku(userCreditNoteDetail); //-- показываем по нажатию правой клавиши остатки товара     
    @defineBarcodeSku(creditNoteDetail); //-- показываем по нажатию правой клавиши все штрихкоды для товара
    @defineBarcodeSku(userCreditNoteDetail); //-- показываем по нажатию правой клавиши все штрихкоды для товара
    @defineInfoSku(creditNoteDetail); //-- показываем по нажатию правой клавиши информацию по товару
    @defineInfoSku(userCreditNoteDetail); //-- показываем по нажатию правой клавиши информацию по товару
    @defineDocumentDialogSupplierCustomerStock(userCreditNote, supplierFilter, customerFilter);
    @defineDocumentDialogSupplierCustomerLegalEntity(userCreditNote, supplierFilter, customerFilter);       
    
    @defineDocumentInterfaceDetailQuantityCustomPrefix (creditNoteDetail, document, ' по накладной');

    documentQuantity[CreditNote.CreditNoteDetail] (CreditNoteDetail creditNoteDetail) += documentQuantity(creditNoteDetail);
        
    @defineDocumentInterfaceDetailDataSumPrefix(creditNote, document, ' по накладной');

    documentSum[CreditNote.CreditNoteDetail] (CreditNoteDetail creditNoteDetail) += documentSum(creditNoteDetail);
        
    @defineDocumentInterfaceDetailDataSumPrefix(creditNote, documentVAT, ' НДС по накладной');

    documentVATSum[CreditNote.CreditNoteDetail] (CreditNoteDetail creditNoteDetail) += documentVATSum(creditNoteDetail);

    @defineDocumentInterfaceDetailDataSumPrefix(creditNote, documentInvoice, ' с НДС по накладной');

    documentInvoiceSum[CreditNote.CreditNoteDetail] (CreditNoteDetail creditNoteDetail) += documentInvoiceSum(creditNoteDetail);
        
    @defineDocumentInterfaceDetailQuantityCustomPrefix (creditNoteDetail, shipment, ' по факту');

    shipmentQuantity[CreditNote.CreditNoteDetail] (CreditNoteDetail creditNoteDetail) += shipmentQuantity(creditNoteDetail);
        
    @defineDocumentInterfaceDetailDataSumPrefix(creditNote, shipment, ' по факту');

    shipmentSum[CreditNote.CreditNoteDetail] (CreditNoteDetail creditNoteDetail) += shipmentSum(creditNoteDetail);

    @defineDocumentInterfaceHeaderSumPrefix (creditNote, shipment, ' по факту');
    @defineDocumentHeaderItemSumCustom(userCreditNote, userCreditNoteDetail, shipment, shipment);
        
    @defineDocumentInterfaceDetailDataSumPrefix(creditNote, shipmentVAT, ' НДС по факту');

    shipmentVATSum[CreditNote.CreditNoteDetail] (CreditNoteDetail creditNoteDetail) += shipmentVATSum(creditNoteDetail);
        
    @defineDocumentInterfaceDetailDataSumPrefix(creditNote, shipmentInvoice, ' с НДС по факту');

    shipmentInvoiceSum[CreditNote.CreditNoteDetail] (CreditNoteDetail creditNoteDetail) += shipmentInvoiceSum(creditNoteDetail);
        
// --------------------------- Формы --------------------------------- //
    
    background = ABSTRACT CASE COLOR (CreditNoteDetail);
    
    FORM userCreditNote 'Акт расхождений'###sign
        OBJECTS c = UserCreditNote PANEL
        PROPERTIES (c) isPosted[CreditNote], nameNumerator, number, series, date, time,
                       nameSupplier ON CHANGE changeSupplier###supplierFilter(c), 
                       nameCustomer ON CHANGE changeCustomer###customerFilter(c), 
                       nameSupplierStock ON CHANGE changeSupplierStock###supplierFilter(c), 
                       nameCustomerStock ON CHANGE changeCustomerStock###customerFilter(c),
                       nameCurrency, seriesNumberContractSku, note,
                       countUserCreditNoteDetail, quantityUserCreditNoteDetail, sumUserCreditNoteDetail,
                       VATSumUserCreditNoteDetail, invoiceSumUserCreditNoteDetail

        OBJECTS d = UserCreditNoteDetail
        PROPERTIES (d) BACKGROUND background(d) index, idBarcodeSku, idSku SHOWIF showIDs(), nameSku, shortNameUOMSku,
                       quantity, price, sum,
                       numberVAT, valueVAT, invoicePrice, VATSum, invoiceSum,
                       name###stockProp##Stock, NEW, deletecd=DELETE GRID

        PROPERTIES(c) DRAW d addDetailDialogSkuStockUserCreditNoteDetail,
                               addDetailInputBarcodeUserCreditNoteDetail, deleteUserCreditNoteDetail
        FILTERS userCreditNote(d) == c

        EVENTS
            ON OK prePost(c)

        EDIT UserCreditNote OBJECT c
    ;

    DESIGN userCreditNote {
        BOX {
            size = (1024, 768);
            NEW mainTabbed {
                tabbed = TRUE;
                fill = 1;
                NEW documentBox {
                    fill = 1;
                    caption = 'Акт расхождений';
                    NEW headerBox {
                        horizontal = TRUE;
                        alignment = STRETCH;
        
                        NEW headerParams {
                            fill = 1;
        
                            MOVE GROUP(documentHeader,c) {
                                lines = 1;
                                alignment = STRETCH;
                                NEW first {
                                    alignment = STRETCH;
                                    horizontal = TRUE;
                                    MOVE PROPERTY(isPosted(c));
                                }
                                NEW second {
                                    alignment = STRETCH;
                                    horizontal = TRUE;
                                    MOVE PROPERTY(nameNumerator(c));
                                    MOVE PROPERTY(number(c)) { flex = 200; };
                                    MOVE PROPERTY(series(c));
                                    MOVE PROPERTY(date(c));
                                    MOVE PROPERTY(time(c));
                                }                    
                            }
                            NEW headerLegalEntity {
                                horizontal = TRUE;
                                alignment = STRETCH;
                                NEW headerSupplier {
                                    caption = 'Поставщик';
                                    horizontal = TRUE;
                                    fill = 1;
                                    MOVE PROPERTY(nameSupplier(c)) { caption = ''; };
                                    MOVE PROPERTY(nameSupplierStock(c)) { caption = 'Склад'; };
        
                                }
                                NEW headerCustomer {
                                    caption = 'Покупатель';
                                    horizontal = TRUE;
                                    fill = 1;
                                    MOVE PROPERTY(nameCustomer(c)) { caption = ''; };
                                    MOVE PROPERTY(nameCustomerStock(c)) { caption = 'Склад'; };
                                }
        
                            }
                            NEW headerTabbedPane {
                                fill = 1;
                                tabbed = TRUE;
                                NEW headerMainParams {
                                    caption = 'Основные параметры';
                                    fill = 1;
                                    MOVE GROUP(documentPrm, c);
                                }
                                NEW headerExtraParams {
                                    caption = 'Дополнительные параметры';
                                    lines = 4;
                                }
                            }    
                        }
        
                        MOVE GROUP(documentSum,c) {
                            lines = 1;
                        }
                    }
                
                    NEW specificationBox {
                        fill = 1;
                        tabbed = TRUE;
                        MOVE BOX(d) {
                            caption = 'Спецификация';
                        }
                    }
                }
            }
            MOVE TOOLBARBOX;
            PROPERTY(formOk()) {
                caption = 'Провести';
            }
            PROPERTY(sumUserCreditNoteDetail(c)) { charWidth = 13;}
            PROPERTY(VATSumUserCreditNoteDetail(c)) { charWidth = 13;}  
            PROPERTY(invoiceSumUserCreditNoteDetail(c)) { charWidth = 13;}            
        }
    }

    @defineDocumentSkuStockPriceListTypeSystemLedger(userCreditNote, stockProp);
    @extendFormDocumentSkuStock(userCreditNote, userCreditNote, c, legalEntityProp, stockProp);
    
    isUserOpened(UserCreditNote c)=  isOpened[CreditNote](c) AND is(c);

    background 'Цвет' = ABSTRACT CASE COLOR (CreditNote);
    background[CreditNote.CreditNote] (CreditNote creditNote) += background(creditNote);

    FORM creditNotes 'Акты расхождений' 
        OBJECTS c = CreditNote LAST
        PROPERTIES (c) READONLYIF isReadonly() BACKGROUND background(c) isClosed, isPosted, number, series, date, time,
                       nameSupplier, nameSupplierStock, nameCustomer, nameCustomerStock,
                       nameCurrency, seriesNumberContractSku, isCommission
        PROPERTIES (c) READONLY BACKGROUND background(c) countCreditNoteDetail, quantityCreditNoteDetail, sumCreditNoteDetail,
                       VATSumCreditNoteDetail, invoiceSumCreditNoteDetail
        PROPERTIES (c) READONLYIF isReadonly() note
        PROPERTIES (c) READONLY PANEL createdNameUser, createdTime, createdHostnameComputer, 
                                            postedNameUser, postedTime, postedHostnameComputer                        

        PROPERTIES (c) NEWSESSION NEW[UserCreditNote], editC = EDIT SHOWIF isOpened(c), deletec=DELETE SHOWIF isUserOpened(c)
        PROPERTIES (c) close[UserCreditNote] SHOWIF isOpened[UserCreditNote](c), open SHOWIF isClosed[UserCreditNote](c)

        PROPERTIES(c) print

        OBJECTS d = CreditNoteDetail
        PROPERTIES (d) READONLY BACKGROUND background(d) index, idBarcodeSku, idSku SHOWIF showIDs(), nameSku, shortNameUOMSku,
                                quantity, price, sum, numberVAT, valueVAT,
                                 VATSum, invoiceSum, name###stockProp##Stock
        FILTERS creditNote(d) == c

        LIST CreditNote OBJECT c
    ;

    DESIGN creditNotes 'Акты расхождений'###sign {
        NEW documentContainer BEFORE TOOLBARBOX {
            fill = 1;

            MOVE BOX(c) { flex = 2.0; }

            NEW documentDetail {
                fill = 1;
                tabbed = TRUE;

                MOVE BOX(d) {
                    fill = 1;
                    caption = 'Спецификация';
                }
                NEW documentHistory {
                    caption = 'История';

                    MOVE GROUP(created,c);
                    MOVE GROUP(posted,c);
                }
                NEW printTab {
                    caption = 'Печатные формы';
                    NEW printContainer {
                        caption = 'Печать';
                        MOVE PROPERTY(print(c));
                    }
                }
                NEW actionContainer {
                    caption = 'Действия';
                    horizontal = TRUE;
                    NEW createdContainer {
                        caption = 'Создание на основе';
                    }
                    NEW machineryContainer {
                        caption = 'Загрузка';
                    }
                }                
            }
        }
    }
    @extendFormEditable(creditNotes);
    @defineFilterIsOpened (creditNote, creditNotes, c); 
    edit(UserCreditNoteDetail d) + {  DIALOG userCreditNote OBJECTS c = userCreditNote(d), d = d DOCKED; }
    
    show 'Просмотреть'  ABSTRACT LIST ( CreditNoteDetail) TOOLBAR;
    show(UserCreditNoteDetail d)  { 
        NEWSESSION {
            SHOW userCreditNote OBJECTS c = userCreditNote(d), d = d DOCKED READONLY;
        }
    }
    show(UserCreditNoteDetail d) + {  show(d); }
    
    show 'Просмотреть' (UserCreditNote i)   { 
        NEWSESSION{
            SHOW userCreditNote OBJECTS c = i DOCKED READONLY;
        }
        
    }TOOLBAR;
        
    showToShow (UserCreditNote i) = is(i);// AND NOT overShowEdit(i);
    EXTEND FORM creditNotes 
        PROPERTIES show(c) SHOWIF showToShow(c) BEFORE editC
    
    ;
    
//--  Связь акта и накладной
    invoiceDetail = ABSTRACT InvoiceDetail (CreditNoteDetail) MATERIALIZED INDEXED;
    invoiceDetail = DATA InvoiceDetail (UserCreditNoteDetail) INDEXED;
    invoiceDetail(UserCreditNoteDetail creditNoteDetail) += invoiceDetail(creditNoteDetail);
    invoiceDetail[CreditNote.CreditNoteDetail] (CreditNoteDetail creditNoteDetail) += invoiceDetail(creditNoteDetail);

    CONSTRAINT invoiceDetail(UserCreditNoteDetail d) AND NOT sku(invoiceDetail(d)) = sku(d) CHECKED BY invoiceDetail[UserCreditNoteDetail]
        MESSAGE 'SKU строки накладной должен соответствовать SKU строки акта расхождений';

    CONSTRAINT invoiceDetail(UserCreditNoteDetail d) AND NOT supplier(invoiceDetail(d)) = supplier(d) CHECKED BY invoiceDetail[UserCreditNoteDetail]
        MESSAGE 'Поставщик строки накладной должен соответствовать поставщику строки акта расхождений';

    CONSTRAINT invoiceDetail(UserCreditNoteDetail d) AND NOT customer(invoiceDetail(d)) = customer(d) CHECKED BY invoiceDetail[UserCreditNoteDetail]
        MESSAGE 'Покупатель строки накладной должен соответствовать покупателю строки акта расхождений';

    descriptionIndexInvoice 'Накладная' (UserCreditNoteDetail userCreditNoteDetail) =  descriptionIndex(invoiceDetail(userCreditNoteDetail));
    descriptionIndexInvoiceInvoice 'Накладная' (CreditNoteDetail creditNoteDetail) =  descriptionIndex(invoiceDetail(creditNoteDetail));

    quantityUserCreditNote (InvoiceDetail d) = GROUP SUM quantity(UserCreditNoteDetail cd) BY invoiceDetail(cd);
    prevQuantityUserCreditNote (InvoiceDetail d) = PREV(quantityUserCreditNote(d));
    
    quantity (invoice, creditNote) = GROUP SUM quantity(CreditNoteDetail creditNoteDetail) BY invoice(invoiceDetail(creditNoteDetail)), creditNote(creditNoteDetail);
    invoices 'Накладные(из строк)'(CreditNote creditNote)  = GROUP
                                                    CONCAT STRING[255](description(Invoice invoice)) IF quantity(invoice, creditNote) , ', '
                                                    ORDER invoice IN invoice CHARWIDTH 60;

    FORM baseCreditNoteDetails 'Строки накладной'
        OBJECTS st = Stock PANEL
        OBJECTS ct = Stock PANEL
    
        OBJECTS d = InvoiceDetail
        PROPERTIES(d) READONLY index, idBarcodeSku, nameSku, shortNameUOMSku,
                               description,
                               quantity, price, sum,
                               numberVAT, valueVAT, VATSum, invoiceSum,
                               name###stockProp##Stock

        FILTERGROUP filterSupplier
                    FILTER  'По складу поставщика' supplierStock(d) = st DEFAULT     
        FILTERGROUP filterCustomer
                    FILTER  'По складу покупателя' customerStock(d) = ct DEFAULT             
    ;

    changeInvoiceDetail (UserCreditNoteDetail c) {
        DIALOG baseCreditNoteDetails OBJECTS st = supplierStock(c),
                                             ct = customerStock(c),
                                             d = invoiceDetail(c) NULL CHANGE
                                     FILTERS sku(d) = sku(c), supplier(d) = supplier(c), customer(d) = customer(c); 
    }

    EXTEND FORM userCreditNote
        PROPERTIES(c) READONLY invoices
        PROPERTIES(d) descriptionIndexInvoice ON CHANGE changeInvoiceDetail(d) BEFORE deletecd
    ;
    DESIGN userCreditNote { headerExtraParams { MOVE GROUP(invoice,c) { lines = 1; horizontal = TRUE; } } }


    EXTEND FORM creditNotes
        PROPERTIES(c) READONLY invoices
        PROPERTIES(d) READONLY descriptionIndexInvoiceInvoice
    ;
    // Записываем значения из накладной
    quantity (UserCreditNoteDetail detail)  <- toShip(invoiceDetail(detail))
        WHEN CHANGED(invoiceDetail(detail));

    price (UserCreditNoteDetail detail)  <- price(invoiceDetail(detail))
        WHEN CHANGED(invoiceDetail(detail));

    invoicePrice (UserCreditNoteDetail detail)  <- invoicePrice(invoiceDetail(detail))
        WHEN CHANGED(invoiceDetail(detail));    

    VAT (UserCreditNoteDetail detail)  <- VAT(invoiceDetail(detail))
        WHEN CHANGED(invoiceDetail(detail));

    valueVAT (UserCreditNoteDetail detail)  <- valueVAT(invoiceDetail(detail))
        WHEN CHANGED(invoiceDetail(detail));

    VATSum (UserCreditNoteDetail detail) <- round((sum(detail) *
        valueVAT (detail) / 100), currency(invoiceDetail(detail)))
        WHEN CHANGED(sum(detail)) OR CHANGED(VAT(detail)) OR CHANGED(currency(detail));
    @deriveDocumentDetailVATPrefixSum (userCreditNote, invoice);

//-- агр.объект
    caseToShip = ABSTRACT CASE NUMERIC[16,5](InvoiceDetail);
    caseToShip (InvoiceDetail detail) += WHEN sku(detail) IS Item THEN toShip(detail);
        
    @defineDocumentInterfaceProperty (invoice, createCreditNote, 'Создать акт расхождения');
    
    overCreateCreditNote = ABSTRACT BOOLEAN (InvoiceDetail);
    
    needToCreditNote (InvoiceDetail invoiceDetail)= createCreditNote(invoiceDetail) AND isPosted(invoiceDetail) AND
        (caseToShip (invoiceDetail) OR overCreateCreditNote(invoiceDetail))
        AND isNotCharge(sku(invoiceDetail)) MATERIALIZED;

    overCopy(UserInvoice s, UserInvoice d) + { 
        createCreditNote(d) <- createCreditNote(s);
    }

    needToCreditNote (invoice) = GROUP SUM 1 IF needToCreditNote(InvoiceDetail invoiceDetail) BY invoice(invoiceDetail) MATERIALIZED;

    EXTEND FORM userInvoice PROPERTIES(i) createCreditNote;
    DESIGN userInvoice {
        headerCreateDocuments {
            NEW headerCreateCreditNote {
                caption = 'Акт расхождения';
                MOVE PROPERTY(createCreditNote(i));
            }
        }
    }

    CLASS InvoiceCreditNote 'Акт расхождения на основе накладной' : CreditNote;
    CLASS InvoiceCreditNoteDetail 'Строка акта расхождения на основе накладной' : CreditNoteDetail;

    @defineDocumentTables(invoiceCreditNote);

    @defineDocumentAggregation(invoice, invoiceCreditNote, needToCreditNote);
    creditNote(InvoiceCreditNoteDetail detail) += invoiceCreditNote(detail);

    index(InvoiceCreditNoteDetail detail) += index(detail);
    
    overDate = ABSTRACT DATE (InvoiceCreditNote);
    overTime = ABSTRACT TIME (InvoiceCreditNote);    
    date(InvoiceCreditNote creditNote) += OVERRIDE overDate(creditNote), date(creditNote);
    time(InvoiceCreditNote creditNote) += OVERRIDE overTime(creditNote), time(creditNote);

    @defineDocumentAggregationStockPrefix(invoice, invoiceCreditNote, supplier, 'Склад поставщика', , );
    supplierStock(InvoiceCreditNote creditNote) += supplierStock(creditNote);

    @defineDocumentAggregationStockPrefix(invoice, invoiceCreditNote, customer, 'Склад покупателя', , );
    customerStock(InvoiceCreditNote creditNote) += customerStock(creditNote);

    @defineDocumentAggregationLegalEntityPrefix(invoice, invoiceCreditNote, supplier, 'Поставщик', , );
    supplier(InvoiceCreditNote creditNote) += supplier(creditNote);

    @defineDocumentAggregationLegalEntityPrefix(invoice, invoiceCreditNote, customer, 'Покупатель', , );
    customer(InvoiceCreditNote creditNote) += customer(creditNote);
    
    contractSku(InvoiceCreditNote creditNote) += contractSku(invoice(creditNote));
    isCommission(InvoiceCreditNote creditNote) += isCommission(invoice(creditNote));

    @defineDocumentAggregationPosted(invoice, invoiceCreditNote);
    isPosted(InvoiceCreditNote creditNote) += isPosted(creditNote);
    
    @defineDocumentAggregationClosed(invoice, invoiceCreditNote);
    isClosed(InvoiceCreditNote creditNote) += isClosed(creditNote);
    
    number 'Номер документа' (InvoiceCreditNote invoiceCreditNote) = number(invoice(invoiceCreditNote));
    number(InvoiceCreditNote creditNote) += number(creditNote);

    series 'Серия документа' (InvoiceCreditNote invoiceCreditNote) = series(invoice(invoiceCreditNote));
    series(InvoiceCreditNote creditNote) += series(creditNote);

    seriesNumber 'Серия/номер документа' (InvoiceCreditNote invoiceCreditNote) = seriesNumber(invoice(invoiceCreditNote));

    noteInvoice 'Примечание' (InvoiceCreditNote invoiceCreditNote) = note(invoice(invoiceCreditNote));
    note(InvoiceCreditNote creditNote) += noteInvoice(creditNote);

    currency  (InvoiceCreditNote invoiceCreditNote) = currency(invoice(invoiceCreditNote));
    currency (InvoiceCreditNote creditNote) += currency(creditNote);
    currency (InvoiceCreditNoteDetail invoiceCreditNoteDetail) = currency(invoiceCreditNote(invoiceCreditNoteDetail)) MATERIALIZED;

    @defineDocumentDescription(invoiceCreditNote, InvoiceCreditNoteDetail, 'Акт расхождения на основе накладной'###sign);
    description (InvoiceCreditNote creditNote) += description(creditNote);
        
    @defineDocumentAggregationDetailSku(invoice, invoiceCreditNote, sku);
    sku(InvoiceCreditNoteDetail creditNoteDetail) +=  sku(creditNoteDetail);

    @defineDocumentAggregationDetailProperty (invoice, invoiceCreditNote, toShip, 'Кол-во');   //                ????????   может надо сделать через минус
    @defineDocumentAggregationDetailProperty (invoice, invoiceCreditNote, caseToShip, 'Кол-во'); 
    
    overQuantity = ABSTRACT NUMERIC[16,5] (InvoiceCreditNoteDetail);
    
    quantity(InvoiceCreditNoteDetail creditNoteDetail) += OVERRIDE overQuantity(creditNoteDetail), caseToShip(creditNoteDetail);

    @defineDocumentAggregationDetailProperty (invoice, invoiceCreditNote, price, 'Цена');

    price(InvoiceCreditNoteDetail creditNoteDetail) += price(creditNoteDetail);

    sum 'Сумма' (InvoiceCreditNoteDetail invoiceCreditNoteDetail) = NUMERIC[18,4](round((quantity(invoiceCreditNoteDetail) *
        price(invoiceCreditNoteDetail)), currency(invoiceCreditNoteDetail)));

    sum(InvoiceCreditNoteDetail creditNoteDetail) += sum(creditNoteDetail);

    invoiceDetail(InvoiceCreditNoteDetail creditNoteDetail) += invoiceDetail(creditNoteDetail);

    @defineDocumentAggregationDetailProperty (invoice, invoiceCreditNote, VAT, 'НДС');
    VAT (InvoiceCreditNoteDetail creditNoteDetail) += VAT(creditNoteDetail);

    @defineDocumentAggregationDetailProperty (invoice, invoiceCreditNote, numberVAT, 'НДС, номер');
    @defineDocumentAggregationDetailProperty (invoice, invoiceCreditNote, valueVAT, 'НДС, %');
    valueVAT (InvoiceCreditNoteDetail creditNoteDetail) += valueVAT(creditNoteDetail);

    invoicePrice (InvoiceCreditNoteDetail detail) += invoicePrice(invoiceDetail(detail));

    VATSum 'Сумма НДС' (InvoiceCreditNoteDetail invoiceCreditNoteDetail) = NUMERIC[18,4](round((sum(invoiceCreditNoteDetail) *
    valueVAT (invoiceCreditNoteDetail) / 100), currency(invoiceCreditNoteDetail)));
    
    invoiceSum 'Сумма с НДС' (InvoiceCreditNoteDetail invoiceCreditNoteDetail) = VATSum(invoiceCreditNoteDetail) (+)
        sum(invoiceCreditNoteDetail);
    overVATSum = ABSTRACT NUMERIC[18,4](InvoiceCreditNoteDetail);
    overInvoiceSum = ABSTRACT NUMERIC[18,4](InvoiceCreditNoteDetail);
    VATSum (InvoiceCreditNoteDetail creditNoteDetail) += OVERRIDE overVATSum(creditNoteDetail), VATSum(creditNoteDetail);
    invoiceSum (InvoiceCreditNoteDetail creditNoteDetail) += OVERRIDE overInvoiceSum(creditNoteDetail), invoiceSum(creditNoteDetail);
    
    edit(InvoiceCreditNote c) + {  edit(invoice(c)); }
    edit(InvoiceCreditNoteDetail d) + {  edit(invoiceDetail(d)); }
    
    overDocumentQuantity = ABSTRACT NUMERIC[16,5] (InvoiceCreditNoteDetail);
    documentQuantity (InvoiceCreditNoteDetail d) += OVERRIDE overDocumentQuantity(d), quantity(invoiceDetail(d)); 
    documentSum (InvoiceCreditNoteDetail d) += sum(invoiceDetail(d)); 
    documentVATSum (InvoiceCreditNoteDetail d) += VATSum(invoiceDetail(d));        
    documentInvoiceSum (InvoiceCreditNoteDetail d) += invoiceSum(invoiceDetail(d)); 
    
    overShipmentQuantity = ABSTRACT NUMERIC[16,5] (InvoiceCreditNoteDetail);
    shipmentQuantity (InvoiceCreditNoteDetail d) += OVERRIDE overShipmentQuantity(d), shipped(invoiceDetail(d)); 
    shipmentSum (InvoiceCreditNoteDetail d) = NUMERIC[18,4](round(((OVERRIDE overShipmentQuantity(d), shipped(invoiceDetail(d))) *
                                     price(d)), currency(d)));
    shipmentSum (InvoiceCreditNoteDetail d) += shipmentSum(d);
    shipmentVATSum (InvoiceCreditNoteDetail d) = NUMERIC[18,4](round((shipmentSum(d) *
                                  valueVAT (d) / 100), currency(d)));   
    shipmentVATSum (InvoiceCreditNoteDetail d) += shipmentVATSum(d);        
    shipmentInvoiceSum (InvoiceCreditNoteDetail d) += shipmentVATSum(d) (+) shipmentSum(d);   
   
    overCreditPrice = ABSTRACT NUMERIC[16,4](InvoiceDetail);
    quantityCredit(InvoiceDetail d) = (quantity(d) (-) shipmentQuantity(d)) IF NOT isCharge(sku(d)); 
    sumCredit (InvoiceDetail d) = NUMERIC[18,4](round2(quantityCredit(d) * (OVERRIDE overCreditPrice(d), price(d)))) PATTERN '#,##0.00##';                                                                                 
    VATSumCredit (InvoiceDetail d) = NUMERIC[18,4](round2(sumCredit(d) * valueVAT (d) / 100)) PATTERN '#,##0.00##';                                                                                                                              
    invoiceSumCredit (InvoiceDetail d) = VATSumCredit(d) (+) sumCredit(d) PATTERN '#,##0.00##';

    TABLE creditInvoice(Invoice);
    quantityCredit(Invoice i) = GROUP SUM quantityCredit(InvoiceDetail d) IF invoice(d)==i MATERIALIZED TABLE creditInvoice;
    sumCredit (Invoice i) =  GROUP SUM sumCredit(InvoiceDetail d) IF invoice(d)==i MATERIALIZED TABLE creditInvoice PATTERN '#,##0.00##';                                                                        
    VATSumCredit (Invoice i) =  GROUP SUM VATSumCredit(InvoiceDetail d) IF invoice(d)==i MATERIALIZED TABLE creditInvoice PATTERN '#,##0.00##';                                                                                                                        
    invoiceSumCredit (Invoice i) =  VATSumCredit(i) (+) sumCredit(i) PATTERN '#,##0.00##';
       
    creditQuantity 'Кол-во (с учетом акта)' (InvoiceDetail d) = quantity(d) (-) (quantityCredit(d) IF createShipment(d));
    creditSum 'Сумма (с учетом акта)' (InvoiceDetail d) = sum(d) (-) (sumCredit(d) IF createShipment(d));
    creditVATSum 'Сумма НДС (с учетом акта)' (InvoiceDetail d) = VATSum(d) (-) (VATSumCredit(d) IF createShipment(d));
    creditInvoiceSum 'Сумма с НДС (с учетом акта)' (InvoiceDetail d) = invoiceSum(d) (-) (invoiceSumCredit(d) IF createShipment(d));    
    
    creditQuantityInvoiceDetail 'Кол-во (с учетом акта)' (Invoice i) = quantityInvoiceDetail(i) (-) (quantityCredit(i) IF createShipment(i));  
    creditSumInvoiceDetail 'Сумма (с учетом акта)' (Invoice i) = sumInvoiceDetail(i) (-) (sumCredit(i) IF createShipment(i));  
    creditVATSumInvoiceDetail 'Сумма НДС (с учетом акта)' (Invoice i) = VATSumInvoiceDetail(i) (-) (VATSumCredit(i) IF createShipment(i));  
    creditInvoiceSumInvoiceDetail 'Сумма с НДС (с учетом акта)' (Invoice i) = invoiceSumInvoiceDetail(i) (-) (invoiceSumCredit(i) IF createShipment(i));  
    

   
    showCreditSum 'Показывать в накладной '##sign##' суммы с учетом акта расхождения' = DATA BOOLEAN ();    
    backgroundCreditNote 'Цвет' (Invoice invoice) = RGB(255, 250, 205) IF invoice IS Invoice;
    EXTEND FORM options
        PROPERTIES() NS###showCreditSum = showCreditSum
    ;
    
    
    DESIGN options {
        invoice##NS {    
            MOVE PROPERTY(NS###showCreditSum);
        }         
    }
    
    EXTEND FORM invoices
        PROPERTIES (i) READONLY SHOWIF showCreditSum() BACKGROUND backgroundCreditNote(i) creditQuantityInvoiceDetail, creditSumInvoiceDetail, creditVATSumInvoiceDetail, creditInvoiceSumInvoiceDetail
        PROPERTIES (d) READONLY SHOWIF showCreditSum() BACKGROUND backgroundCreditNote(i) creditQuantity, creditSum, creditVATSum, creditInvoiceSum
    ;
    EXTEND FORM userInvoice 
        PROPERTIES (i) READONLY SHOWIF showCreditSum() BACKGROUND backgroundCreditNote(i) creditQuantityInvoiceDetail, creditSumInvoiceDetail, creditVATSumInvoiceDetail, creditInvoiceSumInvoiceDetail

        OBJECTS dcn = UserInvoiceDetail
        PROPERTIES (dcn) READONLY index, idBarcodeSku, idSku, nameSku, shortNameUOMSku, nameBatch, quantity
        PROPERTIES (dcn) READONLY BACKGROUND backgroundCreditNote(i) creditQuantity, creditSum, creditVATSum, creditInvoiceSum
        FILTERS userInvoice(dcn) = i
    ;
    DESIGN userInvoice {
        specificationBox {
            NEW creditNoteContainer AFTER BOX(d) {
                caption = 'Акт расхождений';
                showIf = showCreditSum();
                NEW creditNoteSum {
                    horizontal = TRUE;
                    caption = 'Суммы акта';
                
                }
                NEW creditNoteInvoice {
                    horizontal = TRUE;
                    caption = 'Суммы с учетом акта';
                    MOVE PROPERTY(creditQuantityInvoiceDetail(i)) {caption = 'Кол-во (всего)';}
                    MOVE PROPERTY(creditSumInvoiceDetail(i)) {caption = 'Сумма';}
                    MOVE PROPERTY(creditVATSumInvoiceDetail(i)) {caption = 'Сумма НДС';}
                    MOVE PROPERTY(creditInvoiceSumInvoiceDetail(i)) {caption = 'Сумма с НДС';}                               
                }            
                MOVE BOX(dcn);
            }
        }
    }
    
END

META defineCreditNoteBatch(legalEntityProp, stockProp)
    @showDocumentInterfaceDetailBatch(creditNote);

    disableShowBatch[CreditNote.CreditNote] (CreditNote creditNote) += disableShowBatch(creditNote);

    @defineDocumentInterfaceDetailBatch(creditNote, batch);

    batch[CreditNote.CreditNoteDetail] (CreditNoteDetail creditNoteDetail) += batch(creditNoteDetail);


    //    EXTEND FORM userCreditNote PROPERTIES (d) nameBatch AFTER shortNameUOMSku(d);
//    EXTEND FORM creditNotes PROPERTIES (d) READONLY nameBatch AFTER shortNameUOMSku(d);


    batch (UserCreditNoteDetail detail)  <- batch(invoiceDetail(detail))
        WHEN CHANGED(invoiceDetail(detail));

    @defineDocumentAggregationDetailBatch (invoice, invoiceCreditNote);
    batch (InvoiceCreditNoteDetail creditNoteDetail) += batch(creditNoteDetail);
    
    // История по правой кнопке
    @defineBalancesBatch(creditNoteDetail); //-- показываем по нажатию правой клавиши остатки партии
    @defineBalancesBatch(userCreditNoteDetail); //-- показываем по нажатию правой клавиши остатки партии
    @defineMovementBatch(creditNoteDetail, stockProp###stock); //-- показываем по нажатию правой клавиши движение по партии
    @defineMovementBatch(userCreditNoteDetail, stockProp###stock); //-- показываем по нажатию правой клавиши движение по партии    
    
    @defineDocumentBatchStockPriceListTypeSystemLedger(userCreditNote, stockProp);
    @extendFormDocumentBatchStock(userCreditNote, userCreditNote, c, legalEntityProp, stockProp);
END

// Партии с диалогом по складам
META defineCreditNoteBatchDialogStock(stockProp, dialog)

    changeBatch(UserCreditNoteDetail d)  { 
        DIALOG dialogBatchStock##dialog OBJECTS 
                                    st = stockProp(d),
                                    t = dateTime(d),
                                    sk = sku(d),
                                    bt = batch(d) INPUT NULL NEWSESSION DO {
            batch(d) <- bt;                            
        }
    }
    EXTEND FORM userCreditNote PROPERTIES (d) nameBatch ON CHANGE changeBatch(d) AFTER shortNameUOMSku(d);
    EXTEND FORM creditNotes PROPERTIES (d) READONLY nameBatch AFTER shortNameUOMSku(d);
END

META defineCreditNoteBatchDialog(dumb)

    changeBatch(UserCreditNoteDetail userCreditNoteDetail)  { 
        DIALOG dialogBatch OBJECTS sk = sku(userCreditNoteDetail), bt = batch(userCreditNoteDetail) INPUT NULL NEWSESSION DO
            batch(userCreditNoteDetail) <- bt;
    }

    EXTEND FORM userCreditNote PROPERTIES (d) nameBatch ON CHANGE changeBatch(d) AFTER shortNameUOMSku(d);
    EXTEND FORM creditNotes PROPERTIES (d) READONLY nameBatch AFTER shortNameUOMSku(d);

END

META defineCreditNotePaymentCondition (sign)

    paymentCondition 'Условия оплаты' = ABSTRACT PaymentCondition (CreditNote) MATERIALIZED;
    paymentCondition 'Условия оплаты' = DATA PaymentCondition (UserCreditNote);
    paymentCondition(UserCreditNote prop) += paymentCondition(prop);
    paymentCondition[CreditNote.CreditNote] (CreditNote creditNote) += paymentCondition(creditNote);
    
    descriptionPaymentCondition 'Условия оплаты' (CreditNote prop) = description(paymentCondition(prop)) IN base CHARWIDTH 10;
    descriptionPaymentCondition 'Условия оплаты' (UserCreditNote prop) = description(paymentCondition(prop)) CHARWIDTH 10;

    paymentCondition(UserCreditNote prop) <- paymentCondition(contractSku(prop))
        WHEN CHANGED(contractSku(prop));

    EXTEND FORM userCreditNote
        PROPERTIES(c) descriptionPaymentCondition
    ;

    DESIGN userCreditNote{
        GROUP(documentPrm,c){
            MOVE PROPERTY(descriptionPaymentCondition(c));
        }
    }

END
//-- Для логирования объекта
META defineCreditNoteLog(NS, char, sign)
    EXTEND CLASS CreditNote : Document ;
    EXTEND CLASS CreditNoteDetail : DocumentDetail;

    @implementDocumentSupplierCustomer(creditNote);
    @implementDocumentForm(NS.###creditNote, char##i, 'Акт расхождения'###sign, NS###creditNote);

    @defineDocumentLogForm(creditNotes, c);
END

META fillCreditNoteShipmentQuantity (param)
    skipChangedShipmentQuantity = ABSTRACT BOOLEAN (UserCreditNoteDetail);
    shipmentQuantity(UserCreditNoteDetail detail) <- quantity(detail)
        WHEN CHANGED(quantity(detail)) AND NOT skipChangedShipmentQuantity(detail);
    
    @defineDocumentHeaderQuantityCustomPrefix (userCreditNote, userCreditNoteDetail, shipment, ' по факту');
END

META defineUserCreditNoteFormProperties (param)
    backgroundShipped 'Цвет' (CreditNote creditNote) = RGB(255, 224, 255) IF creditNote IS CreditNote;
    backgroundShipped (CreditNoteDetail detail) = backgroundShipped(creditNote(detail));
    
    EXTEND FORM userCreditNote
        PROPERTIES(c) BACKGROUND backgroundShipped(c) READONLY shipmentQuantityUserCreditNoteDetail, shipmentSumUserCreditNoteDetail
        PROPERTIES(d) BACKGROUND backgroundShipped(d) shipmentQuantity, shipmentPrice, shipmentSum
    ;
    
    EXTEND FORM creditNotes
        PROPERTIES(c) BACKGROUND backgroundShipped(c) READONLY shipmentQuantityUserCreditNoteDetail, shipmentSumUserCreditNoteDetail
        PROPERTIES(d) BACKGROUND backgroundShipped(d) READONLY shipmentQuantity[UserCreditNoteDetail], shipmentPrice, shipmentSum[UserCreditNoteDetail]
    ;
END
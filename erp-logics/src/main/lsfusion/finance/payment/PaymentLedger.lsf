MODULE PaymentLedger;

REQUIRE LegalEntity;

// ---------------- Счет ------------- //

CLASS ABSTRACT Account 'Счет';
TABLE account (Account);
name 'Имя' = ABSTRACT STRING[30] (Account) MATERIALIZED;

inactive 'Неактивная' = ABSTRACT BOOLEAN (Account);
active 'Активная' (Account a) = a IS Account AND NOT inactive(a);

currency = ABSTRACT Currency (Account);
nameCurrency 'Валюта счета' = name(currency(Account account));

legalEntity = ABSTRACT LegalEntity (Account);
nameLegalEntity 'Организация' (Account account) = name(legalEntity(account));

FORM accounts 'Счета'
    OBJECTS a = Account
    PROPERTIES(a) READONLY name, nameCurrency, nameLegalEntity
    PROPERTIES(a) NEWSESSION EDIT, DELETE 
    FILTERGROUP inactive FILTER 'Активный' active(a) 'ctrl F10' DEFAULT  
    LIST Account OBJECT a
;

// ---------------- Регистр денег ------------- //

CLASS ABSTRACT PaymentLedger 'Движение денег';
TABLE paymentLedger(PaymentLedger);

dateTime 'Дата/время' = ABSTRACT DATETIME (PaymentLedger) MATERIALIZED;

legalEntityFrom = ABSTRACT LegalEntity (PaymentLedger) MATERIALIZED;
nameLegalEntityFrom 'Организация-плательщик' = name(legalEntityFrom(PaymentLedger ledger));

accountFrom = ABSTRACT Account (PaymentLedger) MATERIALIZED;
nameAccountFrom 'Счет (с)' (PaymentLedger ledger) = name(accountFrom(ledger));

legalEntityTo = ABSTRACT LegalEntity (PaymentLedger) MATERIALIZED;
nameLegalEntityTo 'Организация-получатель' = name(legalEntityTo(PaymentLedger ledger));

accountTo = ABSTRACT Account (PaymentLedger) MATERIALIZED;
nameAccountTo 'Счет (на)' (PaymentLedger ledger) = name(accountTo(ledger));

sum 'Сумма' = ABSTRACT NUMERIC[18,4] (PaymentLedger) MATERIALIZED;

description 'Описание' = ABSTRACT STRING[200] (PaymentLedger) MATERIALIZED;

isPosted 'Проведен' = ABSTRACT BOOLEAN(PaymentLedger);

FORM paymentLedger 'Движение денег'
    OBJECTS pl = PaymentLedger
    PROPERTIES(pl) READONLY isPosted, description, dateTime, nameLegalEntityFrom,
                            nameAccountFrom, nameLegalEntityTo, nameAccountTo,
                            sum
;

NAVIGATOR {
    financeNavigator {
        NEW FOLDER paymentNavigator 'Движение денежных средств' {
            NEW paymentLedger;
        }
    }
}

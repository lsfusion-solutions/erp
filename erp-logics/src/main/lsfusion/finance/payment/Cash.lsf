MODULE Cash;

REQUIRE LegalEntity;

// ----------------------------------- Кассовый счет ------------------------------------------ //

CLASS Account 'Наличный счет';
TABLE account (Account);

// Неактивный
inactive 'Неактивная' = DATA BOOLEAN (Account);
active 'Активная' (Account a) = a IS Account AND NOT inactive(a);

number 'Номер наличного счета'  = DATA STRING[20] (Account) CHARWIDTH 13 NOFLEX;

legalEntity = DATA LegalEntity (Account);
nameLegalEntity 'Организация' = name(legalEntity(Account account));
defaultAccount(legalEntity) = GROUP MAX Account account IF active(account) BY legalEntity(account);

currency = DATA Currency (Account) NONULL;
currency(Account account) <- currency(legalEntity(account))
    WHEN SET(legalEntity(account));
nameCurrency 'Валюта счета' = name(currency(Account account));

FORM account 'Наличный счет'
    OBJECTS a = Account PANEL
    PROPERTIES(a) number, nameCurrency, nameLegalEntity, inactive

    EDIT Account OBJECT a
;

FORM accounts 'Наличные счета'
    OBJECTS a = Account
    PROPERTIES(a) READONLY number, nameCurrency, nameLegalEntity
    PROPERTIES(a) NEWSESSION NEW, EDIT, DELETE 
    FILTERGROUP inactive FILTER 'Активный' active(a) 'ctrl F10' DEFAULT  
    LIST Account OBJECT a
;

EXTEND FORM legalEntity
    OBJECTS ca=Account
    PROPERTIES(ca)  inactive, number, nameCurrency, NEW, DELETE GRID
    FILTERS legalEntity(ca) == l
    FILTERGROUP inactiveca FILTER 'Активный' active(ca) 'ctrl F10' DEFAULT  
;

DESIGN legalEntity {
    extendContainer {
        MOVE BOX(ca) {
            caption = 'Наличные счета';
        }
    }
}
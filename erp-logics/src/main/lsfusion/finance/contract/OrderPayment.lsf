MODULE OrderPayment;

REQUIRE Order, Payment, ContractPayment, OrderInvoice, SaleInvoiceContract, PurchaseInvoiceContract, SaleOrder, PurchaseOrder;

NAMESPACE Order;


TABLE orderPayment(Order, Payment);

showOrder 'Предоплата' = DATA BOOLEAN (PaymentOperation.Operation);

EXTEND FORM PaymentOperation.operation
    PROPERTIES(o) showOrder
;
DESIGN PaymentOperation.operation {
    paramsContainer {
        MOVE PROPERTY(showOrder(o));
    }
}

minOrder (Invoice.Invoice i) = GROUP MIN Order o IF quantity (o, i); 

prePay 'Предоплата' = DATA NUMERIC[18,4] (Payment, Order);

prePay 'Предоплата' (Order o)  =
    GROUP 
    SUM prePay(Payment p, o) IF
    active[OutContractLedger](p) ;
prePay 'Предоплата' (Payment p)  =
    GROUP
    SUM prePay(p, Order o) IF
    isPosted(o) ;
    
prePay (OutContractLedger o) += prePay[Payment](o) IF o IS Payment;    
showOrder (Payment p) = showOrder(operation(p));

EXTEND FORM payment
    PROPERTIES (p) READONLY SHOWIF showOrder(p) prePay
    OBJECTS o = Order GRID 

    PROPERTIES(o) READONLY SHOWIF showOrder(p) date, description, invoiceSumOrderDetail
    PROPERTIES(p, o) SHOWIF showOrder(p) prePay
    ORDERS date(o)

    FILTERS contractSku(o) == contract(p),
            isPosted(o)
    FILTERGROUP filtersO 
        FILTER 'Актуальные' (NOT prePay(o)) OR prePay(p,o) 'F6' DEFAULT
    
    PROPERTIES (p,i) READONLY prePay
;

DESIGN payment {
    BOX {
        NEW tab BEFORE TOOLBARBOX {
            fill = 1;
            type = TABBED;
            MOVE BOX(i) ;
            MOVE BOX(o) {
                caption = 'Предоплата';
            }
        }
        paramContainer {
            MOVE PROPERTY (prePay(p));
        }
        PROPERTY (prePay(p)){background = RGB(255, 250, 205);}
    }
}

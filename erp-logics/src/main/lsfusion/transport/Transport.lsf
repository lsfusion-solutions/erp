MODULE Transport;

REQUIRE System,
        Utils,
        Employee,
        LegalEntity,
        Stock;

// Модели машины
CLASS TruckModel 'Модель';
TABLE truckModel (TruckModel);

name 'Наименование' = DATA ISTRING[50](TruckModel);

FORM truckModel 'Модель машины'
    OBJECTS m=TruckModel PANEL
    PROPERTIES (m) name

    EDIT TruckModel OBJECT m
;

FORM truckModels 'Модели машин'
    OBJECTS m=TruckModel
    PROPERTIES(m) READONLY name
    PROPERTIES(m) NEWSESSION NEW, EDIT, DELETE 

    LIST TruckModel OBJECT m
;

// Машины
CLASS Truck 'Машина';
TABLE truck(Truck);

sid 'Номер' = DATA ISTRING[20](Truck);

truckModel = DATA TruckModel(Truck);
nameTruckModel 'Марка' (Truck truck) = name(truckModel(truck));

name 'Наименование' (Truck truck) = ISTRING[70](CONCAT ',', nameTruckModel(truck), 'г.н. '+sid(truck));

trailer 'Прицеп' = DATA ISTRING[100](Truck);
weight 'Грузоподъёмность (кг)' = DATA NUMERIC[16,4](Truck);
owner 'Владелец' = DATA ISTRING[200](Truck);
notOwnertruck = NOT owner(Truck truck);

legalEntity = DATA LegalEntity (Truck);
nameLegalEntity 'Владелец(Организация)' = fullName(legalEntity(Truck truck));
backgroundLegalEntity (Truck t) = RGB(224,224,224) IF owner(t);
nameOwner 'Владелец' = OVERRIDE owner(Truck t), nameLegalEntity(t); 

inactive 'Неактивный' = DATA BOOLEAN (Truck);
active 'Активный' (Truck t) =  t IS Truck AND NOT inactive(t);

FORM truck 'Машина'
    OBJECTS t=Truck PANEL
    PROPERTIES (t) inactive, sid, nameTruckModel, trailer, weight, 
                   owner, nameLegalEntity BACKGROUND backgroundLegalEntity(t)

    EDIT Truck OBJECT t
;

DESIGN truck {
    OBJECTS { 
        NEW pane {
            NEW main {
                caption = 'Основные';
                horizontal = TRUE;
                alignment = STRETCH;
                MOVE PROPERTY(sid(t));
                MOVE PROPERTY(nameTruckModel(t));
                MOVE PROPERTY(inactive(t));
            }
            NEW params {
                caption = 'Параметры';
                horizontal = TRUE;
                alignment = STRETCH;
                MOVE PROPERTY(trailer(t));
                MOVE PROPERTY(weight(t));
            }
            NEW owner {
                caption = 'Владелец';
                horizontal = TRUE;
                alignment = STRETCH;
                MOVE PROPERTY(owner(t));
                MOVE PROPERTY(nameLegalEntity(t));
            }
        }
    }
}

FORM trucks 'Машины'
    OBJECTS t=Truck
    PROPERTIES(t) READONLYIF isReadonly() sid, nameTruckModel, name, trailer, weight, nameOwner, inactive
    PROPERTIES(t) SHOWIF isReadonly() NEWSESSION NEW, EDIT, DELETE
    PROPERTIES(t) SHOWIF NOT isReadonly() new2 = NEW, del2 = DELETE 
    FILTERGROUP active FILTER 'Активные' active(t) 'F6'  DEFAULT
    
    LIST Truck OBJECT t
;
@extendFormEditable(trucks);

// Водители

CLASS Driver 'Водитель': Employee;

sid 'Табельный номер' = DATA BPSTRING[10](Driver);
type 'Класс' = DATA BPSTRING[10](Driver);

defaultTruck = DATA Truck(Driver);
nameDefaultTruck 'Машина по умолчанию' = name(defaultTruck(Driver driver));


FORM driver 'Водитель'
    OBJECTS e=Driver PANEL
    PROPERTIES(e)      lastName, firstName, initialsEmployee, id SHOWIF showIDs(), namePosition,
                       sid, type, nameDefaultTruck, inactive//nameMainRoleUser

    EDIT Driver OBJECT e
;

DESIGN driver {
    BOX {
        size = (1024, 768);
        NEW header {
            horizontal = FALSE;
            NEW personal {
                caption = 'Личные данные';
                lines = 5;
                MOVE PROPERTY(lastName(e));
                MOVE PROPERTY(firstName(e));   
                MOVE PROPERTY(initialsEmployee(e));                  
                MOVE PROPERTY(id(e));
                MOVE PROPERTY(namePosition(e));
                MOVE PROPERTY(inactive(e));
            }
            NEW driver {
                caption = 'Данные водителя';
                horizontal = TRUE;
                MOVE PROPERTY(nameDefaultTruck(e));
                MOVE PROPERTY(sid(e));
                MOVE PROPERTY(type(e));
            }
        }
        MOVE TOOLBARBOX;
    }
}
FORM drivers 'Водители'
    PROPERTIES isEditable()
    OBJECTS e=Driver
    PROPERTIES(e) READONLYIF isReadonly() lastName, firstName, initialsEmployee, id SHOWIF showIDs(), 
                  namePosition, shortName, sid, type, nameDefaultTruck, inactive//nameMainRoleUser
    PROPERTIES(e) NEWSESSION NEW, EDIT, DELETE 
    LIST Driver OBJECT e
    FILTERGROUP inactive FILTER 'Активные' active(e) 'F6' DEFAULT
;

DESIGN drivers {
    MOVE TOOLBARBOX{
        MOVE PROPERTY(isEditable()) BEFORE TOOLBARRIGHT;
    }
}

NAVIGATOR {
    NEW FOLDER transportNavigator 'Транспорт' WINDOW toolbar IMAGE 'transport.png' {
        NEW FOLDER transportDocuments 'Документы';
        NEW FOLDER transportMasterData 'Справочники' {            
            NEW trucks;
            NEW truckModels;
            NEW drivers;
        }
    }
}

META defineDocumentTransport(doc, obj, container)
    truck = DATA Truck (###doc);
    nameTruck 'Автомобиль' (###doc i) = name(truck(i));
    
    EXTEND FORM doc
        PROPERTIES(obj) nameTruck
    ;
    
    DESIGN doc {
        container {
            NEW transport {
                caption = 'Транспорт';
                MOVE PROPERTY(nameTruck(obj));
            }
        }
    }
END
MODULE RouteGeo;

REQUIRE Route, Geo;

NAMESPACE Route;

showPath 'Показать путь' (Route r)  { 
    numberPath(POI stock) <- OVERRIDE number(stock, r), 1 IF in(stock, r);
    showOnPath(showMapProvider());
} TOOLBAR;

EXTEND FORM route         
    OBJECTS nm = Node MAP
    PROPERTIES     READONLY name = (CONCAT ' ', (number(nm, r) IF in(nm, r)) + '.', name(nm)) 
    PROPERTIES(nm) longitude, latitude
    PROPERTIES READONLY color = RGB(255,0,0) IF in(nm, r) 
    PROPERTIES(nm) READONLY PANEL address
    PROPERTIES(nm, r) PANEL in ON CHANGE changeIn(nm, r), 'Номер' = number
    PROPERTIES(r) DRAW nm TOOLBAR showPath
    
    FILTERS longitude(nm) AND latitude(nm)

    FILTERGROUP selectedM
        FILTER 'Отмеченные' in(nm, r) DEFAULT
;

DESIGN route {
    tabbedPane {
        MOVE BOX(nm) {
            caption = 'Карта';
            GRID(nm) {
                MOVE PROPERTY(address(nm));
                MOVE PROPERTY(in(nm, r));
                MOVE PROPERTY(number(nm, r));
            }
        }
    }
}
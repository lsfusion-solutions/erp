MODULE Trip;

REQUIRE Document, Route;

NAMESPACE Trip;

CLASS Trip 'Рейс';
TABLE trip(Trip);

@defineDocumentHeaderTime(Trip);
@deriveDocumentHeaderTimePrefix(Trip, );

@defineDocumentHeaderNumber(Trip, BPSTRING[2]);
@defineNumeratedDefault(Trip, 'Рейсы', 'РЕ');

trip (number) = GROUP MAX Trip trip BY number(trip);
skipAggrTrip 'Номер рейса может быть неуникальным' = DATA BOOLEAN ();
EXTEND FORM options
    PROPERTIES () skipAggrTrip
;
DESIGN options{
    commons{
        MOVE PROPERTY (skipAggrTrip());
    }
}

//CONSTRAINT SETCHANGED(number(Trip trip)) AND [GROUP SUM 1 BY number(Trip p)](number(trip))>1 AND NOT skipAggrTrip()
//    MESSAGE 'Нарушена уникальность свойства "Номер рейса"' ;

CONSTRAINT SETCHANGED([GROUP SUM 1 BY number(Trip p)](number(Trip trip))>1) AND NOT skipAggrTrip()
    MESSAGE 'Нарушена уникальность свойства "Номер рейса"' ;

driver = DATA Driver (Trip) INDEXED;
nameDriver 'Водитель' (Trip trip) = name[Contact](driver(trip));
sidDriver 'Табельный номер' (Trip trip) = sid(driver(trip));
typeDriver 'Класс' (Trip trip) = type(driver(trip));

truck = DATA Truck (Trip) INDEXED;
sidTruck 'Машина (номер)'(Trip trip) = sid(truck(trip));
weightTruck 'Грузоподъёмность (кг)'(Trip trip) = weight(truck(trip));
nameOwnerTruck 'Владелец'(Trip trip) = nameOwner(truck(trip));
nameTruckModelTruck 'Марка' (Trip trip) = nameTruckModel(truck(trip));
trailerTruck 'Прицеп' (Trip trip) = trailer(truck(trip));
nameTruck 'Автомобиль' (Trip trip) = name(truck(trip));

redirect 'Перенаправление' = DATA STRING (Trip);
forwarder 'Экспедитор' = DATA STRING (Trip);

supplier = DATA LegalEntity (Trip) INDEXED;
nameSupplier 'Организация (поставщик)' (Trip t) = name(supplier(t));
customer = DATA LegalEntity (Trip) INDEXED;
nameCustomer 'Организация (покупатель)' (Trip t) = name(customer(t));

description 'Рейс' (Trip o) = CONCAT ' ', seriesNumber(o), toDateDDMMYY(date(o)) CHARWIDTH 20; 

FORM trip 'Рейс'
    OBJECTS t = Trip PANEL
    PROPERTIES(t) nameNumerator, number, series, date, time, 
                  nameDriver, sidTruck, nameTruckModelTruck, trailerTruck, weightTruck,
                  redirect, forwarder,
                  nameSupplier, nameCustomer
    
    EDIT Trip OBJECT t
;


DESIGN trip {
    NEW header {
        NEW headerTop {
            caption = 'Шапка документа';
            type = CONTAINERH;
            MOVE PROPERTY(nameNumerator(t));
            MOVE PROPERTY(number(t));
            MOVE PROPERTY(series(t));
            MOVE PROPERTY(date(t));
            MOVE PROPERTY(time(t));
        }
        NEW headerCenter {
            type = CONTAINERH;
            NEW truckContainer {
                type = COLUMNS;
                lines = 3;
                caption = 'Параметры машины';
                MOVE PROPERTY(nameDriver(t));
                MOVE PROPERTY(sidTruck(t));
                MOVE PROPERTY(nameTruckModelTruck(t));
                MOVE PROPERTY(trailerTruck(t));
                MOVE PROPERTY(weightTruck(t));
                MOVE PROPERTY(redirect(t));
                MOVE PROPERTY(forwarder(t));
            }

            NEW headerSum {
                caption = 'Итоги';
                type = COLUMNS;
                lines = 1;
            }
        }

        NEW headerFilter {
            type = CONTAINERH;
            caption = 'Параметры';
            MOVE PROPERTY(nameSupplier(t)) {
                caption = 'Поставщик';
            }
            MOVE PROPERTY(nameCustomer(t)){
                caption = 'Покупатель';
            }
        }
    }

    NEW pane {
        fill = 1;
        type = TABBED;
    }
    MOVE TOOLBARBOX;
}

overCopy  ABSTRACT LIST ( Trip, Trip);
copy 'Копировать'(Trip trip)  { 
	NEWSESSION {
	    NEW t = Trip {
	        driver(t) <- driver(trip);
	        truck(t) <- truck(trip);
	
	        EXEC overCopy(t, trip);
	        SHOW trip OBJECTS t = t DOCKED;
	    }
	}
} TOOLBAR;

backgroundTrip 'Цвет' = ABSTRACT CASE COLOR(Trip);

FORM trips 'Рейсы'
    OBJECTS dt = DATE PANEL
    PROPERTIES(dt) valDt = VALUE

    OBJECTS t=Trip
    PROPERTIES(t) READONLY BACKGROUND backgroundTrip(t) number, series, date, time 
    PROPERTIES(t) READONLY nameDriver, sidTruck, nameTruckModelTruck, trailerTruck, weightTruck
    PROPERTIES(t) NEWSESSION NEW, EDIT, DELETE
    PROPERTIES(t)            copy 
    
    FILTERGROUP filterTrips
        FILTER 'Рейсы на дату' date(t) == dt 'F10' DEFAULT    
;

DESIGN trips {
    MOVE BOX(dt);
    MOVE BOX(t);

    NEW pane {
        fill = 2;
        type = TABBED;
    }

    MOVE TOOLBARBOX;
}

FORM dialogDateTrips 'Рейсы'
    OBJECTS d=DATE PANEL 
    PROPERTIES(d) VALUE
    
    OBJECTS t = Trip
    PROPERTIES(t) READONLY number, date, nameDriver
    FILTERGROUP filters FILTER 'Рейсы на дату' date(t)>=d 'F6' DEFAULT 
;

NAVIGATOR {
    transportDocuments {
        NEW trips BEFORE routes;
    }
}
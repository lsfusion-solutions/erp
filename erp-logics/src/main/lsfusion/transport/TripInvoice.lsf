MODULE TripInvoice;

REQUIRE OrderInvoice, Shipment, Trip;

NAMESPACE Trip;

trip = DATA Trip(Invoice) INDEXED;
numberTrip 'Номер рейса' = number(trip(Invoice i));

in 'Включен' (Trip trip, Invoice invoice) = trip(invoice) == trip;

countInvoice 'Кол-во накладных в рейсе' (Trip trip) = GROUP SUM 1 IF in(trip, Invoice invoice) MATERIALIZED;

quantityInvoiced 'Кол-во в накладных' (trip, sku) = GROUP SUM quantity(InvoiceDetail invoiceDetail) BY trip(invoice(invoiceDetail)), sku(invoiceDetail);

invoiceSum 'Сумма с НДС накладных' (trip) = GROUP SUM invoiceSumInvoiceDetail(Invoice invoice) BY trip(invoice);

grossWeightInvoiced 'Суммарный вес накладных' (trip) = GROUP SUM sumGrossWeightInvoiceDetail(Invoice invoice) BY trip(invoice);
netWeightInvoiced 'Суммарный вес накладных (нетто)' (trip) = GROUP SUM sumNetWeightInvoiceDetail(Invoice invoice) BY trip(invoice);

EXTEND FORM trip

    PROPERTIES (t) READONLY grossWeightInvoiced, invoiceSum

    OBJECTS i=Invoice
    PROPERTIES (t, i) in
    PROPERTIES (i) READONLY number, series, date, nameFrom, nameFromStock, nameTo, 
                   nameToStock, invoiceSumInvoiceDetail, sumGrossWeightInvoiceDetail, sumNetWeightInvoiceDetail
    PROPERTIES (i) NEWSESSION editT = EDIT SHOWIF isOpened(i)
    FILTERGROUP filterDateInvoice
        FILTER 'Текущие' date(i) <= date(t) 'F9' DEFAULT
    FILTERGROUP filterInvoice
        FILTER 'Не расписанные или в текущем рейсе' in(t, i) OR NOT trip(i) 'F11'
        FILTER 'В текущем рейсе' in(t, i) 'F10' DEFAULT

    FILTERS customer(i) == customer(t) OR (i IS Invoice AND NOT customer(t)),
            supplier(i) == supplier(t) OR (i IS Invoice AND NOT supplier(t))

    OBJECTS id=InvoiceDetail
    PROPERTIES (id) READONLY idBarcodeSku, nameSku, shortNameUOMSku, quantity, 
                    nameFromStock, nameToStock, sumGrossWeight, sumNetWeight
    FILTERS invoice(id)==i
;

DESIGN trip {
    headerSum {
        MOVE PROPERTY(invoiceSum(t));
        MOVE PROPERTY(grossWeightInvoiced(t));
    }

    pane {
        NEW invoices {
            caption = 'Накладные';
            NEW invoiceSum {
                type = CONTAINERV;
                caption = 'Итоги';
                NEW invoiceSum1 {type = CONTAINERH;}
                NEW invoiceSum2 {type = CONTAINERH;}
            }
            MOVE BOX(i);
            MOVE BOX(id);
        }
    }
}

EXTEND FORM trips
    PROPERTIES (t) READONLY grossWeightInvoiced, invoiceSum
    OBJECTS i=Invoice
    PROPERTIES (i) READONLY number, series, date, nameFrom, nameFromStock, nameTo, nameToStock, invoiceSumInvoiceDetail
    FILTERS in(t, i)
;

DESIGN trips {
    pane {
        MOVE BOX(i) {
            caption = 'Накладные';
        }
    }
}
MODULE LotExtraSecond;

REQUIRE LotExtra;

NAMESPACE Lot;


@defineOption(pathLotImages, 'Путь сохранения изображений оценённых товаров', STRING[100], commons);

@defineOption(defaultResizeLotImageScale, 'Коэффициент сжатия изображений оценённых товаров lsf', DOUBLE, commons); 

lotImageType 'Формат файла изображения' = ABSTRACT VALUE STRING();   // по 11.08.22 был png 
overLotImageType = OVERRIDE lotImageType(), 'jpg';

saveLotImagesAction 'Сохранить изображения оценённых товаров' (DATE d) {
    IF pathLotImages() THEN {
        TRY { mkdir(pathLotImages() + '/' + d); }
        FOR createdDate(Lot l) == d AND image(l) DO {
            TRY { READ CONCAT '/', pathLotImages(), createdDate(l), id(l) + '.' + overLotImageType() TO imageFile; }
            
            IF NOT imageFile() THEN
                WRITE image(l) TO CONCAT '/', pathLotImages(), d, id(l);
        }
    }
}

restoreImage 'Восстановить изображение' (Lot l) {
    TRY { READ CONCAT '/', pathLotImages(), createdDate(l), id(l) + '.' + overLotImageType() TO imageFile; }
//    IF NOT imageFile() THEN
//            TRY { READ CONCAT '/', pathLotImages(), createdDate(l), id(l) + '.png' TO imageFile; }
    
    IF imageFile() THEN {    
        IF defaultResizeLotImageScale() THEN {
            resizeImage(imageFile(), defaultResizeLotImageScale());
            image(l) <- resizedImage();
        }
        ELSE
            image(l) <- imageFile();
    }
    ELSE 
        MESSAGE id(l) + ' файл изображения не найден' NOWAIT; 
        
    APPLY;
}

restoreLotImages 'Восстановить изображения по всем оценённым товарам за дату' (DATE d) {
    FOR createdDate(Lot l) == d DO {
        restoreImage(l);    
    }
}

EXTEND FORM lot
    PROPERTIES restoreImage(l) SHOWIF pathLotImages()
;

DESIGN lot {
    image {
        MOVE PROPERTY(restoreImage(l));
    }
}

openImage 'Просмотреть изображение' (Lot l) { 
    IF pathLotImages() AND defaultResizeLotImageScale() THEN {
        TRY { READ CONCAT '/', pathLotImages(), createdDate(l), id(l) + '.' + overLotImageType() TO imageFile; }
        open(imageFile());        
    }
    ELSE open(image(l)); 
}

EXTEND FORM lots
    PROPERTIES(l) openImage
;

DESIGN lots {
    image {
        MOVE PROPERTY(openImage(l));
    }
}
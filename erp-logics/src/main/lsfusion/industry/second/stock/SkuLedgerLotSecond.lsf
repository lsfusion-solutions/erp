MODULE SkuLedgerLotSecond;

REQUIRE SkuLedgerLot, ItemSecond, LotSecondExtra;

NAMESPACE Stock;

DESIGN currentBalanceSkuStock {
    BOX(lot) {
        caption = 'Оценённые товары';
    }
    PROPERTY (aggregated(lot)) { hide = TRUE; }
    PROPERTY (nameBatch(lot)) { hide = TRUE; }
}

costForeign 'Себестоимость (евро)'= ABSTRACT NUMERIC[16,4] (Batch) MATERIALIZED;

EXTEND FORM batches
    PROPERTIES (bt) READONLY costForeign AFTER cost(bt)
;

skipCheckBalanceLot = DATA BOOLEAN () PREREAD;

CONSTRAINT (CHANGED(currentBalance(Sku sk, Stock st)) OR CHANGED(currentBalanceLot(sk, st)) OR SET(isSecond(sk))) AND isSecond(sk)
    AND (currentBalance(sk, st) OR currentBalanceLot(sk, st)) AND NOT currentBalance(sk, st) = currentBalanceLot(sk, st)
    AND NOT skipCheckBalanceLot()
    MESSAGE 'Не совпадает текущий остаток секонд-товара по маркам';

CONSTRAINT SET(currentBalance(Lot l) > 1)
    MESSAGE 'Суммарный остаток оценённого товара по всем местам хранения больше 1';

WHEN SET(currentBalance(Lot l, Stock st) > 1) DO
    MESSAGE CONCAT ' ', 'Остаток оценённого товара', id(l), '(' + nameSku(l) + ')' , 'на месте хранения', name(st), 'больше 1' NOWAIT;

WHEN SET(currentBalance(Lot l, Stock st) < 0) DO
    MESSAGE CONCAT ' ', 'Остаток оценённого товара', id(l), '(' + nameSku(l) + ')', 'на месте хранения', name(st), 'меньше 0' NOWAIT;

currentWeightLot 'Текущий вес оценённых товаров' = GROUP SUM weight(Lot l) * currentBalance(l, Stock st) BY sku(l), st CHARWIDTH 7 MATERIALIZED;

currentWeight 'Вес остатка (кг)' (Sku sk, Stock st) = OVERRIDE currentBalance(sk, st) IF (isMix(sk) AND split(sk)), currentWeightLot(sk, st) IF isSecond(sk);
currentWeight 'Вес остатка (кг)' = GROUP SUM currentWeight(Sku sk, Stock st) IF isParent(SkuGroup sg, sk) BY sg, st;

currentSumForeignMix 'Сумма остатка (евро)' = GROUP SUM round2(costForeign(Batch bt) * currentBalance(bt, Stock st)) BY sku(bt), st;
currentSumForeignLot 'Сумма остатка (евро)' = GROUP SUM costForeign(Lot l) IF currentBalance(l, Stock st) BY sku(l), st CHARWIDTH 7 MATERIALIZED;

currentSumForeign 'Сумма остатка (евро)' (Sku sk, Stock st) = OVERRIDE currentSumForeignMix(sk, st) IF isMix(sk), currentSumForeignLot(sk, st) IF isSecond(sk);
currentSumForeign 'Сумма остатка (евро)' = GROUP SUM currentSumForeign(Sku sk, Stock st) BY st;

currentWeightAside 'Вес отложенные, кг' =  GROUP SUM weight(Lot l) * prevCurrentBalance(l, Stock st) IF setAside(l) BY sku(l), st CHARWIDTH 7;
currentWeightAside 'Вес отложенные, кг' =  GROUP SUM currentWeightAside(Sku sk, Stock st) BY st CHARWIDTH 7;

EXTEND FORM currentBalanceSkuStock
    PROPERTIES READONLY currentWeight(sk, ss)

    PROPERTIES (s, st) READONLY AFTER currentBalance(s, st) currentWeight, currentWeightAside
    PROPERTIES (s, st) READONLY AFTER currentSum(s, st) currentSumForeign

    PROPERTIES(lot) BEFORE currentBalance(lot, st) READONLY setAside, captionMix, fullDescriptionCycle, costForeign
    PROPERTIES(lot) AFTER currentBalance(lot, st) READONLY weight, price
    PROPERTIES(lot) NEWSESSION EDIT
;

MODULE ItemSizeMappingCustomsGroup;

REQUIRE ItemSizeMapping;

NAMESPACE Item;

// Рост
CLASS SizeHeight 'Рост товара';
TABLE sizeHeight (SizeHeight);
@defineExternalizable(sizeHeight, STRING[100]);

nameCustoms 'Рост' = DATA ISTRING[50](SizeHeight);

FORM sizeHeight 'Рост товара'
    OBJECTS t=SizeHeight PANEL
    PROPERTIES(t) nameCustoms, id SHOWIF showIDs()
    EDIT SizeHeight OBJECT t
;

FORM sizeHeights 'Роста товара'
    OBJECTS t=SizeHeight
    PROPERTIES(t) NEWSESSION nameCustoms READONLY, id SHOWIF showIDs() READONLY, DELETE 
    PROPERTIES(t) NEWSESSION NEW, EDIT
    ORDERS nameCustoms(t)
    LIST SizeHeight OBJECT t
;
DESIGN sizeHeights { BOX { size = (600, 400); } }

// --------------------------------------------  Отображение размеров ---------------------------------------------------- //

exSize 'Размер (бел.)' = DATA STRING[100] (SizeMapping);
sizeHeightCustoms = DATA SizeHeight (SizeMapping);
nameSizeHeightCustoms 'Рост (там.)' (SizeMapping m)= nameCustoms(sizeHeightCustoms(m));    
ageSticker 'Возраст (стикер)' = DATA STRING[30] (SizeMapping);

exSize 'Размер (бел.)' = DATA BOOLEAN (SizeMappingEx);
sizeHeightCustoms 'Рост (там.)' = DATA BOOLEAN (SizeMappingEx);
ageSticker 'Возраст (стикер)' = DATA BOOLEAN (SizeMappingEx);

over(SizeMappingEx mx) += WHEN exSize(mx) THEN TRUE;
over(SizeMappingEx mx) += WHEN sizeHeightCustoms(mx) THEN TRUE;
over(SizeMappingEx mx) += WHEN ageSticker(mx) THEN TRUE;

EXTEND FORM sizeMappings 

    PROPERTIES(m) AFTER sizeWaist(m) nameSizeHeightCustoms, exSize, ageSticker                           
    PROPERTIES(mx) AFTER sizeWaist(mx) sizeHeightCustoms, exSize, ageSticker                           
;

sizeHeightCustoms = DATA SizeHeight (Item);
nameSizeHeightCustoms 'Рост (там.)' (Item item) = nameCustoms(sizeHeightCustoms(item)) IN itemArticle;

userShowSizeHeightCustoms 'Рост (там.)' (itemGroup) = DATA BOOLEAN (ItemGroup) IN itemArticle;

quantityChildWithSizeHeightCustoms (ItemGroup itemGroup) = GROUP SUM 1 IF userShowSizeHeightCustoms(ItemGroup childItemGroup) AND isParent(childItemGroup, itemGroup) MATERIALIZED;
quantityParentWithSizeHeightCustoms (ItemGroup itemGroup) = GROUP SUM 1 IF userShowSizeHeightCustoms(ItemGroup parentItemGroup) AND isParent(itemGroup, parentItemGroup) MATERIALIZED;

showSizeHeightCustoms 'Рост (там.)' (ItemGroup itemGroup) = TRUE IF (quantityChildWithSizeHeightCustoms (itemGroup) (+) quantityParentWithSizeHeightCustoms (itemGroup)) > 0 MATERIALIZED;

showSizeHeightCustoms 'Рост (там.)' (Item item) = TRUE IF quantityParentWithSizeHeightCustoms(itemGroup(item));

backgroundShowSizeHeightCustoms (ItemGroup itemGroup) =
    IF userShowSizeHeightCustoms(itemGroup) THEN
        RGB(0,0,0) IF itemGroup IS ItemGroup
    ELSE
        RGB(203,203,206) IF quantityChildWithSizeHeightCustoms (itemGroup) != descendantNumber(itemGroup)
                         AND NOT quantityParentWithSizeHeightCustoms (itemGroup);

setNullShowSizeHeightCustoms 'Снять признак для всех потомков'(ItemGroup itemGroup)  { 
    FOR isParent (ItemGroup childGroup, itemGroup) DO {
        userShowSizeHeightCustoms(childGroup) <- NULL;
    }
} ASON CONTEXTMENU userShowSizeHeightCustoms[ItemGroup] CONFIRM;

EXTEND FORM itemGroup PROPERTIES(g) userShowSizeHeightCustoms;
EXTEND FORM itemGroups PROPERTIES(g) READONLYIF  isReadonly() showSizeHeightCustoms BACKGROUND backgroundShowSizeHeightCustoms(g) BEFORE deleteg;
EXTEND FORM item PROPERTIES(i) nameSizeHeightCustoms SHOWIF showSizeHeightCustoms(i);
EXTEND FORM items PROPERTIES(i) READONLYIF isReadonly() BEFORE deletei nameSizeHeightCustoms SHOWIF showSizeHeightCustoms(g);

nameSizeHeightCustoms 'Рост (там.)' (Batch batch) = nameSizeHeightCustoms(sku(batch));

showSizeHeightCustoms 'Рост (там.)' (Batch batch)= showSizeHeightCustoms(sku(batch));
sizeHeightCustoms 'Рост (там.)' (Batch batch) = sizeHeightCustoms(sku(batch));

EXTEND FORM batches PROPERTIES(bt) READONLY  nameSizeHeightCustoms SHOWIF showSizeHeightCustoms(sk);

overCopy(Item s, Item d) + { 
    sizeHeightCustoms(d) <- sizeHeightCustoms(s);
}

GROUP itemCustoms 'Описание' : public;

@defineStringItemAttribute(sizeChestCustoms, STRING[10], 'Обхват груди (там.) ', itemCustoms);

@defineStringItemAttribute(sizeNeckCustoms, STRING[10], 'Обхват шеи (там.)', itemCustoms);
@defineItemAttributeBatch (sizeNeckCustoms, 'Обхват шеи (там.)');

@defineStringItemAttribute(footLengthCustoms, STRING[10], 'Длина стопы (там.)', itemCustoms);
@defineItemAttributeBatch (footLengthCustoms, 'Длина стопы (там.)');

@defineStringItemAttribute(insoleLengthCustoms, STRING[10], 'Длина стельки (там.)', itemCustoms);
@defineItemAttributeBatch (insoleLengthCustoms, 'Длина стельки (там.)');

@defineStringItemAttribute(sizeHeadCustoms, STRING[10], 'Обхват головы (там.)', itemCustoms);
@defineItemAttributeBatch (sizeHeadCustoms, 'Обхват головы');

@defineStringItemAttribute(sizeBrushCustoms, STRING[10], 'Обхват кисти (там.)', itemCustoms);
@defineItemAttributeBatch (sizeBrushCustoms, 'Обхват кисти');

@defineStringItemAttribute(sizeHipsCustoms, STRING[10], 'Обхват бедер (там.)', itemCustoms);
@defineItemAttributeBatch (sizeHipsCustoms, 'Обхват бедер');

@defineStringItemAttribute(sizeWaistCustoms, STRING[10], 'Обхват талии (там.)', itemCustoms);
@defineItemAttributeBatch (sizeWaistCustoms, 'Обхват талии');

@defineStringItemAttribute(exSize, STRING[100], 'Размер (бел.)', itemCustoms);
@defineItemAttributeBatch (exSize, 'Размер (бел.)');

@createSizeMappingExAttribute(sizeHeightCustoms, 'рост (там.)', nameSizeHeightCustoms, );
@createSizeMappingExAttribute(sizeChest, 'обхват груди (там.)', sizeChestCustoms, customs);
@createSizeMappingExAttribute(sizeNeck, 'обхват шеи (там.)', sizeNeckCustoms, customs);
@createSizeMappingExAttribute(footLength, 'длину стопы (там.)', footLengthCustoms, customs);
@createSizeMappingExAttribute(insoleLength, 'длину стельки (там.)', insoleLengthCustoms, customs);
@createSizeMappingExAttribute(sizeHead, 'обхват головы (там.)', sizeHeadCustoms, customs);
@createSizeMappingExAttribute(sizeBrush, sizeHand, 'обхват кисти (там.)', sizeBrushCustoms, customs);
@createSizeMappingExAttribute(sizeHips, 'обхват бедер (там.)', sizeHipsCustoms, customs);
@createSizeMappingExAttribute(sizeWaist, 'обхват талии (там.)', sizeWaistCustoms, customs);

// Дополнительные атрибуты для стикера
@defineStringItemAttribute(sizeSticker, ISTRING[20], 'Размер (стикер)', itemSticker);
@defineItemAttributeBatch (sizeSticker, 'Размер (стикер)');

@defineStringItemAttribute(ageSticker, STRING[30], 'Возраст', itemSticker);
@defineItemAttributeBatch (ageSticker, 'Возраст');

overCopy(Item s, Item d) + { 
    exSize(d) <- exSize(s);
    sizeChestCustoms(d) <- sizeChestCustoms(s);
    sizeNeckCustoms(d) <- sizeNeckCustoms(s);
    footLengthCustoms(d) <- footLengthCustoms(s);
    insoleLengthCustoms(d) <- insoleLengthCustoms(s);        
    sizeHeadCustoms(d) <- sizeHeadCustoms(s);    
    sizeBrushCustoms(d) <- sizeBrushCustoms(s);    
    sizeHipsCustoms(d) <- sizeHipsCustoms(s);    
    sizeWaistCustoms(d) <- sizeWaistCustoms(s);    
    ageSticker(d) <- ageSticker(s);  
}

DESIGN item {
    itemPrimaryColumn1 {
        MOVE GROUP(itemCustoms,i) AFTER regionPrm;
        MOVE GROUP(itemSticker,i) { columns = 3;}
    }

    PROPERTY(composition(i)) { charWidth = 30;}
    PROPERTY(originalCustomsGroup(i)) { charWidth = 11;}
    PROPERTY(originalComposition(i)) { charWidth = 11;}
    PROPERTY(caption(i)) { charWidth = 20;}
    PROPERTY(originalCaption(i)) { charWidth = 20;}
    PROPERTY(composition(i)) { charWidth = 30;}
}
                                            
DESIGN itemGroup {
    attributes {
        MOVE GROUP(itemCustoms,g) {
            columns = 6;
        }
    }
}

@createSizeMappingExAttribute(ageSticker, 'возраст (стикер)', ageSticker, );
@createSizeMappingExAttribute(exSize, 'размер (бел)', exSize, );

mapSizeSticker 'Заполнить размер (стикер) по соответствию'()  { 
    sizeSticker(Item i) <- ISTRING[50](exSizeSizeMapping(i))
        WHERE overSizeMapping(itemGroup(i), size(i), brand(i)) AND inMap(i) AND exSizeSizeMapping(i);
}
mapSizeSticker 'Заполнить размер (стикер) по соответствию'(Item i)  { 
    inMap(Item item) <- NULL;
    inMap(i) <- TRUE;
    mapSizeSticker();
} ASON CONTEXTMENU sizeSticker[Item];
     
NAVIGATOR {
    skuNavigator {
        MOVE sizeMappings;        
    }
}

//-- Заполнение матрицы

@createSizeMappingAttribute(exSize, STRING[100]);
@createSizeMappingAttribute(sizeHeightCustoms, SizeHeight, customs);
@createSizeMappingAttribute(ageSticker, STRING[30]);
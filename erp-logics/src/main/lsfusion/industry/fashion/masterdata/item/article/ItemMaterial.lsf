MODULE ItemMaterial;

REQUIRE Item;

NAMESPACE Item;

// Материал
CLASS Material 'Материал';
TABLE material(Material);

name 'Наименование' = DATA ISTRING[100](Material);
shortName 'Наименование (сокр.)' = DATA ISTRING[50](Material);

@defineExternalizable(material, STRING[100]);

FORM material 'Материал'
    OBJECTS t=Material PANEL
    PROPERTIES(t) name, shortName, id SHOWIF showIDs()
    EDIT Material OBJECT t
;

FORM materials 'Материалы'
    OBJECTS t=Material
    PROPERTIES(t) NEWSESSION name READONLY, shortName READONLY , id SHOWIF showIDs() READONLY, DELETE 
    PROPERTIES(t) NEWSESSION NEW, EDIT
    ORDER name(t)
    LIST Material OBJECT t
;
DESIGN materials { BOX { size = (600, 400); } }

TABLE materialItem (Material, Item);
percent 'Процент' = DATA INTEGER (Material, Item);

percentMaterials 'Процент' (Item i) = GROUP SUM percent(Material m,i);

calcComposition 'Состав' (Item i) = STRING[255](
                       (GROUP CONCAT (percent(Material m,i)+'% '+name(m)) IF percent(m,i), ', ' ORDER DESC percent(m,i)))
                       CHARWIDTH 50 MATERIALIZED;

WHEN LOCAL FORMS item CHANGED(calcComposition(Item i)) AND NOT (CHANGED (composition(i))) DO {
    composition(i) <- calcComposition(i);            
} 
backgroundMaterials (Item i) = RGB(255,0,0) IF i IS Item AND (percentMaterials(i) != 100.0);

FORM itemMaterial 'Материалы'
    OBJECTS i=Item PANEL 
    PROPERTIES(i) READONLY percentMaterials BACKGROUND  backgroundMaterials(i)
    
    OBJECTS t=Material
    PROPERTIES(t) NEWSESSION name READONLY, DELETE 
    PROPERTIES(t,i) percent
    
    PROPERTIES(t) NEWSESSION NEW, EDIT
    ORDER name(t)
;
DESIGN itemMaterial { BOX { size = (600, 400); } }     

createComposition 'Выбрать состав'(Item item)  {          
    SHOW itemMaterial OBJECTS i = item ; 
} ASON CONTEXTMENU composition[Item];  

//добавление на форму атрибутов товара
in 'Отм.' = DATA BOOLEAN (Material);
allowReplaceMaterial = DATA LOCAL BOOLEAN ();

FORM confirmReplaceMaterial 'Объединяемые материалы'
    OBJECTS material = Material PANEL 

    OBJECTS o = Material  
    PROPERTIES(o) READONLY name
    FILTERS o==material

    OBJECTS o2 = Material
    PROPERTIES(o2) in
    PROPERTIES(o2) READONLY name
    FILTERS in(o2)
;

DESIGN confirmReplaceMaterial {
    REMOVE BOX(material);
    NEW splitContainer{
        type = SPLITV;
        fill = 1;
        MOVE BOX(o){
            caption = 'Основной материал';
        }
        MOVE BOX(o2){
            caption = 'Удаляемый материал';
        }           
    }
    MOVE TOOLBARBOX;
}

replace 'Объединить'(Material material)  { 
    DIALOG confirmReplaceMaterial OBJECTS o = material DO {
        allowReplaceMaterial() <- TRUE;
        FOR in(Material o) AND allowReplaceMaterial() DO{
            IF o != material THEN {
                FOR percent(o, Item item) DO {
                    IF percent(material, item) THEN {
                        percent(material, item) <- percent(o, item) + percent(material, item);   
                        percent(o, item) <- NULL;                        
                    } ELSE {
                        percent(material, item) <- percent(o, item);
                        percent(o, item) <- NULL;
                    }                    
                    composition(item) <- calcComposition(item);                                                                         
                }
                in(o) <- NULL;
                DELETE o;                  
            } ELSE {
                MESSAGE 'Выделенный материал не может совпадать с объединяемым';
            }
        }
        apply();
    }
    in(Material o) <- NULL;           
}

EXTEND FORM attributesItem
    OBJECTS material = Material
    PROPERTIES in(material)
    PROPERTIES(material) READONLY name
    PROPERTIES(material) READONLY shortName, id SHOWIF showIDs()
    PROPERTIES(material) NEWSESSION NEW, EDIT, DELETE         
    ORDER name(material)
    PROPERTIES replace(material) TOOLBAR
;

DESIGN attributesItem {
    tabContainer {
        MOVE BOX(material){
            caption = 'Материалы';
        }
    }
}

MODULE LabelTextile;

REQUIRE Label;

NAMESPACE Label;


CLASS PrintLabelType 'Тип этикетки (печать)';
TABLE printLabelType (PrintLabelType);

@defineExternalizable(printLabelType, STRING[20]);

name 'Наименование' = DATA STRING[50](PrintLabelType) CHARWIDTH 25;
namePrint 'Наименование (печать)' = DATA STRING[50](PrintLabelType) CHARWIDTH 25;
showPrint 'Отображать при печати' = DATA BOOLEAN (PrintLabelType);

labelType = DATA LabelType(PrintLabelType);
nameLabelType 'Шаблон по умолчанию'(PrintLabelType t) = name(labelType(t));

isIndividual 'На отдельный товар' = DATA BOOLEAN(PrintLabelType);
note 'Примечание' = DATA STRING[100](PrintLabelType);

FORM printLabelType 'Тип этикетки (печать)'
    OBJECTS t=PrintLabelType PANEL
    PROPERTIES(t) id, namePrint, name, showPrint, nameLabelType, isIndividual, note

    EDIT PrintLabelType OBJECT t
;

FORM printLabelTypes 'Типы этикеток (печать)'
    OBJECTS t=PrintLabelType
    PROPERTIES(t) READONLY id, namePrint, name, showPrint, nameLabelType, isIndividual, note
    PROPERTIES(t) NEWSESSION NEW, EDIT, DELETE
    ORDERS id(t)
;

FORM printLabelTypesList 'Типы этикеток (печать)'
    OBJECTS t=PrintLabelType
    PROPERTIES(t) READONLY id, namePrint, name, nameLabelType, note
    ORDERS id(t)

    LIST PrintLabelType OBJECT t
;

printLabelType = DATA PrintLabelType(LabelType);
namePrintLabelType 'Тип этикетки (печать)'(LabelType l) = name(printLabelType(l));

forTesting 'Тестовый' = DATA BOOLEAN (LabelType);
lotPrint 'Печать марки' = DATA BOOLEAN (LabelType);

EXTEND FORM labelType
    PROPERTIES(l) namePrintLabelType, forTesting, lotPrint
;

DESIGN labelType 'Тип ценника/Шаблон этикетки '{
    BOX(l) {caption = 'Тип ценника/Шаблон этикетки';}
    PROPERTY(width(l)){charWidth = 5;}
    PROPERTY(height(l)){charWidth = 5;}
    PROPERTY(nameOrientation(l)){charWidth = 10;}
    PROPERTY(fileTemplate(l)){charWidth = 25;}
    PROPERTY(namePriceListType(l)){charWidth = 10;}

    wor0 {
        MOVE PROPERTY(lotPrint(l));
    }    
    wor1 {
        MOVE PROPERTY(inactive(l));
        MOVE PROPERTY(forTesting(l));
    }
    wor2 {
        MOVE PROPERTY(namePrintLabelType(l));
    }
}

EXTEND FORM labelTypes
    ORDERS name(l)

    FILTERGROUP withFile FILTER 'С файлом' fileTemplate(l)
;

DESIGN labelTypes 'Типы ценников/Шаблоны этикеток' {
    BOX(l) {caption = 'Тип ценника/Шаблон этикетки';}

    PROPERTY(width(l)){charWidth = 5;}
    PROPERTY(height(l)){charWidth = 5;}
    PROPERTY(nameOrientation(l)){charWidth = 10;}
    PROPERTY(fileTemplate(l)){charWidth = 25;}
    PROPERTY(namePriceListType(l)){charWidth = 10;}
}

FORM stickerTypes 'Шаблоны этикеток'
    OBJECTS l = LabelType
    PROPERTIES(l) READONLY id, name, active
    PROPERTIES(l) NEWSESSION NEW, EDIT, DELETE
    FILTERGROUP inactive FILTER 'Активные' active(l) 'F10' DEFAULT

    PROPERTIES(l) READONLY forTesting
    PROPERTIES(l) READONLYIF isReadonly() namePrintLabelType, lotPrint
    ORDERS name(l)

    FILTERS NOT fileTemplate(l)
;

@extendFormEditable(stickerTypes);


DESIGN stickerTypes {
    BOX(l) {caption = 'Шаблон этикетки';}
}

MODULE LabelDirectPrintTextileLot;

REQUIRE LabelDirectPrintTextile, Lot;

NAMESPACE Label;

stickerTransaction = DATA StickerTransaction(Lot) INDEXED;
dateTimeStickerTransaction 'Дата/время печати' (Lot l) = dateTime(stickerTransaction(l));
printed 'Напечатана' (Lot l) = stickerTransaction(l) AND NOT skipReports(stickerTransaction(l)) MATERIALIZED;
skipPrint 'Запрет печати' = ABSTRACT BOOLEAN (Lot);
notPrinted (Lot l) = l IS Lot AND NOT printed(l) MATERIALIZED INDEXED;

countNotPrintedLot 'Кол-во ненапечатанных марок' = GROUP SUM 1 IF notPrinted(Lot l) AND NOT skipPrint(l) BY sku(l) MATERIALIZED;

unPrint 'Отменить печать' (Lot l) {
    NEWSESSION {
        stickerTransaction(l) <- NULL;
        APPLY;
    }
} TOOLBAR CONFIRM;

EXTEND FORM lots
    PROPERTIES(l) READONLY AFTER nameSku(l) printed, dateTimeStickerTransaction
    PROPERTIES(l) unPrint SHOWIF printed(l)
;

@defineOption(testDatamatrix, 'Тестовый datamatrix для печати', STRING, items);
@defineOption(printTestDatamartix, 'Печатать тестовый datamatrix', BOOLEAN, items);
@defineOption(datamartixTag, 'Тег datamatrix в шаблонах этикеток (с префиксом и постфиксом)', STRING, items);
@defineOption(enableSinglePrintLotSendToPrinter, 'Печать этикетки с datamatrix одним файлом', BOOLEAN, items);
@defineOption(logPrintDatamartix, 'Записывать в лог переданные на печать datamatrix', BOOLEAN, items);

DESIGN options {
    items {
        NEW printDatamatrix {
            caption = 'Параметры печати datamatrix';
            MOVE PROPERTY(testDatamatrix());
            MOVE PROPERTY(printTestDatamartix());
            MOVE PROPERTY(datamartixTag());
            MOVE PROPERTY(enableSinglePrintLotSendToPrinter());
            MOVE PROPERTY(logPrintDatamartix());
        }
    }
}

countLots 'Кол-во марок' (StickerTransaction stickerTransaction) = GROUP SUM 1 BY stickerTransaction(Lot l);
stickerIndex 'Номер этикетки' (Lot l) = PARTITION SUM 1 IF NOT skipReports(stickerTransaction(l)) ORDER l BY stickerTransaction(l);

EXTEND FORM stickerTransactions
    PROPERTIES(l) READONLY countLots AFTER quantity(l)

    OBJECTS lot = Lot
    PROPERTIES (lot) READONLY stickerIndex, id, idSku, nameSku
    FILTERS stickerTransaction(lot) == l
;

DESIGN stickerTransactions {
    main {
        MOVE BOX(lot) { fill = 0.3; }
    }
}

lotReserveIndex 'Порядок резерва' (Lot l) = PARTITION SUM 1 IF notPrinted(l) AND NOT skipPrint(l) ORDER l BY sku(l);

reservedLots 'Зарезервировать марки' (StickerTransaction stickerTrans, INTEGER quantity) {
    IF countNotPrintedLot(item(stickerTrans)) < quantity THEN {
        MESSAGE 'Количество этикеток для печати не может первышать доступное количество марок';
        RETURN;
    }
    NEWSESSION {
        FOR sku(Lot lot) == item(stickerTrans) AND iterate(INTEGER q, 1, quantity) AND lotReserveIndex(lot) == q DO {
            stickerTransaction(lot) <- stickerTrans;
            
            IF logPrintDatamartix() THEN {
                logToFile('labelDirectPrint', '[Reserve datamatrix] ' + id(lot)
                    + ' , computer: ' + hostname(currentComputer()) + ', user: ' + login(currentUser()));
            }
        }
        APPLY;
    }
}

extraPreparePrintLotText = ABSTRACT TEXT (StickerTransaction, TEXT, Lot);
preparePrintLotText (StickerTransaction stickerTrans, TEXT text, Lot lot) =
    replace((OVERRIDE extraPreparePrintLotText(stickerTrans, text, lot), text), datamartixTag(), code(lot));

preparePrintLot 'Подготовка этикетки к печати марки (множество файлов)' (StickerTransaction stickerTrans, Lot lot) {
    IF NOT datamartixTag() THEN {
        MESSAGE 'Для печати этикеток с марками должен быть задан тег Datamatrix';
        RETURN;
    }
    
    IF NOT stickerTransaction(lot) == stickerTrans THEN {
        MESSAGE 'Марка должна быть зарезервирована в заданном реестре';
        RETURN;
    }

    fullFormattedText() <- preparePrintLotText(stickerTrans, formattedText(), lot);
}

prepareSinglePrintLot 'Подготовка этикетки к печати марки (один файл)' (StickerTransaction stickerTrans) {
    IF NOT datamartixTag() THEN {
        MESSAGE 'Для печати этикеток с марками должен быть задан тег Datamatrix';
        RETURN;
    }

    LOCAL formattedTextUnit = TEXT ();
    formattedTextUnit() <- fullFormattedText() + '\n';
    fullFormattedText() <- '';

    reservedLots(stickerTrans, quantity(stickerTrans));

    LOCAL formattedTextUnit = TEXT (Lot);
    formattedTextUnit(Lot l) <- preparePrintLotText(stickerTrans, formattedTextUnit(), l) WHERE stickerTransaction(l) == stickerTrans;

    fullFormattedText() <- GROUP CONCAT formattedTextUnit(Lot l), '\n' ORDER l;
}

dropDatamartixTag 'Очистить тег Datamartix' () {
    fullFormattedText() <- replace(fullFormattedText(), datamartixTag(), '') WHERE datamartixTag();
}

useCustomSendToPrinter () += TRUE;
customSendToPrinter (StickerTransaction stickerTrans) + {
    IF countNetworkPrinter(stock(stickerTrans)) THEN {

        IF countPrinters(stock(stickerTrans),printLabelType(stickerTrans)) == 1 AND notShowNetworkPrinterDialog() THEN {
            printer() <- currentPrinter(stock(stickerTrans), printLabelType(stickerTrans));
        } ELSE {
            DIALOG networkPrintersLabel OBJECTS s = stock(stickerTrans), l = printLabelType(stickerTrans), p INPUT NOMANAGESESSION DO
                printer() <- p;
        }

        CASE WHEN networkPrinterType(printer()) == NetworkPrinterType.clientComPort THEN {
            IF NOT printTestDatamartix() AND lotPrint(labelType(stickerTrans)) AND item(stickerTrans) THEN {
                IF NOT enableSinglePrintLotSendToPrinter() THEN {
                    reservedLots(stickerTrans, quantity(stickerTrans));
                    FOR stickerTransaction(Lot lot) == stickerTrans NOINLINE DO {
                        preparePrintLot(stickerTrans, lot);
                        sendToPrinterClientComPort(stickerTrans); // внутри цикла, т.к. если хоть одна отправилась - уже считаем транзакцию напечатанной
                    }
                } ELSE {
                    prepareSinglePrintLot(stickerTrans);
                    sendToPrinterClientComPort(stickerTrans);
                }
            }
            ELSE {
                dropDatamartixTag();
                sendToPrinterClientComPort(stickerTrans);
            }
        }

        WHEN networkPrinterType(printer()) == NetworkPrinterType.clientFile THEN {
            IF NOT printTestDatamartix() AND lotPrint(labelType(stickerTrans)) AND item(stickerTrans) THEN {
                IF NOT enableSinglePrintLotSendToPrinter() THEN {
                    reservedLots(stickerTrans, quantity(stickerTrans));
                    FOR stickerTransaction(Lot lot) == stickerTrans NOINLINE DO {
                        preparePrintLot(stickerTrans, lot);
                        sendToPrinterClientFile(stickerTrans);
                    }
                } ELSE {
                    prepareSinglePrintLot(stickerTrans);
                    sendToPrinterClientFile(stickerTrans);
                }
            }
            ELSE {
                dropDatamartixTag();
                sendToPrinterClientFile(stickerTrans);
            }
        }
        ELSE {  // в том числе, для NetworkPrinterType.clientPrintService
            IF NOT printTestDatamartix() AND lotPrint(labelType(stickerTrans)) AND item(stickerTrans) THEN {
                IF NOT enableSinglePrintLotSendToPrinter() THEN {
                    reservedLots(stickerTrans, quantity(stickerTrans));
                    FOR stickerTransaction(Lot lot) == stickerTrans NOINLINE DO {
                        preparePrintLot(stickerTrans, lot);
                        sendToPrinterDefault(stickerTrans);
                    }
                } ELSE {
                    prepareSinglePrintLot(stickerTrans);
                    sendToPrinterDefault(stickerTrans);
                }
            }
            ELSE {
                dropDatamartixTag();
                sendToPrinterDefault(stickerTrans);
            }
        }
    }
    ELSE
        MESSAGE 'Принтеры не найдены' NOWAIT;
}
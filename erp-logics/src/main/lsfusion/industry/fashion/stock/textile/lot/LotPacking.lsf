MODULE LotPacking;

REQUIRE Lot, PacketTextile, BinPacketLedger;

NAMESPACE Lot;


packet = DATA Packet (Lot) INDEXED;
idPacket 'Пакет' (Lot lot) = id(packet(lot)) IN id CHARWIDTH 10;

EXTEND FORM lots
    PROPERTIES(l) READONLY idPacket AFTER nameSku(l)
;

isClosed 'Закрыт' = DATA BOOLEAN (Packet);
isOpened 'Открыт' (Packet packet) = packet IS Packet AND NOT isClosed(packet) MATERIALIZED;

EXTEND FORM packets
    PROPERTIES READONLY isClosed(s)
;

isClosedPacket 'Закрыт' (Lot lot) = isClosed(packet(lot));
isOpenedPacket 'Открыт' (Lot lot) = isOpened(packet(lot));

CONSTRAINT SETCHANGED(packet(Lot lot)) AND isClosedPacket(lot)
    MESSAGE 'Запрещено добавлять марки в закрытый пакет';

countLotsPacket 'Кол-во марок' (Packet packet) = GROUP SUM 1 IF packet(Lot lot) == packet;
countLotsPacket 'Кол-во марок' (Sku sku, Packet packet) = GROUP SUM 1 IF packet(Lot lot) == packet AND sku(lot) == sku;
remainingLotsQuantity 'Нераспределенное количество' (Sku sku, Packet packet) = currentQuantity(sku, packet) (-) countLotsPacket(sku, packet);
countIncompleteSku 'Товары с расхождениями' (Packet packet) = GROUP SUM 1 IF remainingLotsQuantity(Sku sku, packet);

CONSTRAINT SET(isClosed(Packet packet)) AND NOT countLotsPacket(packet)
    MESSAGE 'Запрещено закрывать пустой пакет';

CONSTRAINT SET(isClosed(Packet packet)) AND countIncompleteSku(packet)
    MESSAGE 'Запрещено закрытие пакета при наличии расхождений количества марок и товара';

CONSTRAINT SETCHANGED(packet(Lot lot)) AND packet(lot) AND sku(lot) AND NOT currentQuantity(sku(lot), packet(lot))
    MESSAGE 'Запрещено добавлять в пакет марки товара, отсутствующего в пакете';

META defineConstraintStateDocumentPacketDetailLot (object)
    CONSTRAINT SETCHANGED(packet(###object##PacketDetail d))
        AND isOpened(packet(d)) AND countLotsPacket(packet(d))
        MESSAGE 'Пакет должен не содержать марок, или быть закрытым';
END;

META defineConstraintStateDocumentPacketDetailLotAccepted (object)
    CONSTRAINT SET(isAccepted(###object##PacketDetail d))
        AND isOpened(packet(d)) AND countLotsPacket(packet(d))
        MESSAGE 'Пакет должен не содержать марок, или быть закрытым (приёмка)';
END;

META defineConstraintStateDocumentPacketDetailLotPosted (object)
    CONSTRAINT (SET(isPosted(###object##PacketDetail d)) OR SETCHANGED(packet(d))) AND isPosted(d)
        AND isOpened(packet(d)) AND countLotsPacket(packet(d))
        MESSAGE 'Пакет должен не содержать марок, или быть закрытым (проведение)';
END;

onStock = ABSTRACT BOOLEAN (Packet);

CONSTRAINT DROPPED(packet(Lot lot)) AND PREV(packet(lot)) AND PREV(isClosedPacket(lot))
    AND (onStock(PREV(packet(lot))) OR NOT shipmentDate(PREV(packet(lot))))
    MESSAGE 'Запрещено изъятие марок из закрытого пакета';

CONSTRAINT SETCHANGED(packet(Lot lot)) AND PREV(packet(lot))
    AND (onStock(PREV(packet(lot))) OR NOT shipmentDate(PREV(packet(lot))))
    MESSAGE 'Запрещено перемещение марок между пакетами без изъятия';

CONSTRAINT DROPPED(isClosed(Packet packet)) AND onStock(packet)
    MESSAGE 'Запрещено открытие пакета, находящегося на складе';

CONSTRAINT CHANGED(packet(Lot lot)) AND onStock(packet(lot))
    MESSAGE 'Запрещено изменять марки в пакете, находящемся на складе';

META defineExtendPacketDetailLotProperties (object, formDocument, formDocumentPacketDetail)
    isClosedPacket 'Закрыт' (###object##PacketDetail d) = isClosed(packet(d));
    countLotsPacketPacket 'Кол-во марок' (###object##PacketDetail d) = countLotsPacket(packet(d));
    
    EXTEND FORM ##object
        PROPERTIES(##formDocumentPacketDetail) SHOWIF packets(##formDocument) AFTER index(##formDocumentPacketDetail) isClosedPacket, countLotsPacketPacket
    ;
END;
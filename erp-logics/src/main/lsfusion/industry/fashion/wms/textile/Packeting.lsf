MODULE Packeting;

REQUIRE PacketSkuLedger, BarcodeRequest, LabelDirectPrintTextile, Numerator;

NAMESPACE Packet;


CLASS PacketingDetail 'Строка комплектации нового пакета' : PacketSkuLedger;
TABLE packetingDetail (PacketingDetail);

dateTime 'Дата/время' = DATA DATETIME (PacketingDetail) INDEXED;
dateProduction 'Дата производства' = DATA DATE (PacketingDetail);

stock = DATA Stock (PacketingDetail) INDEXED;
nameStock 'Склад' (PacketingDetail ledger) = name(stock(ledger)) IN id  CHARWIDTH 10;

packet = DATA Packet (PacketingDetail) NONULL DELETE INDEXED;
idPacket 'Пакет' (PacketingDetail ledger) = id(packet(ledger)) IN id CHARWIDTH 10;

sku = DATA Sku (PacketingDetail) INDEXED;
idBarcodeSku 'Штрихкод' (PacketingDetail ledger) = id(barcode(sku(ledger))) IN id;
nameSku 'Товар' (PacketingDetail ledger) = name(sku(ledger)) IN id  CHARWIDTH 20;

quantity 'Кол-во' = DATA NUMERIC[16,5] (PacketingDetail);

INDEX packet(PacketingDetail d), sku(d), dateTime(d);

// по PacketSkuLedger
dateTime (PacketingDetail ledger) += dateTime(ledger);
dateProduction (PacketingDetail ledger) += dateProduction(ledger);
stock (PacketingDetail ledger) += stock(ledger);
packet (PacketingDetail ledger) += packet(ledger);
sku (PacketingDetail ledger) += sku(ledger);
quantity (PacketingDetail ledger) += quantity(ledger);

WHEN LOCAL SET (PacketingDetail l IS PacketingDetail) DO {
    dateTime(l) <- currentDateTime();
}

maxPacketingDetail= GROUP MAX PacketingDetail d BY packet(d) MATERIALIZED INDEXED;
countPacketingDetail= GROUP SUM 1 BY packet(PacketingDetail d);

stock 'Цех комплектации' (Packet b) = stock(maxPacketingDetail(b)) MATERIALIZED INDEXED;
nameStock 'Цех комплектации' (Packet b) = nameStock(maxPacketingDetail(b));
dateProduction 'Дата производства' (Packet b) = dateProduction(maxPacketingDetail(b)) MATERIALIZED INDEXED;

idArticleSku 'Модель'(PacketingDetail d) = idArticle(sku(d)) CHARWIDTH 10;
nameSizeSku 'Размер'(PacketingDetail d) = nameSize(sku(d)) CHARWIDTH 10;
nameSortSku 'Сорт'(PacketingDetail d) = nameSort(sku(d)) CHARWIDTH 5;

FORM packetings 'Регистр комплектаций пакетов'
    OBJECTS dates = (dFrom = DATE, dTo = DATE) PANEL
    PROPERTIES valFrom = VALUE(dFrom), valTo = VALUE(dTo)

    OBJECTS l = PacketingDetail
    PROPERTIES(l) READONLY dateTime, nameStock, idPacket, idArticleSku, idBarcodeSku, nameSku, nameSizeSku, quantity
    FILTERS date(l) >= dFrom AND date(l) <= dTo
;

DESIGN packetings {
    NEW top {
        horizontal = TRUE;
        NEW dates {
            caption = 'Период';
            horizontal = TRUE;
            MOVE PROPERTY (valFrom){caption = 'Дата (с)';}
            MOVE PROPERTY (valTo){caption = 'Дата (по)';}
        }
    }

    MOVE BOX (l);
    MOVE TOOLBARBOX;
}

NAVIGATOR {
    packetNavigator {
        NEW packetings;
    }
}

EXTEND FORM packet
    PROPERTIES(s) shipmentDate
;

EXTEND FORM packets
    PROPERTIES(s) READONLY nameStock, dateProduction, shipmentDate
;

packetsQuantity 'Количество этикеток' = DATA LOCAL NESTED INTEGER();
overChangePacketsQuantity  ABSTRACT( );

@defineOption(maxPacketingQuantity, 'Максимальное кол-во этикеток при печати (если не указано, то 5000)', INTEGER, label);

overMaxPacketingQuantity() = OVERRIDE maxPacketingQuantity(), 5000;

changePacketsQuantity()  {
    INPUT q = INTEGER DO {
        packetsQuantity() <- OVERRIDE q IF q <= overMaxPacketingQuantity(), 1;
    }
    overChangePacketsQuantity();
}

setSort ABSTRACT (PrintLabelType, LabelType, Sort);
setYear ABSTRACT ();

overFilterCreatePacketingDetail = ABSTRACT BOOLEAN (NetworkPrinter);

FORM createPacketingDetail 'Печать этикеток'
    OBJECTS plt = PrintLabelType PANEL
    PROPERTIES namePrint(plt) SELECTOR
    FILTERS showPrint(plt)

    OBJECTS lt = LabelType PANEL
    PROPERTIES name(lt) SELECTOR
    FILTERS plt == printLabelType(lt) OR NOT printLabelType(lt),
        active(lt), NOT fileTemplate(lt)

    OBJECTS sr = Sort PANEL
    PROPERTIES name(sr) ON CHANGE setSort(plt, lt, sr)

    OBJECTS dt = DATE PANEL
    PROPERTIES year = VALUE(dt) ON CHANGE setYear()

    PROPERTIES packetsQuantity() ON CHANGE changePacketsQuantity()

    OBJECTS np = NetworkPrinter
    PROPERTIES (np) READONLY name, path, nameStock BACKGROUND (IF NOT stock(np) == stock(currentComputer()) THEN RGB(255,200,216)), namePrintLabelType CHANGEABLE
    FILTERS in(np, currentUser()) OR overFilterCreatePacketingDetail(np)

    EVENTS
        ON INIT { 
            SEEK createPacketingDetail.sr = sortName('1');
            SEEK createPacketingDetail.dt = currentDate();
            packetsQuantity() <- 1;
        }
;

DESIGN createPacketingDetail {
    BOX {
        size = (1024, 768);
        NEW tabs BEFORE TOOLBARBOX {
            tabbed = TRUE;
            fill = 1;
            NEW mainTab {
                caption = 'Печать';
                NEW topRow1 {
                    horizontal = TRUE;
                    NEW printLabelType {
                        caption = 'Тип этикетки';
                        MOVE PROPERTY(namePrint(plt)) {
                            caption = '';
                            font = 'bold 14';
                            charWidth = 15;
                        }
                    }
                    NEW labelType {
                        caption = 'Шаблон этикетки';
                        MOVE PROPERTY (name(lt)){
                            caption = '';
                            font = 'bold 14';
                            charWidth = 20;
                        }
                    }
                    NEW datePrint {
                        caption = 'Дата печати';
                        MOVE PROPERTY(year) {
                            caption = '';
                            font = 'bold 14';
                        }
                    }
                }
                NEW topRow2 {
                    horizontal = TRUE;
                    MOVE BOX(sr) {
                        caption = 'Сорт';
                         PROPERTY(name(sr)) {
                            caption = '';
                            charWidth = 4;
                            font = 'bold 14';
                        }
                    }
                    NEW quantityP {
                        caption = 'Кол-во этикеток';
                        MOVE PROPERTY (packetsQuantity()) {
                            caption = '';
                            font = 'bold 14';
                        }
                    }
                }
            }
            NEW printers {
                caption = 'Принтеры';
                MOVE BOX (np);
            }
        }
    }
}

NAVIGATOR {
    stockDashboardNavigator {
        NEW createPacketingDetail;
    }
}

incrementedPacketingValue = DATA LOCAL NESTED STRING[48] ();
incrementedPacketingIntValue = DATA LOCAL NESTED LONG ();

reservePacketingPackets 'Зарезервировать номера пакетов' (INTEGER quantity) {
    NEWSESSION {
        incrementedPacketingValue() <- NULL;
        incrementedPacketingIntValue() <- NULL;

        IF curValue(defaultNumeratorPacket()) + quantity >= maxValue(defaultNumeratorPacket()) THEN
            MESSAGE 'Счетчик нумератора ' + name(defaultNumeratorPacket()) + ' достиг максимального значения. Обратитесь к администратору.';
        ELSE {
            APPLY {
                incrementedPacketingValue() <- curStringValue(defaultNumeratorPacket());
                incrementedPacketingIntValue() <- curValue(defaultNumeratorPacket());
                curValue(Numerator n) <- curValue(n) + quantity WHERE n == defaultNumeratorPacket();
            }
        }
    }
}
MODULE WriteOffBinLedgerFuel;

REQUIRE WriteOffBinLedger, BinLedgerFuel, WriteOffFuel;

NAMESPACE WriteOff;

volume[BinLedger] (WriteOffBinLedger ledger) += volume(writeOffDetail(ledger));
density[BinLedger] (WriteOffBinLedger ledger) += density(writeOffDetail(ledger));

densityBOutBin 'Плотность расчётная в резервуаре (до), кг/л' (UserWriteOffDetail writeOffDetail) = prevDensityB(outBin(writeOffDetail), dateTime(writeOffDetail));
lastDailyReportDensityOutBin 'Плотность наблюдаемая в резервуаре (из), кг/л' (UserWriteOffDetail writeOffDetail) = lastDailyReportDensity(sku(writeOffDetail), outBin(writeOffDetail));

fillDensityDailyReportOutBin 'Заполнить плотностью наблюдаемой (из последнего сменного отчёта)' (UserWriteOffDetail d) {
    IF lastDailyReportDensityOutBin(d) THEN {
        fillDensity(d, lastDailyReportDensityOutBin(d));
    } ELSE {
        MESSAGE 'Отсутствует плотность наблюдаемая (из последнего сменного отчёта)' NOWAIT;
    }
} ASON CONTEXTMENU density[UserWriteOffDetail];

fillDensityAOutBin 'Заполнить расчётным значением плотности в резервуаре (до)' (UserWriteOffDetail d) {
    IF densityBOutBin(d) THEN {
        fillDensity(d,densityBOutBin(d));
    } ELSE {
        MESSAGE 'Отсутствует плотность расчётная в резервуаре (до)' NOWAIT;
    }
} ASON CONTEXTMENU density[UserWriteOffDetail];

//overBackgroundDensity(UserWriteOffDetail d) += WHEN density(d) != densityBOutBin(d) THEN RGB(255,154,154);

EXTEND FORM userWriteOff
    PROPERTIES(d) SHOWIF useBins(w) READONLY AFTER nameOutBin(d) densityBOutBin BACKGROUND backgroundBins(w)
;

//----------------------------- переименование ячейки в резервуар -----------------------------//
DESIGN operation {
    PROPERTY(useBins(o)) { caption = 'Резервуары'; }
    PROPERTY(nameInBin(o,stc)) { caption = 'Резервуар (в)'; hide = TRUE; }
    PROPERTY(nameOutBin(o,stc)) { caption = 'Резервуар (из)'; }
}
DESIGN userWriteOff {
    headerBins { caption = 'Резервуары'; }
    PROPERTY(useBins(w)) { caption = 'Резервуары'; }
    PROPERTY(nameInBin(w)) { caption = 'Резервуар (в)'; hide = TRUE; }
    PROPERTY(nameOutBin(w)) { caption = 'Резервуар (из)'; }
    PROPERTY(nameInBin(d)) { caption = 'Резервуар (в)'; hide = TRUE; }
    PROPERTY(nameOutBin(d)) { caption = 'Резервуар (из)'; }
}
DESIGN writeOffs {
    PROPERTY(nameInBin(d)) { caption = 'Резервуар (в)'; hide = TRUE; }
    PROPERTY(nameOutBin(d)) { caption = 'Резервуар (из)'; }
}

//----------------------------- Технологический отпуск из ТРК -----------------------------//

onStarted() + {
    IF NOT reason('Технологический отпуск из ТРК') THEN NEW r = Reason {
        name(r) <- 'Технологический отпуск из ТРК';
    }
}

outGun 'Пистолет (из)' = DATA Gun(UserWriteOffDetail);
outGun 'Пистолет (из)' = ABSTRACT Gun(WriteOffDetail) MATERIALIZED;
outGun (UserWriteOffDetail writeOffDetail) += outGun(writeOffDetail);

nameOutGun 'Пистолет (из)' (WriteOffDetail writeOffDetail) = fullNumber(outGun(writeOffDetail)) CHARWIDTH 12;
nameOutGun 'Пистолет (из)' (UserWriteOffDetail writeOffDetail) = fullNumber(outGun(writeOffDetail)) CHARWIDTH 12;

CONSTRAINT outGun(WriteOffDetail detail) AND outBin(detail) AND NOT bin(outGun(detail)) == outBin(detail)
    CHECKED BY outGun[WriteOffDetail] MESSAGE 'Пистолет (из) и резервуар (из) не имеют связи';

checkOutGun (WriteOffDetail detail) = reason(detail) == reason('Технологический отпуск из ТРК');

CONSTRAINT isPosted(WriteOffDetail d) AND checkOutGun(d) AND NOT outGun(d)
    MESSAGE 'Для строки списания обязан быть задан пистолет (из)';

EXTEND FORM userWriteOff
    PROPERTIES(d) SHOWIF useBins(w) AFTER nameOutBin(d) nameOutGun BACKGROUND backgroundBins(w)
;

volumeOutTechFuel 'Расход из резервуара (технологический), л' (Sku sku, Bin bin, Gun gun, DATETIME dateTimeFrom, DATETIME dateTimeTo) =
    GROUP SUM volume(WriteOffBinLedger ledger) IF isPosted(ledger) AND dateTime(ledger) >= dateTimeFrom AND dateTime(ledger)<= dateTimeTo AND reason(writeOffDetail(ledger)) == reason('Технологический отпуск из ТРК') BY sku(ledger), outBin(ledger), outGun(writeOffDetail(ledger));
quantityOutTechFuel 'Расход из резервуара (технологический)' (Sku sku, Bin bin, Gun gun, DATETIME dateTimeFrom, DATETIME dateTimeTo) =
    GROUP SUM quantity(WriteOffBinLedger ledger) IF isPosted(ledger) AND dateTime(ledger) >= dateTimeFrom AND dateTime(ledger)<= dateTimeTo AND reason(writeOffDetail(ledger)) == reason('Технологический отпуск из ТРК') BY sku(ledger), outBin(ledger), outGun(writeOffDetail(ledger));

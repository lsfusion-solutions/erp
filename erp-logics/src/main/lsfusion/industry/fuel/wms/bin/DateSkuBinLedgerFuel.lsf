MODULE DateSkuBinLedgerFuel;

REQUIRE DateSkuBinLedger, BinLedgerFuel, ZReportFuel, BinFuel;

NAMESPACE Bin;

pump 'ТРК' (receiptDetail) = DATA Pump (ReceiptSaleDetail);
pump 'ТРК' (receiptDetail) = DATA Pump (ReceiptReturnDetail);

pump 'ТРК' = ABSTRACT Pump (ReceiptDetail) MATERIALIZED;
namePump 'ТРК' (ReceiptDetail receiptDetail) = fullName(pump(receiptDetail)) CHARWIDTH 20;
numberPump 'Номер ТРК' (ReceiptDetail receiptDetail) = number(pump(receiptDetail));
pump(ReceiptReturnDetail detail) += pump(detail);
pump(ReceiptSaleDetail detail) += pump(detail);

gun 'Пистолет' (receiptDetail) = DATA Gun (ReceiptSaleDetail);
gun 'Пистолет' (receiptDetail) = DATA Gun (ReceiptReturnDetail);

gun 'Пистолет' = ABSTRACT Gun (ReceiptDetail) MATERIALIZED;
nameGun 'Пистолет' (ReceiptDetail receiptDetail) = fullName(gun(receiptDetail)) CHARWIDTH 20;
fullNumberGun 'Номер пистолета' (ReceiptDetail receiptDetail) = fullNumber(gun(receiptDetail)) CHARWIDTH 20;
gun(ReceiptReturnDetail detail) += gun(detail);
gun(ReceiptSaleDetail detail) += gun(detail);

CONSTRAINT DROPPED(Gun a IS Gun) AND [GROUP SUM 1 IF NOT gun(ReceiptDetail d) BY PREV(gun(d))](a)
    MESSAGE 'Запрещено удалять пистолет при наличии строк чеков с ним';

CONSTRAINT DROPPED(Pump a IS Pump) AND [GROUP SUM 1 IF NOT pump(ReceiptDetail d) BY PREV(pump(d))](a)
    MESSAGE 'Запрещено удалять ТРК при наличии строк чеков с ним';

//setGun (ReceiptDetail d)  { 
//    DIALOG dialogGunStock OBJECTS s = departmentStore(d), g = gun(d) CHANGE;
//}

//CONSTRAINT gun(ReceiptDetail detail) AND NOT stock(gun(detail)) == departmentStore(detail)
//    CHECKED BY gun[ReceiptDetail] MESSAGE 'Пистолет не доступeн для отдела магазина';

EXTEND FORM zReport
    PROPERTIES(d) BACKGROUND backgroundBins(d)  numberPump, fullNumberGun //ON CHANGE setGun(d)
;
EXTEND FORM zReports
    PROPERTIES(d) READONLY BACKGROUND backgroundBins(d) numberPump, fullNumberGun
;

volumeReceiptSaleDetail 'Объем, л' (department, sku, date, bin) = GROUP SUM volume(ReceiptDetail d) IF isPosted(d) AND d IS ReceiptSaleDetail BY departmentStore(d), sku(d), date(d), bin(d);
lastReceiptSaleDetail (department, sku, date, bin) = GROUP LAST ReceiptDetail d IF isPosted(d) AND d IS ReceiptSaleDetail ORDER dateTime(d), d BY departmentStore(d), sku(d), date(d), bin(d);
densityReceiptSaleDetail 'Плотность, кг/л' (DepartmentStore department, Sku sku, DATE date, Bin bin) = density(lastReceiptSaleDetail(department,sku,date,bin));

volume[BinLedger] (DateSaleSkuBinLedger ledger) += volumeReceiptSaleDetail(departmentStore(ledger),sku(ledger),date(ledger),bin(ledger));
density[BinLedger] (DateSaleSkuBinLedger ledger) += densityReceiptSaleDetail(departmentStore(ledger),sku(ledger),date(ledger),bin(ledger));

volumeReceiptReturnDetail 'Объем, л' (department, sku, date, bin) = GROUP SUM volume(ReceiptDetail d) IF isPosted(d) AND d IS ReceiptReturnDetail BY departmentStore(d), sku(d), date(d), bin(d);
lastReceiptReturnDetail (department, sku, date, bin) = GROUP LAST ReceiptDetail d IF isPosted(d) AND d IS ReceiptReturnDetail ORDER dateTime(d), d BY departmentStore(d), sku(d), date(d), bin(d);
densityReceiptReturnDetail 'Плотность, кг/л' (DepartmentStore department, Sku sku, DATE date, Bin bin) = density(lastReceiptReturnDetail(department,sku,date,bin));

volume[BinLedger] (DateReturnSkuBinLedger ledger) += volumeReceiptReturnDetail(departmentStore(ledger),sku(ledger),date(ledger),bin(ledger));
density[BinLedger] (DateReturnSkuBinLedger ledger) += densityReceiptReturnDetail(departmentStore(ledger),sku(ledger),date(ledger),bin(ledger));

//----------------------------- переименование ячейки в резервуар -----------------------------//
DESIGN zReport {
    PROPERTY(nameBin(d)) { caption = 'Резервуар'; }
}
DESIGN zReports {
    PROPERTY(nameBin(d)) { caption = 'Резервуар'; }
}
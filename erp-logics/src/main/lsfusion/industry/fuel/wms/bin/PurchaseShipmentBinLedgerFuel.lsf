MODULE PurchaseShipmentBinLedgerFuel;

REQUIRE PurchaseShipmentBinLedger, BinLedgerFuel, PurchaseShipmentFuel;

NAMESPACE Purchase;

volume[BinLedger] (ShipmentBinLedger ledger) += volume(shipmentDetail(ledger));
density[BinLedger] (ShipmentBinLedger ledger) += density(shipmentDetail(ledger));

volumeInBin 'Объем резервуара' (UserInvoiceDetail invoiceDetail) = volume(inBin(invoiceDetail));
volumeBInBin 'Объем в резервуарe (до), л' (UserInvoiceDetail invoiceDetail) = prevVolumeB(inBin(invoiceDetail), shipmentDateTime(invoiceDetail));

backgroundDiffValueBin (UserInvoiceDetail d) = CASE
    WHEN volumeBInBin(d) (+) shipmentVolume(d) > volumeInBin(d) THEN overColorValueBin()
    WHEN volumeBInBin(d) AND NOT prevVolumeB(sku(d), inBin(d), shipmentDateTime(d)) THEN overColorValueBin()
    ELSE backgroundShipped(d);

EXTEND FORM userInvoice
    PROPERTIES(d) READONLY AFTER nameInBin(d) volumeInBin BACKGROUND backgroundShipped(d), volumeBInBin BACKGROUND backgroundDiffValueBin(d)
;

WHEN (SET(isPosted(UserInvoiceDetail d)) OR SET(createShipment(d)) OR CHANGED(inBin(d)) OR CHANGED(shipmentDateTime(d)) OR CHANGED(shipmentVolume(d)))
      AND isPosted(d) AND createShipment(d) AND inBin(d) AND isFuel(sku(d)) AND volumeBInBin(d) (+) shipmentVolume(d) > volumeInBin(d) DO {
    MESSAGE 'Внимание! Объем резервуара ' + nameInBin(d) + ' превышен!' NOWAIT;
}
WHEN (SET(isPosted(UserInvoiceDetail d)) OR SET(createShipment(d)) OR CHANGED(inBin(d)) OR CHANGED(shipmentDateTime(d)) OR CHANGED(shipmentVolume(d)) OR CHANGED(sku(d)))
      AND isPosted(d) AND createShipment(d) AND volumeBInBin(d) AND isFuel(sku(d)) AND NOT prevVolumeB(sku(d), inBin(d), shipmentDateTime(d)) DO {
    MESSAGE 'Внимание! В резервуар ' + nameInBin(d) + ' залито несколько видов топлива!' NOWAIT;
}

densityBInBin 'Плотность в резервуаре (до), кг/л' (UserInvoiceDetail invoiceDetail) = prevDensityB(inBin(invoiceDetail), dateTime(invoiceDetail));

overBackgroundDensity(UserInvoiceDetail d) += WHEN abs(density(d) - densityBInBin(d)) > (OVERRIDE invoiceDensityDeviation(), 0.0) THEN RGB(255,154,154);
overBackgroundShipmentDensity(UserInvoiceDetail d) += WHEN abs(density(d) - densityBInBin(d)) > (OVERRIDE invoiceDensityDeviation(), 0.0) THEN RGB(255,154,154);

EXTEND FORM userInvoice
    PROPERTIES(d) READONLY AFTER nameInBin(d) densityBInBin BACKGROUND backgroundShipped(i)
;

//----------------------------- переименование ячейки в резервуар -----------------------------//
DESIGN operation {
    PROPERTY(useBins(o)) { caption = 'Резервуары'; }
    PROPERTY(nameInBin(o,stc)) { caption = 'Резервуар (в)'; }
    PROPERTY(nameOutBin(o,stc)) { caption = 'Резервуар (из)'; }
}
DESIGN userInvoice {
    headerBins { caption = 'Резервуары'; }
    PROPERTY(useBins(i)) { caption = 'Резервуары'; }
    REMOVE PROPERTY(nameInBin(i)); // { caption = 'Резервуар (в)'; }
    REMOVE PROPERTY(nameOutBin(i)); // { caption = 'Резервуар (из)'; }
    PROPERTY(nameInBin(d)) { caption = 'Резервуар'; }
    PROPERTY(nameOutBin(d)) { caption = 'Резервуар (из)'; hide = TRUE;}
}
DESIGN invoices {
    PROPERTY(nameInBin(d)) { caption = 'Резервуар'; }
    PROPERTY(nameOutBin(d)) { caption = 'Резервуар (из)'; hide = TRUE;}
}
DESIGN invoiceDetails {
    PROPERTY(nameInBin(d)) { caption = 'Резервуар'; }
    PROPERTY(nameOutBin(d)) { caption = 'Резервуар (из)'; hide = TRUE;}
}

MODULE PurchaseShipmentBinLedgerFuel;

REQUIRE PurchaseShipmentBinLedger, BinLedgerFuel, PurchaseShipmentFuel;

NAMESPACE Purchase;

volume[BinLedger] (ShipmentBinLedger ledger) += volume(shipmentDetail(ledger));
density[BinLedger] (ShipmentBinLedger ledger) += density(shipmentDetail(ledger));

volumeInBin 'Объем резервуара (в)' (UserInvoiceDetail invoiceDetail) = volume(inBin(invoiceDetail));
volumeAInBin 'Объем в резервуарe (в) (после), л' (UserInvoiceDetail invoiceDetail) = volumeA(inBin(invoiceDetail), shipmentDateTime(invoiceDetail));

backgroundDiffValueBin (UserInvoiceDetail d) = CASE
    WHEN volumeAInBin(d) > volumeInBin(d) THEN overColorValueBin()
    WHEN (GROUP SUM 1 IF volumeA(Sku sku, inBin(d), shipmentDateTime(d))) > 1 THEN overColorValueBin()
    ELSE backgroundShipped(d);

EXTEND FORM userInvoice
    PROPERTIES(d) READONLY AFTER nameInBin(d) volumeInBin BACKGROUND backgroundShipped(d), volumeAInBin BACKGROUND backgroundDiffValueBin(d)
;

WHEN (SET(isPosted(UserInvoiceDetail d)) OR SET(createShipment(d)) OR CHANGED(inBin(d)) OR CHANGED(shipmentDateTime(d)) OR CHANGED(shipmentVolume(d)))
      AND isPosted(d) AND createShipment(d) AND inBin(d) AND isFuel(sku(d)) AND volumeAInBin(d) > volumeInBin(d) DO {
    MESSAGE 'Внимание! Объем резервуара ' + nameInBin(d) + ' превышен!' NOWAIT;
}
WHEN (SET(isPosted(UserInvoiceDetail d)) OR SET(createShipment(d)) OR CHANGED(inBin(d)) OR CHANGED(shipmentDateTime(d)) OR CHANGED(shipmentVolume(d)))
      AND isPosted(d) AND createShipment(d) AND inBin(d) AND isFuel(sku(d)) AND (GROUP SUM 1 IF volumeA(Sku sku, inBin(d), shipmentDateTime(d))) > 1 DO {
    MESSAGE 'Внимание! В резервуар ' + nameInBin(d) + ' залито несколько видов топлива!' NOWAIT;
}

densityBInBin 'Текущая плотность в резервуаре (в) (до), кг/л' (UserInvoiceDetail invoiceDetail) = densityB(inBin(invoiceDetail), dateTime(invoiceDetail));

overBackgroundDensity(UserInvoiceDetail d) += WHEN abs(density(d) (-) densityBInBin(d)) > (OVERRIDE invoiceDensityDeviation(), 0.0) THEN RGB(255,154,154);
overBackgroundShipmentDensity(UserInvoiceDetail d) += WHEN abs(density(d) (-) densityBInBin(d)) > (OVERRIDE invoiceDensityDeviation(), 0.0) THEN RGB(255,154,154);

EXTEND FORM userInvoice
    PROPERTIES(d) READONLY AFTER nameInBin(d) densityBInBin BACKGROUND backgroundShipped(i)
;

//----------------------------- переименование ячейки в резервуар -----------------------------//
DESIGN operation {
    PROPERTY(useBins(o)) { caption = 'Резервуары'; }
    PROPERTY(nameInBin(o,stc)) { caption = 'Резервуар (в)'; }
    PROPERTY(nameOutBin(o,stc)) { caption = 'Резервуар (из)'; }
}
DESIGN userInvoice {
    headerBins { caption = 'Резервуары'; }
    PROPERTY(useBins(i)) { caption = 'Резервуары'; }
    PROPERTY(nameInBin(i)) { caption = 'Резервуар (в)'; }
    PROPERTY(nameOutBin(i)) { caption = 'Резервуар (из)'; }
    PROPERTY(nameInBin(d)) { caption = 'Резервуар (в)'; }
    PROPERTY(nameOutBin(d)) { caption = 'Резервуар (из)'; }
}
DESIGN invoices {
    PROPERTY(nameInBin(d)) { caption = 'Резервуар (в)'; }
    PROPERTY(nameOutBin(d)) { caption = 'Резервуар (из)'; }
}
DESIGN invoiceDetails {
    PROPERTY(nameInBin(d)) { caption = 'Резервуар (в)'; }
    PROPERTY(nameOutBin(d)) { caption = 'Резервуар (из)'; }
}

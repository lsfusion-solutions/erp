MODULE BinFuel;

REQUIRE Bin, PumpFuel;

NAMESPACE Bin;

number 'Номер' = DATA INTEGER (Bin) NONULL;
volume 'Объем, л' = DATA NUMERIC[16,5] (Bin) CHARWIDTH 7;
pipeVolume 'Объём трубопровода, л' = DATA NUMERIC[16,5] (Bin) CHARWIDTH 7;

EXTEND FORM bin
    PROPERTIES(s) number, volume, pipeVolume
;

DESIGN bin {
    mainContainer {
        MOVE PROPERTY (number(s)) FIRST;
    }
    NEW fuelContainer BEFORE additionalContainer {
        caption = 'Параметры резервуара';
        lines = 3;
        MOVE PROPERTY (volume(s));
        MOVE PROPERTY (pipeVolume(s));
    }
}

EXTEND FORM bins
    PROPERTIES(s) READONLY number, volume, pipeVolume
;
EXTEND FORM binStocks
    PROPERTIES(b) READONLY AFTER canonicalName(b) number, volume, pipeVolume
;

DESIGN binStocks {
    PROPERTY (bins(g)){hide = TRUE;}
    PROPERTY (bins(s)){hide = TRUE;}
}

bin = DATA Bin(Gun);
nameBin 'Резервуар' (Gun gun) = name(bin(gun));

CONSTRAINT stock(Gun gun) AND bin(gun) AND NOT stock(gun)==stock(bin(gun))
    CHECKED BY bin[Gun] MESSAGE 'Склад и резервуар для пистолета не имеют связи';

EXTEND FORM gun
    PROPERTIES(g) nameBin BEFORE name(g)
;

EXTEND FORM guns
    PROPERTIES(g) READONLY nameBin BEFORE name(g)
;

EXTEND FORM gunBins
    PROPERTIES(g) READONLY nameBin AFTER fullName(g)
;

NAVIGATOR {
    binNavigator {
        MOVE pumps;
        MOVE guns;
        MOVE gunBins;
    }
}
MODULE BinFuel;

REQUIRE Bin;

NAMESPACE Bin;

number 'Номер' = DATA INTEGER (Bin) NONULL;
volume 'Объем, л' = DATA NUMERIC[16,5] (Bin) CHARWIDTH 7;

EXTEND FORM bin
    PROPERTIES(s) number, volume
;
EXTEND FORM bins
    PROPERTIES(s) READONLY number, volume
;
EXTEND FORM binStocks
    PROPERTIES(b) READONLY AFTER canonicalName(b) number, volume
;

CLASS Pump 'ТРК';
TABLE pump (Pump);

@defineExternalizable(pump, STRING[100]);

name 'Наименование' = DATA ISTRING[100](Pump) IN id;
number 'Номер' = DATA INTEGER (Pump) NONULL;

stock = DATA Stock(Pump);
nameStock 'Склад' (Pump pump) = name(stock(pump));

CONSTRAINT stock(Pump pump) AND NOT isCompany(stock(pump))
    CHECKED BY stock[Pump] MESSAGE 'Для ТРК выбран склад, не являющийся складом компании';

showIDsFuel = DATA BOOLEAN();

FORM pump 'ТРК'
    OBJECTS p = Pump PANEL
    PROPERTIES(p) name, id SHOWIF showIDs() AND showIDsFuel(), number, nameStock
    EDIT Pump OBJECT p
;

filterStock = DATA LOCAL NESTED Stock();
nameFilterStock 'Склад' () = name(filterStock());

filterPump = DATA LOCAL NESTED Pump();
nameFilterPump 'ТРК' () = name(filterPump());

CONSTRAINT filterStock() AND NOT isCompany(filterStock())
    CHECKED BY filterStock[] MESSAGE 'Для фильтра выбран склад, который не принадлежит компании';
CONSTRAINT filterPump() AND NOT isCompany(stock(filterPump()))
    CHECKED BY filterPump[] MESSAGE 'Для фильтра выбран ТРК, который не принадлежит компании';
CONSTRAINT filterStock() AND filterPump() AND NOT stock(filterPump()) == filterStock()
    CHECKED BY filterPump[] MESSAGE 'Для фильтра выбран ТРК, который не принадлежит выбранному складу'; 

FORM pumps 'ТРК'
    OBJECTS p = Pump
    PROPERTIES(p) READONLY name, id SHOWIF showIDs() AND showIDsFuel(), number, nameStock
    PROPERTIES(p) NEWSESSION NEW, EDIT, DELETE
    ORDERS name(p)
    
    PROPERTIES() nameFilterStock
    FILTERS stock(p) == filterStock() OR NOT filterStock()
    
    LIST Pump OBJECT p
;

CLASS Gun 'Пистолет' ;
TABLE gun (Gun);

@defineExternalizable(gun, STRING[100]);

name 'Наименование' = DATA ISTRING[100](Gun) IN id;
number 'Номер' = DATA INTEGER (Gun) NONULL;

stock = DATA Stock(Gun);
nameStock 'Склад' (Gun gun) = name(stock(gun));

CONSTRAINT stock(Gun gun) AND NOT isCompany(stock(gun))
    CHECKED BY stock[Gun] MESSAGE 'Для пистолета выбран склад, не являющийся складом компании';

pump = DATA Pump(Gun);
namePump 'ТРК' (Gun gun) = name(pump(gun));

CONSTRAINT stock(Gun gun) AND pump(gun) AND NOT stock(gun) == stock(pump(gun))
    CHECKED BY pump[Gun] MESSAGE 'Склад и ТРК для пистолета не имеют связи';

bin = DATA Bin(Gun);
nameBin 'Резервуар' (Gun gun) = name(bin(gun));

CONSTRAINT stock(Gun gun) AND bin(gun) AND NOT in(stock(gun),bin(gun))
    CHECKED BY bin[Gun] MESSAGE 'Склад и резервуар для пистолета не имеют связи';

documentNumber 'Номер полный (ТРК/пистолет)' (Gun gun) = CONCAT '/', number(pump(gun)), number(gun);
documentName 'Наименование (для документов)' (Gun gun) = 'Пистолет ' + documentNumber(gun);

FORM gun 'Пистолет'
    OBJECTS g = Gun PANEL
    PROPERTIES(g) name, id SHOWIF showIDs() AND showIDsFuel(), number, nameStock, namePump, nameBin
    EDIT Gun OBJECT g
;

FORM guns 'Пистолеты'
    OBJECTS g = Gun
    PROPERTIES(g) READONLY name, id SHOWIF showIDs() AND showIDsFuel(), number, documentName, documentNumber, nameStock, namePump, nameBin
    PROPERTIES(g) NEWSESSION NEW, EDIT, DELETE
    ORDERS name(g)
    
    PROPERTIES() nameFilterStock, nameFilterPump
    FILTERS stock(g) == filterStock() OR NOT filterStock(),
            pump(g) == filterPump() OR NOT filterPump()
    
    LIST Gun OBJECT g
;

FORM gunBins 'Привязка пистолетов'
    OBJECTS ts = Stock PANEL
    PROPERTIES(ts) SELECTOR name
    FILTERS isCompany(ts)
    
    OBJECTS p = Pump
    PROPERTIES(p) READONLY name, number
    FILTERS stock(p) == ts
    ORDERS name(p)
    PROPERTIES(p) NEWSESSION NEW, EDIT, DELETE
    
    OBJECTS g = Gun
    PROPERTIES(g) READONLY name, number, documentName, documentNumber, nameBin
    FILTERS stock(g) == ts, pump(g) == p
    ORDERS name(g)
    PROPERTIES(g) NEWSESSION NEW, EDIT, DELETE
;

DESIGN gunBins {
    OBJECTS {
        NEW tables AFTER BOX(ts) {
            fill = 1;
            horizontal = TRUE;
            MOVE BOX(p) {
                fill = 1;
            }
            MOVE BOX(g) {
                fill = 3;
            }
            
        }
    }
}

NAVIGATOR {
    binNavigator {
        NEW pumps;
        NEW guns;
        NEW gunBins;
    }
}
MODULE ZReportFuelLtr;

REQUIRE ZReportFuel, ItemFuelLtr; 

NAMESPACE ZReport;

weight 'Масса (топливо), кг' (receiptDetail) = DATA NUMERIC[16,5] (ReceiptSaleDetail);
weight 'Масса (топливо), кг' (receiptDetail) = DATA NUMERIC[16,5] (ReceiptReturnDetail);

weight 'Масса (топливо), кг' = ABSTRACT NUMERIC[16,5] (ReceiptDetail) MATERIALIZED;
weight(ReceiptReturnDetail detail) += weight(detail);
weight(ReceiptSaleDetail detail) += weight(detail);

signedWeight 'Масса (топливо), кг' = ABSTRACT NUMERIC[16,5] (ReceiptDetail) MATERIALIZED;
signedWeight(ReceiptReturnDetail detail) += -weight(detail);
signedWeight(ReceiptSaleDetail detail) += weight(detail);

weightReceiptDetail 'Масса (топливо, всего), кг' (receipt) = GROUP SUM weight(ReceiptDetail idetail) BY receipt(idetail) IN documentSum CHARWIDTH 8 MATERIALIZED;
weightReceiptDetail 'Масса (топливо, всего), кг' (zReport) = GROUP SUM signedWeight(ReceiptDetail detail) BY zReport(detail) IN documentSum MATERIALIZED;

EXTEND FORM zReport
    PROPERTIES(z) weightReceiptDetail
    PROPERTIES(b) weightReceiptDetail AFTER quantityReceiptDetail(b)
    PROPERTIES(d) READONLYIF NOT isFuelLtr(sku(d)) BACKGROUND background(d) weight AFTER quantity(d)
;

overCopy(ReceiptDetail d, ReceiptDetail detail) + {
    weight(d) <- weight(detail);
}

EXTEND FORM zReports
    PROPERTIES(z) READONLY weightReceiptDetail AFTER quantityReceiptDetail(z)
    PROPERTIES(b) READONLY weightReceiptDetail AFTER quantityReceiptDetail(b)
    PROPERTIES(d) READONLY weight AFTER quantity(d)
;

DESIGN zReport {
    PROPERTY (volume(d)) { showIf = NULL; }
    PROPERTY (volumeReceiptDetail(b)) { showIf = NULL; }
    PROPERTY (volumeReceiptDetail(z)) { showIf = NULL; }
}

DESIGN zReports {
    PROPERTY (volume(d)) { showIf = NULL; }
    PROPERTY (volumeReceiptDetail(b)) { showIf = NULL; }
    PROPERTY (volumeReceiptDetail(z)) { showIf = NULL; }
}


MODULE PurchaseInvoiceFuel;

REQUIRE PurchaseInvoice, SkuLedgerFuel, ItemFuel;

NAMESPACE Purchase;

showTemperatureInvoice 'Отображать температуру в накладных' = DATA BOOLEAN () PREREAD;
invoiceDensityDeviation 'Допустимое отклонение плотности в накладных, кг/л' = DATA NUMERIC[9,5] ();

EXTEND FORM options
    PROPERTIES () showTemperatureInvoice, invoiceDensityDeviation
;
DESIGN options {
    purchase {
        MOVE PROPERTY(showTemperatureInvoice());
        MOVE PROPERTY(invoiceDensityDeviation());
    }
}

@defineOperationProperty(showFuel, 'Топливо', showContainer);

showFuel(Invoice i) = showFuel(operation(i));
showFuel(InvoiceDetail d) = showFuel(operation(d));
showFuel(UserInvoice i) = showFuel(operation(i));
showFuel(UserInvoiceDetail d) = showFuel(operation(d));

temperature 'Температура, °С' = ABSTRACT NUMERIC[5,1] (InvoiceDetail) CHARWIDTH 7 MATERIALIZED;
temperature 'Температура, °С' = DATA NUMERIC[5,1] (UserInvoiceDetail) CHARWIDTH 7;
temperature (UserInvoiceDetail invoiceDetail) += temperature(invoiceDetail);

volume 'Объём (топливо), л' = ABSTRACT NUMERIC[16,5] (InvoiceDetail) CHARWIDTH 7 MATERIALIZED;
volume 'Объём (топливо), л' = DATA NUMERIC[16,5] (UserInvoiceDetail) CHARWIDTH 7;
volume (UserInvoiceDetail invoiceDetail) += volume(invoiceDetail);

density 'Плотность, кг/л' = ABSTRACT NUMERIC[9,5] (InvoiceDetail) CHARWIDTH 7 MATERIALIZED;
density 'Плотность, кг/л' = DATA NUMERIC[9,5] (UserInvoiceDetail) CHARWIDTH 7;
density (UserInvoiceDetail invoiceDetail) += density(invoiceDetail);

@fuelVolumeConstraintInvoice(invoiceDetail);
@fuelDensity(userInvoiceDetail, 'накладная (закупка)');

volumeInvoiceDetail 'Объём (топливо, всего), л' (invoice) = GROUP SUM volume(InvoiceDetail idetail) BY invoice(idetail) IN documentSum CHARWIDTH 8 MATERIALIZED;
volumeUserInvoiceDetail 'Объём (топливо, всего), л' (userInvoice) = GROUP SUM volume(UserInvoiceDetail idetail) BY userInvoice(idetail) IN documentSum CHARWIDTH 8 MATERIALIZED;

overBackgroundDensity =  ABSTRACT CASE COLOR (UserInvoiceDetail);

EXTEND FORM userInvoice
    PROPERTIES(i) SHOWIF showFuel(i) volumeUserInvoiceDetail
    PROPERTIES(d) READONLYIF NOT isFuel(sku(d)) SHOWIF (showQuantity(i) AND showFuel(i)) volume  BACKGROUND backgroundQuantity(d) AFTER quantity(d)
    PROPERTIES(d) READONLYIF NOT (isFuel(sku(d)) OR extraIsFuel(sku(d))) SHOWIF showFuel(i) density AFTER volume(d) ON CHANGE changeDensity(d) ON CONTEXTMENU fillDensity15C(d) ON CONTEXTMENU fillDensityFromTable(d) BACKGROUND overBackgroundDensity(d)
    PROPERTIES(d) READONLYIF NOT (isFuel(sku(d)) OR extraIsFuel(sku(d))) SHOWIF (showTemperatureInvoice() AND showFuel(i)) temperature
;

overCopy(UserInvoiceDetail d, InvoiceDetail detail) + {
    volume(d) <- volume(detail);
    density(d) <- density(detail);
    temperature(d) <- temperature(detail) IF showTemperatureInvoice();
}
overCopy[UserInvoiceDetail,UserInvoiceDetail](UserInvoiceDetail d, UserInvoiceDetail detail) + {
    volume(d) <- volume(detail);
    density(d) <- density(detail);
    temperature(d) <- temperature(detail) IF showTemperatureInvoice();
}

EXTEND FORM invoices
    PROPERTIES(i) READONLY SHOWIF showFuel(i) BACKGROUND background(i) volumeInvoiceDetail AFTER quantityInvoiceDetail(i)
    PROPERTIES(d) READONLY SHOWIF showFuel(i) volume AFTER quantity(d)
    PROPERTIES(d) READONLY SHOWIF showFuel(i) density AFTER volume(d)
    PROPERTIES(d) READONLY SHOWIF (showFuel(i) AND showTemperatureInvoice()) temperature
;


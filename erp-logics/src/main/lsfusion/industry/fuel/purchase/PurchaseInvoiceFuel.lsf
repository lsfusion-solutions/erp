MODULE PurchaseInvoiceFuel;

REQUIRE PurchaseInvoice, SkuLedgerFuel, ItemFuel;

NAMESPACE Purchase;

showTemperatureInvoice 'Отображать температуру в накладных' = DATA BOOLEAN () PREREAD;
invoiceDensityDeviation 'Допустимое отклонение плотности в накладных, кг/л' = DATA NUMERIC[8,4] ();

EXTEND FORM options
    PROPERTIES () showTemperatureInvoice, invoiceDensityDeviation
;
DESIGN options {
    purchase {
        MOVE PROPERTY(showTemperatureInvoice());
        MOVE PROPERTY(invoiceDensityDeviation());
    }
}

temperature 'Температура, °С' = ABSTRACT NUMERIC[5,1] (InvoiceDetail) CHARWIDTH 7 MATERIALIZED;
temperature 'Температура, °С' = DATA NUMERIC[5,1] (UserInvoiceDetail) CHARWIDTH 7;
temperature (UserInvoiceDetail invoiceDetail) += temperature(invoiceDetail);

volume 'Объем, л' = ABSTRACT NUMERIC[16,5] (InvoiceDetail) CHARWIDTH 7 MATERIALIZED;
volume 'Объем, л' = DATA NUMERIC[16,5] (UserInvoiceDetail) CHARWIDTH 7;
volume (UserInvoiceDetail invoiceDetail) += volume(invoiceDetail);

density 'Плотность, кг/л' = ABSTRACT NUMERIC[8,4] (InvoiceDetail) CHARWIDTH 7 MATERIALIZED;
density 'Плотность, кг/л' = DATA NUMERIC[8,4] (UserInvoiceDetail) CHARWIDTH 7;
density (UserInvoiceDetail invoiceDetail) += density(invoiceDetail);

//quantity[Invoice.InvoiceDetail](InvoiceDetail detail) += quantity(detail);
CONSTRAINT CHANGED(volume(InvoiceDetail detail)) AND negativeValueInvoices() AND isFuel(skuGroup(sku(detail))) AND volume(detail) < 0
    MESSAGE 'Объем в строке накладной должен быть неотрицательным';

@fuelDensity(userInvoiceDetail, 'накладная (закупка)');

volumeInvoiceDetail 'Объем (всего), л' (invoice) = GROUP SUM volume(InvoiceDetail idetail) BY invoice(idetail) IN documentSum CHARWIDTH 8 MATERIALIZED;
volumeUserInvoiceDetail 'Объем (всего), л' (userInvoice) = GROUP SUM volume(UserInvoiceDetail idetail) BY userInvoice(idetail) IN documentSum CHARWIDTH 8 MATERIALIZED;

overBackgroundDensity =  ABSTRACT CASE COLOR (UserInvoiceDetail);

EXTEND FORM userInvoice
    PROPERTIES(i) volumeUserInvoiceDetail
    PROPERTIES(d) READONLYIF NOT isFuel(sku(d)) volume SHOWIF showQuantity(i) BACKGROUND backgroundQuantity(d) AFTER quantity(d)
    PROPERTIES(d) READONLYIF NOT isFuel(sku(d)) density AFTER volume(d) ON CHANGE changeDensity(d) ON CONTEXTMENU fillDensity15C(d) BACKGROUND overBackgroundDensity(d)
    PROPERTIES(d) READONLYIF NOT isFuel(sku(d)) temperature SHOWIF showTemperatureInvoice()
;

overCopy(UserInvoiceDetail d, InvoiceDetail detail) + {
    volume(d) <- volume(detail);
    density(d) <- density(detail);
    temperature(d) <- temperature(detail) IF showTemperatureInvoice();
}
overCopy[UserInvoiceDetail,UserInvoiceDetail](UserInvoiceDetail d, UserInvoiceDetail detail) + {
    volume(d) <- volume(detail);
    density(d) <- density(detail);
    temperature(d) <- temperature(detail) IF showTemperatureInvoice();
}

EXTEND FORM invoices
    PROPERTIES(i) READONLY BACKGROUND background(i) volumeInvoiceDetail AFTER quantityInvoiceDetail(i)
    PROPERTIES(d) READONLY volume AFTER quantity(d)
    PROPERTIES(d) READONLY density AFTER volume(d)
    PROPERTIES(d) READONLY temperature SHOWIF showTemperatureInvoice()
;

CONSTRAINT (SETCHANGED(volume(InvoiceDetail d)) OR SETCHANGED(operation(d)) OR SET(isPosted(d))) AND isFuel(skuGroup(sku(d))) AND isPosted(d) AND volume(d) < 0.0
    AND banNegativeQuantityInvoice(operation(d))
    MESSAGE 'Запрещено вводить отрицательный объем в накладную'; 

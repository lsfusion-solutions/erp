MODULE PurchaseInvoiceFuelLtr;

REQUIRE PurchaseInvoiceFuel, ItemFuelLtr;

NAMESPACE Purchase;

weight 'Масса (топливо), кг' = ABSTRACT NUMERIC[16,5] (InvoiceDetail) CHARWIDTH 7 MATERIALIZED;
weight 'Масса (топливо), кг' = DATA NUMERIC[16,5] (UserInvoiceDetail) CHARWIDTH 7;
weight (UserInvoiceDetail invoiceDetail) += weight(invoiceDetail);

CONSTRAINT (SETCHANGED(weight(InvoiceDetail d)) OR SETCHANGED(operation(d)) OR SET(isPosted(d))) AND isFuelLtr(sku(d)) AND isPosted(d) AND weight(d) < 0.0
    AND (banNegativeQuantityInvoice(operation(d)) OR negativeValueInvoices())
    MESSAGE 'Запрещено вводить отрицательную массу топлива в накладную';

WHEN LOCAL (CHANGED(quantity(UserInvoiceDetail d)) OR CHANGED(weight(d))) AND NOT CHANGED(density(d)) AND isFuelLtr(sku(d)) AND quantity(d) AND weight(d) DO {
    density(d) <- NUMERIC[8,4](weight(d) / quantity(d));
}

extraFillDensity (UserInvoiceDetail d, NUMERIC[8,4] n) + {
    IF isFuelLtr(sku(d)) THEN {
        IF n THEN {
            density(d) <- n;
            IF NOT quantity(d) THEN {
                quantity(d) <- NUMERIC[16,5](round3(weight(d) / density(d)));
            } ELSE IF NOT weight(d) THEN {
                weight(d) <- NUMERIC[16,5](round3(quantity(d) * density(d)));
            } ELSE {
                ASK 'Нажмите "Да" для пересчета массы (кг) или "Нет" для пересчета кол-ва (л)' yes = YESNO DO {
                    IF yes THEN {
                        weight(d) <- NUMERIC[16,5](round3(quantity(d) * density(d)));
                    } ELSE {
                        quantity(d) <- NUMERIC[16,5](round3(weight(d) / density(d)));
                    }
                } ELSE {
                    density(d) <- PREV(density(d));
                }
            }
        } ELSE {
            density(d) <- NUMERIC[8,4](weight(d) / quantity(d));
        }
    }
}

weightInvoiceDetail 'Масса (топливо, всего), кг' (invoice) = GROUP SUM weight(InvoiceDetail idetail) BY invoice(idetail) IN documentSum CHARWIDTH 8 MATERIALIZED;
weightUserInvoiceDetail 'Масса (топливо, всего), кг' (userInvoice) = GROUP SUM weight(UserInvoiceDetail idetail) BY userInvoice(idetail) IN documentSum CHARWIDTH 8 MATERIALIZED;

showWeightFuel = ABSTRACT VALUE BOOLEAN (Invoice);

EXTEND FORM userInvoice
    PROPERTIES(i) SHOWIF (showWeightFuel(i) AND showFuel(i)) weightUserInvoiceDetail
    PROPERTIES(d) READONLYIF NOT isFuelLtr(sku(d)) SHOWIF (showWeightFuel(i) AND showFuel(i)) weight AFTER quantity(d)
;

overCopy(UserInvoiceDetail d, InvoiceDetail detail) + {
    weight(d) <- weight(detail);
}

overCopy[UserInvoiceDetail,UserInvoiceDetail](UserInvoiceDetail d, UserInvoiceDetail detail) + {
    weight(d) <- weight(detail);
}

EXTEND FORM invoices
    PROPERTIES(i) READONLY SHOWIF (showWeightFuel(i) AND showFuel(i)) BACKGROUND background(i) weightInvoiceDetail AFTER quantityInvoiceDetail(i)
    PROPERTIES(d) READONLY SHOWIF (showWeightFuel(i) AND showFuel(i)) weight AFTER quantity(d)
;

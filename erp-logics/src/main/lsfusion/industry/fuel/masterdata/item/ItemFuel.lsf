MODULE ItemFuel;

REQUIRE Item;

NAMESPACE Item;

@defineItemGroupValue(isFuel, 'Топливо', BOOLEAN);

isFuel 'Топливо' (Item i) = isFuel(itemGroup(i)) MATERIALIZED;

GROUP itemDensity 'Плотность' : public;

density15C 'Плотность при 15°С, кг/л' = DATA NUMERIC[9,5] (Item) IN itemDensity;
ktr 'КТР' = DATA NUMERIC[8,5] (Item) IN itemDensity;

@defineItemAttribute(density15C, 'Плотность при 15°С, кг/л', itemDensity);

EXTEND FORM itemGroup PROPERTIES(g) userShowDensity15C;
EXTEND FORM itemOptions PROPERTIES(g) READONLYIF isReadonly() showDensity15C BACKGROUND backgroundShowDensity15C(g);
DESIGN itemGroup {
    itemGroupDetail1 {
        MOVE GROUP(itemDensity,g) {
        }
    }
}

overCopy(Item s, Item d) + { 
    density15C(d) <- density15C(s);
    ktr(d) <- ktr(s);
}

extraIsFuel = ABSTRACT BOOLEAN (Item);

EXTEND FORM item 
    PROPERTIES(i) SHOWIF (showDensity15C(i) OR extraIsFuel(i)) density15C, ktr
;

DESIGN item {
    itemPrimaryColumn2 {
        MOVE GROUP(itemDensity,i) {
            alignment = STRETCH;
//            lines = 2;
        }
    }
}

EXTEND FORM items 
    PROPERTIES(i) READONLYIF isReadonly() BEFORE deletei SHOWIF showDensity15C(g) density15C, ktr
;

//----------------------- Переход на новый основной классификатор ---------------------------//
overReplaceToItemGroup(CustomGroup g, ItemGroup ng) + {
    userShowDensity15C(ng) <- userShowDensity15C(itemGroup(g));
}

CLASS TermperatureLevel 'Температурный уровень';
TABLE termperatureLevel(TermperatureLevel);

temperature 'Температура, °С' = DATA NUMERIC[5,1](TermperatureLevel);

density 'Плотность, кг/л' = DATA NUMERIC[9,5](Item, TermperatureLevel);

EXTEND FORM item
    OBJECTS tli = TermperatureLevel
    PROPERTIES SHOWIF (isFuel(i) OR extraIsFuel(i)) temperature(tli) READONLY, density(i, tli)
    ORDERS temperature(tli)
;
    
DESIGN item {
    itemDetail {
        NEW densityTable {
            caption = 'Таблица плотностей';
            MOVE BOX(tli);
        }
    }
}

FORM densityTable 'Таблица плотностей'
    OBJECTS tl = TermperatureLevel
    PROPERTIES (tl) temperature, NEW, DELETE
    
    OBJECTS i = Item PANEL
    PROPERTIES name(i) SELECTOR 
    FILTERS isFuel(i) OR extraIsFuel(i)
    
    OBJECTS tli = TermperatureLevel
    PROPERTIES temperature(tli) READONLY, density(i, tli)
    ORDERS temperature(tli)
;

DESIGN densityTable {
    OBJECTS {
        NEW mainTabs {
            tabbed = TRUE;
            fill = 1;
            NEW tableT {
                caption = 'Температурные уровни';
                MOVE BOX(tl);
            }
            NEW tableD {
                caption = 'Плотность топлива';
                MOVE BOX(i) {
                    PROPERTY (name(i)) { charWidth = 50; }
                }
                MOVE BOX(tli) {
                    fill = 1;
                }
            }
        }
    }
}

FORM densityTableDialog 'Таблица плотностей'
    OBJECTS i = Item PANEL
    PROPERTIES READONLY name(i)

    OBJECTS tli = TermperatureLevel
    PROPERTIES READONLY temperature(tli), density(i, tli)
    ORDERS temperature(tli)
;

DESIGN densityTableDialog {
    OBJECTS {
        NEW main {
            fill = 1;
            MOVE BOX(i) {
                PROPERTY (name(i)) { charWidth = 50; }
            }
            MOVE BOX(tli) {
                fill = 1;
            }
        }
    }
}

NAVIGATOR {
    skuNavigator {
        NEW densityTable;
    }
}
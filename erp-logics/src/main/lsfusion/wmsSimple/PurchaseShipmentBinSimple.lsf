MODULE PurchaseShipmentBinSimple;

REQUIRE PurchaseBinSimple, PurchaseShipment, Item, BinSimple;

NAMESPACE Purchase;

dontSetBinPurchase 'Не заполнять ячейки на приходе' = DATA BOOLEAN ();

EXTEND FORM options
    PROPERTIES dontSetBinPurchase()
;

DESIGN options {
    purchase {
        MOVE PROPERTY(dontSetBinPurchase());
    }
}

@defineDocumentInterfaceDetailBin(shipment);
overCopy[UserShipmentDetail,UserShipmentDetail](UserShipmentDetail d, UserShipmentDetail detail) + {       
    bin(d) <- bin(detail);
}
@extendFormInterfaceDetailBin(shipment, s, , quantity);
bin(UserShipmentDetail detail) <- (GROUP MAX Bin bin IF in(sku(detail), bin) AND stock(bin)==customerStock(detail)) IF NOT dontSetBinPurchase() 
    WHEN (CHANGED(customerStock(detail)) OR
         CHANGED(sku(detail))) AND NOT allowReplaceItem();

@deriveDocumentOperationProperty(UserShipment, showBin);

//-- invoice
@defineInvoiceShipmentBin(customerStock);

bin(UserInvoiceDetail detail) <- (GROUP MAX Bin bin IF in(sku(detail), bin) AND stock(bin)==customerStock(detail)) IF NOT dontSetBinPurchase()
    WHEN (CHANGED(customerStock(detail)) OR
          CHANGED(sku(detail))) AND NOT allowReplaceItem();

@deriveDocumentOperationProperty(UserInvoice, showBin);

//-- Проводим по регистру (ячейка)
//bin (ShipmentBatch ledger) += bin(shipmentDetail(ledger));

//---------------------------------Новый функционал ----------------------------------//

@defineDocumentDetailBinCustom(userShipment, current, ' текущая');
currentBin (UserShipmentDetail detail) = currentBin(userShipment(detail));

EXTEND FORM userShipment
    PROPERTIES(s) SHOWIF showBin(s) nameCurrentBin
;

DESIGN userShipment {
    headerBin {
        MOVE PROPERTY(nameCurrentBin(s)) AFTER PROPERTY(showBin(s));
    }
}

WHEN LOCAL FORMS userShipment
    SET(UserShipmentDetail detail IS UserShipmentDetail) AND currentBin(detail)
        DO bin(detail) <- currentBin(detail) WHERE currentBin(detail);

WHEN LOCAL FORMS userShipment
    CHANGED(invoiceDetail(UserShipmentDetail detail)) AND NOT bin(detail)
        DO bin(detail) <- bin(invoiceDetail(detail));



MODULE CountryImport;

REQUIRE SkuLedger;

countryImport = ABSTRACT Country(Batch) MATERIALIZED;
nameCountryImport 'Страна ввоза' (Batch b) = name(countryImport(b)) MINCHARWIDTH 15 PREFCHARWIDTH 20;
prevCountryImport (Batch batch) = PREV(countryImport(batch));

META defineDocumentCountryImport(document, object, showProp)

    EXTEND FORM user###document
        OBJECTS di = User###document##Detail GRID
        PROPERTIES (di) READONLY SHOWIF showProp(object) index, idBarcodeSku, nameSku,
                        nameBatch
        FILTERS user###document(di) == object
    ;

    DESIGN user###document{
        specificationBox{
            NEW countryImportContainer AFTER BOX(d) {
                caption = 'Страна ввоза';
                NEW headerCountryimportContainer { type = CONTAINERH; }
                MOVE BOX(di);
            }
        }
    }
END


META defineDocumentCountryImport(prop, document, object, caption)
    @defineDocumentCountryImport(prop, ###prop, document, object, caption);
END

META defineDocumentCountryImport(prop, propClass, document, object, caption, showProp)

    prop = ABSTRACT propClass (###document##Detail);
    name###prop caption (###document##Detail detail) = name(prop(detail));

    prop = DATA propClass (User###document);
    name###prop caption (User###document document) = name(prop(document));

    data###prop = DATA propClass (User###document##Detail);
    prop (User###document##Detail detail) =
        OVERRIDE data###prop(detail), prop(user###document(detail)) MATERIALIZED INDEXED;
    prop(User###document##Detail detail) += prop(detail);

    name###prop caption (User###document##Detail detail) = name(prop(detail));

    overCopy(User###document##Detail d, User###document##Detail detail) += {      
        data###prop(d) <- data###prop(detail);
    } 
    
    backgroup###prop##User = ABSTRACT COLOR (###document##Detail);

    EXTEND FORM user###document
        PROPERTIES(object) name###prop SHOWIF showProp(object)
        PROPERTIES(d) SHOWIF showProp(object) BACKGROUND backgroup###prop##User(d) name###prop
        PROPERTIES(di) SHOWIF showProp(object) BACKGROUND backgroup###prop##User(di) name###prop
    ;

    DESIGN user###document {
        headerCountryimportContainer {
            MOVE PROPERTY(name###prop(object));
        }
    }

    EXTEND FORM document##s
        PROPERTIES(d) SHOWIF showProp(object) READONLY name###prop
    ;
END

EXTEND FORM batches
    PROPERTIES (bt) READONLY nameCountryImport
;

EXTEND FORM currentBalanceBatchStock
    PROPERTIES (bt) READONLY nameCountryImport
;
EXTEND FORM balanceBatchStock
    PROPERTIES (bt) READONLY nameCountryImport
;
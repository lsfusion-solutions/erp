MODULE SalePurchaseShipmentGeneralLedger;

REQUIRE GeneralLedger, SalePurchaseShipment, DimensionStock;

NAMESPACE Sale;

//------------------- Внутреннее перемещение  --------------------//

EXTEND CLASS Shipment : GeneralLedger.GLDocument;
isPosted[GLDocument](Shipment document) += isPosted(document);
name(Shipment document) += description(document);

number[GLDocument](Shipment document) += number(document);
series[GLDocument](Shipment document) += series(document);

dateTime[GLDocument](Shipment document) += dateTime(document);
operation[GLDocument](Shipment document) += operation(document);

//editGLDocument (GLDocument)+= ACTION editInvoice(GLDocument);

////////////////////////////// Внутреннее перемещение (магазин-магазин) //////////////////////////////////
//-------------------------------- Розничная сумма ----------------------------------//
//-- Тара
@defineGeneralLedgerAggregationOperation (shipment,                                // основание
                                          ShipRetC,                                // идентификатор
                                          supplier,                                // компания
                                          sumContainerShipmentDetail,      // сумма
                                          description,                             // описание
                                          '41.3',                                  // дебет
                                          '41.3',                                  // кредит
                                          'by_default',                            // идентификатор плана счетов
                                          'by_default_sale_shipment_retail'         // идентификатор операции
                                          );
debit(ShipRetCGeneralLedger generalLedger,DimensionType dimensionType) += WHEN CLASS(shipment(generalLedger)) AND dimensionType == DimensionType.stock THEN customerStock(shipment(generalLedger));
credit(ShipRetCGeneralLedger generalLedger,DimensionType dimensionType) += WHEN CLASS(shipment(generalLedger)) AND dimensionType == DimensionType.stock THEN supplierStock(shipment(generalLedger));

//-- Не тара
@defineGeneralLedgerAggregationOperation (shipment,
                                          ShipRetNotC,
                                          supplier,
                                          sumNotContainerShipmentDetail,
                                          description,
                                          '41.2',
                                          '41.2',
                                          'by_default',
                                          'by_default_sale_shipment_retail'
                                          );
debit(ShipRetNotCGeneralLedger generalLedger,DimensionType dimensionType) += WHEN CLASS(shipment(generalLedger)) AND dimensionType == DimensionType.stock THEN customerStock(shipment(generalLedger));
credit(ShipRetNotCGeneralLedger generalLedger,DimensionType dimensionType) += WHEN CLASS(shipment(generalLedger)) AND dimensionType == DimensionType.stock THEN supplierStock(shipment(generalLedger));

// ----------------------------------- Стандартные данные ----------------------------------- //

@defineLoadDefaultOperationPrefix (Sale, iname, isid, sale);

loadDefaultOperationsSalePurchaseRetailBy 'Загрузить стандартные операции внутреннего перемещения м/ду магазинами' () = ACTION {
    EXEC loadDefaultSaleOperation ('Перемещение между собственными магазинами', 'by_default_sale_shipment_retail');
} ;

loadDefaultOperations () += ACTION loadDefaultOperationsSalePurchaseRetailBy();
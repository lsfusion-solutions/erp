MODULE SaleInvoiceGeneralLedger;

REQUIRE GeneralLedger, SaleInvoice, DimensionLegalEntity, DimensionStock;

NAMESPACE Sale;

//------------------- Отгрузка по безналу --------------------//

EXTEND CLASS Invoice : GeneralLedger.GLDocument;
isPosted[GLDocument](Invoice document) += isPosted(document);
name[GLDocument](Invoice document) += description(document);

number[GLDocument](Invoice document) += number(document);
series[GLDocument](Invoice document) += series(document);

dateTime[GLDocument](Invoice document) += dateTime(document);
operation[GLDocument](Invoice document) += operation(document);
//editGLDocument (GLDocument)+= ACTION editInvoice(GLDocument);

////////////////////////////// Отгрузка по безналу (из розницы) //////////////////////////////////
//------------------- Розничная сумма --------------------//
//-- Тара
@defineGeneralLedgerAggregationOperation (invoice,                                 // основание
                                          InvRetC,                                 // идентификатор
                                          supplier,                                // компания
                                          invoiceSumContainerInvoiceDetail, // сумма
                                          description,                             // описание
                                          '62.1',                                  // дебет
                                          '41.3',                                  // кредит
                                          'by_default',                            // идентификатор плана счетов
                                          'by_default_sale_invoice_retail'         // идентификатор операции
                                          );
debit(InvRetCGeneralLedger generalLedger,DimensionType dimensionType) += WHEN CLASS(invoice(generalLedger)) AND dimensionType == DimensionType.organization THEN customer(invoice(generalLedger));
credit(InvRetCGeneralLedger generalLedger,DimensionType dimensionType) += WHEN CLASS(invoice(generalLedger)) AND dimensionType == DimensionType.stock THEN supplierStock(invoice(generalLedger));

//-- Товар
@defineGeneralLedgerAggregationOperation (invoice,
                                          InvRetI,
                                          supplier,
                                          invoiceSumItemInvoiceDetail,
                                          description,
                                          '62.1',
                                          '41.2',
                                          'by_default',
                                          'by_default_sale_invoice_retail'
                                          );
debit(InvRetIGeneralLedger generalLedger,DimensionType dimensionType) += WHEN CLASS(invoice(generalLedger)) AND dimensionType == DimensionType.organization THEN customer(invoice(generalLedger));
credit(InvRetIGeneralLedger generalLedger,DimensionType dimensionType) += WHEN CLASS(invoice(generalLedger)) AND dimensionType == DimensionType.stock THEN supplierStock(invoice(generalLedger));

// ----------------------------------- Стандартные данные ----------------------------------- //

@defineLoadDefaultOperation (Sale, iname, isid);
loadDefaultOperationsSaleRetailBy 'Загрузить стандартные операции отгрузки товара из магазина' () = {
    EXEC loadDefaultOperation ('Отгрузка товара из магазина по безналичному расчету', 'by_default_sale_invoice_retail');
} ;

loadDefaultOperations () += { loadDefaultOperationsSaleRetailBy(); }

@extendFormGeneralLedgerDocument(invoices, i);

MODULE  BOMHeatTreatmentBy;

REQUIRE BOMBy;

NAMESPACE BOM;

// ----------------- Акт контрольной проработки-------------------- //
//overActQuantity(Product p) = OVERRIDE actQuantity(BOM(p)), quantity(p); 
actNettoNetWeight 'Вес нетто изделия для акта г' (Product p) = actQuantity(BOM(p))* (OVERRIDE netWeight(sku(p)), 1.0 IF p IS Product)*1000;
nameSkus 'Компоненты (все)' = GROUP 
    BY BOM BOM, DATE date CONCAT Stock.name(Sku sku) IF recBruttoQuantity(BOM, sku, date), ', '
    ORDER Stock.name(sku);
    
netWeightProduct 'Вес' (BOM) = GROUP BY BOM(Product p) SUM netWeight(sku(p))*1000;   
wastageHeat 'Отходы при тепловой обработке, %' = ABSTRACT VALUE NUMERIC[8,3] (Component ,DATE );  
wastageCold 'Отходы при холодной обработке, %' = ABSTRACT VALUE NUMERIC[8,3] (Component ,DATE );   
wastageHeat (Component c,DATE date) += 0.0 IF c IS Component AND date IS DATE;  
wastageCold (Component c,DATE date) += 0.0 IF c IS Component AND date IS DATE;  
    
actNettoNetWeight 'Выход полуфабриката нетто, г' (Component c)=nettoQuantity(c)*actQuantity(BOM(c))*1000*(OVERRIDE overNetWeight(c), 1.0 IF c IS Component)/quantityProduct(BOM(c));
actBruttoNetWeight 'Выход полуфабриката брутто, г' (Component c,DATE dt)=overBruttoQuantity(c,dt)*actQuantity(BOM(c))*1000.0/quantityProduct(BOM(c));
actBruttoNetWeightEx 'Выход полуфабриката брутто, г' (Component c,DATE dt) = IF wastageHeat(c,dt) == 0.0
    THEN actNettoNetWeight(c) IF dt IS DATE 
    ELSE actBruttoNetWeight(c,dt)*(100.0-wastageCold(c,dt))/100.0;

actBruttoNetWeight 'Вес товара для компонента, если он полуфабрикат' (Component component, Sku sku, DATE date)= recBruttoQuantity(BOM(material(component)), sku, date) *
    bruttoQuantity(component, date)/
    (quantity(material(component)) IF quantity(material(component))!=0) *
    (OVERRIDE netWeight[Item](sku), 1.0 IF sku IS Sku)*
    actQuantity(BOM(component))*1000/
    (quantityProduct(BOM(component)) IF quantityProduct(BOM(component))!=0); 

actBruttoNetWeightSkus 'Вес компонента, если он полуфабрикат по товарам' (component, date)= GROUP BY Component component, DATE date SUM actBruttoNetWeight(component, Sku sku, date);

actNBruttoNetWeight 'Вес товара для компонента, если он полуфабрикат' (Component component, Sku sku, DATE date)= recNettoQuantity(BOM(material(component)), sku) *
    bruttoQuantity(component, date)/
    (quantity(material(component)) IF quantity(material(component))!=0) *
    (OVERRIDE netWeight[Item](sku), 1.0 IF sku IS Sku)*
    actQuantity(BOM(component))*1000/
    (quantityProduct(BOM(component)) IF quantityProduct(BOM(component))!=0); 

actNBruttoNetWeightSkus 'Вес компонента, если он полуфабрикат по товарам' (component, date)= GROUP BY Component component, DATE date SUM actNBruttoNetWeight(component, Sku sku, date);

actBruttoNetWeight 'Вес изделия по товарам, брутто г' (Product product, Sku sku, DATE date)= recBruttoQuantity(BOM(product), sku, date) *
    actQuantity(BOM(product))*1000/
    (quantity(product) IF (quantity(product)!=0)) *
    (OVERRIDE netWeight[Item](sku), 1.0 IF sku IS Sku); 

actBruttoNetWeightSkus 'Вес брутто изделия по товарам, г' (product, date)= GROUP BY Product product, DATE date SUM actBruttoNetWeight(product, Sku sku, date);
actPerc '%' (Product p,DATE dt) = (1 - (actNettoNetWeight(p)/ (actBruttoNetWeightSkus(p,dt) IF actBruttoNetWeightSkus(p,dt)!=0)))*100;
overFullNameCompany (BOM b) = OVERRIDE fullName(company(b)), fullNameLegalEntity(actStock(b));
FORM actStudyOfCulinaryBOM 'Акт контрольной проработки'
    OBJECTS dt=DATE PANEL
    PROPERTIES (dt) date = VALUE , extractYearBOMs 

    OBJECTS b=BOM PANEL 
    PROPERTIES(b)  READONLY products, productsUOM, name, number, series, fromDate, toDate, components, overNettoNetWeightProduct, overNettoNetWeightComponent, pricePercent, note
    PROPERTIES(b) nameOwnershipCompany, nameCompany, overFullNameCompany, shortNameOwnershipCompany, recipes, extractYearFromDate,
                  actQuantity, nameActStock, actStudyOfCulinaryCommittee, commonNameEmployeeCommittee, netWeightProduct, date    
   
    PROPERTIES(b,dt) firstShortNameChief, namePositionChief, nameSkus

    OBJECTS p = Product
    PROPERTIES(p) READONLY nameSku, shortNameUOM 
    PROPERTIES READONLY actNettoNetWeight(p), actBruttoNetWeightSkus(p,dt), actPerc(p,dt)
                  
    FILTERS       BOM(p) == b

    OBJECTS c = Component GRID
    FILTERS       BOM(c) == b,
                  material(c) IS Product     
    PROPERTIES    READONLY  nameMaterial(c), wastageHeat(c,dt), wastageCold(c,dt), actBruttoNetWeight(c,dt), actBruttoNetWeightEx(c,dt), 
                  actNettoNetWeight(c), actBruttoNetWeightSkus(c,dt), actNBruttoNetWeightSkus(c,dt)         

    OBJECTS sk = Sku
    PROPERTIES(sk) READONLY Stock.name 

    PROPERTIES     READONLY actBruttoNetWeight(c,sk,dt)
    FILTERS        actBruttoNetWeight(c,sk,dt), actNBruttoNetWeight(c,sk,dt)
    
    OBJECTS cc = Component
    PROPERTIES(cc) READONLY nameMaterial, shortNameUOM, overNettoNetWeight
    PROPERTIES    READONLY  wastageHeat(cc,dt), wastageCold(cc,dt), actBruttoNetWeight(cc,dt), actBruttoNetWeightEx(cc,dt),
                  actNettoNetWeight(cc)                                    
                  
    FILTERS       BOM(cc) == b,
                  material(cc) IS Sku     
                  
    OBJECTS e=Employee
    PROPERTIES(e) READONLY name[Contact], namePosition
    FILTERS       inCommittee(b,e)                  
              
;
printActStudyOfCulinaryNew 'Акт контрольной проработки новый'(BOM BOM)  = {
    DIALOG dialogDate OBJECTS d INPUT DO
        PRINT actStudyOfCulinaryBOM OBJECTS dt = d, b = BOM ;
} IMAGE 'print.png' IN print;

EXTEND FORM BOMs
    PROPERTIES(b)  printActStudyOfCulinaryNew   
;
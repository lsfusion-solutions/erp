MODULE InvoiceAlcohol;

REQUIRE ItemAlcohol, Invoice;

NAMESPACE Invoice;

alcoholDirectionType = ABSTRACT CASE AlcoholDirectionType(Invoice);
nameAlcoholDirectionType 'Тип движения алкоголя' (Invoice i) = staticCaption(alcoholDirectionType(i));
alcoholDirectionType (InvoiceDetail d) = alcoholDirectionType(invoice(d)) PERSISTENT;
    
alcoholIncomeType = ABSTRACT CASE AlcoholIncomeType(Invoice);
nameAlcoholIncomeType 'Тип приобретения алкоголя' (Invoice i) = staticCaption(alcoholIncomeType(i));
alcoholIncomeType (InvoiceDetail d) = alcoholIncomeType(invoice(d)) PERSISTENT;
 
alcoholSupplierType = ABSTRACT AlcoholSupplierType(InvoiceDetail) PERSISTENT INDEXED;
nameAlcoholSupplierType 'Тип поставщика алкоголя' (InvoiceDetail i) = staticCaption(alcoholSupplierType(i));

alcoholSupplierType = ABSTRACT AlcoholSupplierType(Invoice) PERSISTENT;
nameAlcoholSupplierType 'Тип поставщика алкоголя' (Invoice i) = staticCaption(alcoholSupplierType(i));

volume 'Объем' = ABSTRACT NUMERIC[16,5] (InvoiceDetail) PERSISTENT;
signetVolume 'Объем' = ABSTRACT NUMERIC[16,5] (InvoiceDetail) PERSISTENT; 

volumeInvoiceDetailLiterTypesFromTo 'Объем, л' = GROUP SUM signetVolume(InvoiceDetail d) IF 
    isParent(Group group, sku(d)) AND isPosted(d) AND
    date(d) >= DATE dateFrom AND date(d) <= DATE dateTo
        BY group, customerStock(d), alcoholDirectionType(d), alcoholIncomeType(d), 
            alcoholSupplierType(d), extractMonth(date(d)), dateFrom, dateTo; 

volumeInvoiceDetailTypes 'Объем, тыс дал.' = NUMERIC[20,8]([ =GROUP SUM signetVolume(InvoiceDetail d) IF 
    isParent(Group group, sku(d)) AND isPosted(d) AND
    date(d) >= DATE dateFrom AND date(d) <= DATE dateTo
        BY group, customerStock(d), alcoholDirectionType(d), alcoholIncomeType(d), 
            alcoholSupplierType(d), extractMonth(date(d)), dateFrom, dateTo](StatisticGroup group,Stock stock,AlcoholDirectionType type1,AlcoholIncomeType type2,AlcoholSupplierType type3,Month month,DATE dateFrom,DATE dateTo)) /
            (OVERRIDE 1.0 IF group IS StatisticGroup, conversionFactor(group));    

                                                    
year 'Отчетный год' = extractYear(DATE dFrom) IF dFrom IS DATE;              

FORM alcoholStockReports 'Отчет по алкоголю'
    OBJECTS dates = (dFrom = DATE, dTo = DATE) FIXED PANEL
    PROPERTIES valDFrom = OBJVALUE(dFrom), valDTo = OBJVALUE(dTo)
       
    OBJECTS dt = DATE FIXED PANEL
    PROPERTIES valDT = OBJVALUE(dt)
    
    OBJECTS c = LegalEntity FIXED PANEL
    PROPERTIES SELECTOR name(c)
    FILTERS isCompany(c)   
    
    OBJECTS gt = GroupType FIXED PANEL
    PROPERTIES(gt) SELECTOR name
       
    OBJECTS aaa = (sk = Group, s=Stock, type1 = AlcoholDirectionType, type2 = AlcoholIncomeType, type3 = AlcoholSupplierType, m = Month)  
    PROPERTIES name(s), id(s), number(type1), number(type2), number(type3), number(m), year(dFrom) TODRAW aaa,id(sk), name(sk) 

    PROPERTIES(sk,s,type1,type2,type3,m,dFrom,dTo)  volumeInvoiceDetailTypes 

    ORDER BY number(m),id(sk)
    
    FILTERS number(m) >= extractMonthNumber(dFrom), number(m) <= extractMonthNumber(dTo),            
            groupType(sk) == gt,
            active(sk),
            isCompany(s),
            legalEntity(s) == c
    
    FILTERS volumeInvoiceDetailTypes(sk,s,type1,type2,type3,m,dFrom,dTo)
;

volumeInvoiceDetail 'Объем, тыс дал.' = NUMERIC[20,8]([ =GROUP SUM signetVolume(InvoiceDetail d) IF 
    isParent(Group group, sku(d)) AND isPosted(d) AND
    date(d) >= DATE dateFrom AND date(d) <= DATE dateTo //AND alcoholDirectionTypeInvoiceDetail(d)==AlcoholDirectionType.income
        BY group, customerStock(d), supplier(d), alcoholDirectionType(d), extractMonth(date(d)), dateFrom, dateTo](StatisticGroup group,Stock stock,LegalEntity supplier,AlcoholDirectionType type,Month month,DATE dateFrom,DATE dateTo)) /
            (OVERRIDE 1.0 IF group IS StatisticGroup, conversionFactor(group));  

volumeInvoiceDetailCustomerFromTo 'Объем, тыс дал. (итого)' = GROUP SUM 
    (NUMERIC[20,8](signetVolume(InvoiceDetail d))/(OVERRIDE 1.0 IF group(GroupType groupType, sku(d)) IS StatisticGroup, conversionFactor(group(groupType, sku(d))))) IF    
    isPosted(d) AND date(d) >= DATE dateFrom AND date(d) <= DATE dateTo// AND alcoholDirectionTypeInvoiceDetail(d)==AlcoholDirectionType.income
        BY groupType, customer(d), alcoholDirectionType(d), dateFrom, dateTo;

FORM alcoholStockSupplierReports 'Отчет по алкоголю (поставщики)'
    OBJECTS dates = (dFrom = DATE, dTo = DATE) FIXED PANEL
    PROPERTIES valDFrom = OBJVALUE(dFrom), valDTo = OBJVALUE(dTo)
    
    OBJECTS c = LegalEntity FIXED PANEL
    PROPERTIES SELECTOR name(c)
    FILTERS isCompany(c)       
    
    OBJECTS gt = GroupType FIXED PANEL
    PROPERTIES(gt) SELECTOR name
       
    OBJECTS aaa = (sk = Group, s=Stock, l = LegalEntity, type = AlcoholDirectionType, m = Month)  
    PROPERTIES name(s), id(s), name(l), number(type), number(m), year(dFrom) TODRAW aaa,id(sk), name(sk) 

    PROPERTIES GRID volumeInvoiceDetail(sk,s,l,type,m,dFrom,dTo)//, volumeInvoiceDetailGroupTypeCustomerDateFromTo(gt,c,dFrom,dTo)
               

    ORDER BY number(m), name(l), id(sk)
    
    FILTERS number(m) >= extractMonthNumber(dFrom), number(m) <= extractMonthNumber(dTo),
            isCompany(s),
            groupType(sk) == gt,            
            active(sk),            
            legalEntity(s) == c        
                
    OBJECTS t = AlcoholDirectionType FIXED GRID
    PROPERTIES(t) READONLY staticCaption, number                
    PROPERTIES volumeInvoiceDetailCustomerFromTo(gt,c,t,dFrom,dTo)
    
    FILTERS volumeInvoiceDetail(sk,s,l,type,m,dFrom,dTo)
;

NAVIGATOR {
    statisticsNavigator{
        ADD alcoholStockReports;
        ADD alcoholStockSupplierReports;
    }
}
   
META defineInvoiceAlcohol(dumb)
    alcoholSupplierType = ABSTRACT AlcoholSupplierType(InvoiceDetail);
    nameAlcoholSupplierType 'Тип поставщика алкоголя' (InvoiceDetail d) = staticCaption(alcoholSupplierType(d));
    alcoholSupplierType = DATA AlcoholSupplierType(UserInvoiceDetail);
    nameAlcoholSupplierType 'Тип поставщика алкоголя' (UserInvoiceDetail d) = staticCaption(alcoholSupplierType(d));
    alcoholSupplierType(UserInvoiceDetail d) += alcoholSupplierType(d); 
    alcoholSupplierType[Invoice.InvoiceDetail](InvoiceDetail d) += alcoholSupplierType(d);  
    
    alcoholSupplierType (UserInvoiceDetail d) <- alcoholSupplierType(supplier[InvoiceDetail](d),sku[InvoiceDetail](d))
        WHEN CHANGED(sku[InvoiceDetail](d)) OR CHANGED(supplier[InvoiceDetail](d));
        
    alcoholSupplierType = ABSTRACT AlcoholSupplierType(Invoice);
    nameAlcoholSupplierType 'Тип поставщика алкоголя' (Invoice i) = staticCaption(alcoholSupplierType(i));    
    alcoholSupplierType = DATA AlcoholSupplierType(UserInvoice);
    nameAlcoholSupplierType 'Тип поставщика алкоголя' (UserInvoice i) = staticCaption(alcoholSupplierType(i));
    alcoholSupplierType(UserInvoice i) += alcoholSupplierType(i);  
    alcoholSupplierType[Invoice.Invoice](Invoice i) += alcoholSupplierType(i);  
    
    EXTEND FORM userInvoice 
        
    ;
    overCopy(UserInvoiceDetail d, UserInvoiceDetail detail) += ACTION {      
        alcoholSupplierType(d) <- alcoholSupplierType(detail);
    } 

    EXTEND FORM userInvoice
        PROPERTIES (i) nameAlcoholSupplierType
        PROPERTIES(d) nameAlcoholSupplierType
        
        OBJECTS da = UserInvoiceDetail
        PROPERTIES (da) index, idBarcodeSku, nameSku,
                        nameAlcoholSupplierType, DELETE GRID
        FILTERS userInvoice(da) == i        

    ;

    DESIGN userInvoice{
        specification.box{
            NEW alcoholContainer {
                caption = 'Алкоголь';
                NEW headerAlcoholContainer { type = CONTAINERH; MOVE PROPERTY (nameAlcoholSupplierType(i));}
                MOVE da.box;
            }
        }
    } 
    EXTEND FORM invoices
        PROPERTIES(d) READONLY nameAlcoholSupplierType
    ;    
    
    WHEN SESSION FORMS userInvoice CHANGED (alcoholSupplierType(userInvoice(UserInvoiceDetail d))) DO {
        alcoholSupplierType (d) <- alcoholSupplierType(userInvoice(d));  
    }
  
END
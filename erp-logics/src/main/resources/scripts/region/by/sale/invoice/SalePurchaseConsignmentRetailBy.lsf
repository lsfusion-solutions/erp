MODULE SalePurchaseConsignmentRetailBy;

REQUIRE PricingSalePurchase, ConsignmentBy, PricingSale, SaleOperationConsignmentBy;

NAMESPACE Sale;


useRetailPrice 'Использовать розничные цены в ТТН (отгрузка на магазин)' = DATA BOOLEAN (Operation);
useRetailPrice 'Использовать розн. цены в ТТН' (InvoiceDetail invoiceDetail) = useRetailPrice(operation(invoice(invoiceDetail)));

EXTEND FORM operation
    PROPERTIES(o) useRetailPrice
;
DESIGN operation {
    consignment {
        MOVE PROPERTY(useRetailPrice(o));
    }
}
         
dataPrice(InvoiceDetail d) += WHEN useRetailPrice(d) THEN 
                                    IF createPurchasePricing(d) 
                                        THEN purchaseRetailPrice(d)
                                        ELSE retailPrice(d);
                         
dataSum(InvoiceDetail d) += WHEN useRetailPrice(d) THEN 
                                    IF createPurchasePricing(d) 
                                        THEN purchaseRetailSum(d) (-) purchaseRetailVATSum(d)
                                        ELSE retailSum(d) (-) retailVATSum(d);

dataVAT(InvoiceDetail d) += WHEN useRetailPrice(d) THEN 
                                    IF createPurchasePricing(d) 
                                        THEN valuePurchaseRetailVAT(d)
                                        ELSE valueRetailVAT(d);
                
dataSumVAT(InvoiceDetail d) += WHEN useRetailPrice(d) THEN 
                                    IF createPurchasePricing(d) 
                                        THEN purchaseRetailVATSum(d)  
                                        ELSE retailVATSum(d);  
        
dataSumInvoice(InvoiceDetail d) += WHEN useRetailPrice(d) THEN 
                                    IF createPurchasePricing(d) 
                                        THEN purchaseRetailSum(d)
                                        ELSE retailSum(d);


MODULE RepricingPurchaseInvoiceDashboard;

REQUIRE Dashboard, RepricingPurchaseInvoice, RepricingLabel, RepricingMachinery;

NAMESPACE Repricing;

createExtraValuationNew 'Создать дооценку'(Stock department, DATETIME timeFrom, DATETIME timeTo, Operation operation) = {
	NEWSESSION {
	    createExtraValuation(department, timeFrom, timeTo, operation);   
	}
}

FORM repricingInvoiceDashboard 'Дооценка'
    OBJECTS d = DepartmentStore PANEL
    PROPERTIES (d) SELECTOR name
    FILTERS isCompany(d)
        
    OBJECTS o = Repricing.Operation PANEL
    PROPERTIES (o) SELECTOR name
    FILTERS in(currentUser(), o)

    OBJECTS t=DATETIME PANEL
    PROPERTIES(t) VALUE        
    
    OBJECTS tt=DATETIME PANEL
    PROPERTIES(tt) VALUE            
     
    OBJECTS i = Purchase.Invoice
    PROPERTIES (i) READONLY isClosed, isPosted,
                            number, series, date, time,
                            nameSupplier, nameSupplierStock, nameCustomer, nameCustomerStock,            
                            countInvoiceDetail, quantityInvoiceDetail, sumInvoiceDetail,
                            VATSumInvoiceDetail, invoiceSumInvoiceDetail
    PROPERTIES (i) NEWSESSION EDIT SHOWIF isOpened(i)
    FILTERGROUP filters FILTER 'Накладные, по которым не было дооценки' filterToRepricingDate(i,d,t,tt) 'F9' DEFAULT     
    
    OBJECTS p = Repricing
    PROPERTIES (p) READONLY isPosted, number, series, date, time, beforeDate,
                   nameDepartmentStore, countRepricingDetail, diffSumRepricingDetail,
                   diffMarkupSumRepricingDetail, diffVATSumRepricingDetail,
                   diffRetailSumRepricingDetail, numberDisposal, skipChangeLedger,
                   statusLabel BACKGROUND backgroundPrintLabelTransaction(p),
                   statusMachinery ON SHORTCUT createSnapshotMachineryPriceTransaction(p) BACKGROUND backgroundCreateMachineryPriceTransaction(p), 
                   note                      
               
    PROPERTIES (p) createLabelTransaction SHOWIF showPrintLabelTransaction(p), 
                   createSnapshotLabelTransaction 
                   
    PROPERTIES(p)  createMachineryPriceTransaction SHOWIF showCreateMachineryPriceTransaction(p)
    PROPERTIES (p)  TOOLBAR print, printXls                                           
    PROPERTIES (p) NEWSESSION NEW, EDIT SHOWIF showEditUser(p), deletep = DELETE SHOWIF showDeleteUser(p)    
               
    PROPERTIES (d,t,tt,o)  createExtraValuationNew TODRAW i TOOLBAR     
                     
    FILTERGROUP filters2 FILTER 'Переоценки на текущую дату' date(p)==currentDate() 'F5' DEFAULT                         
                                               
;
DESIGN repricingInvoiceDashboard {
    BOX {
        type = CONTAINERV;

        NEW topContainer {
            type = CONTAINERH;
            MOVE BOX(d);
            MOVE BOX(o);
            MOVE BOX(t) { caption = 'Время с';}
            MOVE BOX(tt) { caption = 'Время по';}
        }
        MOVE BOX(i);
        MOVE BOX(p);
        MOVE TOOLBARBOX;
    }
}

EXTEND FORM repricingInvoiceDashboard FILTERS (in(currentUser(), operation(i))) OR 
                         (NOT operation(i));
@extendFormFilterRoleAccess(repricing, p, repricingInvoiceDashboard); 

NAVIGATOR {
    retailDashboardNavigator {
        ADD repricingInvoiceDashboard;
    }
}
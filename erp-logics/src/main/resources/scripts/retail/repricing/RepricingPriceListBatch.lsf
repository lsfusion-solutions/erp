MODULE RepricingPriceListBatch;

REQUIRE RepricingPriceList, RepricingPriceLimitFixed;

NAMESPACE Repricing;

managementRetailPrice 'Цена (управленческая)' = DATA LOCAL NESTED NUMERIC[16,4] (Batch);
inDashboardRepricing 'Вкл' = DATA LOCAL NESTED BOOLEAN (Batch); 
//assignAttributeBatch = ACTION ABSTRACT (Stock, Operation, DATETIME);

calculateDashboardManagementRepricingBatch 'По прайсам'(Stock d, Operation o, DATETIME dt) = ACTION {
    managementRetailPrice(Batch batch) <- NULL;
    managementRetailPrice(Batch batch) <- prevPriceA((OVERRIDE retailPriceListType(d), priceListType(o)), batch, d, dt) 
        WHERE currentBalance(batch, d) > 0 AND skipRepricing(sku(batch), d); // Берем только те товары, которые исключили из переоценки по товарам
    
    inDashboardRepricing(Batch batch) <- managementRetailPrice(batch) != priceABatch(SystemLedgerPriceListType.retailPricingPriceListType, batch, d, dt); 
    formRefresh();
} TOOLBAR;

//repForm =  DATA LOCAL UserRepricing();  
createDashboardRepricingNotFormBatchDetail (UserRepricing r, Stock d, DATETIME dt) = ACTION {
    FOR inDashboardRepricing(Batch batch) NEW rd = UserRepricingDetail DO {
        userRepricing(rd) <- r;
        sku(rd) <- sku(batch);
        batch(rd) <- batch;
        quantity(rd) <- currentBalance(batch, d);
        retailPrice(rd) <- managementRetailPrice(batch);
        curRetailPrice(rd) <- prevPriceB(SystemLedgerPriceListType.retailPricingPriceListType, batch, d, dt);
        price(rd) <- round2(prevPriceA[PriceListType,Batch,Stock,DATETIME](SystemLedgerPriceListType.supplierPricingPriceListType, batch, d, dt));
//                overPriceListUserRepricingDetail(rd);
    }

}

createDashboardRepricingNotFormBatch 'Создать переоценку'(Stock d, Operation o, DATETIME dt) = ACTION {
    IF [ = GROUP SUM 1 IF inDashboardRepricing(Batch batch)]() THEN {
        FOR NEW r = UserRepricing DO {
            departmentStore(r) <- d;
            operation(r) <- o;
            date(r) <- toDate(dt);
            time(r) <- toTime(dt);  
            isPosted(r) <- TRUE;
    
            createDashboardRepricingNotFormBatchDetail(r, d, dt);
            repForm() <- r;
        }
    }
} TOOLBAR;

createDashboardRepricingBatch 'Создать переоценку'(Stock d, Operation o, DATETIME dt) = ACTION NEWSESSION NESTED LOCAL  {
    createDashboardRepricingNotFormBatch(d, o, dt);
    SHOW userRepricing OBJECTS p = repForm() MANAGESESSION DOCKED NOCANCEL;
    repForm() <- NULL;      
}  

createOverManagementRepricing(Stock department) += ACTION {
    calculateDashboardManagementRepricingBatch(department, toManagementRepricingOperation(), currentDateTime());
    createDashboardRepricingNotFormBatch(department, toManagementRepricingOperation(), currentDateTime());     
}

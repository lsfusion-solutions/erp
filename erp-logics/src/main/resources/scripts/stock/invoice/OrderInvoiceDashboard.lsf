MODULE OrderInvoiceDashboard;

REQUIRE Dashboard, OrderInvoice;

NAMESPACE Invoice;

excludeFilterInvoiced = ABSTRACT BOOLEAN (Order);
filterInvoiced = TRUE IF toInvoice(Order o) AND NOT excludeFilterInvoiced(o);  
background = ABSTRACT CASE COLOR (Order);

FORM orderInvoiceDashboard 'Приемка по заказам' AUTOREFRESH 60

    OBJECTS d = DATE FIXED PANEL
    PROPERTIES(d) date = OBJVALUE
    
    OBJECTS o=Order FIXED GRID
    PROPERTIES (o) selected
    PROPERTIES (o) READONLY BACKGROUND background(o) isPosted, number, series, date, nameFrom, nameFromStock, nameTo, nameToStock, 
                   nameOperation, quantityOrderDetail, sumOrderDetail, VATSumOrderDetail, invoiceSumOrderDetail, note,
                   shipmentDate, shipmentTime, objectClassName    
    FILTERS in(o), isPosted(o)
    FILTERGROUP filters FILTER 'На дату' shipmentDate(o) <= d 'F5' DEFAULT
    FILTERGROUP filterInvoiced 
        FILTER 'Не оприходованы' filterInvoiced(o) 'F7' DEFAULT
        FILTER 'Оприходованы' NOT filterInvoiced(o) 'F6'
    
    OBJECTS od=OrderDetail FIXED GRID
    PROPERTIES (od) READONLY index, idBarcodeSku, nameSku, shortNameUOMSku,
                    packQuantity
    PROPERTIES (od) READONLY quantity, price, sum,
                   valueVAT, VATSum, invoiceSum,
                   nameSupplierStock, shipmentDate, shipmentTime, toInvoice  
    FILTERS order(od)==o
    ORDER BY index(od)
    
    OBJECTS i=Invoice FIXED GRID
    PROPERTIES (i) READONLY isPosted, number, series, date, time,                                 
                   nameFrom, nameFromStock, nameTo, nameToStock,
                   nameCurrency, countInvoiceDetail, quantityInvoiceDetail, sumInvoiceDetail,
                   VATSumInvoiceDetail, invoiceSumInvoiceDetail

    PROPERTIES (i) edit SHOWIF overShowEdit(i)
    PROPERTIES     NEWSESSION deletei=DELETE(i) TOOLBAR SHOWIF overShowDelete(i)  
    FILTERS in(i)
    FILTERGROUP filters1 FILTER 'На дату' date(i) == d 'F5' DEFAULT
    
    PROPERTIES(o) createMultiUserInvoice  
;

DESIGN orderInvoiceDashboard {
    main{

        NEW dash BEFORE functions.box{
            fill = 1;
            type = SPLITV;
            NEW header {
                fill = 1;                         
                MOVE o.box;

            }
            NEW pickings {
                fill = 1;
                type = TABBED;
                MOVE i.box {
                    i.panel {
                        type = CONTAINERH;
                    }
                }
                MOVE od.box {
                    caption = 'Детализация заказа';
                }
            }
        }          
        NEW secondContainer BEFORE dash {
            type = CONTAINERH;
            caption = 'Шапка';
            MOVE PROPERTY(date);
        }
        PROPERTY(packQuantity(od)) { background = #D4FFD4; }
        PROPERTY(quantity(od)) { background = #D4FFD4; }
        PROPERTY(price(od)) { background = #D4FFD4; }
    }
}
 
@defineFilterIsOpened (order, orderInvoiceDashboard, o);
@defineFilterIsOpened (invoice, orderInvoiceDashboard, i);
@extendFormFilterAccessStock(Order, o, orderInvoiceDashboard, toStock, company);
@extendFormFilterAccessStock(Invoice, i, orderInvoiceDashboard, toStock, company);
@extendFormFilterRoleAccessNS(order, o, orderInvoiceDashboard, Operation); 
@extendFormFilterRoleAccessNS(invoice, i, orderInvoiceDashboard, Operation); 
NAVIGATOR {
    purchaseDashboardNavigator {
        ADD orderInvoiceDashboard;
    }
}



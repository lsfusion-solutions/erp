MODULE OrderLedgerArticle;

REQUIRE StockArticleDocument, OrderLedger;

NAMESPACE OrderLedger;

FORM orderArticleLedgerNotDate 'Резерв по артикулам'

    OBJECTS st = Stock FIXED PANEL
    PROPERTIES  SELECTOR name(st)

    OBJECTS ar = Article FIXED PANEL
    PROPERTIES SELECTOR id(ar)

    OBJECTS s = OrderLedger
    PROPERTIES(s) READONLY BACKGROUND backgroundReserve(s) statusReserve     
    PROPERTIES(s) READONLY  date, 
                  dateTime, nameContact, nameContactStock, description,
                  toShipQuantity, quantity
    ORDER BY statusReserve(s), dateTime(s)
                  
    FILTERS stock(s)==st,
            article(sku(s))==ar

    FILTERS isPosted(s) AND NOT isClosed(s)
    FILTERGROUP toShipQuantityFilters
        FILTER 'Не выполнено' toShipQuantity(s) > 0 'F7' DEFAULT   

    FILTERGROUP stockTimesFilters
        FILTER 'Резерв (закупки) всего' isPurchase(s) 'F11'
        FILTER 'Резерв (продажи) всего' TRUE AND NOT isPurchase(s) 'F10'
;

DESIGN orderArticleLedgerNotDate {
    main {
        type = CONTAINERV;
        NEW row{
            type = CONTAINERH;
            MOVE st.box;
            MOVE ar.box;
        }
        MOVE s.box;
    }
    MOVE functions.box;
}

FORM orderArticleLedger 'Резерв по артикулам'

    OBJECTS t = DATETIME FIXED PANEL
    PROPERTIES  dVal=OBJVALUE(t)

    OBJECTS st = Stock FIXED PANEL
    PROPERTIES  SELECTOR name(st)

    OBJECTS ar = Article FIXED PANEL
    PROPERTIES SELECTOR id(ar)

    OBJECTS s = OrderLedger
    PROPERTIES(s) READONLY BACKGROUND backgroundReserve(s) statusReserve     
    PROPERTIES(s) READONLY BACKGROUND background(s,t)  date, 
                  dateTime, nameContact, nameContactStock, description,
                  toShipQuantity, quantity
    ORDER BY statusReserve(s)
                  
    FILTERS stock(s)==st,
            article(sku(s))==ar

    FILTERS isPosted(s) AND NOT isClosed(s)
    FILTERGROUP toShipQuantityFilters
        FILTER 'Не выполнено' toShipQuantity(s) > 0 'F7' DEFAULT   

    FILTERGROUP stockTimesFilters
        FILTER 'Резерв (закупки) всего' isPurchase(s) 'F11'
        FILTER 'Резерв (продажи) всего' TRUE AND NOT isPurchase(s) 'F10'
        FILTER 'Резерв (закупки) на дату' dateTime(s) < t AND isPurchase(s) 'F9'
        FILTER 'Резерв (продажи) на дату' dateTime(s)< t AND NOT isPurchase(s) 'F8'
        FILTER 'На дату' dateTime(s) < t 'F7'


;

DESIGN orderArticleLedger {
    main {
        type = CONTAINERV;
        NEW row{
            type = CONTAINERH;
            MOVE t.box;
            MOVE st.box;
            MOVE ar.box;
        }
        MOVE s.box;
    }
    MOVE functions.box;
}
//-- Для просмотра резерва несколько складов
META extendFormDocumentArticleStockOrderLedger(object)
    reviewReserve 'Резерв' (Article article, Stock stock, ###object object) = ACTION {
        IF shipmentDateTime(object) THEN {
            FORM orderArticleLedger OBJECTS t=shipmentDateTime(object), st=stock, ar = article ;
        } ELSE {
            FORM orderArticleLedgerNotDate OBJECTS st=stock, ar = article ;    
        }
    }
    
END
//-- Для просмотра резерва один склад
META extendFormDocumentArticleOrderLedger(object, stockProp)
    reviewReserve 'Резерв' (Article article, ###object object) = ACTION {
        IF dateTime(object) THEN {
            FORM orderArticleLedger OBJECTS t=dateTime(object), st=stockProp(object), ar = article ;
        } ELSE {
            FORM orderArticleLedgerNotDate OBJECTS st=stockProp(object), ar = article ;
        }
    }
    
END
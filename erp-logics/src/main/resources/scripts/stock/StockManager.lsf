MODULE StockManager;

REQUIRE Stock, LegalEntityManager;

NAMESPACE Stock;

manager = DATA Employee (LegalEntity, Stock);
nameManager 'Менеджер' (LegalEntity l, Stock s) = shortName(manager(l, s));
overManager(LegalEntity l, Stock s) = OVERRIDE manager(l) IF s IS Stock, manager(l, s);

inManager 'Отм' (Employee e, LegalEntity l, Stock s) = e ==  manager(l, s);

EXTEND FORM legalEntity 
    TREE stockTreeM bm=STRING[3], sgm = StockGroup PARENT parent
    PROPERTIES READONLY OBJVALUE(bm), sgmTreeName = name(sgm)
    ORDER BY sgmTreeName
    FILTERS stringEqualsAll(bm)

    OBJECTS stm=Stock
    PROPERTIES(stm) READONLY name
    PROPERTIES nameManager(l, stm)
    FILTERS isParent(sgm, stm) OR (stm IS Stock AND NOT sgm),
            isCompany(stm)
;

DESIGN legalEntity {
    managerContainer {
        NEW managerStock{
            type = SPLITH;
            fill = 1;
            MOVE stockTreeM.tree.box {
                fill = 1;
                caption = 'Группа складов';
            }
            MOVE stm.box {
                fill = 3;
            }
        }
    }
}

EXTEND FORM employee
    OBJECTS stm=Stock PANEL 
    PROPERTIES READONLY name(stm) SELECTOR
    FILTERS isCompany(stm)
    
    TREE legalEntityTreeMS lgms = LegalEntityGroup PARENT parent
    PROPERTIES READONLY  lgmsTreeName = name(lgms)
    ORDER BY lgmsTreeName

    OBJECTS lms = LegalEntity GRID
    PROPERTIES READONLY name(lms), nameManager(lms, stm)
    PROPERTIES inManager(e, lms, stm)
    ORDER BY name(lms)
    FILTERS isParent(lgms, lms) OR (lms IS LegalEntity AND NOT lgms)
;

DESIGN employee {
    managerTabContainer {
        NEW managerStockContainer {
            caption = 'По складу';
            fill = 1;
            MOVE stm.box;
            NEW managerStockSplit {
                fill = 1;
                type = SPLITH;
                MOVE legalEntityTreeMS.tree.box {fill = 1;};
                MOVE lms.box {fill = 3;};
            }
        }
    }
}
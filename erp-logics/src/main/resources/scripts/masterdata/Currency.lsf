MODULE Currency;

REQUIRE System, Time, MasterData, DefaultData;

CLASS Currency 'Валюта';
TABLE currency(Currency);

name 'Наименование' = DATA VARISTRING[50](Currency) IN recognize;
sid 'Код валюты' = DATA STRING[3] (Currency);

symbol 'Символ' = DATA STRING[5] (Currency);
shortName 'Наименование (сокр.)' = DATA STRING[3] (Currency);
documentName 'Наименование (в документах)' = DATA STRING[10] (Currency);
currencyShortName (string) = GROUP AGGR Currency currency BY shortName(currency) WHERE currency IS Currency;

defaultCurrency = DATA Currency (); 
nameDefaultCurrency 'Валюта по умолчанию' = name(defaultCurrency()) PREFCHARWIDTH 30;

// Формы

FORM currency 'Валюта'
    OBJECTS c = Currency FIXED PANEL
    PROPERTIES (c)  name, shortName, sid, symbol, documentName

    EDIT Currency OBJECT c
;

FORM currencies 'Валюты'
    OBJECTS c = Currency
    PROPERTIES(c) READONLY name, shortName, sid, symbol, documentName

    PROPERTIES(c) NEWSESSION NEW, EDIT, DELETE TOOLBAR

    LIST Currency OBJECT c
;

EXTEND FORM options
    PROPERTIES() nameDefaultCurrency
;

DESIGN options {
    commons {
        MOVE PROPERTY(nameDefaultCurrency());
    }
}

// ---------------------------------- Типы обмена ---------------------------- //

CLASS TypeExchange 'Тип обмена';
TABLE typeExchange(TypeExchange);
TABLE rateExchange (TypeExchange, Currency, DATE);

name 'Наименование' = DATA VARISTRING[50](TypeExchange) IN recognize;

currency 'Валюта типа обмена'= DATA Currency (TypeExchange);
nameCurrency 'Валюта типа обмена' (TypeExchange typeExchange) = name(currency(typeExchange)) IN base;

FORM typeExchange 'Тип обмена'
    OBJECTS t = TypeExchange FIXED PANEL
    PROPERTIES(t) name, nameCurrency
    
    EDIT TypeExchange OBJECT t
;

// Расчет курсов на дату

rate 'Курс обмена' = DATA NUMERIC[20,13] (TypeExchange, Currency, DATE);
typeExchange (name) = GROUP AGGR TypeExchange typeExchange BY name(typeExchange) WHERE typeExchange IS TypeExchange;

rateOn 'Курс обмена' (typeExchange, currency, date) =
    GROUP LAST rate(TypeExchange typeExchange, Currency currency, DATE dateIn)
          BY   typeExchange, currency, DATE date
          ORDER dateIn
          WHERE rate(typeExchange, currency, dateIn) AND dateIn <= date COMPLEX;

curRate 'Текущий курс обмена' (TypeExchange typeExchange, Currency currency) =
    rateOn(typeExchange, currency, currentDate());

// -------------------------------------------- Курсы валют --------------------------------- //

FORM dialogTypeExchangeCurrency 'Курс обмена на дату'
    OBJECTS t=TypeExchange FIXED PANEL
    OBJECTS c=Currency FIXED PANEL
    OBJECTS d=DATE FIXED PANEL
    PROPERTIES READONLY name(t),  nameCurrency(t), name(c)
    PROPERTIES val=OBJVALUE(d), rate(t,c,d)

;
DESIGN dialogTypeExchangeCurrency {
    main {
        type = CONTAINERV;
        MOVE t.box {
            MOVE PROPERTY(name(t)) { focusable = FALSE; preferredCharWidth = 40;}
        }
        MOVE c.box{
            type = CONTAINERV;
            MOVE d.box {
                type = CONTAINERH;

                MOVE PROPERTY(rate(t,c,d));
            };
            NEW row {
                caption = 'Валюты обмена';
                type = CONTAINERH;
                MOVE PROPERTY(name(c)) { caption = 'Валюта (из)'; preferredCharWidth = 20;}
                MOVE PROPERTY(nameCurrency(t)) { caption = 'Валюта (в)'; preferredCharWidth = 20;}
            }
        }
    }
    MOVE functions.box;
}

dialog 'Добавить' (TypeExchange typeExchange, Currency currency) = ACTION SHOW dialogTypeExchangeCurrency OBJECTS t = typeExchange, c = currency  TOOLBAR;
delete 'Удалить' (TypeExchange typeExchange, Currency currency, DATE date) = ACTION ASSIGN rate(typeExchange, currency, date) <- NULL IMAGE 'delete.png';


parent = DATA TypeExchange (TypeExchange);
nameParent 'Родительский '  = name(parent(TypeExchange t));

//typeTypeExchangeCurrency = GROUP AGGR t BY parentTypeExchange(t), currencyTypeExchange(t);

isRevers 'Обратный' = DATA BOOLEAN (TypeExchange);

EXTEND FORM typeExchange    
    PROPERTIES(t) nameParent, isRevers
;

//createCrosTypeExchangeCurrency 'Создать кросс-курс' = ACTION (type, currency) {
//    IF NOT typeTypeExchangeCurrency(type, currency) THEN {
//        FOR NEW t = TypeExchange DO {
//            currencyTypeExchange(t) <- currency;
//            nameTypeExchange(t) <- VARISTRING[50](CONCAT '', 'Кросс '+nameTypeExchange(type), '-('+shortNameCurrency(currency)+')');   
//            parentTypeExchange(t) <- type;
//            
//            FOR  curRateTypeExchangeCurrency(type, c) DO {
//                rateExchange(t, c, date) <- NUMERIC[20,13](curRateTypeExchangeCurrency(type, currency)/(curRateTypeExchangeCurrency(type, c) IF curRateTypeExchangeCurrency(type, c)!=0)) WHERE date == currentDate();   
//            }
//        }    
//    } ELSE {
//        MESSAGE 'Кросс-курс уже существует для данной валюты и типа обмена';
//    } 
//};


FORM typeExchangeCurrencyDate 'Курсы валют'

    OBJECTS t = TypeExchange
    PROPERTIES(t) READONLY name, nameCurrency, nameParent, isRevers
    PROPERTIES(t) NEWSESSION NEW, EDIT, DELETE TOOLBAR

    OBJECTS c = Currency
    PROPERTIES(c) READONLY name
    PROPERTIES(c) NEWSESSION NEW, EDIT, DELETE TOOLBAR
    PROPERTIES(t, c) READONLY curRate
//    PROPERTIES(t, c)  createCrosTypeExchangeCurrency TOOLBAR 

    FILTERGROUP inactive FILTER 'Есть курс' curRate(t, c) 'F11' DEFAULT    

    OBJECTS d = DATE
    PROPERTIES(d) READONLY OBJVALUE

    PROPERTIES(t, c, d) rate
    PROPERTIES(t, c) dialog TODRAW d PANEL
    PROPERTIES(t, c, d) delete

    FILTERS rate(t, c, d)

    LIST TypeExchange OBJECT t

;

DESIGN typeExchangeCurrencyDate {
    main {
        type = CONTAINERV;
        NEW split {
            fill = 1;
            type = SPLITV;
            MOVE t.box;
            NEW topContainer {
                fill = 2;
                type = TABBED;
                NEW currentExchangeRates {
                    caption = 'Текущие курсы';
                    type = SPLITH;
                    MOVE c.box;
                    MOVE d.box;
                }
                NEW importExchangeRates {
                    caption = 'Импорт курсов';
                }
            }
        }
        MOVE functions.box;
    }
}

NAVIGATOR {
    masterData {
        NEW currencyNavigator 'Валюты и курсы' {
            ADD currencies;
            ADD typeExchangeCurrencyDate;
        }
    }
}

// --------------------------------------------------- Макросы по добавлению валюты в документы ------------------------------------ //
META defineDocumentHeaderCurrency (object)
    currency (object) = DATA Currency (###object);
    nameCurrency 'Валюта' (###object object)= name(currency(object)) IN documentPrm MINCHARWIDTH 10 PREFCHARWIDTH 20;
    shortNameCurrency 'Валюта (сокр.)' (###object object) = shortName(currency(object));
END
META defineDocumentAbstractHeaderCurrency (object)
    currency (object) = ABSTRACT Currency (###object) PERSISTENT;
    nameCurrency 'Валюта' (###object object)= name(currency(object)) IN documentPrm MINCHARWIDTH 10 PREFCHARWIDTH 20;
    shortNameCurrency 'Валюта (сокр.)' (###object object) = shortName(currency(object));
END
META defineDocumentInterfaceHeaderCurrency (object)
    @defineDocumentAbstractHeaderCurrency(object);
    @defineDocumentHeaderCurrency(user###object);
    currency (User###object object) += currency(object);
END

META defineDocumentDetailCurrency (object, detail)
    currency (###detail idetail) = currency(object(idetail)) PERSISTENT;
    nameCurrency 'Валюта' (###detail idetail) = nameCurrency(object(idetail));
    shortNameCurrency 'Валюта (сокр.)' (###detail idetail) = shortName(currency(idetail));
END
META defineDocumentInterfaceDetailCurrency (object, detail)
    @defineDocumentDetailCurrency(object, detail);
    @defineDocumentDetailCurrency(user###object, user###detail);
END

META defineDocumentCurrency (object, detail)
    @defineDocumentHeaderCurrency(object);
    @defineDocumentDetailCurrency(object, detail);
END
META defineDocumentAbstractCurrency (object, detail)
    @defineDocumentAbstractHeaderCurrency(object);
    @defineDocumentDetailCurrency(object, detail);
END
META defineDocumentInterfaceCurrency (object, detail)
    @defineDocumentInterfaceHeaderCurrency(object);
    @defineDocumentInterfaceDetailCurrency(object, detail);
END

META defineDocumentCurrency (object)
    @defineDocumentCurrency(object, object##Detail);
END
META defineDocumentAbstractCurrency (object)
    @defineDocumentAbstractCurrency(object, object##Detail);
END
META defineDocumentInterfaceCurrency (object)
    @defineDocumentInterfaceCurrency(object, object##Detail);
END

META deriveDocumentCurrency (object, stockProp)
    @deriveDocumentCurrency(object, stockProp, currencyStock);
END

META deriveDocumentCurrency (object, objectProp, currencyProp)
    currency (###object object) <- currency(objectProp(object)) WHEN CHANGED(objectProp(object));
END

// --------------------------------------------------- Стандартные значения ------------------------------------ //

loadDefaultCurrency 'Добавить валюты'(STRING[3] ishortName, VARISTRING[50] iname, STRING[5] isymbol) = ACTION {
    FOR NEW c = Currency DO {
       ASSIGN shortName(c) <- ishortName;
       ASSIGN name(c) <- iname;
       ASSIGN symbol(c) <- isymbol;
    }
}

overLoadDefaultCurrencies = ACTION ABSTRACT LIST ();

loadDefaultCurrencies 'Загрузить стандартные валюты'() = ACTION {
    EXEC loadDefaultCurrency('AUD', 'Австралийский доллар', ' ');
    EXEC loadDefaultCurrency('BGN', 'Болгарский лев', ' ');
    EXEC loadDefaultCurrency('HUF', 'Венгерский форинт', ' ');
    EXEC loadDefaultCurrency('UAH', 'Гривна', ' ');
    EXEC loadDefaultCurrency('DKK', 'Датская крона', ' ');
    EXEC loadDefaultCurrency('USD', 'Доллар США', '$');
    EXEC loadDefaultCurrency('EUR', 'Евро', '€');
    EXEC loadDefaultCurrency('PLN', 'Злотый', ' ');
    EXEC loadDefaultCurrency('INR', 'Индийская рупия', ' ');
    EXEC loadDefaultCurrency('ISK', 'Исландская крона', ' ');
    EXEC loadDefaultCurrency('CAD', 'Канадский доллар', ' ');
    EXEC loadDefaultCurrency('CNY', 'Китайский юань', ' ');
    EXEC loadDefaultCurrency('KWD', 'Кувейтский динар', ' ');
    EXEC loadDefaultCurrency('LVL', 'Латвийский лат', ' ');
    EXEC loadDefaultCurrency('LTL', 'Литовский лит', ' ');
    EXEC loadDefaultCurrency('MDL', 'Молдавский лей', ' ');
    EXEC loadDefaultCurrency('NOK', 'Норвежская крона', ' ');
    EXEC loadDefaultCurrency('RUB', 'Российский рубль', ' ');
    EXEC loadDefaultCurrency('RON', 'Румынский лей', ' ');
    EXEC loadDefaultCurrency('XDR', 'СДР', ' ');
    EXEC loadDefaultCurrency('SGD', 'Сингапурский доллар', ' ');
    EXEC loadDefaultCurrency('KGS', 'Сом', ' ');
    EXEC loadDefaultCurrency('KZT', 'Тенге', ' ');
    EXEC loadDefaultCurrency('TRY', 'Турецкая лира', ' ');
    EXEC loadDefaultCurrency('GBP', 'Фунт стерлингов', ' ');
    EXEC loadDefaultCurrency('CZK', 'Чешская крона', ' ');
    EXEC loadDefaultCurrency('SEK', 'Шведская крона', ' ');
    EXEC loadDefaultCurrency('CHF', 'Швейцарский франк', ' ');
    EXEC loadDefaultCurrency('JPY', 'Иена', ' ');
    EXEC loadDefaultCurrency('IRR', 'Иранский риал', ' ');
    EXEC loadDefaultCurrency('BLR', 'Белорусский рубль', ' ');

    EXEC overLoadDefaultCurrencies();
} IN loadDefault;

@implementLoadDefaultData(loadDefaultCurrencies);
MODULE ProductionOrderDemand;

REQUIRE ProductionOrder, SaleOrder;

NAMESPACE Production;

//-- Формирование заказа (продажа) для пополнения остатков на складе собственного производства

demandStock = DATA Stock(Order);
nameDemandStock 'Склад' = name(demandStock(Order order));
currentBalanceDemand 'Текущий остаток' (Sku sku,Order o) = currentBalance(sku, demandStock(o));

CONSTRAINT  demandStock(Order o) AND NOT isCompany(demandStock(o)) CHECKED BY demandStock[Order]    
        MESSAGE 'Склад для заказа не является складом компании';
demandStock (Order order) <- productsStock(order)
    WHEN CHANGED(productsStock(order));


quantityDemand 'Количество' = DATA LOCAL NESTED NUMERIC[16,5] (Sku,Order);
quantityDemandSkus (Order o) = GROUP SUM quantityDemand(Sku s,o);

createUserOrderProduction 'Создать заказ (продажа)'(Order order)  = {
    IF quantityDemandSkus(order) THEN {
        NEWSESSION NESTED (quantityDemand[Sku,Order]) {
            NEW o = UserOrder {
                supplier(o) <- productsLegalEntityStock(order);
                customer(o) <- productsLegalEntityStock(order);
                supplierStock(o) <- productsStock(order);
                customerStock(o) <- componentsStock(order);
                
                FOR quantityDemand(Sku sku,order) ==NUMERIC[16,5] q  NEW d = UserOrderDetail DO {
                    userOrder(d) <- o;
                    sku(d) <- sku;
                    quantity(d) <- q;
                }
                SHOW userOrder OBJECTS o=o DOCKED;
            }
        }
    }
}

EXTEND FORM order
    PROPERTIES (o) nameDemandStock

    OBJECTS sub = Sku 
    PROPERTIES(sub) READONLY name, idBarcode, shortNameUOM
    PROPERTIES currentBalanceDemand(sub,o) READONLY , quantityDemand(sub,o)    
    FILTERS sk ==sub OR fromSku(uniqueSubstituteFromTo(sk,sub))
    FILTERGROUP substitute
        FILTER 'Замены' fromSku(uniqueSubstituteFromTo(sk,sub)) 'F10' DEFAULT 
        
    OBJECTS dem = Sku 
    PROPERTIES(dem) READONLY name, idBarcode, shortNameUOM  
    PROPERTIES quantityDemand(dem,o) 
    FILTERS quantityDemand(dem,o) 
     
    PROPERTIES createUserOrderProduction(o) TOOLBAR TODRAW dem
    
;
DESIGN order {
    price2 {
        type = TABBED;
        price2a {
            caption = 'Цены';
        }
        price2b {
            caption = 'Пополнение остатков';
            NEW price2b1{
                caption = 'Фильтр складов для заказа';
                MOVE PROPERTY(nameDemandStock(o)); 
            }
            NEW price2b2{
                fill = 1;
                type = SPLITV;
                MOVE BOX(sub) {caption ='Подбор';}  
                MOVE BOX(dem) {caption ='Заказ';}   
            }
            
        }     
    }
}
WHEN LOCAL FORMS order CHANGED(productsStock(Order order)) DO {                                              
    demandStock (order) <- productsStock(order);
}

